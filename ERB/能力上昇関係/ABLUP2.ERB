;Ｖ感覚のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;Ｖ感覚のLvUP
;-------------------------------------------------
@ABLUP2
;オトコは却下
SIF IS_MALE(TARGET)
	RETURN 0

DRAWLINE
;PRINTL ヴァギナの感度をUPします
;PRINTL Ｖ感覚が高まると、通常のセックスやバイブからより大きな快楽が得られます
;DRAWLINE
;最大Lvは5、[セックス狂]が付いている場合はLv10まで解放
IF ABL:Ｖ感覚 >= 2 && IS処女(TARGET)
	PRINTW これ以上UPするには非処女である必要があります
	RETURN 0
ELSEIF ABL:Ｖ感覚 >= 5 && TALENT:セックス狂 == 0
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:Ｖ感覚 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要なＶ感覚の珠
A = 0
;必要なＶ経験
B = 0

;条件別にＯＫかダメかを記録する
;Ｖ感覚の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足）
I = 0

CALL CAN_ABLUP2

PRINTFORM [0] - %PALAMNAME:[[PALAM:快Ｖ]]%の珠×{JUEL:快Ｖ}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 
;Ｖ経験
PRINTFORML       %EXPNAME:[[EXP:Ｖ経験]]%　　{EXP:Ｖ経験}/{B}

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:Ｖ感覚 += 1

IF RESULT == 0
	JUEL:快Ｖ -= A
ENDIF

PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%のレベルが{ABL:Ｖ感覚}になりました

RETURN 0

;-------------------------------------------------
;Ｖ感覚のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP2
;オトコは却下
SIF IS_MALE(TARGET)
	RETURN 0
SIF ABL:Ｖ感覚 >= 3 && IS処女(TARGET)
	RETURN 0

;Ｖ感覚はLv5が上限、[セックス狂]が付いている場合はLv10まで解放
SIF ABL:Ｖ感覚 >= 5 && TALENT:セックス狂 == 0
	RETURN 0
SIF ABL:Ｖ感覚 >= 10
	RETURN 0

;判定変数を空に
A = 0
B = 0
I = 0

IF ABL:Ｖ感覚 == 0
	A = 1
	B = 0
ELSEIF ABL:Ｖ感覚 == 1
	A = 20
	B = 0
ELSEIF ABL:Ｖ感覚 == 2
	A = 400
	B = 15
ELSEIF ABL:Ｖ感覚 == 3
	A = 8000
	B = 50
ELSEIF ABL:Ｖ感覚 == 4
	A = 20000
	B = 100
ELSEIF ABL:Ｖ感覚 == 5
	A = 40000
	B = 150
ELSEIF ABL:Ｖ感覚 == 6
	A = 60000
	B = 200
ELSEIF ABL:Ｖ感覚 == 7
	A = 90000
	B = 250
ELSEIF ABL:Ｖ感覚 == 8
	A = 120000
	B = 350
ELSEIF ABL:Ｖ感覚 == 9
	A = 180000
	B = 500
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:Ｖ感覚 == 4
		TIMES A , 2.00
		TIMES B , 2.00
	ELSEIF ABL:Ｖ感覚 == 5
		TIMES A , 2.50
		TIMES B , 2.50
	ELSEIF ABL:Ｖ感覚 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
	ENDIF
ENDIF

;Ｖ鈍感
IF IS_Ｖ鈍感(TARGET)
	TIMES A , 1.20
	TIMES B , 1.10
ENDIF

;淫乱
IF TALENT:淫乱
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;色情狂
IF TALENT:絶倫
	TIMES A , 0.20
	TIMES B , 0.20
ENDIF
;セックス狂
IF TALENT:セックス狂
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;Ｖ敏感
IF IS_Ｖ敏感(TARGET)
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

IF TALENT:淫魔
	TIMES A , 0.01
	TIMES B , 0.1
ENDIF

;最低でも1個は必要
SIF A < 1
	A = 1
SIF B < 1 && ABL:Ｖ感覚 >= 3
	B = 1

;快Ｖの珠は足りている？
SIF JUEL:快Ｖ < A
	I |= 1
;性交経験は足りている？
SIF EXP:Ｖ経験 < B
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;