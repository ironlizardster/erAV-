;技巧のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;技巧のLvUP
;-------------------------------------------------
@ABLUP12
DRAWLINE
;PRINTL 奴隷の技巧を高めます
;PRINTL 技巧が高いと、性行為や奉仕の際に相手をイカせやすくなります
;DRAWLINE
;技巧はLv10が上限
IF ABL:技巧 >= 10
	PRINTW 既にMAXです
	RETURN 0
;技巧＋話術は１６以上にならない
;ただし[交渉上手]を除く
ELSEIF ABL:技巧 + ABL:話術 >= 15 && TALENT:交渉上手 == 0
	PRINTFORMW 技巧({ABL:技巧})＋話術({ABL:話術})の上限は15です
	RETURN 0
ENDIF

;必要な習得の珠
A = 0

;条件別にＯＫかダメかを記録する
;習得の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足）
I = 0

CALL CAN_ABLUP12

PRINTFORM [0] - %PALAMNAME:[[PALAM:習得]]%の珠×{JUEL:習得}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:技巧 += 1

IF RESULT == 0
	JUEL:習得 -= A
ENDIF

PRINTFORML %ABLNAME:[[ABL:技巧]]%のレベルが{ABL:技巧}になりました

RETURN 0

;-------------------------------------------------
;技巧のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP12
;技巧はLv10が上限
SIF ABL:技巧 >= 10
	RETURN 0
;技巧＋話術は１６以上にならない
SIF ABL:技巧 + ABL:話術 >= 15 && TALENT:交渉上手 == 0
	RETURN 0

;判定変数を空に
A = 0
I = 0

IF ABL:技巧 == 0
	A = 1
ELSEIF ABL:技巧 == 1
	A = 25
ELSEIF ABL:技巧 == 2
	A = 200
ELSEIF ABL:技巧 == 3
	A = 3000
ELSEIF ABL:技巧 == 4
	A = 8000
ELSEIF ABL:技巧 == 5
	A = 12000
ELSEIF ABL:技巧 == 6
	A = 16000
ELSEIF ABL:技巧 == 7
	A = 22000
ELSEIF ABL:技巧 == 8
	A = 28000
ELSEIF ABL:技巧 == 9
	A = 35000
ENDIF

;一線越えない
IF TALENT:一線越えない
	SIF ABL:技巧 == 3
		TIMES A , 1.50
	SIF ABL:技巧 == 4
		TIMES A , 2.00
	SIF ABL:技巧 == 5
		TIMES A , 2.50
	SIF ABL:技巧 >= 6
		TIMES A , 3.00
ENDIF

;素直
SIF TALENT:素直
	TIMES A , 0.95
;無関心
SIF TALENT:クール
	TIMES A , 1.05
;好奇心
SIF TALENT:好奇心
	TIMES A , 0.95
;保守的
SIF TALENT:保守的
	TIMES A , 1.10

;抑圧
IF IS_抑圧(TARGET)
	TIMES A , 1.10
;解放
ELSEIF IS_解放(TARGET)
	TIMES A , 0.90
ENDIF

;抵抗
SIF TALENT:抵抗
	TIMES A , 1.20

;恥じらい
IF IS_恥じらい(TARGET)
	TIMES A , 1.05
;恥薄い
ELSEIF IS_恥薄い(TARGET)
	TIMES A , 0.95
ENDIF

;習得早い
IF IS_習得早い(TARGET)
	TIMES A , 0.80
;習得遅い
ELSEIF IS_習得遅い(TARGET)
	TIMES A , 1.50
ENDIF

;舌使い
SIF TALENT:舌使い
	TIMES A , 0.95
;献身的
SIF TALENT:献身的
	TIMES A , 0.95
;汚れ無視
SIF TALENT:汚れ無視
	TIMES A , 0.95

IF TALENT:淫魔
	TIMES A , 0.01
ENDIF

;最低でも1個は必要
SIF A < 1
	A = 1

;習得の珠は足りている？
SIF JUEL:習得 < A
	I |= 1

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;