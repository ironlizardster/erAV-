;従順のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;従順のLvUP
;-------------------------------------------------
@ABLUP10
DRAWLINE
;PRINTL 奴隷の従順さを高めます
;PRINTL 従順さが高くなると命令に従いやすくなります
;DRAWLINE
;従順はLv5が上限
;[愛][妄信]が付いている場合は10まで上昇可能
;[セフレ]で7まで上げられます。ようわからんけど。
IF ABL:信頼 >= 5 && (TALENT:恋心 == 0 && TALENT:妄信 == 0 && TALENT:セフレ == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:信頼 >= 7 && (TALENT:恋心 == 0 && TALENT:妄信 == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:信頼 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要な恐怖の珠
A = 0
;必要な恭順の珠
B = 0
;必要な欲情の珠
C = 0
;必要な屈服の珠
D = 0
;必要な異常経験回数
E = 0

;条件別にＯＫかダメかを記録する
;恐怖の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足）
I = 0
;恭順の珠による可否（J=0:可、J&1:珠不足、J&2:経験不足）
J = 0
;欲情の珠による可否（K=0:可、K&1:珠不足、K&2:経験不足、K=256:そのレベルでは自動不可）
K = 0
;屈服の珠による可否（L=0:可、L&1:珠不足、L&2:経験不足、L=256:そのレベルでは自動不可）
L = 0

CALL CAN_ABLUP10

;異常経験が必要か
SIF E > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{E}以上(現在{EXP:異常経験})かつ

IF A > 0
	;恐怖の珠で上げる
	PRINTFORM [0] - %PALAMNAME:[[PALAM:恐怖]]%の珠×{JUEL:恐怖}/{A} ……
	IF I == 0
		PRINT ＯＫ
	ELSE
		SIF I & 1
			PRINT 珠不足 
		SIF I & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

;恭順の珠で上げる
PRINTFORM [1] - %PALAMNAME:[[PALAM:恭順]]%の珠×{JUEL:恭順}/{B} ……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
	SIF J & 2
		PRINT 経験不足 
ENDIF
PRINTL 

IF C > 0
	;欲情の珠で上げる
	PRINTFORM [2] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{C} ……
	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

IF D > 0
	;屈服の珠で上げる
	PRINTFORM [3] - %PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{D} ……
	IF L == 0
		PRINT ＯＫ
	ELSE
		SIF L & 1
			PRINT 珠不足 
		SIF L & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF K == 256 && RESULT == 2
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL 条件を満たしていません
	RESTART
ELSEIF L == 256 && RESULT == 3
	RESTART
ELSEIF L != 0 && RESULT == 3
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:信頼 += 1

IF RESULT == 0
	JUEL:恐怖 -= A
ELSEIF RESULT == 1
	JUEL:恭順 -= B
ELSEIF RESULT == 2
	JUEL:欲情 -= C
ELSEIF RESULT == 3
	JUEL:屈服 -= D
ENDIF

PRINTFORML %ABLNAME:[[ABL:信頼]]%のレベルが{ABL:信頼}になりました

RETURN 0

;-------------------------------------------------
;従順のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP10
;従順はLv5が上限、[愛][妄信]が付いている場合はLv10まで解放
SIF ABL:信頼 >= 5 && (TALENT:恋心 == 0 && TALENT:妄信 == 0 && TALENT:セフレ == 0)
	RETURN 0
SIF ABL:信頼 >= 7 && (TALENT:恋心 == 0 && TALENT:妄信 == 0)
	RETURN 0
SIF ABL:信頼 >= 10
	RETURN 0

;判定変数を空に
I = 0
J = 0
K = 0
L = 0

A = 0
B = 0
C = 0
D = 0
E = 0

;不可は-10に変更(TIMESの関係)
IF ABL:信頼 == 0
	A = 10
	B = 10
	C = 300
	D = 200
ELSEIF ABL:信頼 == 1
	A = 150
	B = 100
	C = 1000
	D = 1200
ELSEIF ABL:信頼 == 2
	A = 1000
	B = 800
	C = 2000
	D = 3000
ELSEIF ABL:信頼 == 3
	A = 3000
	B = 3000
	C = -10
	D = 12000
ELSEIF ABL:信頼 == 4
	A = 8000
	B = 5000
	C = -10
	D = -10
ELSEIF ABL:信頼 == 5
	A = 12000
	B = 10000
	C = -10
	D = -10
ELSEIF ABL:信頼 == 6
	A = 25000
	B = 20000
	C = -10
	D = -10
ELSEIF ABL:信頼 == 7
	A = -10
	B = 40000
	C = -10
	D = -10
ELSEIF ABL:信頼 == 8
	A = -10
	B = 80000
	C = -10
	D = -10
ELSEIF ABL:信頼 == 9
	A = -10
	B = 150000
	C = -10
	D = -10
ENDIF

;ＬＶ４から５に上げるときは異常経験必要
;（素質：[臆病][素直][即落ち][淫乱][愛][妄信]なら無視できる）
IF ABL:信頼 == 4 && (TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:淫乱 == 0 && TALENT:即落ち == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0)
	E = 1
;ＬＶ７から８に上げるときは異常経験が２以上必要
;（素質：[臆病][素直][即落ち][淫乱][愛][妄信]なら無視できる）
ELSEIF ABL:信頼 == 7 && (TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:淫乱 == 0 && TALENT:即落ち == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0)
	E = 2
ENDIF

;臆病
IF TALENT:臆病
	TIMES A , 0.50
	TIMES B , 0.90
	TIMES D , 0.90
ENDIF
;反抗的
IF TALENT:ナマイキ
	TIMES A , 2.00
	TIMES B , 1.50
	TIMES C , 1.20
	TIMES D , 1.50
ENDIF
;気丈
IF TALENT:気丈
	TIMES A , 3.00
	TIMES B , 1.50
	TIMES C , 1.20
	TIMES D , 1.50
ENDIF
;素直
IF TALENT:素直
	TIMES B , 0.80
	TIMES D , 0.90
ENDIF
;生意気
IF TALENT:活発
	TIMES A , 1.20
	TIMES B , 1.50
	TIMES D , 1.20
ENDIF
;プライド高い
IF TALENT:プライド高い
	TIMES A , 1.20
	TIMES B , 1.50
	TIMES D , 2.00
;プライド低い
ELSEIF TALENT:プライド低い
	TIMES B , 0.80
	TIMES D , 0.80
ENDIF

;抑圧
IF TALENT:抑圧
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 2.00
	TIMES D , 1.20
;解放
ELSEIF TALENT:解放
	TIMES C , 0.50
ENDIF

;抵抗
IF TALENT:抵抗
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF

;淫乱
SIF TALENT:淫乱
	TIMES C , 0.50
;愛
SIF TALENT:恋心
	TIMES B , 0.75
;妹
SIF TALENT:妹
	TIMES B , 0.80
;幼馴染み
SIF TALENT:幼馴染み
	TIMES B , 0.80
;姉
SIF TALENT:姉
	TIMES B , 0.80
;大和撫子
SIF TALENT:大和撫子
	TIMES B , 0.90
;妄信
SIF TALENT:妄信
	TIMES B , 0.20
;永遠の愛
SIF TALENT:永遠の愛
	TIMES B , 0.20
;メイド
SIF TALENT:アンドロイド
	TIMES B , 0.20

;女神
SIF TALENT:天使
	TIMES B , 0.10
;黒ギャル
SIF TALENT:天使
	TIMES B , 1.20

;嫉妬
IF TALENT:嫉妬
	TIMES B , 5.00
	TIMES C , 0.80
	TIMES D , 2.00
ENDIF

;最低でも1個は必要
SIF B < 1
	B = 1

;恐怖の珠は必要か？
IF A > 0
	;恐怖の珠は足りている？
	SIF JUEL:恐怖 < A
	I |= 1
ELSE
	I = 256
ENDIF

;恭順の珠は足りている？
SIF JUEL:恭順 < B
	J |= 1

;欲情の珠は必要か？
IF C > 0
	;欲情の珠は足りている？
	SIF JUEL:欲情 < C
		K |= 1
ELSE
	K = 256
ENDIF

;屈服の珠は足りている？
IF D > 0
	;屈服の珠で上げる
	SIF JUEL:屈服 < D
		L |= 1
ELSE
	L = 256
ENDIF

;異常経験は足りている？
IF E > EXP:異常経験
	I |= 2
	J |= 2
	K |= 2
	L |= 2
ENDIF

IF I == 0 || J == 0 || K == 0 || L ==0
	RETURN 1,I,J,K,L
ELSE
	RETURN 0
ENDIF
