;Ａ感覚のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;Ａ感覚のLvUP
;-------------------------------------------------
@ABLUP3
DRAWLINE
;PRINTL アナルの感度をUPします
;PRINTL Ａ感覚が高まると、アナルセックスやアナル責めからより大きな快楽が得られます
;DRAWLINE
;最大Lvは5、[尻穴狂い]が付いている場合はLv10まで解放
IF ABL:Ａ感覚 >= 5 && TALENT:尻穴狂い == 0
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:Ａ感覚 >= 3 && TALENT:Ａ処女 == 1
	PRINTW これ以上UPするにはアナル非処女である必要があります
	RETURN 0
ELSEIF ABL:Ａ感覚 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要なＡ感覚の珠
A = 0
;必要なＡ経験回数
B = 0


;条件別にＯＫかダメかを記録する
;Ａ感覚の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足）
I = 0

CALL CAN_ABLUP3

PRINTFORM [0] - %PALAMNAME:[[PALAM:快Ａ]]%の珠×{JUEL:快Ａ}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 
PRINTFORML       %EXPNAME:[[EXP:Ａ経験]]%　　{EXP:Ａ経験}/{B}
PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:Ａ感覚 += 1

IF RESULT == 0
	JUEL:快Ａ -= A
ENDIF

PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%のレベルが{ABL:Ａ感覚}になりました

RETURN 0

;-------------------------------------------------
;Ａ感覚のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP3
;Ａ感覚はLv5が上限、[尻穴狂い]が付いている場合はLv10まで解放
SIF ABL:Ａ感覚 >= 5 && TALENT:尻穴狂い == 0
	RETURN 0
SIF ABL:Ａ感覚 >= 3 && TALENT:Ａ処女 == 1
	RETURN 0
SIF ABL:Ａ感覚 >= 10
	RETURN 0

;判定変数を空に
A = 0
B = 0
I = 0

IF ABL:Ａ感覚 == 0
	A = 1
	B = 2
ELSEIF ABL:Ａ感覚 == 1
	A = 20
	B = 10
ELSEIF ABL:Ａ感覚 == 2
	A = 400
	B = 30
ELSEIF ABL:Ａ感覚 == 3
	A = 8000
	B = 75
ELSEIF ABL:Ａ感覚 == 4
	A = 20000
	B = 150
ELSEIF ABL:Ａ感覚 == 5
	A = 40000
	B = 180
ELSEIF ABL:Ａ感覚 == 6
	A = 60000
	B = 250
ELSEIF ABL:Ａ感覚 == 7
	A = 90000
	B = 350
ELSEIF ABL:Ａ感覚 == 8
	A = 120000
	B = 500
ELSEIF ABL:Ａ感覚 == 9
	A = 180000
	B = 600
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:Ａ感覚 == 4
		TIMES A , 2.00
		TIMES B , 2.00
	ELSEIF ABL:Ａ感覚 == 5
		TIMES A , 2.50
		TIMES B , 2.50
	ELSEIF ABL:Ａ感覚 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
	ENDIF
ENDIF

;A鈍感
IF TALENT:Ａ鈍感
	TIMES A , 1.20
	TIMES B , 1.10
ENDIF

;淫乱
IF TALENT:淫乱
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

;色情狂
IF TALENT:絶倫
	TIMES A , 0.20
	TIMES B , 0.20
ENDIF

;尻穴狂い
IF TALENT:尻穴狂い
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;A敏感
IF TALENT:Ａ敏感
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
IF TALENT:淫魔
	TIMES A , 0.01
	TIMES B , 0.1
ENDIF

;最低でも1個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1

;快Ａの珠は足りている？
SIF JUEL:快Ａ < A
	I |= 1
;Ａ経験は足りているか？
SIF EXP:Ａ経験 < B
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;
;
;