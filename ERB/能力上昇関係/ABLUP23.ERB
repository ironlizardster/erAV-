;ＢＬっ気のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;ＢＬっ気のLvUP
;-------------------------------------------------
@ABLUP23
;オトコでないととダメ
SIF HAVE_VAGINA(TARGET)
	RETURN 0
DRAWLINE
;PRINTL 奴隷のＢＬっ気を高めます
;PRINTL ＢＬっ気が高いと、男同士の性行為や奉仕からより大きな快楽を得られるようになります
;DRAWLINE
;ＢＬっ気はLv5が上限
;[解放][倒錯的][両刀][狂気]が付いている場合はLv10まで解放
IF ABL:ＢＬっ気 >= 5 && (!IS_解放(TARGET) && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:狂気 == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:ＢＬっ気 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要な欲情の珠
A = 0
;必要なＢＬ経験
B = 0
;必要な屈服の珠
C = 0
;必要な快Ａの珠
D = 0
;必要な異常経験回数
E = 0

;条件別にＯＫかダメかを記録する
;欲情＋屈服の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
I = 0
;快Ｃの珠による可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
J = 0

CALL CAN_ABLUP23

;ＬＶ３から４、４から５に上げるときは異常経験必要
SIF E > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{E}以上(現在{EXP:異常経験})かつ

;欲情＋屈服の珠で上げる場合
PRINTFORM [0] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
SIF C > 0
	PRINTFORML 　　　%PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{C}
;ＢＬ経験
PRINTFORML 　　　%EXPNAME:[[EXP:ＢＬ経験]]%　{EXP:ＢＬ経験}/{B}

;快Ａの珠で上げる場合
IF D > 0
	PRINTFORM [1] - %PALAMNAME:[[PALAM:快Ａ]]%の珠×{JUEL:快Ａ}/{D} ……
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
	;ＢＬ経験
	PRINTFORML 　　　%EXPNAME:[[EXP:ＢＬ経験]]%　{EXP:ＢＬ経験}/{B}
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF J == 256 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:ＢＬっ気 += 1

IF RESULT == 0
	JUEL:欲情 -= A
	JUEL:屈服 -= C
ELSEIF RESULT == 1
	JUEL:快Ａ -= D
ENDIF

PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%のレベルが{ABL:ＢＬっ気}になりました

RETURN 0

;-------------------------------------------------
;ＢＬっ気のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP23
;オトコでないとダメ
SIF HAVE_VAGINA(TARGET)
	RETURN 0
;ＢＬっ気はLv5が上限
;[解放][倒錯的][両刀][狂気]が付いている場合はLv10まで解放
SIF ABL:ＢＬっ気 >= 5 && (!IS_解放(TARGET) && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:狂気 == 0)
	RETURN 0
SIF ABL:ＢＬっ気 >= 10
	RETURN 0

;判定変数を空に
I = 0
J = 0

A = 0
B = 0
C = 0
D = 0
E = 0

IF ABL:ＢＬっ気 == 0
	A = 200
	B = 50
	C = 0
	D = 1000
ELSEIF ABL:ＢＬっ気 == 1
	A = 1000
	B = 150
	C = 0
	D = 5000
ELSEIF ABL:ＢＬっ気 == 2
	A = 3000
	B = 300
	C = 1000
	D = 0
ELSEIF ABL:ＢＬっ気 == 3
	A = 8000
	B = 500
	C = 2000
	D = 0
ELSEIF ABL:ＢＬっ気 == 4
	A = 20000
	B = 800
	C = 5000
	D = 0
ELSEIF ABL:ＢＬっ気 == 5
	A = 40000
	B = 1200
	C = 10000
	D = 0
ELSEIF ABL:ＢＬっ気 == 6
	A = 80000
	B = 1800
	C = 13000
	D = 0
ELSEIF ABL:ＢＬっ気 == 7
	A = 150000
	B = 2600
	C = 18000
	D = 0
ELSEIF ABL:ＢＬっ気 == 8
	A = 200000
	B = 3600
	C = 30000
	D = 0
ELSEIF ABL:ＢＬっ気 == 9
	A = 300000
	B = 5000
	C = 50000
	D = 0
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:ＢＬっ気 == 3
		TIMES A , 1.50
		TIMES B , 1.50
		TIMES C , 1.50
	ELSEIF ABL:ＢＬっ気 == 4
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
	ELSEIF ABL:ＢＬっ気 == 5
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES C , 2.50
	ELSEIF ABL:ＢＬっ気 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
	ENDIF
ENDIF

;ＬＶ３以上に上げるときは異常経験必要（素質：[解放][倒錯的][両刀][狂気]なら無視できる）
SIF ABL:ＢＬっ気 >= 3 && (!IS_解放(TARGET) && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:狂気 == 0)
	E = ABL:ＢＬっ気 - 2

;素直
IF TALENT:素直
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
	TIMES D , 0.95
ENDIF
;無関心
IF TALENT:クール
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
	TIMES D , 1.20
ENDIF
;好奇心
IF TALENT:好奇心
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
	TIMES D , 0.95
ENDIF
;保守的
IF TALENT:保守的
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
	TIMES D , 1.20
ENDIF

;貞操観念
IF IS_貞操観念(TARGET)
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
	TIMES D , 1.20
;貞操無頓着
ELSEIF IS_貞操無頓着(TARGET)
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
	TIMES D , 0.95
ENDIF

;男嫌い
IF TALENT:男性不信
	TIMES A , 3.00
	TIMES B , 3.00
	TIMES C , 3.00
	TIMES D , 3.00
ENDIF
;献身的
IF TALENT:献身的
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
	TIMES D , 0.95
ENDIF

;快感に素直
IF IS_快感に素直(TARGET)
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
	TIMES D , 0.95
;快感の否定
ELSEIF IS_快感の否定(TARGET)
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
	TIMES D , 1.20
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
ENDIF
;両刀
IF TALENT:両刀
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
ENDIF
;狂気
IF TALENT:狂気
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
ENDIF

;最低でも１回・１個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1

;異常経験が不足
IF EXP:異常経験 < E
	I |= 2
	J |= 2
ENDIF

;欲情の珠が不足
SIF JUEL:欲情 < A
	I |= 1
;屈服の珠が不足
SIF JUEL:屈服 < C
	I |= 1
;ＢＬ経験が不足
SIF EXP:ＢＬ経験 < B
	I |= 2

;Ａ感覚で上げる場合
IF D > 0
	;快Ａの珠が不足
	SIF JUEL:快Ａ < D
		J |= 1
	;ＢＬ経験が不足
	SIF EXP:ＢＬ経験 < B
		J |= 2
ELSE
	J = 256
ENDIF

IF I == 0 || J == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;