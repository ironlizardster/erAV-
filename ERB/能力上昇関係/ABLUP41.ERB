;触手中毒のLvUP処理とその可否判定
;eraIm@s_ver.0.17βdのスクリプトをベースに処理を簡略化

;-------------------------------------------------
;触手中毒のLvUP
;-------------------------------------------------
@ABLUP41
DRAWLINE
;PRINTL 奴隷の触手への執着心を高めます
;PRINTL 触手中毒が高くなると、触手とのセックスや奉仕行為への満足感が深まり、
;PRINTL 頻繁に触手相手に性行為をしていないと心が落ち着かなくなります
;DRAWLINE
;;触手中毒はLv5が上限
;;[苗床]が付いている場合はLv10まで解放
IF ABL:触手中毒 >= 5 && TALENT:苗床 == 0
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:触手中毒 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要な欲情の珠
A = 0
;必要な屈服の珠
B = 0
;必要な触手経験
C = 0
;必要な異常経験回数
F = 0

CALL CAN_ABLUP41

;上げるときは異常経験必要（素質：[淫乱][中毒しやすい][苗床]なら無視できる）
SIF F > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{F}以上(現在{EXP:異常経験})かつ

;欲望が必要。苗床の場合は信頼
IF TALENT:苗床
	PRINTFORML %ABLNAME:[[ABL:信頼]]%{ABL:触手中毒 + 1}LV以上(現在{ABL:信頼}LV)かつ
ELSE
	PRINTFORML %ABLNAME:[[ABL:欲望]]%{ABL:触手中毒 + 1}LV以上(現在{ABL:欲望}LV)かつ
ENDIF
PRINTFORM [0] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{B}
PRINTFORML 　　　%EXPNAME:[[EXP:触手経験]]%　{EXP:触手経験}/{C}
PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:触手中毒 += 1
IF RESULT == 0
	JUEL:欲情 -= A
	JUEL:屈服 -= B
ENDIF

PRINTFORML %ABLNAME:[[ABL:触手中毒]]%のレベルが{ABL:触手中毒}になりました

RETURN 0

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@CAN_ABLUP41
;触手中毒はLv5が上限
;[苗床]が付いている場合はLv10まで解放
SIF ABL:触手中毒 == 10
	RETURN 0
SIF ABL:触手中毒 >= 5 && TALENT:苗床 == 0
	RETURN 0

;条件別にＯＫかダメかを記録する
I = 0

IF ABL:触手中毒 == 0
	A = 2000
	B = 2000
	C = 30
ELSEIF ABL:触手中毒 == 1
	A = 5000
	B = 5000
	C = 100
ELSEIF ABL:触手中毒 == 2
	A = 10000
	B = 10000
	C = 220
ELSEIF ABL:触手中毒 == 3
	A = 20000
	B = 20000
	C = 400
ELSEIF ABL:触手中毒 == 4
	A = 30000
	B = 30000
	C = 800
ELSEIF ABL:触手中毒 == 5
	A = 45000
	B = 45000
	C = 1600
ELSEIF ABL:触手中毒 == 6
	A = 75000
	B = 75000
	C = 2000
ELSEIF ABL:触手中毒 == 7
	A = 100000
	B = 100000
	C = 2800
ELSEIF ABL:触手中毒 == 8
	A = 200000
	B = 200000
	C = 4000
ELSEIF ABL:触手中毒 == 9
	A = 300000
	B = 300000
	C = 6000
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:売春中毒 == 3
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
	ELSEIF ABL:売春中毒 == 4
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES C , 2.50
	ELSEIF ABL:売春中毒 >= 5
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
	ENDIF
ENDIF

F = 0
;２以上に上げるときは異常経験必要（素質：[中毒しやすい][淫乱][苗床]なら無視できる）
SIF ABL:触手中毒 >= 2 && (TALENT:中毒しやすい == 0 && TALENT:淫乱 == 0 && TALENT:苗床 == 0)
		F = ABL:触手中毒 + 1

;自制心
IF TALENT:自制心
	TIMES A , 2.50
	TIMES B , 2.50
	TIMES C , 1.50
ENDIF
;快感に素直
IF TALENT:快感に素直
	TIMES A , 0.75
	TIMES B , 0.75
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES A , 1.75
	TIMES B , 1.75
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
ENDIF
;狂気
IF TALENT:狂気
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;動物耳
IF TALENT:動物耳
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF
;苗床
IF TALENT:苗床
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF

;最低でも１回・１個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1

;苗床の場合は信頼で判定
IF TALENT:苗床
	;信頼が必要
	SIF ABL:信頼 < ABL:触手中毒 + 1
		I |= 4
	;欲情の珠は足りている？
	SIF JUEL:欲情 < A
		I |= 1
	;屈服の珠は足りている？
	SIF JUEL:屈服 < B
		I |= 1
ELSE
	;欲望が必要
	SIF ABL:欲望 < ABL:触手中毒 + 1
		I |= 4
	;欲情の珠は足りている？
	SIF JUEL:欲情 < A
		I |= 1
	;屈服の珠は足りている？
	SIF JUEL:屈服 < B
		I |= 1
ENDIF
;触手経験は足りている？
SIF EXP:触手経験 < C
	I |= 2
;異常経験は足りている？
SIF EXP:異常経験 < F
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;
;
;