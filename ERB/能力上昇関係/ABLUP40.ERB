;獣姦中毒のLvUP処理とその可否判定
;eraIm@s_ver.0.17βdのスクリプトをベースに処理を簡略化

;-------------------------------------------------
;獣姦中毒のLvUP
;-------------------------------------------------
@ABLUP40
DRAWLINE
;PRINTL 奴隷の獣姦への執着心を高めます
;PRINTL 獣姦中毒が高くなると、動物とのセックスや奉仕行為への満足感が深まり、
;PRINTL 頻繁に動物相手に性行為をしていないと心が落ち着かなくなります
;DRAWLINE
;獣姦中毒はLv5が上限
;[動物好き][牝犬]が付いている場合はLv10まで解放
IF ABL:獣姦中毒 >= 5 && (TALENT:動物好き == 0 && TALENT:牝犬 == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:獣姦中毒 >= 10
	PRINTW 既にMAXです
	RETURN 0
;精液中毒＋レズ中毒＋獣姦中毒は11以上にならない
;例外多数。ABLUP33参照。
ELSEIF ABL:精液中毒 + ABL:レズ中毒 + ABL:獣姦中毒 >= 10 && TALENT:精愛味覚 == 0 && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:男性不信 == 0 && TALENT:動物好き == 0 && TALENT:牝犬 == 0
	PRINTFORMW 精液中毒({ABL:精液中毒})＋レズ中毒({ABL:レズ中毒})＋獣姦中毒({ABL:獣姦中毒})の上限は10です
	RETURN 0
ENDIF

;必要な欲情の珠
A = 0
;必要な屈服の珠
B = 0
;必要な獣姦経験
C = 0
;必要な異常経験回数
F = 0

CALL CAN_ABLUP40

;上げるときは異常経験必要（素質：[淫乱][中毒しやすい][雌犬]なら無視できる）
SIF F > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{F}以上(現在{EXP:異常経験})かつ

;欲望が必要
IF TALENT:牝犬
	;雌犬の場合は無視する
	PRINTFORML %ABLNAME:[[ABL:信頼]]%{ABL:獣姦中毒 + 1}LV以上(現在{ABL:信頼}LV)かつ
ELSE
	PRINTFORML %ABLNAME:[[ABL:欲望]]%{ABL:獣姦中毒 + 1}LV以上(現在{ABL:欲望}LV)かつ
ENDIF
PRINTFORM [0] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{B}
PRINTFORML 　　　%EXPNAME:[[EXP:獣姦経験]]%　{EXP:獣姦経験}/{C}
PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:獣姦中毒 += 1
IF RESULT == 0
	JUEL:欲情 -= A
	JUEL:屈服 -= B
ENDIF

PRINTFORML %ABLNAME:[[ABL:獣姦中毒]]%のレベルが{ABL:獣姦中毒}になりました

RETURN 0

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@CAN_ABLUP40
;獣姦中毒はLv5が上限
;[動物好き][牝犬]が付いている場合はLv10まで解放
SIF ABL:獣姦中毒 == 10
	RETURN 0
SIF ABL:獣姦中毒 >= 5 && (TALENT:動物好き == 0 && TALENT:牝犬 == 0)
	RETURN 0
;精液中毒＋レズ中毒＋獣姦中毒は11以上にならない
SIF ABL:精液中毒 + ABL:レズ中毒 + ABL:獣姦中毒 >= 10 && TALENT:精愛味覚 == 0 && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:男性不信 == 0 && TALENT:動物好き == 0 && TALENT:牝犬 == 0
	RETURN 0

;条件別にＯＫかダメかを記録する
I = 0

IF ABL:獣姦中毒 == 0
	A = 2000
	B = 2000
	C = 30
ELSEIF ABL:獣姦中毒 == 1
	A = 5000
	B = 5000
	C = 100
ELSEIF ABL:獣姦中毒 == 2
	A = 10000
	B = 10000
	C = 220
ELSEIF ABL:獣姦中毒 == 3
	A = 20000
	B = 20000
	C = 400
ELSEIF ABL:獣姦中毒 == 4
	A = 30000
	B = 30000
	C = 800
ELSEIF ABL:獣姦中毒 == 5
	A = 45000
	B = 45000
	C = 1600
ELSEIF ABL:獣姦中毒 == 6
	A = 75000
	B = 75000
	C = 2000
ELSEIF ABL:獣姦中毒 == 7
	A = 100000
	B = 100000
	C = 2800
ELSEIF ABL:獣姦中毒 == 8
	A = 200000
	B = 200000
	C = 4000
ELSEIF ABL:獣姦中毒 == 9
	A = 300000
	B = 300000
	C = 6000
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:売春中毒 == 3
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
	ELSEIF ABL:売春中毒 == 4
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES C , 2.50
	ELSEIF ABL:売春中毒 >= 5
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
	ENDIF
ENDIF

F = 0
;２以上に上げるときは異常経験必要（素質：[中毒しやすい][淫乱][牝犬]なら無視できる）
SIF ABL:獣姦中毒 >= 2 && (TALENT:中毒しやすい == 0 && TALENT:淫乱 == 0 && TALENT:牝犬 == 0)
		F = ABL:獣姦中毒 + 1

;自制心
IF TALENT:自制心
	TIMES A , 2.50
	TIMES B , 2.50
	TIMES C , 1.50
ENDIF
;快感に素直
IF TALENT:快感に素直
	TIMES A , 0.75
	TIMES B , 0.75
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES A , 1.75
	TIMES B , 1.75
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
ENDIF
;狂気
IF TALENT:狂気
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;動物耳
IF TALENT:動物耳
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF
;牝犬
IF TALENT:牝犬
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;愛(心が主人に向いているので獣になびきにくい)
IF TALENT:恋心
	TIMES A , 1.80
	TIMES B , 1.80
	TIMES C , 1.50
ENDIF

;最低でも１回・１個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1

;雌犬の場合は従順で判定
IF TALENT:牝犬
	;従順が必要
	SIF ABL:信頼 < ABL:獣姦中毒 + 1
		I |= 4
	;欲情の珠は足りている？
	SIF JUEL:欲情 < A
		I |= 1
	;屈服の珠は足りている？
	SIF JUEL:屈服 < B
		I |= 1
ELSE
	;欲望が必要
	SIF ABL:欲望 < ABL:獣姦中毒 + 1
		I |= 4
	;欲情の珠は足りている？
	SIF JUEL:欲情 < A
		I |= 1
	;屈服の珠は足りている？
	SIF JUEL:屈服 < B
		I |= 1
ENDIF
;獣姦経験は足りている？
SIF EXP:獣姦経験 < C
	I |= 2
;異常経験は足りている？
SIF EXP:異常経験 < F
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;
;
;