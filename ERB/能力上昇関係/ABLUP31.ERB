;自慰中毒のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化
;eraIm@s_ver.0.17βdのスクリプトを参考に修正

;-------------------------------------------------
;自慰中毒のLvUP
;-------------------------------------------------
@ABLUP31
DRAWLINE
;PRINTL 奴隷の自慰への執着心を高めます
;PRINTL 自慰中毒が高くなると、自慰行為への満足感が深まり、
;PRINTL 常に自慰をしていないと心が落ち着かなくなります
;DRAWLINE
;自慰中毒はLv5が上限
;[愛][淫乱][自慰しやすい][快感に素直][自慰狂い][乳狂い]が付いている場合はLv10まで解放
IF ABL:自慰中毒 >= 5 && (TALENT:恋心 == 0 && TALENT:淫乱 == 0 && TALENT:自慰しやすい == 0 && !IS_快感に素直(TARGET) && TALENT:自慰狂い == 0 && TALENT:乳狂い == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:自慰中毒 >= 10
	PRINTW 既にMAXです
	RETURN 0
;性交中毒＋自慰中毒は11以上にならない
ELSEIF ABL:性交中毒 + ABL:自慰中毒 >= 15 && TALENT:淫魔 == 0 && TALENT:天使 == 0
	PRINTFORMW 性交中毒({ABL:性交中毒})＋自慰中毒({ABL:自慰中毒})の上限は15です
	RETURN 0
ENDIF

;必要な欲情の珠
A = 0
;必要な快Ｃの珠
B = 0
;必要な恥情の珠
C = 0
;必要な自慰経験回数
D = 0
;必要な調教自慰経験回数
E = 0
;必要な異常経験回数
F = 0

;条件別にＯＫかダメかを記録する
;自慰経験で上げる場合の可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
I = 0
;調教自慰経験で上げる場合の可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
J = 0

CALL CAN_ABLUP31

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[解放][自慰しやすい][中毒しやすい][淫乱化]なら無視できる）
SIF F > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{F}以上(現在{EXP:異常経験})かつ

;露出癖が必要
PRINTFORML %ABLNAME:[[ABL:露出癖]]%{ABL:自慰中毒 + 1}LV以上(現在{ABL:露出癖}LV)かつ

;Ｃ感覚が必要
PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%{ABL:自慰中毒 + 1}LV以上(現在{ABL:Ｃ感覚}LV)かつ

;自慰経験で上げる場合
PRINTFORM [0] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:快Ｃ]]%の珠×{JUEL:快Ｃ}/{B}
PRINTFORML 　　　%PALAMNAME:[[PALAM:恥情]]%の珠×{JUEL:恥情}/{C}
PRINTFORML 　　　%EXPNAME:[[EXP:自慰経験]]%　{EXP:自慰経験}/{D}

;調教自慰経験で上げる場合
PRINTFORM [1] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
	SIF J & 2
		PRINT 経験不足 
	SIF J & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:快Ｃ]]%の珠×{JUEL:快Ｃ}/{B}
PRINTFORML 　　　%PALAMNAME:[[PALAM:恥情]]%の珠×{JUEL:恥情}/{C}
PRINTFORML 　　　%EXPNAME:[[EXP:調教自慰経験]]%　{EXP:調教自慰経験}/{E}

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:自慰中毒 += 1

IF RESULT == 0 || RESULT == 1
	JUEL:欲情 -= A
	JUEL:快Ｃ -= B
	JUEL:恥情 -= C
ENDIF

PRINTFORML %ABLNAME:[[ABL:自慰中毒]]%のレベルが{ABL:自慰中毒}になりました

RETURN 0

;-------------------------------------------------
;自慰中毒のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP31
;自慰中毒はLv5が上限
;[愛][淫乱][自慰しやすい][快感に素直][自慰狂い][淫乳]が付いている場合はLv10まで解放
SIF ABL:自慰中毒 >= 10
	RETURN 0
SIF ABL:自慰中毒 >= 5 && (TALENT:恋心 == 0 && TALENT:淫乱 == 0 && TALENT:自慰しやすい == 0 && !IS_快感に素直(TARGET) && TALENT:自慰狂い == 0 && TALENT:乳狂い == 0)
	RETURN 0
;性交中毒LV＋自慰中毒LVは10が上限
SIF ABL:性交中毒 + ABL:自慰中毒 >= 15 && TALENT:淫魔 == 0 && TALENT:天使 == 0
	RETURN 0

;判定変数を空に
I = 0
J = 0

A = 0
B = 0
C = 0
D = 0
E = 0
F = 0

IF ABL:自慰中毒 == 0
	A = 3000
	B = 10000
	C = 1000
	D = 100
	E = 20
ELSEIF ABL:自慰中毒 == 1
	A = 6000
	B = 25000
	C = 3000
	D = 250
	E = 40
ELSEIF ABL:自慰中毒 == 2
	A = 12000
	B = 50000
	C = 6000
	D = 500
	E = 60
ELSEIF ABL:自慰中毒 == 3
	A = 20000
	B = 100000
	C = 15000
	D = 1000
	E = 100
ELSEIF ABL:自慰中毒 == 4
	A = 32000
	B = 200000
	C = 30000
	D = 1500
	E = 150
ELSEIF ABL:自慰中毒 == 5
	A = 50000
	B = 250000
	C = 40000
	D = 2000
	E = 200
ELSEIF ABL:自慰中毒 == 6
	A = 70000
	B = 320000
	C = 50000
	D = 3000
	E = 320
ELSEIF ABL:自慰中毒 == 7
	A = 100000
	B = 500000
	C = 70000
	D = 4000
	E = 500
ELSEIF ABL:自慰中毒 == 8
	A = 150000
	B = 800000
	C = 100000
	D = 6000
	E = 800
ELSEIF ABL:自慰中毒 == 9
	A = 200000
	B = 1000000
	C = 150000
	D = 8000
	E = 1000
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:自慰中毒 == 3
		TIMES A , 1.50
		TIMES B , 1.50
		TIMES C , 1.50
		TIMES D , 1.50
		TIMES E , 1.50
	ELSEIF ABL:自慰中毒 == 4
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
		TIMES D , 2.00
		TIMES E , 2.00
	ELSEIF ABL:自慰中毒 == 5
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES C , 2.50
		TIMES D , 2.50
		TIMES E , 2.50
	ELSEIF ABL:自慰中毒 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
		TIMES D , 3.00
		TIMES E , 3.00
	ENDIF
ENDIF

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[解放][自慰しやすい][淫乱][中毒しやすい][狂気]なら無視できる）
IF ABL:自慰中毒 == 2 && (!IS_解放(TARGET) && TALENT:自慰しやすい == 0 && TALENT:中毒しやすい == 0 && TALENT:淫乱 == 0 && TALENT:狂気 == 0)
	F = ABL:自慰中毒 - 1
	IF EXP:異常経験 < F
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
ENDIF

;自慰しやすい
IF TALENT:自慰しやすい
	TIMES A , 0.25
	TIMES B , 0.25
	TIMES C , 0.25
	TIMES D , 0.25
	TIMES E , 0.25
ENDIF

;中毒しやすい
IF TALENT:中毒しやすい
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
	TIMES E , 0.75
ENDIF

;淫乱化
IF TALENT:淫乱
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

IF TALENT:絶倫
	TIMES A , 0.10
	TIMES B , 0.10
	TIMES C , 0.10
	TIMES D , 0.10
	TIMES E , 0.10
ENDIF

IF TALENT:淫魔
	TIMES A , 0.01
	TIMES B , 0.01
	TIMES C , 0.01
	TIMES D , 0.01
	TIMES E , 0.01
ENDIF

;異常経験が不足
IF F > EXP:異常経験
	I |= 2
	J |= 2
ENDIF

;露出癖が自慰中毒＋１レベルでないと能力不足
IF ABL:露出癖 < ABL:自慰中毒 + 1
	I |= 4
	J |= 4
ENDIF

;Ｃ感覚が自慰中毒＋１レベルでないと能力不足
IF ABL:Ｃ感覚 < ABL:自慰中毒 + 1
	I |= 4
	J |= 4
ENDIF

;最低でも1回・1個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1
SIF D < 1
	D = 1
SIF E < 1
	E = 1

;自慰経験で上げる場合
;欲情の珠が不足
SIF JUEL:欲情 < A
	I |= 1
;快Ｃの珠が不足
SIF JUEL:快Ｃ < B
	I |= 1
;恥情の珠が不足
SIF JUEL:恥情 < C
	I |= 1
;自慰経験が不足
SIF EXP:自慰経験 < D
	I |= 2

;調教自慰経験で上げる場合
;欲情の珠で上げる
SIF JUEL:欲情 < A
	J |= 1
;快Ｃの珠で上げる
SIF JUEL:快Ｃ < B
	J |= 1
;恥情の珠で上げる
SIF JUEL:恥情 < C
	J |= 1
;調教自慰経験が必要
SIF EXP:調教自慰経験 < E
	J |= 2

IF I == 0 || J == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;