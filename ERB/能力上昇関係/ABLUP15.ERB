;話術のLvUP処理とその可否判定
;eraIm@s_ver.0.17βdのスクリプトをベースに処理を簡略化

;-------------------------------------------------
;話術のLvUP
;-------------------------------------------------
@ABLUP15
DRAWLINE
;PRINTL 奴隷の話術を高めます
;PRINTL 話術が高いと、娼館での営業や接待の成功率が高まります
;DRAWLINE
;話術はLv10が上限
IF ABL:話術 >= 10
	PRINTW 既にMAXです
	RETURN 0
;技巧＋話術は１６以上にならない
;ただし[交渉上手]を除く
ELSEIF ABL:技巧 + ABL:話術 >= 15 && TALENT:交渉上手 == 0
	PRINTFORMW 技巧({ABL:技巧})＋話術({ABL:話術})の上限は15です
	RETURN 0
ENDIF


;条件別にＯＫかダメかを記録する
;必要な習得の珠
A = 0
;必要な調教会話経験
B = 0
;必要な売春経験
C = 0

;条件別にＯＫかダメかを記録する
;習得の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足）
I = 0

CALL CAN_ABLUP15

PRINTFORM [0] - %PALAMNAME:[[PALAM:習得]]%の珠×{JUEL:習得}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 
PRINTFORML       %EXPNAME:[[EXP:会話経験]]%　{EXP:会話経験}/{B} or
PRINTFORML       %EXPNAME:[[EXP:売春経験]]%　{EXP:売春経験}/{C}

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:話術 += 1

IF RESULT == 0
	JUEL:習得 -= A
ENDIF

PRINTFORML %ABLNAME:[[ABL:話術]]%のレベルが{ABL:話術}になりました

RETURN 0

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@CAN_ABLUP15
;話術はLv10が上限
SIF ABL:話術 >= 10
	RETURN 0
SIF ABL:技巧 + ABL:話術 >= 15 && TALENT:交渉上手 == 0
	RETURN 0

;条件別にＯＫかダメかを記録する
A = 0
B = 0
C = 0
I = 0

IF ABL:話術 == 0
	A = 1
	B = 1
	C = 5
ELSEIF ABL:話術 == 1
	A = 10
	B = 5
	C = 20
ELSEIF ABL:話術 == 2
	A = 100
	B = 15
	C = 50
ELSEIF ABL:話術 == 3
	A = 1500
	B = 40
	C = 100
ELSEIF ABL:話術 == 4
	A = 3000
	B = 80
	C = 150
ELSEIF ABL:話術 == 5
	A = 4000
	B = 120
	C = 180
ELSEIF ABL:話術 == 6
	A = 5200
	B = 150
	C = 250
ELSEIF ABL:話術 == 7
	A = 7500
	B = 180
	C = 320
ELSEIF ABL:話術 == 8
	A = 9000
	B = 220
	C = 350
ELSEIF ABL:話術 == 9
	A = 13000
	B = 250
	C = 400
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:話術 == 3
		TIMES A , 1.50
		TIMES C , 1.25
	ELSEIF ABL:話術 == 4
		TIMES A , 2.00
		TIMES C , 1.50
	ELSEIF ABL:話術 == 5
		TIMES A , 2.50
		TIMES C , 1.75
	ELSEIF ABL:話術 >= 6
		TIMES A , 3.00
		TIMES C , 2.00
	ENDIF
ENDIF

;反抗的
IF TALENT:ナマイキ
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;素直
IF TALENT:素直
	TIMES A , 0.90
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;生意気
IF TALENT:活発
	TIMES A , 1.25
	TIMES B , 1.15
	TIMES C , 1.15
ENDIF
;プライド高い
IF TALENT:プライド高い
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF
;無関心
IF TALENT:クール
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;感情乏しい
IF TALENT:感情乏しい
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;好奇心
IF TALENT:好奇心
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;楽観的
IF TALENT:ポジティブ
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
;悲観的
ELSEIF TALENT:ネガティブ
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;目立ちたがり
IF TALENT:目立ちたがり
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;抑圧
IF TALENT:抑圧
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
;解放
ELSEIF TALENT:解放
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

;抵抗
IF TALENT:抵抗
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;恥じらい
IF TALENT:恥じらい
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
;恥薄い
ELSEIF TALENT:恥薄い
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;習得早い
IF TALENT:素質
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
;習得遅い
ELSEIF TALENT:習得遅い
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;謎の魅力
IF TALENT:謎の魅力
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF
;小悪魔
IF TALENT:小悪魔
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;饒舌
IF TALENT:饒舌
	TIMES A , 0.60
	TIMES B , 0.60
	TIMES C , 0.80
ENDIF

;目立ちたがり
IF TALENT:優等生
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF

;最低でも1回・1個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1

;習得の珠は足りている？
SIF JUEL:習得 < A
	I |= 1
;調教会話経験か売春経験は足りている？
SIF EXP:会話経験 < B && EXP:売春経験 < C
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;