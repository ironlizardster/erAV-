;---------------------------------------------------------
;一日の終了時、営業させていた場合その結果を表示します
;---------------------------------------------------------
@YUUKAKU_RESULT
SIF TIME != 0
	CALL WORKING_RESULT

;---------------------------------------------------------
;営業している奴隷を探します
;---------------------------------------------------------
@WORKING_RESULT

DRAWLINE

REPEAT CHARANUM
	;営業を行っていない人(CFLAG:12が0の人)はスルー
	SIF CFLAG:COUNT:営業内容 == 0
		CONTINUE
	TARGET = COUNT
	CALL WORKING_MAIN
	COUNT = TARGET
REND

IF (FLAG:指導テキスト設定 & 2)
	PRINTFORML  ☆現在の娼館人気:{EXP:MASTER:人気}☆
	DRAWLINE
ENDIF

;---------------------------------------------------------
;営業の計算の根幹です
;---------------------------------------------------------
@WORKING_MAIN

;Aは様々な変数に用いられます。HAPPYから、MONEYの間は、顧客の満足度として機能しますので、この間は操作してはいけません
A = 0
;Bは様々な変数に用いられます
B = 0
;Cは常連客かどうかのチェックに使われます
C = 0
;Dは満足度の数値になります
D = 0
;EはV、A経験があるかのチェックに使われます。&1でＡ&2でＶ&4で処女喪失
E = 0
;Fは体力ダウンの基準値として使われます
F = 0
;Gはやってきたお客さんの数として使われます
G = 0
;HはWORK_S_～～等の外部変数で失敗・成功の判定値として使用
;I,Jはその他の一時変数(人気の変動等)に使用
I = 0
J = 0
;L は特殊営業から呼び出される際に、参加するメンバーの数として使用
;Nは数字表示用の関数@FIGURE_INDENT（TRAIN_MAIN.ERBに記述）の引数として使用
N = 0
;変数T:XXは調教中に獲得した経験の記録に使われています
REPEAT 100
	TCVAR:COUNT = 0
REND
;変数U:XXは調教中に獲得した珠の記録に使われています
REPEAT 101
	U:COUNT = 0
REND

IF (FLAG:指導テキスト設定 & 2) == 0
	PRINTFORML %NAME:TARGET%の営業結果：
ELSE
	PRINTFORM %NAME:TARGET%:
ENDIF

;ここから各営業内容の関数へ飛びます
;会話営業の場合
IF CFLAG:営業内容 == 1 || (CAN_GET_PREGNANT(TARGET) && CFLAG:労役妊娠回避 && TALENT:Ａ処女 && !CFLAG:初キス相手)
	CALL WORK_1ST

;奉仕営業の場合
ELSEIF CFLAG:営業内容 == 2 || CFLAG:営業内容 == 7 || (CAN_GET_PREGNANT(TARGET) && CFLAG:労役妊娠回避 && TALENT:Ａ処女)
	CALL WORK_2ND

;Ａ営業の場合
ELSEIF CFLAG:営業内容 == 3 || (CAN_GET_PREGNANT(TARGET) && CFLAG:労役妊娠回避 && !TALENT:Ａ処女)
	CALL WORK_3RD

;Ｖ営業の場合
ELSEIF CFLAG:営業内容 == 4
	CALL WORK_4TH

;ＳＭ営業の場合
ELSEIF CFLAG:営業内容 == 5
	CALL WORK_5TH

;出張営業の場合
ELSEIF CFLAG:営業内容 == 6
	CALL WORK_6TH

ENDIF

SIF (FLAG:指導テキスト設定 & 2) == 0
	DRAWLINE
CALL WORK_MONEY
SIF (FLAG:指導テキスト設定 & 2) == 0
	DRAWLINE
CALL WORK_EXP
SIF (FLAG:指導テキスト設定 & 2) == 0
	DRAWLINE
CALL WORK_DOWN
SIF (FLAG:指導テキスト設定 & 2) == 0
	DRAWLINE
CALL WORK_BASE

;死亡
IF BASE:体力 <= 0
	PRINTFORML その日の営業が終わるなり、%CALLNAME:TARGET%は突っ伏すように倒れた…
	CFLAG:営業内容 = 0
	;キャラの死亡確認
	CALL CHARADEAD_CHECK
;瀕死
ELSEIF BASE:体力 <= 500
	PRINTFORML 体力が限界に来ています。営業メンバーから外れました
	CFLAG:営業内容 = 0
ENDIF

;処女喪失相手を記録
IF CFLAG:売春処女喪失 && !CFLAG:処女喪失相手
	CALL 処女喪失実行(TARGET,初相手_売春客男,BASE:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_なし,"知らないオトコの人")
ENDIF

;営業関連の計算が終わったら処女喪失判定等のフラグをリセット
CFLAG:売春処女喪失 = 0
CFLAG:Ｖ売春否定 = 0
CFLAG:Ａ売春否定 = 0
CFLAG:便所公開浣腸 = 0
CFLAG:売春Ｖ不能 = 0

SIF (FLAG:指導テキスト設定 & 2) == 0
	WAIT

DRAWLINE

;---------------------------------------------------------
;客の人数の計算
;---------------------------------------------------------
;この関数は各営業内容の最初に計算されます
;---------------------------------------------------------
@WORK_PERSON_A

B = 0

;素質『常連客』があるとプラス
IF TALENT:常連客
	REPEAT TALENT:常連客
		B += RAND:2
	REND
ENDIF

IF EXP:被写経験 == 0 && CFLAG:娼館デビュー == 0
	;売春デビュー時は、店の人気を参考する
	D = EXP:MASTER:人気
	D /= 10
	IF D
		REPEAT D
			B += RAND:2
		REND
	ENDIF
ELSE
	;デビューしてからは奴隷の人気を参考
	D = EXP:人気
	D /= 10
	IF D
		REPEAT D
			B += RAND:2
		REND
	ENDIF
ENDIF

;店の人気に応じてプラス
D = EXP:MASTER:人気
D /= 20
IF D
	REPEAT D
		B += RAND:2
	REND
ENDIF



G = B
G += ABL:性交中毒 / 2
SIF TALENT:淫乱 == 1
	TIMES G, 1.50
SIF TALENT:絶倫 == 1
	TIMES G, 1.80

G *= BASE:魅力値
G /= 100

SIF G < 1
	G = 1 

;客の数の最終決定
;計算結果で客が２人未満になったら２人にする
SIF G < 2
	G = 2

;会話営業の場合、客の数がちょっと増えます
SIF CFLAG:営業内容 == 1
	TIMES G , 1.50

;常連客が代表のお客かどうか
IF TALENT:常連客
	SIF RAND:10 < TALENT:常連客
		C = 1
ENDIF


;続いて、満足度の決定
;Dで満足度判定を行います

;ランダムで40を追加します。売春経験に応じて期待値は上昇します
IF EXP:売春経験 > 10000 && C
	D = 40
ELSEIF (EXP:売春経験 > 5000 && C) || EXP:売春経験 > 10000
	D = RAND:11 + 30
ELSEIF (EXP:売春経験 > 3000 && C) || EXP:売春経験 > 5000
	D = RAND:16 + 25
ELSEIF (EXP:売春経験 > 1000 && C) || EXP:売春経験 > 3000
	D = RAND:21 + 20
ELSEIF (EXP:売春経験 > 500 && C) || EXP:売春経験 > 1000
	D = RAND:26 + 15
ELSEIF (EXP:売春経験 > 100 && C) || EXP:売春経験 > 500
	D = RAND:31 + 10
ELSEIF (EXP:売春経験 > 50 && C) || EXP:売春経験 > 100
	D = RAND:36 + 5
ELSE
	D = RAND:41
ENDIF
E = 0

VARSET LOCAL, 0
LOCAL = RAND:10
PRINTFORM {G}人の客から指名が入った%NAME:TARGET%は

IF TALENT:彼氏あり == 1 && ITEM:コスプレ衣装
	IF LOCAL == 0
		PRINTFORM 
	ELSEIF LOCAL == 1
		PRINTFORM ブルセラマニア
	ELSEIF LOCAL == 2
		PRINTFORM メイド好き
	ELSEIF LOCAL == 3
		PRINTFORM 巫女さん好き
	ELSEIF LOCAL == 4
		PRINTFORM アイドルオタク
	ELSEIF LOCAL == 5
		PRINTFORM 女教師好き
	ELSEIF LOCAL == 6
		PRINTFORM ナースフェチ
	ELSEIF LOCAL == 7
		PRINTFORM バニーガール好き
	ELSEIF LOCAL == 8
		PRINTFORM ブルセラマニア
	ELSEIF LOCAL == 9 && CFLAG:営業内容 == 5 && ABL:サドっ気 + TALENT:サド * 5 >= ABL:マゾっ気 + TALENT:マゾ * 5
		PRINTFORM ドＭ
	ELSEIF LOCAL == 9 && CFLAG:営業内容 == 5 && ABL:サドっ気 + TALENT:サド * 5 < ABL:マゾっ気 + TALENT:マゾ * 5
		PRINTFORM ドＳ
	ELSEIF LOCAL == 9
		PRINTFORM 水着フェチ
	ENDIF
	SIF LOCAL != 0
		PRINTFORM の
	PRINTFORML 常連客のリクエストに応えて、
ENDIF

IF LOCAL == 0
	SIF TALENT:乳狂い
		PRINTFORM つん、と勃起した
	SIF (CFLAG:ピアス状況 & 1)
		PRINTFORM リングピアスが光る
	PRINTFORM 乳首が透けて見える、シースルーのベビードール
ELSEIF LOCAL == 1
	SIF TALENT:高校生 == 1 || TALENT:中学生
		PRINTFORM 自身の通う
	PRINTFORM 学園の制服
ELSEIF LOCAL == 2
	PRINTFORM 本格的な縫製のメイド服
ELSEIF LOCAL == 3
	PRINTFORM 脇を見せるような大胆なアレンジの施された巫女服
ELSEIF LOCAL == 4
	IF TALENT:[[TALENT:Emerald Queen]]
		PRINTFORM %TALENTNAME:[[TALENT:Emerald Queen]]%
	ELSEIF TALENT:[[TALENT:Lily Princess]] 
		PRINTFORM %TALENTNAME:[[TALENT:Lily Princess]]%
	ELSEIF TALENT:ColorfulPureGirls
		PRINTFORM %TALENTNAME:[[TALENT:ColorfulPureGirls]]%
	ELSEIF TALENT:Triptych
		PRINTFORM %TALENTNAME:[[TALENT:Triptych]]%
	ELSEIF TALENT:オリジナルユニット
		PRINTFORM %STR:アイドルユニット名%
	ELSEIF TALENT:アイドル
		PRINTFORM 自身
	ELSE
		PRINTFORM %TALENTNAME:[[TALENT:ColorfulPureGirls]]%
	ENDIF
	PRINTFORM のステージ衣装
ELSEIF LOCAL == 5
	PRINTFORM タイトスカートのスーツ
ELSEIF LOCAL == 6
	PRINTFORM 淡いピンクのナース服
ELSEIF LOCAL == 7
	PRINTFORM 大胆に胸元の開いたバニースーツ
ELSEIF LOCAL == 8
	PRINTFORM 濃紺のブルマに%CALLNAME:TARGET%と書かれた名札が縫い付けられた体操服
ELSEIF LOCAL == 9 && CFLAG:営業内容 == 5
	PRINTFORM エナメルボンデージ
ELSEIF LOCAL == 9
	IF TALENT:[[TALENT:Mermaid girl]]
		PRINTFORM %CALLNAME:TARGET%が競技の際に着ているぴっちりとしたスイムスーツ
	ELSE
		PRINTFORM 旧型のスクール水着
	ENDIF
ENDIF

PRINTFORML 姿で『お相手』をすることになった……
PRINTFORML 

;---------------------------------------------------------
;客の満足度の計算
;---------------------------------------------------------
@WORK_HAPPY

;売春内容によらないボーナス
;淫乱持ちは2、謎の魅力持ちは2、娼婦は3ポイント、傾城は4ポイントが満足度に加算

SIF TALENT:淫乱
	D += 2
SIF TALENT:謎の魅力
	D += 2
SIF TALENT:風俗嬢
	D += 3
SIF TALENT:高級娼婦
	D += 4

;コンドーム使用中だと満足度2/3
IF CFLAG:営業コンドーム && CFLAG:営業内容
	D *= 2
	D /= 3
ENDIF

;満足度の最終判定、Dの値によって５段階の評価がなされます

IF D > 65
;最高評価です
	A = 4
ELSEIF D > 45
;いい評価です
	A = 3
ELSEIF D > 30
;普通の評価です
	A = 2
ELSEIF D > 15
;悪い評価です
	A = 1
ELSE
;最低評価です
	A = 0
ENDIF

;---------------------------------------------------------
;営業内容によって得た経験や素質の変化を計算
;---------------------------------------------------------
@WORK_EXP
;娼婦デビュー
IF CFLAG:娼館デビュー == 0
	SIF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
		PRINTFORML 【%NAME:TARGET%が風俗嬢としてデビューしました】
	TCVAR:異常経験 += 1
	B = A
	B *= 10
	;人気者
	SIF TALENT:人気者 && B < 40
		B = 40
	SIF TALENT:アイドル
		B = 60
	;謎の魅力、魅惑補正
	SIF TALENT:魅惑 || TALENT:謎の魅力
		B += 10
	SIF TALENT:ColorfulPureGirls || TALENT:[[TALENT:Mermaid girl]] || TALENT:[[TALENT:ONLY ONE EMPRESS]] || TALENT:ILMATAR || TALENT:AVALOKITESVARA || TALENT:QueenB
		B = 100
	SIF TALENT:オリジナルユニット
		B = 80
	SIF TALENT:Triptych || TALENT:[[TALENT:Emerald Queen]] || TALENT:[[TALENT:Lily Princess]]
		B = 90
	IF B == 0
		J = 0
		SIF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
			PRINTFORML 残念ながら人気は全く得られなかったが、これから頑張っていこう
	ELSE
		J = B
	ENDIF
	
	;デビューしたかどうかをCFLAGで保存
	CFLAG:娼館デビュー = 1
	
	;デビュー時は、もらえるお金に一定のプラス補正がかかります
	TCVAR:人気 += 10
	
;満足度によって人気が上下します
;満足度最低
ELSEIF A <= 0
	;個人人気
	J = -6
	;娼館人気
	I = -5
;満足度悪い
ELSEIF A == 1
	;個人人気
	J = -3
	;娼館人気
	I = -1
;満足度普通
ELSEIF A == 2
	;個人人気
	J = 1
	;娼館人気
	I = 0
;満足度いい
ELSEIF A == 3
	;個人人気
	J = 2
	;娼館人気
	I = 1
;満足度最高
ELSEIF A == 4
	;個人人気
	J = 3
	;娼館人気
	I = 1
;満足度超最高
ELSEIF A >= 5
	;個人人気
	J = 5
	;娼館人気
	I = 2
ENDIF

;特殊営業の参加人数(FLAG:特殊営業参加人数)が多いと、一人あたりの娼館人気上昇は下がる
IF FLAG:特殊営業参加人数 >= 3 && I >= 1
	IF A >= 5
		I = 1
	ELSE 
		I = 0
	ENDIF
ELSEIF FLAG:特殊営業参加人数 == 2 && I >= 1
	IF A >= 4
		I = 1
	ELSE 
		I = 0
	ENDIF
ENDIF

;人気の上限は100
SIF EXP:人気 + J >= 100
	J = 100 - EXP:人気
SIF EXP:MASTER:人気 + I >= 100
	I = 100 - EXP:MASTER:人気

;人気の下限は0
SIF EXP:人気 + J <= 0
	J = 0 - EXP:人気
SIF EXP:MASTER:人気 + I <= 0
	I = 0 - EXP:MASTER:人気

;人気・娼館人気変化の表示
IF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
	PRINT  人気　　　　:
	N = EXP:人気
	CALL FIGURE_INDENT
	PRINTV EXP:人気
	IF J >= 0
		PRINT  + 
		N = J
		CALL FIGURE_INDENT
		PRINTV J
	ELSE
		PRINT  - 
		;eraBASICはマイナス演算子を扱えないのでわざとこうしています
		J = 0 - J
		N = J
		CALL FIGURE_INDENT
		PRINTV J
		J = 0 - J
	ENDIF
	PRINT  = 
	N = EXP:人気 + J
	CALL FIGURE_INDENT
	PRINTVL EXP:人気 + J

	PRINT  娼館人気　　:
	N = EXP:MASTER:人気
	CALL FIGURE_INDENT
	PRINTV EXP:MASTER:人気
	IF I >= 0
		PRINT  + 
		N = I
		CALL FIGURE_INDENT
		PRINTV I
	ELSE
		PRINT  - 
		;eraBASICはマイナス演算子を扱えないのでわざとこうしています
		I = 0 - I
		N = I
		CALL FIGURE_INDENT
		PRINTV I
		I = 0 - I
	ENDIF
	PRINT  = 
	N = EXP:MASTER:人気 + I
	CALL FIGURE_INDENT
	PRINTVL EXP:MASTER:人気 + I
ELSE
	PRINTFORML (人気:{EXP:人気 + J})
ENDIF

EXP:人気 += J
EXP:MASTER:人気 += I

;満足度最高の場合常連客が付くことあり
IF A >= 4
	IF TALENT:常連客 == 0 && (C == 0 || CFLAG:営業内容 == 3 || CFLAG:営業内容 == 4 || CFLAG:営業内容 == 5) && TALENT:一線越えない == 0
		PRINTFORML 客の一人は%CALLNAME:TARGET%を大変気に入ったようだ…
		PRINTFORMW %CALLNAME:TARGET%に【%TALENTNAME:[[TALENT:常連客]]%】がついた
		TALENT:常連客 = 1
	ENDIF
ENDIF

SIF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
	DRAWLINE

;Ｖ経験増加量＝客に膣内射精された回数と解釈して妊娠フラグを加算
SIF TCVAR:Ｖ経験 && CFLAG:営業コンドーム == 0 
	CFLAG:客奴膣内精子量 += TCVAR:Ｖ経験

;『TCVAR:X』のXの値に応じて経験が上昇します
REPEAT 76
	IF TCVAR:COUNT
		IF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
			PRINTFORM  %EXPNAME:COUNT%
			IF COUNT == 0 || COUNT == 1
				PRINT 　　　
			ELSEIF COUNT == 11 || COUNT == 21 || COUNT == 30 || COUNT == 33 || COUNT == 73 || COUNT == 75
				PRINT 
			ELSEIF COUNT == 22 || COUNT == 32 || COUNT == 52 || COUNT == 53
				PRINT 　
			ELSE
				PRINT 　　
			ENDIF
			PRINT :
			N = EXP:COUNT
			CALL FIGURE_INDENT
			PRINTV EXP:COUNT
			PRINTFORM  + 
			N = TCVAR:COUNT
			CALL FIGURE_INDENT
			PRINTV TCVAR:COUNT
			PRINTFORM  = 
			N = EXP:COUNT + TCVAR:COUNT
			CALL FIGURE_INDENT
			PRINTVL EXP:COUNT + TCVAR:COUNT
		ENDIF
		EXP:COUNT += TCVAR:COUNT
		TCVAR:COUNT = 0
	ENDIF
REND

SIF CFLAG:営業内容 == 0
	DRAWLINE

;『U:X』のXの値に応じて珠を獲得します
REPEAT 101
	IF U:COUNT
		IF (FLAG:指導テキスト設定 & 2) == 0 || CFLAG:営業内容 == 0
			PRINTFORM  %PALAMNAME:COUNT%の珠　　
			PRINT :
			N = JUEL:COUNT
			CALL FIGURE_INDENT
			PRINTV JUEL:COUNT
			PRINTFORM  + 
			N = U:COUNT
			CALL FIGURE_INDENT
			PRINTV U:COUNT
			PRINTFORM  = 
			N = JUEL:COUNT + U:COUNT
			CALL FIGURE_INDENT
			PRINTVL JUEL:COUNT + U:COUNT
		ENDIF
		JUEL:COUNT += U:COUNT
		U:COUNT = 0
	ENDIF
REND

;娼婦、傾城、求愛を取得します
;娼婦の条件：売春経験100以上、反発刻印なし、屈服刻印Lv3、人気40以上
IF EXP:売春経験 >= 100 && MARK:反発刻印 == 0 && TALENT:風俗嬢 == 0 && EXP:人気 >= 40 && TALENT:高級娼婦 == 0 && ABL:売春中毒 >= 2
	PRINTFORML %CALLNAME:TARGET%の営業時の様子がおかしい…
	PRINTFORMW %CALLNAME:TARGET%は風俗嬢として働くことにやりがいを感じ始めたようだ……
		TALENT:アルバイト = 0
		TALENT:バイト戦士 = 0
		TALENT:ニート = 0
	TALENT:風俗嬢 = 1
	MAXBASE:魅力値 += 10
	BASE:魅力値 += 10
	IF TALENT:貞操観念 == 1
		PRINTFORML 《%CALLNAME:TARGET%は【%TALENTNAME:[[TALENT:貞操観念]]%】を失った》
		TALENT:貞操観念 = 0
	ELSEIF TALENT:貞操観念 == 0 && TALENT:貞操無頓着 == 0
		PRINTFORML 《%CALLNAME:TARGET%は【%TALENTNAME:[[TALENT:貞操無頓着]]%】になった》
		TALENT:貞操無頓着 = 1
	ENDIF
ENDIF
;傾城の条件：売春経験200以上、反発刻印なし、屈服刻印Lv3、娼婦取得、人気100
IF EXP:売春経験 >= 200 && MARK:反発刻印 == 0 && TALENT:風俗嬢 == 1 &&TALENT:高級娼婦 == 0 && EXP:人気 >= 100 && ABL:売春中毒 >= 4
	PRINTFORML %CALLNAME:TARGET%はすっかり娼館の顔となったようだ
	PRINTFORML 今日も%CALLNAME:TARGET%に相手をしてもらいたがる客が何人も訪れてきていた……
	PRINTFORMW %NAME:TARGET%は【%TALENTNAME:[[TALENT:高級娼婦]]%】を得た
	TALENT:高級娼婦 = 1
	TALENT:風俗嬢 = 0
	MAXBASE:魅力値 += 20
	BASE:魅力値 += 20
	IF TALENT:貞操観念 == 1
		PRINTFORML 《%CALLNAME:TARGET%は【%TALENTNAME:[[TALENT:貞操観念]]%】を失った》
		TALENT:貞操観念 = 0
	ELSEIF TALENT:貞操観念 == 0 && TALENT:貞操無頓着 == 0
		PRINTFORML 《%CALLNAME:TARGET%は【%TALENTNAME:[[TALENT:貞操無頓着]]%】になった》
		TALENT:貞操無頓着 = 1
	ENDIF
ENDIF
;求愛の条件：売春経験100以上、営業愛情経験300以上、永遠の愛未取得、常連客がいる、人気60以上
IF !TALENT:永遠の愛 && EXP:売春経験 >= 100 && EXP:営業愛情経験 >= 300 + (TALENT:恋心 * EXP:愛情経験) && TALENT:常連客 && !TALENT:彼氏あり && EXP:人気 >= 60
	PRINTFORML %CALLNAME:TARGET%の営業時の様子がおかしい…
	PRINTFORML %CALLNAME:TARGET%は常連客と恋仲になったようだ……
	PRINTFORMW %NAME:TARGET%は【%TALENTNAME:[[TALENT:彼氏あり]]%】を得た
	SIF TALENT:恋心
		TALENT:恋心 = 0
	TALENT:彼氏あり = 1
	CFLAG:彼氏有 = 1
	CFLAG:彼氏タイプ += RAND:6
ENDIF

;---------------------------------------------------------
;獲得額の計算
;---------------------------------------------------------
;計算式は次の通り
;「キャラの元値」×「買った人数」×「プレイ内容補正」×「満足度補正」
;---------------------------------------------------------
@WORK_MONEY

;キャラの元値
CALL WORK_MONEYD

;買った人数
B *= G

;コンドームの代金を引く
B -= CFLAG:営業コンドーム * G * 200

;マイナスになる事はない
SIF B < 0
	B = 0

;会話営業の場合、客が多く来るので帳尻あわせを
SIF CFLAG:営業内容 == 1
	TIMES B , 0.75

;プレイ内容補正
B *= TCVAR:人気

;娼婦だとちょっと増える
SIF TALENT:風俗嬢
	TIMES B , 1.10
;傾城だと結構増える
SIF TALENT:高級娼婦
	TIMES B , 1.30

;人気が高いと高さに応じて増える
IF EXP:人気 >= 100
	TIMES B , 1.30
ELSEIF EXP:人気 >= 80
	TIMES B , 1.20
ELSEIF EXP:人気 >= 60
	TIMES B , 1.10
ENDIF

;満足度補正
IF A == 0
	SIF (FLAG:指導テキスト設定 & 2) == 0
		PRINTFORML サービスの悪さに大量のクレームがよせられ、一部の料金を払い戻す羽目になりました
	TIMES B , 0.50
ELSEIF A == 1
	TIMES B , 1.00
ELSEIF A == 2
	TIMES B , 1.00
ELSEIF A == 3
	TIMES B , 1.20
ELSEIF A == 4
	TIMES B , 1.30
ENDIF



IF (FLAG:指導テキスト設定 & 2) == 0
	IF CFLAG:営業内容 == 6
		PRINTFORML 帰り際に客から今日の分の料金を受け取りました
	ELSE
		PRINTFORML 今日は{G}人の顧客が訪れました
	ENDIF
	PRINTFORML %CALLNAME:TARGET%は営業によって{B}ポイントを稼ぎました
ELSE
	IF A == 0
		PRINT 大失敗:
	ELSEIF A == 1
		PRINT 失敗:
	ELSEIF A == 2
		PRINT 普通:
	ELSEIF A == 3
		PRINT 成功:
	ELSEIF A == 4
		PRINT 大成功:
	ENDIF
	PRINTFORM {G}人の客から{B}ポイント獲得
ENDIF

MONEY += B

;---------------------------------------------------------
;キャラの元値の計算
;---------------------------------------------------------
;;基本費用は200、素質によってさらに加算されることになります
;;現在はテストも兼ねてわりと適当な値ですが、追々バランスを取りながら変更していく予定です
;営業が効率良過ぎるので、erapoke導入に際して大幅にダンピング
;HoX_pre2でさらにダンピング
;---------------------------------------------------------
@WORK_MONEYD

;基本額
B = 100

;能力ボーナス：従順・欲望・話術・奉仕精神は営業内容に問わずボーナスとして加算
;従順
IF ABL:信頼 <= 3
	B += 5
ELSEIF ABL:信頼 == 4
	B += 10
ELSEIF ABL:信頼 == 5
	B += 20
ELSEIF ABL:信頼 == 6
	B += 25
ELSEIF ABL:信頼 == 7
	B += 30
ELSEIF ABL:信頼 == 8
	B += 35
ELSEIF ABL:信頼 == 9
	B += 45
ELSEIF ABL:信頼 >= 10
	B += 50
ENDIF

;欲望
IF ABL:欲望 <= 3
	B += 10
ELSEIF ABL:欲望 == 4
	B += 20
ELSEIF ABL:欲望 == 5
	B += 30
ELSEIF ABL:欲望 == 6
	B += 50
ELSEIF ABL:欲望 == 7
	B += 70
ELSEIF ABL:欲望 == 8
	B += 90
ELSEIF ABL:欲望 == 9
	B += 120
ELSEIF ABL:欲望 >= 10
	B += 150
ENDIF

;話術：会話営業だった場合通常よりも多く加算
IF CFLAG:営業内容 == 1
	IF ABL:話術 == 1
		B += 10
	ELSEIF ABL:話術 == 2
		B += 20
	ELSEIF ABL:話術 == 3
		B += 40
	ELSEIF ABL:話術 == 4
		B += 70
	ELSEIF ABL:話術 == 5
		B += 100
	ELSEIF ABL:話術 >= 6
		B += 150
	ENDIF
ELSE
	IF ABL:話術 == 1
		B += 5
	ELSEIF ABL:話術 == 2
		B += 10
	ELSEIF ABL:話術 == 3
		B += 20
	ELSEIF ABL:話術 == 4
		B += 35
	ELSEIF ABL:話術 == 5
		B += 50
	ELSEIF ABL:話術 >= 6
		B += 75
	ENDIF
ENDIF

;奉仕精神：出張営業だった場合通常よりも多く加算
IF CFLAG:営業内容 == 6
	IF ABL:奉仕精神 == 1
		B += 15
	ELSEIF ABL:奉仕精神 == 2
		B += 30
	ELSEIF ABL:奉仕精神 == 3
		B += 60
	ELSEIF ABL:奉仕精神 == 4
		B += 105
	ELSEIF ABL:奉仕精神 == 5
		B += 150
	ELSEIF ABL:奉仕精神 >= 6
		B += 225
	ENDIF
ELSE
	IF ABL:奉仕精神 == 1
		B += 10
	ELSEIF ABL:奉仕精神 == 2
		B += 20
	ELSEIF ABL:奉仕精神 == 3
		B += 40
	ELSEIF ABL:奉仕精神 == 4
		B += 70
	ELSEIF ABL:奉仕精神 == 5
		B += 100
	ELSEIF ABL:奉仕精神 >= 6
		B += 150
	ENDIF
ENDIF

;奉仕営業だった場合、奉仕技術に応じて加算
IF CFLAG:営業内容 == 2 || CFLAG:営業内容 == 7
	IF ABL:奉仕技術 == 1
		B += 15
	ELSEIF ABL:奉仕技術 == 2
		B += 25
	ELSEIF ABL:奉仕技術 == 3
		B += 40
	ELSEIF ABL:奉仕技術 == 4
		B += 60
	ELSEIF ABL:奉仕技術 == 5
		B += 75
	ELSEIF ABL:奉仕技術 >= 6
		B += 100
	ENDIF
ENDIF

;ＡorＶ営業だった場合、性交技術に応じて加算
IF CFLAG:営業内容 == 3 || CFLAG:営業内容 == 4
	IF ABL:性交技術 == 1
		B += 30
	ELSEIF ABL:性交技術 == 2
		B += 50
	ELSEIF ABL:性交技術 == 3
		B += 75
	ELSEIF ABL:性交技術 == 4
		B += 100
	ELSEIF ABL:性交技術 == 5
		B += 150
	ELSEIF ABL:性交技術 >= 6
		B += 200
	ENDIF
ENDIF

;ＳＭ営業だった場合、サドっ気orマゾっ気に応じて加算（高い方を判定）
IF CFLAG:営業内容 == 5
	IF ABL:サドっ気 >= ABL:マゾっ気
		IF ABL:サドっ気 == 1
			B += 15
		ELSEIF ABL:サドっ気 == 2
			B += 25
		ELSEIF ABL:サドっ気 == 3
			B += 40
		ELSEIF ABL:サドっ気 == 4
			B += 60
		ELSEIF ABL:サドっ気 == 5
			B += 75
		ELSEIF ABL:サドっ気 >= 6
			B += 100
		ENDIF
	ELSEIF ABL:マゾっ気 > ABL:サドっ気
		IF ABL:マゾっ気 == 1
			B += 15
		ELSEIF ABL:マゾっ気 == 2
			B += 25
		ELSEIF ABL:マゾっ気 == 3
			B += 40
		ELSEIF ABL:マゾっ気 == 4
			B += 60
		ELSEIF ABL:マゾっ気 == 5
			B += 75
		ELSEIF ABL:マゾっ気 >= 6
			B += 100
		ENDIF
	ENDIF
ENDIF

;素質ボーナス
;淫乱
SIF TALENT:淫乱
	B += 20
;魅力
SIF TALENT:魅力
	B += 40
;魅惑
SIF TALENT:魅惑
	B += 40
;謎の魅力
SIF TALENT:謎の魅力
	B += 75
;人気者
SIF TALENT:人気者
	B += 35
;巨乳か貧乳
SIF TALENT:巨乳 || TALENT:貧乳
	B += 25
;ふたなり
SIF TALENT:ふたなり
	B += 15
;小柄体系
SIF TALENT:小柄
	B += 25
;アイドル
SIF TALENT:アイドル
	B += 50

;HoX pre2にて、ビデオバリアントなのに！という声に応えてざっくり切り下げ
B /= 10

;---------------------------------------------------------
;否定の珠の計算
;---------------------------------------------------------
;従順が高くないとガンガン溜まっていきます
;従順が高くても溜まるのは溜まります
;---------------------------------------------------------
@WORK_DOWN

A = G
B = 6 - ABL:信頼
SIF B < 1
	B = 1
A *= 5
A *= B
A *= B

;経験補正
;処女喪失だとかなり増える
SIF CFLAG:売春処女喪失
	TIMES A , 10.00

;Ｖ経験が足りないとちょっと増える
SIF CFLAG:Ｖ売春否定
	TIMES A , 1.50

;Ａ経験が足りないとちょっと増える
SIF CFLAG:Ａ売春否定
	TIMES A ,1.50

;売春中毒のレベルに応じてちょっとずつ減る
IF ABL:売春中毒 == 1
	TIMES A , 0.90
ELSEIF ABL:売春中毒 == 2
	TIMES A , 0.80
ELSEIF ABL:売春中毒 == 3
	TIMES A , 0.70
ELSEIF ABL:売春中毒 == 4
	TIMES A , 0.60
ELSEIF ABL:売春中毒 >= 5
	TIMES A , 0.50
ENDIF

;素質補正
;無関心だと少なくなる
SIF TALENT:クール
	TIMES A , 0.90
;感情乏しいと少なくなる
SIF TALENT:感情乏しい
	TIMES A , 0.90
;娼婦だとさらに少なくなる
SIF TALENT:風俗嬢
	TIMES A , 0.80
;求愛だと凄く少なくなる
SIF TALENT:彼氏あり
	TIMES A , 0.50
;プライドが高いと増える
SIF TALENT:プライド高い
	TIMES A , 1.50
;貞操観念だと凄く増える
SIF TALENT:貞操観念
	TIMES A , 2.50
;淫乱だとやや少なくなる
SIF TALENT:淫乱
	TIMES A , 0.80
SIF TALENT:絶倫
	TIMES A , 0.01
;【愛】してると嫌な気分
SIF TALENT:恋心
	TIMES A , 1.20
;嫉妬してるとこれは酷い
SIF TALENT:嫉妬
	A *= 100

IF (FLAG:指導テキスト設定 & 2) == 0
	PRINT  否定の珠　　:
	N = JUEL:否定
	CALL FIGURE_INDENT
	PRINTV JUEL:否定
	PRINT  + 
	N = A
	CALL FIGURE_INDENT
	PRINTV A
	PRINT  = 
	N = JUEL:否定 + A
	CALL FIGURE_INDENT
	PRINTVL JUEL:否定 + A
ENDIF

JUEL:否定 += A

;否定の珠の許容量を決定します
;従順の150倍+100まで許容できます。LV2はルート2倍、LV3はルート3倍
A = ABL:信頼
A *= 150
A += 100
B = A
TIMES B , 1.41
C = A
TIMES C , 1.73

IF (FLAG:指導テキスト設定 & 2) == 0
	PRINT  許容量　　　:
	IF MARK:反発刻印 == 0
		BAR JUEL:否定, A, 32
		PRINTFORML ({JUEL:否定}/{A})
	ELSEIF MARK:反発刻印 == 1
		BAR JUEL:否定, B, 32
		PRINTFORML ({JUEL:否定}/{B})
	ELSE
		BAR JUEL:否定, C, 32
		PRINTFORML ({JUEL:否定}/{C})
	ENDIF
ENDIF

IF MARK:反発刻印 != 3 && JUEL:否定 > C
	PRINTL 反発刻印LV3を取得
	PRINTL これ以上売春を続ける事が出来ません
	MARK:反発刻印 = 3
	CFLAG:営業内容 = 0
ELSEIF MARK:反発刻印 != 2 && JUEL:否定 > B
	PRINTL 反発刻印LV2を取得
	PRINTL これ以上売春を続ける事が出来ません
	MARK:反発刻印 = 2
	CFLAG:営業内容 = 0
ELSEIF MARK:反発刻印 != 1 && JUEL:否定 > A
	PRINTL 反発刻印LV1を取得
	MARK:反発刻印 = 1
ENDIF


;---------------------------------------------------------
;消費体力の計算
;---------------------------------------------------------
@WORK_BASE
;人数に応じて、ダメージの基準値を決定します
IF G > 45
	LOSEBASE:体力 = 50
ELSEIF G > 34
	LOSEBASE:体力 = 45
ELSEIF G > 24
	LOSEBASE:体力 = 40
ELSEIF G > 15
	LOSEBASE:体力 = 35
ELSEIF G > 7
	LOSEBASE:体力 = 30
ELSE
	LOSEBASE:体力 = 25
ENDIF

;営業種目による補正
LOSEBASE:体力 *= F

;経験補正
;処女喪失だとかなり増える
IF CFLAG:売春処女喪失
	TIMES LOSEBASE:体力 , 2.00
	BASE:魅力値 -= 20
ENDIF

;Ｖ経験が足りないとちょっと増える
IF CFLAG:Ｖ売春否定
	TIMES LOSEBASE:体力 , 1.10
	BASE:魅力値 -= 2
ENDIF

;Ａ経験が足りないとちょっと増える
IF CFLAG:Ａ売春否定
	TIMES LOSEBASE:体力 , 1.20
	BASE:魅力値 -= 2
ENDIF

;淫乱だとちょっと少なくなる
SIF TALENT:淫乱
	TIMES LOSEBASE:体力 , 0.80

;売春中毒のレベルに応じてちょっとずつ減る
IF ABL:売春中毒 == 1
	TIMES LOSEBASE:体力 , 0.90
ELSEIF ABL:売春中毒 == 2
	TIMES LOSEBASE:体力 , 0.80
ELSEIF ABL:売春中毒 == 3
	TIMES LOSEBASE:体力 , 0.70
ELSEIF ABL:売春中毒 == 4
	TIMES LOSEBASE:体力 , 0.60
ELSEIF ABL:売春中毒 >= 5
	TIMES LOSEBASE:体力 , 0.50
ENDIF

IF (FLAG:指導テキスト設定 & 2) == 0
	PRINT  体力消費　　:
	N = BASE:体力
	CALL FIGURE_INDENT
	PRINTV BASE:体力
	PRINT  - 
	N = LOSEBASE:体力
	CALL FIGURE_INDENT
	PRINTV LOSEBASE:体力
	PRINT  = 
	N = BASE:体力 - LOSEBASE:体力
	CALL FIGURE_INDENT
	PRINTVL BASE:体力 - LOSEBASE:体力
	BASE:魅力値 -= RAND:6
ENDIF

BASE:体力 -= LOSEBASE:体力
LOSEBASE:体力 = 0

IF (FLAG:指導テキスト設定 & 2) == 0
	PRINT  体力　　　　:
	BAR BASE:体力, MAXBASE:体力, 32
	PRINTFORML ({BASE:体力}/{MAXBASE:体力})
ENDIF
