;===========================================================
;乱交パーティーの処理はここで
;===========================================================
;eraIm@s_ver.0.17βdより流用して改造(pawapokya-)
;===========================================================
@SEX_ORGY
DRAWLINE

REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	;コンドーム使用フラグ外す
	CFLAG:COUNT:営業コンドーム = 0
	;売春時の状況フラグ初期化
	CFLAG:COUNT:売春処女喪失 = 0
	CFLAG:COUNT:Ｖ売春否定 = 0
	CFLAG:COUNT:Ａ売春否定 = 0
	CFLAG:COUNT:便所公開浣腸 = 0
REND

;特殊営業のフラグを初期化
FLAG:特殊営業で夜の部を開催フラグ = 0
FLAG:特殊営業時のキャラの相性 = 0
FLAG:特殊営業時お揃い衣装 = 0
FLAG:特殊営業タイプ詳細 = 0

S = 0
;特別イベント可否のチェック
;全員が自慰中毒レベル1以上ならオナニーコンテスト開催可
E = 1
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 && ABL:COUNT:自慰中毒 == 0
		E = 0
REND
SIF E == 1
	S |= 1
;全員が精液中毒レベル1以上で主人がオトコかふたなりならフェラチオコンテスト開催可
E = 1
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 && ABL:COUNT:精液中毒 == 0
		E = 0
REND
SIF TALENT:MASTER:オトコ == 0 && TALENT:MASTER:ふたなり == 0
	E = 0
SIF E == 1
	S |= 2
;全員がマゾっ気レベル1以上なら人間競馬開催可
E = 1
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 && ABL:COUNT:マゾっ気 == 0
		E = 0
REND
SIF E == 1
	S |= 4
;資金が最低でも1000ポイント必要
SIF MONEY < 1000
	S = 0

;特別イベントを行うかどうか選択
$INPUT_LOOP_01
IF S
	PRINTL パーティーで特別イベントを行いますか？
	PRINTL  [0] - 行わない
	SIF S & 1
		PRINTL  [1] - オナニーコンテスト
	SIF S & 2
		PRINTL  [2] - フェラチオコンテスト
	SIF S & 4
		PRINTL  [3] - 人間競馬
	INPUT
	IF RESULT == 0
		PRINTFORMW 通常の乱交パーティーを行います
	ELSEIF RESULT == 1 && (S & 1)
		PRINTFORMW 乱交パーティーの最後にオナニーコンテストを行います
		FLAG:特殊営業で夜の部を開催フラグ = 1
	ELSEIF RESULT == 2 && (S & 2)
		PRINTFORMW 乱交パーティーの最後にフェラチオコンテストを行います
		FLAG:特殊営業で夜の部を開催フラグ = 2
	ELSEIF RESULT == 3 && (S & 4)
		PRINTFORMW 乱交パーティーの最後に人間競馬を行います
		FLAG:特殊営業で夜の部を開催フラグ = 3
	ELSEIF RESULT == 100
		RETURN 100
	ELSE
		GOTO INPUT_LOOP_01
	ENDIF
	DRAWLINE
ENDIF

;ローカル変数初期化
A = 0
B = 0
C = 0
D = 0
E = 0

;参加する客の人数
G = 0
M = 0
N = 0
O = 0
I = 0
J = 0

S = 0

Q = 0
R = 0
X = 0

;特別イベント勝利者
W = 0
;特別イベントで主人公とペアを組んだキャラ
Y = 0

X = 0
Z = 0

REPEAT 101
	TCVAR:COUNT = 0
	U:COUNT = 0
REND

;調教対象と助手を空に
TARGET = -1
ASSI = -1

;特殊営業参加キャラの相性をチェック
CALL WORK_SUB_RELATION

;客の数を計算
;参加するキャラの人気の平均値に応じて客が来ます
E = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加
		E += EXP:COUNT:人気
REND
E /= FLAG:特殊営業参加人数
C = E / 5
G = C / 4
IF C
	IF C < 13
		REPEAT C
			G += RAND:2
		REND
	ELSEIF C < 16
		TIMES B , 1.20
		REPEAT C
			G += RAND:2
		REND
	ELSEIF C < 20
		TIMES B , 1.50
		REPEAT C
			G += RAND:2
		REND
	ELSEIF C < 24
		TIMES B , 2.00
		REPEAT C
			G += RAND:2
		REND
	ELSE
		TIMES B , 2.00
		REPEAT C
			G += RAND:3
		REND
	ENDIF
ENDIF

;娼館の人気に応じてさらにプラス
C = (EXP:MASTER:人気 / 10)
IF C
	REPEAT C
		G += RAND:2
	REND
ENDIF

;参加者が特定の素質を持っていた場合ボーナス
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	;人気者
	SIF TALENT:COUNT:人気者
		G += 1
	;娼婦
	SIF TALENT:COUNT:風俗嬢
		G += 2
	;傾城
	SIF TALENT:COUNT:高級娼婦
		G += 3
	;アイドル
	SIF TALENT:COUNT:アイドル
		G += 3
	;令嬢
	SIF TALENT:COUNT:令嬢
		G += 3
	;貴族
	SIF TALENT:COUNT:貴族
		G += 5
REND

;5人以下だった場合、10人にします
SIF G < 5
	G = 5

;参加人数以下だった場合、参加人数と同数にします
SIF G < FLAG:特殊営業参加人数
	G = FLAG:特殊営業参加人数

PRINTFORMW %NAME:MASTER%主催の乱交パーティーを開催した……
PRINTFORML 今回のパーティーに集まった{G-1}人の客たちを、
CALL WORK_SUB_PRINT_FULLNAME
PRINTFORML は、揃って出迎えた
PRINTL
WAIT

;主人公が童貞なら童貞喪失
IF IS童貞(MASTER)
	;童貞喪失相手（乱交）はここで記録
	CALL 童貞喪失実行(MASTER,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,"女性客")
ENDIF


;特殊営業時メッセージ
CALL WORK_MESSAGE_ORGY_01

REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	TARGET = COUNT

	;口上の判定
	TFLAG:口上番号 = 114
	CALL SELF_KOJO(TARGET)
	;念のため口上用フラグを空に
	TFLAG:口上番号 = 0

	;処女喪失
	IF CFLAG:COUNT:売春Ｖ不能 == 0 && IS処女(COUNT)
		IF CFLAG:COUNT:処女喪失相手 == 0
			;処女喪失相手（乱交）はここで記録
				CFLAG:COUNT:131 = RAND:6
					IF CFLAG:131 == 0
						CALL 処女喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交)
					ELSEIF CFLAG:131 == 1
						CSTR:COUNT:処女喪失相手 = 知らないオトコの人
						CALL 処女喪失実行(COUNT,初相手_売春客男,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
					ELSEIF CFLAG:131 == 2
						CSTR:COUNT:処女喪失相手 = 知らないオンナの人
						CALL 処女喪失実行(COUNT,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
					ELSEIF CFLAG:131 == 3
						CSTR:COUNT:処女喪失相手 = バイブ
						CALL 処女喪失実行(COUNT,初相手_バイブ,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
					ELSEIF CFLAG:131 == 4
						CSTR:COUNT:処女喪失相手 = 極太バイブ
						CALL 処女喪失実行(COUNT,初相手_極太バイブ,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
					ELSE
						CSTR:COUNT:処女喪失相手 = 誰かわからない
						CALL 処女喪失実行(COUNT,初相手_不明,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
					ENDIF
		ENDIF

	ENDIF
	IF ISＡ処女(COUNT)
		CFLAG:COUNT:132 += RAND:3 
			IF CFLAG:COUNT:132 == 0 
				CALL Ａ処女喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交)
			ELSEIF CFLAG:COUNT:132 == 1
				CSTR:COUNT:Ａ処女喪失相手 = 知らないオトコの人
				CALL Ａ処女喪失実行(COUNT,初相手_売春客男,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:Ａ処女喪失相手)
			ELSEIF CFLAG:COUNT:132 == 2
				CSTR:COUNT:Ａ処女喪失相手 = 知らないオンナの人
				CALL Ａ処女喪失実行(COUNT,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:Ａ処女喪失相手)
			ELSE
				CSTR:COUNT:Ａ処女喪失相手 = 誰かわからない
				CALL Ａ処女喪失実行(COUNT,初相手_不明,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:Ａ処女喪失相手)
			ENDIF
	ENDIF
	IF IS童貞(COUNT)
		CFLAG:COUNT:133 += RAND:3
			IF CFLAG:COUNT:133 == 0
				CSTR:COUNT:童貞喪失相手 = 知らないオンナの人
			ELSEIF CFLAG:COUNT:133 == 1 && TALENT:MASTER:オトコ == 0
				CSTR:COUNT:童貞喪失相手 = %CALLNAME:MASTER%
				IF IS処女(MASTER) && CFLAG:MASTER:処女喪失相手 == 0
					CALL 処女喪失実行(MASTER,NO:COUNT + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CALLNAME:COUNT)
				ENDIF
				CALL 童貞喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CALLNAME:MASTER)
			ELSE
				CSTR:COUNT:童貞喪失相手 = 誰かわからない
				CALL 処女喪失実行(COUNT,初相手_不明,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_乱交,CSTR:COUNT:処女喪失相手)
			ENDIF
	ENDIF
	IF !CFLAG:COUNT:初キス相手
			CFLAG:COUNT:初キス相手 += RAND:8 
				IF CFLAG:COUNT:初キス相手 == 0 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%
				ELSEIF CFLAG:COUNT:初キス相手 == 0 && NO:COUNT == [[キャラ:宮間かなで]] && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = お兄ちゃん
				ELSEIF CFLAG:COUNT:初キス相手 == 0 && NO:COUNT == [[キャラ:宮間かなで]] && TALENT:COUNT:ギャル系 == 1
					CSTR:COUNT:初キス相手 = アニキ
				ELSEIF CFLAG:COUNT:初キス相手 == 1
					CSTR:COUNT:初キス相手 = 知らないオトコの人
				ELSEIF CFLAG:COUNT:初キス相手 == 2
					CSTR:COUNT:初キス相手 = 知らないオンナの人
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のおちん○ん
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のオチ○ポ
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:ギャル系 == 1 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のチンポ
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = お兄ちゃんのおちん○ん
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = お兄ちゃんのオチ○ポ
				ELSEIF CFLAG:COUNT:初キス相手 == 3 && TALENT:COUNT:ギャル系 == 1 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = アニキのチンポ
				ELSEIF CFLAG:COUNT:初キス相手 == 4 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオトコの人のおちん○ん
				ELSEIF CFLAG:COUNT:初キス相手 == 4 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオトコの人のオチ○ポ
				ELSEIF CFLAG:COUNT:初キス相手 == 4 && TALENT:COUNT:ギャル系 == 1
					CSTR:COUNT:初キス相手 = 知らないオトコの人のチンポ
				ELSEIF CFLAG:COUNT:初キス相手 == 5 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオンナの人のアソコ
				ELSEIF CFLAG:COUNT:初キス相手 == 5 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオンナの人のオマ○コ
				ELSEIF CFLAG:COUNT:初キス相手 == 5 && TALENT:COUNT:ギャル系 == 1
					CSTR:COUNT:初キス相手 = 知らないオンナの人のマンコ
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のお尻の穴
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のアナル
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:ギャル系 == 1 && NO:COUNT != [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = %CALLNAME:MASTER%のケツ穴
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = お兄ちゃんのお尻の穴
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = お兄ちゃんのアナル
				ELSEIF CFLAG:COUNT:初キス相手 == 6 && TALENT:COUNT:ギャル系 == 1 && NO:COUNT == [[キャラ:宮間かなで]]
					CSTR:COUNT:初キス相手 = アニキのケツ穴
				ELSEIF CFLAG:COUNT:初キス相手 == 7 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオトコの人のお尻の穴
				ELSEIF CFLAG:COUNT:初キス相手 == 7 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオトコの人のアナル
				ELSEIF CFLAG:COUNT:初キス相手 == 7 && TALENT:COUNT:ギャル系 == 1
					CSTR:COUNT:初キス相手 = 知らないオトコの人のケツ穴
				ELSEIF CFLAG:COUNT:初キス相手 == 8 && TALENT:COUNT:淫乱 == 0 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオンナの人のお尻の穴
				ELSEIF CFLAG:COUNT:初キス相手 == 8 && TALENT:COUNT:淫乱 == 1 && TALENT:COUNT:ギャル系 == 0
					CSTR:COUNT:初キス相手 = 知らないオンナの人のアナル
				ELSEIF CFLAG:COUNT:初キス相手 == 8 && TALENT:COUNT:ギャル系 == 1
					CSTR:COUNT:初キス相手 = 知らないオンナの人のケツ穴
				ENDIF
		SELECTCASE CFLAG:COUNT:初キス相手
			CASE 0
				CALL 初キス喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_口,CSTR:COUNT:初キス相手)
			CASE 1
				CALL 初キス喪失実行(COUNT,初相手_売春客男,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_口,CSTR:COUNT:初キス相手)
			CASE 2
				CALL 初キス喪失実行(COUNT,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_口,CSTR:COUNT:初キス相手)
			CASE 3
				CALL 初キス喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_男性器,CSTR:COUNT:初キス相手)
			CASE 4
				CALL 初キス喪失実行(COUNT,初相手_売春客男,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_男性器,CSTR:COUNT:初キス相手)
			CASE 5
				CALL 初キス喪失実行(COUNT,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_女性器,CSTR:COUNT:初キス相手)
			CASE 6
				CALL 初キス喪失実行(COUNT,NO:MASTER + 1,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_肛門,CSTR:COUNT:初キス相手)
			CASE 7
				CALL 初キス喪失実行(COUNT,初相手_売春客男,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_肛門,CSTR:COUNT:初キス相手)
			CASE 8
				CALL 初キス喪失実行(COUNT,初相手_売春客女,BASE:COUNT:実年齢,DAY:日付_月,DAY:日付_日,初場所_娼館,初状況_肛門,CSTR:COUNT:初キス相手)
		ENDSELECT
	ENDIF
REND

;特別イベント処理
;オナニーコンテスト
IF FLAG:特殊営業で夜の部を開催フラグ == 1
	CALL ONANY_CONTEST
;フェラチオコンテスト
ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 2
	CALL FELLATIO_CONTEST
;人間競馬
ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 3
	CALL HUMAN_RACING
ENDIF

;特別イベント口上
IF FLAG:特殊営業で夜の部を開催フラグ != 0
	REPEAT CHARANUM
		SIF CFLAG:COUNT:特殊営業参加 == 0
			CONTINUE
		TARGET = COUNT

		;口上の判定
		TFLAG:口上番号 = 115
		CALL SELF_KOJO(TARGET)
		;念のため口上用フラグを空に
		TFLAG:口上番号 = 0
	REND
ENDIF

PRINTFORMW ……
PRINTFORML 

;成否判定
;客の欲望をチェック、乱交パーティーでの値は5です
A = 0
B = 0
C = 0

A = 5
;客の欲望Aをキャラの欲望の平均値が上回った場合は二倍、下回った場合は等倍が加算されます
E = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加
		E += ABL:COUNT:欲望
REND
E /= FLAG:特殊営業参加人数

IF E >= A 
	B += E * 2
ELSE
	B += E
ENDIF
E = 0

;参加者全員の技能・素質による修正値を加算
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE

	;全員の売春中毒を加算
	B += ABL:COUNT:売春中毒

	;性交中毒・精液中毒は1/2を加算
	B += (ABL:COUNT:性交中毒 / 2) + (ABL:COUNT:自慰中毒 / 2)

	;奉仕技術、性交技術は1/2を加算
	B += (ABL:COUNT:奉仕技術 / 2) + (ABL:COUNT:性交技術 / 2)

	;各素質に応じて加減
	;処女だった
	SIF CFLAG:COUNT:売春処女喪失
		B += 10
	;反抗的
	SIF TALENT:COUNT:ナマイキ && ABL:COUNT:欲望 <= 3
		B -= 3
	;無関心
	SIF TALENT:COUNT:クール && ABL:COUNT:欲望 <= 3
		B -= 5
	;感情乏しい
	SIF TALENT:COUNT:感情乏しい
		B -= 5
	;抑圧
	SIF TALENT:COUNT:抑圧
		B -= 3
	;舌使い
	SIF TALENT:COUNT:舌使い
		B += 1
	;献身的
	SIF TALENT:COUNT:献身的
		B += 2
	;快感に素直
	SIF TALENT:COUNT:快感に素直
		B += 3
	;淫乱
	SIF TALENT:COUNT:淫乱
		B += 10
	;倒錯的
	SIF TALENT:COUNT:倒錯的
		B += 3
	;マゾ
	SIF TALENT:COUNT:マゾ
		B += 5
	;露出狂
	SIF TALENT:COUNT:露出狂
		B += 5
	;魅惑
	SIF TALENT:COUNT:魅惑
		B += 2
	;謎の魅力
	SIF TALENT:COUNT:謎の魅力
		B += 5
	;爆乳
	SIF TALENT:COUNT:爆乳
		B += 3
	;巨乳
	SIF TALENT:COUNT:巨乳
		B += 2
	;貧乳をみる
	;小柄・幼稚・未熟があれば逆にボーナス
	IF TALENT:COUNT:貧乳
		IF TALENT:COUNT:小柄 || TALENT:COUNT:幼稚 || TALENT:COUNT:未熟
			B += 2
		ELSE
			B -= 2
		ENDIF
	ENDIF
	;絶壁をみる
	;幼稚・未熟があれば逆にボーナス
	IF TALENT:COUNT:絶壁
		IF TALENT:COUNT:幼稚 || TALENT:COUNT:未熟
			B += 3
		ELSE
			B -= 3
		ENDIF
	ENDIF
	;人気者
	SIF TALENT:COUNT:人気者
		B += 3
	;娼婦
	SIF TALENT:COUNT:風俗嬢
		B += 5
	;傾城
	SIF TALENT:COUNT:高級娼婦
		B += 10
	;常連客
	SIF TALENT:COUNT:常連客
		B += 3
	
	;Ｖ使用不可キャラ
	SIF CFLAG:COUNT:売春Ｖ不能
		B -= 10

	;狂気・崩壊があると満足度激減
	SIF TALENT:COUNT:狂気 || TALENT:COUNT:崩壊
		B /= 5
	
REND
	
C = 30 + A

;合計が目標値C以上なら大成功、C*3/4以上なら成功、下回れば失敗
IF B >= C
	PRINTFORML 今回の乱交パーティーに客達は大変満足したようだ
	A = 4
ELSEIF B >= C * 3 / 4
	PRINTFORML 今回の乱交パーティーに客達は満足したようだ
	A = 2
ELSE
	PRINTFORML どうやら、客達の欲望を満たすことは出来なかったようだ……
	A = 1
ENDIF

WAIT

DRAWLINE

PLAYER = TARGET
;パラメータ変化
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	TARGET = COUNT
	PRINTFORML %NAME:TARGET%のパラメータが以下の通り変化しました

	;参加人数が多ければ一人あたりの負担減
	;Ｖ使用回数は人数＋０～５
	TCVAR:Ｖ経験 = (G / FLAG:特殊営業参加人数) + RAND:6
	;Ｖ使用不可フラグチェック
	SIF CFLAG:売春Ｖ不能
		TCVAR:Ｖ経験 = 0
		
	;Ａ使用回数は人数＋０～５
	TCVAR:Ａ経験 = (G / FLAG:特殊営業参加人数) + RAND:6
		
	;フェラ回数は人数＋０～４
	TCVAR:フェラ経験 = (G / FLAG:特殊営業参加人数) + RAND:5
		
	;精液経験はＶ＋Ａ＋フェラ回数
	TCVAR:精液経験 = TCVAR:Ｖ経験 + TCVAR:Ａ経験 + TCVAR:フェラ経験

	;総性交経験はＶ＋Ａ使用回数
	TCVAR:性交経験 = TCVAR:Ｖ経験 + TCVAR:Ａ経験

	;否定の珠の計算（基礎値は人数ベースで、従順に応じて減少）
	M = 12 - ABL:信頼
	;否定の珠が増加しないという事はない
	SIF M <= 0
		M = 1
	U:100 = G * M

	;プライドが高いと増える
	SIF TALENT:活発
		TIMES U:100 , 1.50
	;無関心だと少なくなる
	SIF TALENT:クール
		TIMES U:100 , 0.90
	;感情乏しいと少なくなる
	SIF TALENT:感情乏しい
		TIMES U:100 , 0.90
	;目立ちたがりだと少なくなる
	SIF TALENT:目立ちたがり
		TIMES U:100 , 0.80
	;抑圧だと増える
	SIF TALENT:抑圧
		TIMES U:100 , 1.20
	;解放だと少なくなる
	SIF TALENT:解放
		TIMES U:100 , 0.80
	;恥じらいだと増える
	SIF TALENT:恥じらい
		TIMES U:100 , 1.10
	;露出狂だと一気に少なくなる
	SIF TALENT:露出狂
		TIMES U:100 , 0.50

	;成否に応じてさらに上下
	IF A >= 2
		TIMES U:100 , 0.80
	ELSE
		TIMES U:100 , 1.20
	ENDIF

	;特別イベントをやると増える
	IF FLAG:特殊営業で夜の部を開催フラグ == 1
		TIMES U:100 , 1.20
	ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 2
		TIMES U:100 , 1.20
	ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 3
		TIMES U:100 , 1.50
	ENDIF

	;輪姦によって発生する経験・珠
	X = COUNT
	CALL WORK_SP_SEX_01

	;オナニーコンテストをやった
	IF FLAG:特殊営業で夜の部を開催フラグ == 1
		TCVAR:絶頂経験 += 3
		TCVAR:自慰経験 += 3
		TCVAR:調教自慰経験 += 3
	;フェラチオコンテストをやった
	ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 2
		TCVAR:フェラ経験 += 1
		TCVAR:精液経験 += 2
	;人間競馬をやった
	ELSEIF FLAG:特殊営業で夜の部を開催フラグ == 3
		TCVAR:被虐快楽経験 += 10 * ABL:マゾっ気
	ENDIF

	DRAWLINE

	CALL WORK_EXP
	COUNT = X

	;反発刻印取得の判定
	;従順の100倍+100まで許容します
	O = ABL:信頼 * 100 + 100

	;反発刻印LV2はLV1の√3倍、LV3は√5倍で獲得
	I = O
	TIMES I , 2.23
	J = O
	TIMES J , 1.73

	IF MARK:反発刻印 != 3 && JUEL:否定 > I
		PRINTL  反発刻印LV3を取得
		MARK:COUNT:反発刻印 = 3
		IF MARK:4 <= 2
			MARK:4 = 3
			;従順がLV8以下ならば従順が2LVダウン（[感情乏しい]の場合はダウンなし）
			IF ABL:信頼 <= 8 && TALENT:感情乏しい == 0
				ABL:信頼 -= 2
				PRINTFORML  そして%ABLNAME:[[ABL:信頼]]%がLV{ABL:信頼}に下がった
			ENDIF
		ENDIF
	ELSEIF MARK:反発刻印 != 2 && JUEL:否定 > J
		PRINTL  反発刻印LV2を取得
		MARK:反発刻印 = 2
		IF MARK:4 <= 1
			MARK:4 = 2
			;従順がLV4以下ならば従順が1LVダウン（[感情乏しい]の場合はダウンなし）
			IF ABL:信頼 <= 4 && TALENT:感情乏しい == 0
				ABL:信頼 -= 1
				PRINTFORML  そして%ABLNAME:[[ABL:信頼]]%がLV{ABL:信頼}に下がった
			ENDIF
		ENDIF
	ELSEIF MARK:反発刻印 != 1 && JUEL:否定 > O
		PRINTL  反発刻印LV1を取得
		MARK:反発刻印 = 1
	ENDIF

	DRAWLINE

	;体力減少の計算
	LOSEBASE:体力 = G * 25

	;処女喪失だとかなり増える
	SIF CFLAG:売春処女喪失
		TIMES LOSEBASE:体力 , 2.00

	;売春中毒のレベルに応じてちょっとずつ減る
	IF ABL:売春中毒 == 1
		TIMES LOSEBASE:体力 , 0.90
	ELSEIF ABL:売春中毒 == 2
		TIMES LOSEBASE:体力 , 0.80
	ELSEIF ABL:売春中毒 == 3
		TIMES LOSEBASE:体力 , 0.70
	ELSEIF ABL:売春中毒 == 4
		TIMES LOSEBASE:体力 , 0.60
	ELSEIF ABL:売春中毒 >= 5
		TIMES LOSEBASE:体力 , 0.50
	ENDIF
	
	;Ｖ経験、Ａ経験が一定値以下だった場合体力消費増加
	SIF EXP:Ｖ経験 <= 200 && CFLAG:売春Ｖ不能 == 0
		TIMES LOSEBASE:体力 , 1.10
	SIF EXP:Ａ経験 <= 200
		TIMES LOSEBASE:体力 , 1.20

	;人間競馬だと疲れる
	SIF FLAG:特殊営業で夜の部を開催フラグ == 3
		TIMES LOSEBASE:体力 , 1.30

	;参加人数に応じて疲労軽減
	IF FLAG:特殊営業参加人数 >= 6
		TIMES LOSEBASE:体力 , 0.60
	ELSEIF FLAG:特殊営業参加人数 >= 5
		TIMES LOSEBASE:体力 , 0.70
	ELSEIF FLAG:特殊営業参加人数 >= 4
		TIMES LOSEBASE:体力 , 0.80
	ENDIF

	;警備員の人数に応じてちょっとずつ減る（最大2割減）
	LOSEBASE:体力 *= 100 - FLAG:警備員人数 * 2
	LOSEBASE:体力 /= 100
	
	;警備員の人数に応じて即死回避
	IF LOSEBASE:体力 >= BASE:体力
		;警備員の人数*9+9%の確率で体力が1残る（救済フラグを立てる）
		SIF RAND:100 < (FLAG:警備員人数 * 9 + 9)
		LOSEBASE:体力 = BASE:体力 - 1
	ENDIF

	SIF LOSEBASE:体力 >= BASE:体力
		LOSEBASE:体力 = BASE:体力

	PRINT  体力消費　　:
	N = BASE:体力
	CALL FIGURE_INDENT
	PRINTV BASE:体力
	PRINT  - 
	N = LOSEBASE:体力
	CALL FIGURE_INDENT
	PRINTV LOSEBASE:体力
	PRINT  = 
	N = BASE:体力 - LOSEBASE:体力
	CALL FIGURE_INDENT
	PRINTVL BASE:体力 - LOSEBASE:体力

	BASE:体力 -= LOSEBASE:体力

	PRINT  体力　　　　:
	BAR BASE:体力, MAXBASE:体力, 32
	PRINTFORML ({BASE:体力}/{MAXBASE:体力})

	;体力が0以下、または500以下だった場合の処理
	IF BASE:体力 <= 0
		PRINTFORML %CALLNAME:TARGET%は乱交パーティーが終わった後で突っ伏すように倒れた
		PRINTFORML 何をやっても反応がない……
		WAIT
		;キャラの死亡確認
		CALL CHARADEAD_CHECK
	ELSEIF BASE:体力 <= 500
		PRINTFORML 体力が限界に来ています。休ませてあげてください
	ENDIF

	DRAWLINE
	WAIT
REND
TARGET = PLAYER

;売上金を計算します
Q = 0

;基準額
V = 100000
PRINT 基準額
PRINTFORML  +{V}
Q += V
V = 0

;大成功によるボーナス
IF A >= 3
	V = 130
	PRINT 大成功
	PRINTFORML  ×{V/100}.{V%100}
	Q *= V
	Q /= 100
	V = 0
;失敗によるペナルティ
ELSEIF A == 1
	V = 70
	PRINT 失敗
	PRINTFORML  ×{V/100}.{V%100}
	Q *= V
	Q /= 100
	V = 0
ENDIF

;参加者による補正
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	;処女喪失ならば増える
	IF CFLAG:COUNT:売春処女喪失
		V = 200
		PRINTFORM 処女喪失(%CALLNAME:COUNT%)
		PRINTFORML  ×{V/100}.{V%100}
		Q *= V
		Q /= 100
		V = 0
	ENDIF
	;Ｖ使用不可キャラがいると減る
	IF CFLAG:COUNT:売春Ｖ不能
		V = 70
		IF TALENT:COUNT:オトコ
			PRINT オトコ
		ELSE
			PRINT Ｖ不可
		ENDIF
		PRINTFORM (%CALLNAME:COUNT%)
		PRINTFORML  ×{V/100}.{V%100}
		Q *= V
		Q /= 100
		V = 0
	ENDIF
REND

;共通する獲得資金処理
CALL WORK_SP_MONEY_CM

PRINTFORML 今回の乱交パーティーでは{Q}ポイントのチケットで{G-1}人の客を招待し、

;チケット一枚あたりの金額×客の人数が収入となる（参加者の内1人は主人公なので-1）
Q *= G - 1

PRINTFORML 合計{Q}ポイント稼ぎました
MONEY += Q

;特殊営業終了処理
CALL WORK_SP_AFTER

WAIT

BEGIN TURNEND

RETURN 1

;-----------------------------------------------------------
;オナニーコンテスト
;-----------------------------------------------------------
@ONANY_CONTEST
PRINTFORMW …
PRINTFORML 
PRINTFORML 乱交パーティーの締めくくりに余興として、
PRINTFORML オナニーコンテストを行うことにした……
WAIT
PRINTFORML 《奴隷たちが一斉にオナニーを行い、誰が一番最初に三回絶頂するかを競います》
WAIT
$INPUT_LOOP_02
PRINTFORML 誰に賭けますか？
DRAWLINE
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	PRINTFORML  [{COUNT}] - %NAME:COUNT%
	;ついでに達成回数をクリア
	CFLAG:COUNT:便所公開浣腸 = 0
REND
DRAWLINE
INPUT

IF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP_02
ELSEIF CFLAG:RESULT:特殊営業参加 == 0
	GOTO INPUT_LOOP_02
ELSEIF RESULT == 100
	RETURN 0
ENDIF

Y = RESULT

PRINTFORML パーティー会場の壁際に並んだ奴隷たちは、
PRINTFORML %CALLNAME:MASTER%の合図に合わせて、一斉にオナニーを始めた……
WAIT

$LOOP_ONANY

;登録番号で有利不利が決まらないように判定順をランダムにする処理
F = FLAG:特殊営業参加人数
E = RAND:F
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	IF E == 0
		TARGET = COUNT
		BREAK
	ELSE
		E -= 1
	ENDIF
REND

;判定変数の設定
E = 0
;自慰中毒は2倍
E += ABL:自慰中毒*2
;露出癖は等倍
E += ABL:露出癖
;抑圧
SIF TALENT:抑圧
	E -= 2
;解放
SIF TALENT:解放
	E += 2
;自慰しやすい
SIF TALENT:自慰しやすい
	E += 2
;自慰狂い
SIF TALENT:自慰狂い
	E += 5
;判定変数Eが乱数(0～99)より小さければ失敗
SIF E < RAND:100
	GOTO LOOP_ONANY

;絶頂回数を加算
CFLAG:便所公開浣腸 += 1
IF ABL:Ａ感覚 > ABL:Ｖ感覚 && ABL:Ａ感覚 > ABL:Ｃ感覚
	PRINT 自分のアナルをほじり続けていた
ELSEIF ABL:Ｖ感覚 > ABL:Ｃ感覚 && TALENT:オトコ == 0 && CFLAG:売春Ｖ不能 == 0
	PRINT ヴァギナに指を出し入れし続けていた
ELSEIF TALENT:オトコ || TALENT:ふたなり
	PRINT ペニスをしごき続けていた
ELSE
	PRINT クリトリスをいじり続けていた
ENDIF
PRINTFORM %CALLNAME:TARGET%が
IF CFLAG:便所公開浣腸 == 1
	PRINT ぶるっと体を震わせ
ELSEIF CFLAG:便所公開浣腸 == 2
	PRINT 切なそうな声を上げ
ELSEIF CFLAG:便所公開浣腸 == 3
	PRINT 大きく背中をのけぞらせ
ENDIF

IF TALENT:オトコ || TALENT:ふたなり
	PRINTL て、ザーメンを勢いよく迸らせた……
ELSEIF CFLAG:便所公開浣腸 == 3
	PRINTL て、
	PRINTL 股間から愛液の筋を飛ばした……
ELSE
	PRINTL た……
ENDIF
IF CFLAG:便所公開浣腸 == 1
	PRINT そして頬を赤くしながら片手を上げて、
ELSEIF CFLAG:便所公開浣腸 == 2
	PRINT そしてオナニーを続けながら、上ずった声で
ELSEIF CFLAG:便所公開浣腸 == 3
	PRINT 最後に息を弾ませながら、
ENDIF
PRINTFORMW {CFLAG:便所公開浣腸}回目の絶頂を迎えた事を申告した
PRINTFORML 

;絶頂回数が３未満ならコンテスト継続
SIF CFLAG:便所公開浣腸 < 3
	GOTO LOOP_ONANY

PRINTFORML %CALLNAME:TARGET%は、これで計三回の絶頂を迎えた
WAIT
PRINTFORML 《オナニーコンテストの優勝者は、%NAME:TARGET%に決定しました》
W = TARGET
WAIT
IF Y == TARGET
	PRINTFORML %CALLNAME:MASTER%の予想は見事に的中し、
	PRINTFORML 他の客が賭けた{FLAG:特殊営業参加人数*1000}ポイントが%CALLNAME:MASTER%の物になった
	MONEY += FLAG:特殊営業参加人数*1000
ELSE
	PRINTFORML 残念ながら、%CALLNAME:MASTER%の予想は外れてしまった……
	PRINTFORML %CALLNAME:Y%に賭けた1000ポイントは没収されてしまった
	MONEY -= 1000
ENDIF

WAIT

RETURN 1

;-----------------------------------------------------------
;フェラチオコンテスト
;-----------------------------------------------------------
@FELLATIO_CONTEST
PRINTFORMW …
PRINTFORML 
PRINTFORML 乱交パーティーの締めくくりに余興として、
PRINTFORML 奴隷と客をペアにしてフェラチオコンテストを行った……
WAIT
PRINTFORML 《奴隷がペアを組んだ客にフェラチオを行い、どのペアが最初に発射するか競います》
WAIT
$INPUT_LOOP_02
PRINTFORML 誰に賭けますか？（ここで選んだ奴隷が%CALLNAME:MASTER%とペアを組みます）
DRAWLINE
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	PRINTFORML  [{COUNT}] - %NAME:COUNT%
	;ついでに達成回数をクリア
	CFLAG:COUNT:便所公開浣腸 = 0
REND
DRAWLINE
INPUT

IF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP_02
ELSEIF CFLAG:RESULT:特殊営業参加 == 0
	GOTO INPUT_LOOP_02
ELSEIF RESULT == 100
	RETURN 0
ENDIF

Y = RESULT

PRINTFORML %CALLNAME:Y%が、%CALLNAME:MASTER%の股間に顔を埋める位置にかがみ込んだ
SIF TALENT:Y:恋心
	PRINTFORML 股間から、%CALLNAME:Y%は信頼のこもった眼差しで見上げている
WAIT

PRINTFORML ペアを組んだ相手の足の間にひざまずいた奴隷たちは、
PRINTFORML %CALLNAME:MASTER%の合図に合わせて一斉に目の前の肉棒をしゃぶり始めた……
WAIT

$LOOP_FELLATIO

;登録番号で有利不利が決まらないように判定順をランダムにする処理
F = FLAG:特殊営業参加人数
E = RAND:F
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	IF E == 0
		TARGET = COUNT
		BREAK
	ELSE
		E -= 1
	ENDIF
REND

;判定変数の設定
E = 0
;精液中毒は2倍
E += ABL:精液中毒*2
;奉仕精神は等倍
E += ABL:奉仕精神
;好奇心
SIF TALENT:好奇心
	E += 2
;保守的
SIF TALENT:保守的
	E -= 2
;舌使い
SIF TALENT:舌使い
	E += 5
;献身的
SIF TALENT:献身的
	E += 2
;主人公と組んで主人公が早漏
SIF TARGET == Y && TALENT:MASTER:早漏
	E += 5
;判定変数Eが乱数(0～99)より小さければ失敗
SIF E < RAND:100
	GOTO LOOP_FELLATIO

;射精ゲージを加算
CFLAG:便所公開浣腸 += 1
IF CFLAG:便所公開浣腸 == 1
	IF TARGET == Y
		PRINTFORML %CALLNAME:TARGET%の舌使いに、%CALLNAME:MASTER%は思わず声を洩らした……
	ELSE
		PRINTFORML %CALLNAME:TARGET%にペニスをしゃぶられている客が微かな声を上げた……
	ENDIF
	WAIT
	GOTO LOOP_FELLATIO
ELSEIF  CFLAG:便所公開浣腸 == 2
	IF TARGET == Y
		PRINTFORML %CALLNAME:TARGET%に舐めまわされている%CALLNAME:MASTER%の陰茎の根元が強く疼いている
	ELSE
		PRINTFORML %CALLNAME:TARGET%の口に出し入れされている客のペニスが強く脈打っている
	ENDIF
	PRINTFORML 射精の瞬間が近付いているようだ……
	WAIT
	GOTO LOOP_FELLATIO
ENDIF 

;射精ゲージが３以上になったら終了

IF TARGET == Y
	PRINTFORMW 限界を迎えた%CALLNAME:MASTER%は、%CALLNAME:TARGET%の口の中へ思い切り射精した
ELSE
	PRINTFORMW %CALLNAME:TARGET%に奉仕させていた客が、%CALLNAME:TARGET%の口の中に大量に射精した
ENDIF
SIF ABL:精液中毒 >= 3 || TALENT:舌使い
	PRINTFORMW %CALLNAME:TARGET%は精液を飲み終えると、舌を伸ばしてペニスに残った精液も綺麗に舐め取った
IF TARGET == Y && TALENT:恋心
	PRINTFORML 股間でうごめく頭を%CALLNAME:MASTER%が軽く撫でて労をねぎらってやると、
	PRINTFORMW %CALLNAME:TARGET%は嬉しそうに下腹部に頬をすりよせてきた
ENDIF
WAIT
PRINTFORML 《フェラチオコンテストの優勝者は、%NAME:TARGET%に決定しました》
W = TARGET
WAIT
IF Y == TARGET
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:Y%のペアは見事に勝利し、
	PRINTFORML 他の客が賭けた{FLAG:特殊営業参加人数*1000}ポイントが%CALLNAME:MASTER%の物になった
	MONEY += FLAG:特殊営業参加人数*1000
ELSE
	PRINTFORML 残念ながら、%CALLNAME:MASTER%と%CALLNAME:Y%のペアは敗北した……
	PRINTFORML %CALLNAME:Y%に賭けた1000ポイントは没収されてしまった
	MONEY -= 1000
ENDIF

WAIT

RETURN 1

;-----------------------------------------------------------
;人間競馬
;-----------------------------------------------------------
@HUMAN_RACING
PRINTFORMW …
PRINTFORML 
PRINTFORML 乱交パーティーの締めくくりに余興として、
PRINTFORML 奴隷を牝馬代わりに使った人間競馬を行った……
WAIT
PRINTFORML 《四つん這いになった奴隷の背中に客が一人ずつまたがり、
PRINTFORML 　会場の端から端まで五往復走らせて一位を競います》
WAIT
$INPUT_LOOP_02
PRINTFORML 誰に賭けますか？（ここで選んだ奴隷が%CALLNAME:MASTER%の乗る馬になります）
DRAWLINE
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	TARGET = COUNT
	PRINTFORML  [{COUNT}] - %NAME:COUNT%
	;ついでに周回数と消耗体力をクリア
	CFLAG:COUNT:便所公開浣腸 = 0
	CFLAG:COUNT:55 = 0
REND
DRAWLINE
INPUT

IF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP_02
ELSEIF CFLAG:RESULT:特殊営業参加 == 0
	GOTO INPUT_LOOP_02
ELSEIF RESULT == 100
	RETURN 0
ENDIF

Y = RESULT
Z = 0

PRINTFORM 床の上に両手両足を突いた%CALLNAME:Y%の背中に、
PRINTFORMW %CALLNAME:MASTER%が体重をかけて跨った
IF (TALENT:Y:小柄 && TALENT:MASTER:小柄 == 0) || (TALENT:Y:大柄 == 0 && TALENT:MASTER:大柄)
	PRINTFORML %CALLNAME:Y%は%CALLNAME:MASTER%の体重にほとんど押し潰されそうになり、
	PRINTFORMW 顔を真っ赤にして両腕を突っ張り、辛うじて胴体を浮かせているのがやっとだ
	PRINTFORM 正直、この騎手と
	IF TALENT:Y:オトコ
		PRINT 牡
	ELSE
		PRINT 牝
	ENDIF
	PRINTFORML 馬の組み合わせは無理があったかもしれない――
ELSEIF (TALENT:Y:大柄 && TALENT:MASTER:大柄 == 0) || (TALENT:Y:小柄 == 0 && TALENT:MASTER:小柄)
	PRINTFORMW %CALLNAME:Y%の背中は、%CALLNAME:MASTER%の体重にもしっかりと持ち堪えている
	PRINTFORM これは、なかなか頼りがいのある
	IF TALENT:Y:オトコ
		PRINT 牡
	ELSE
		PRINT 牝
	ENDIF
	PRINTFORML 馬になってくれそうだ
ELSE
	PRINTFORML %CALLNAME:Y%は%CALLNAME:MASTER%の体重に背中を大きくしならせたものの、
	PRINTFORML なんとか歯を食いしばって両手両足に力をこめ、持ち堪えた
ENDIF
WAIT

PRINTFORML 騎手役の客を背中に乗せてスタートラインに並んだ騎馬役の奴隷たちは、
PRINTFORML %CALLNAME:MASTER%の合図に合わせて、一斉にホールの反対側の壁目指して這いずり始めた
WAIT

$INPUT_LOOP_03
FLAG:特殊営業タイプ詳細 = 0
;調教時テキスト省略設定の場合は、自動的に競技状況を省略する
IF (FLAG:指導テキスト設定 & 1)
	PRINTL ……競技実行中……
	PRINTL 
ELSE
	PRINTFORML 競技中の状況を省略しますか？
	PRINTFORML  [0] - 省略する
	PRINTFORML  [1] - 省略しない
	INPUT
	IF RESULT == 1
		FLAG:特殊営業タイプ詳細 = 1
	ELSEIF RESULT == 0
		PRINTL ……競技実行中……
		PRINTL 
	ELSEIF RESULT != 0
		GOTO INPUT_LOOP_03
	ENDIF
ENDIF

$LOOP_RACING

REPEAT CHARANUM
	TARGET = COUNT

	SIF CFLAG:特殊営業参加 == 0
		CONTINUE
	SIF BASE:体力 - CFLAG:55 < 100
		CONTINUE

	;判定変数の設定
	E = 20
	;残り体力による修正
	E += (BASE:体力 - CFLAG:55) / 200
	;大柄
	SIF TALENT:大柄
		E += 5
	;小柄
	SIF TALENT:小柄
		E -= 5
	;未熟
	SIF TALENT:未熟
		E -= 5
	;主人と組んで主人が小柄
	SIF TARGET == Y && TALENT:MASTER:小柄
		E += 5
	;主人と組んで主人が大柄
	SIF TARGET == Y && TALENT:MASTER:大柄
		E -= 5
	;反抗的
	SIF TALENT:ナマイキ
		E -= 5
	;素直
	SIF TALENT:素直
		E += 5

	;失敗時でも体力は2消耗
	CFLAG:55 += 2
	IF BASE:体力 - CFLAG:55 < 100
		CFLAG:便所公開浣腸 = -1
		Z += 1
		IF TARGET == Y && FLAG:特殊営業タイプ詳細
			PRINTFORML 息を激しく喘がせながらよろよろと進んでいた%CALLNAME:TARGET%は、
			PRINTFORMW ついに%CALLNAME:MASTER%を乗せたままぺったり床に這いつくばった
			PRINTFORMW 何度か尻を平手で叩いてやったが、もう一歩も進めないらしい
			PRINTFORML 残念ながら、ここでリタイヤのようだ……
			PRINTFORML （残り{FLAG:特殊営業参加人数-Z}人）
			WAIT
		ELSEIF FLAG:特殊営業タイプ詳細
			PRINTFORML 客を背中に乗せてよろよろと進んでいた%CALLNAME:TARGET%は、
			PRINTFORMW ぺったりと床に這いつくばって、息を喘がせたまま動かなくなった
			PRINTFORML どうやら、リタイヤのようだ……
			PRINTFORML （残り{FLAG:特殊営業参加人数-Z}人）
			WAIT
		ENDIF
		CONTINUE
	ENDIF

	;判定変数Eが乱数(0～99)より小さければ失敗
	SIF E < RAND:100
		CONTINUE

	;体力の消耗
	CFLAG:55 += 10 + RAND:5
	;大柄
	SIF TALENT:大柄
		CFLAG:55 -= 5
	;小柄
	SIF TALENT:小柄
		CFLAG:55 += 10
	;主人と組んで主人が小柄
	SIF TARGET == Y && TALENT:MASTER:小柄
		CFLAG:55 -= 5
	;主人と組んで主人が大柄
	SIF TARGET == Y && TALENT:MASTER:大柄
		CFLAG:55 += 10

	;走行距離を加算
	CFLAG:便所公開浣腸 += 1

	IF (CFLAG:便所公開浣腸 == 10 || CFLAG:便所公開浣腸 == 30 || CFLAG:便所公開浣腸 == 50 || CFLAG:便所公開浣腸 == 70 || CFLAG:便所公開浣腸 == 90) && FLAG:特殊営業タイプ詳細
		IF TARGET == Y
			PRINTFORM %CALLNAME:MASTER%を背中に乗せた%CALLNAME:TARGET%が、
		ELSE
			PRINTFORM 客を背中に乗せた%CALLNAME:TARGET%が、
		ENDIF
		CALL HUMAN_RACING_ORDER
		PRINTL で向こうの壁に手を突いた
		PRINT （残り
		IF CFLAG:便所公開浣腸 == 90
		PRINTL 半往復）
		ELSE
		PRINTFORML {4 - (CFLAG:便所公開浣腸 / 20)}往復半）
		ENDIF
		WAIT
	ELSEIF (CFLAG:便所公開浣腸 == 20 || CFLAG:便所公開浣腸 == 40 || CFLAG:便所公開浣腸 == 60 || CFLAG:便所公開浣腸 == 80) && FLAG:特殊営業タイプ詳細
		IF TARGET == Y
			PRINTFORM %CALLNAME:MASTER%を乗せた%CALLNAME:TARGET%が、
		ELSE
			PRINTFORM 客を背中に乗せた%CALLNAME:TARGET%が、
		ENDIF
		CALL HUMAN_RACING_ORDER
		PRINTL でスタートラインに戻って来た
		PRINTFORML （残り{5 - (CFLAG:便所公開浣腸 / 20)}往復）
		IF BASE:体力 - CFLAG:55 < 1200 && BASE:体力 - CFLAG:55 >= 700
			PRINTFORML %CALLNAME:TARGET%はそろそろ息が上がりかけて、手足もふらついている
		ELSEIF BASE:体力 - CFLAG:55 < 700 && BASE:体力 - CFLAG:55 >= 300
			PRINTFORML %CALLNAME:TARGET%は顔を真っ赤にして息を喘がせ、それでもなんとかよろよろと進んでいる
		ELSEIF BASE:体力 - CFLAG:55 < 300 && BASE:体力 - CFLAG:55 >= 100
			PRINTFORML 全身汗だくの%CALLNAME:TARGET%はほとんど床に体を擦り付けて、気力だけで這いずっている
		ENDIF
		WAIT
	ELSEIF CFLAG:便所公開浣腸 >= 100
		IF TARGET == Y
			PRINTFORM %CALLNAME:MASTER%を乗せた%CALLNAME:TARGET%が、
		ELSE
			PRINTFORM 客を背中に乗せた%CALLNAME:TARGET%が、
		ENDIF
		PRINTL 乱交パーティーに集まった客たちの拍手の中、一番にゴールの壁にタッチした
		WAIT
		BREAK
	ENDIF

	IF BASE:体力 - CFLAG:55 < 100
		CFLAG:便所公開浣腸 = -1
		Z += 1
		IF TARGET == Y && FLAG:特殊営業タイプ詳細
			PRINTFORML 息を激しく喘がせながらよろよろと進んでいた%CALLNAME:TARGET%は、
			PRINTFORMW ついに%CALLNAME:MASTER%を乗せたままぺったり床に這いつくばった
			PRINTFORMW 何度か尻を平手で叩いてやったが、もう一歩も進めないらしい
			PRINTFORML 残念ながら、ここでリタイヤのようだ……
			PRINTFORML （残り{FLAG:特殊営業参加人数-Z}人）
			WAIT
		ELSEIF FLAG:特殊営業タイプ詳細
			PRINTFORML 客を背中に乗せてよろよろと進んでいた%CALLNAME:TARGET%は、
			PRINTFORMW ぺったりと床に這いつくばって、息を喘がせたまま動かなくなった
			PRINTFORML どうやら、リタイヤのようだ……
			PRINTFORML （残り{FLAG:特殊営業参加人数-Z}人）
			WAIT
		ENDIF
	ENDIF

REND

;完走者が出たか、あるいは全員リタイアしたかのチェック
E = 0
REPEAT CHARANUM
	TARGET = COUNT
	SIF CFLAG:特殊営業参加 == 0
		CONTINUE
	SIF CFLAG:便所公開浣腸 >= 100
		E = TARGET
REND

IF E
	TARGET = E
	PRINTFORMW 《人間競馬の優勝者は、%NAME:TARGET%に決定しました》
	W = TARGET
	CALL HUMAN_RACING_RESULT
	WAIT
	IF Y == TARGET
		PRINTFORML %CALLNAME:MASTER%と%CALLNAME:Y%のペアは見事に勝利し、
		PRINTFORML 他の客が賭けた{FLAG:特殊営業参加人数*1000}ポイントが%CALLNAME:MASTER%の物になった
		MONEY += FLAG:特殊営業参加人数*1000
	ELSE
		PRINTFORML 残念ながら、%CALLNAME:MASTER%と%CALLNAME:Y%のペアは敗北した……
		PRINTFORML %CALLNAME:Y%に賭けた1000ポイントは没収されてしまった
		MONEY -= 1000
	ENDIF
	
	WAIT
	
	RETURN 1
ELSEIF Z >= FLAG:特殊営業参加人数
	PRINTFORML 決着が付く前に、すべての牝馬がリタイアしてしまった
	PRINTFORML 《今回の人間競馬は引き分けです》
	WAIT
	RETURN 1
ELSE
	GOTO LOOP_RACING
ENDIF

@HUMAN_RACING_ORDER
X = COUNT
E = 1
REPEAT CHARANUM
	SIF COUNT == TARGET
		CONTINUE
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	SIF CFLAG:COUNT:便所公開浣腸 >= CFLAG:便所公開浣腸
		E += 1
REND
IF E == 1
	PRINT トップ
ELSEIF E == FLAG:特殊営業参加人数 - Z
	PRINT ドン尻
ELSE
	PRINTFORM {E}位
ENDIF
COUNT = X

@HUMAN_RACING_RESULT
DRAWLINE
PRINTL     ～結果発表～
F:1 = TARGET
F:2 = 1
$RACING_RESULT_LOOP
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	TARGET = COUNT
	CALL HUMAN_RACING_ORDER_2
	SIF E != F:2
		CONTINUE
	SIF F:2 < 10
		PRINT  
	PRINTFORM   {F:2}位 %NAME:TARGET%
	SIF Y == TARGET
		PRINTFORM (騎手:%NAME:MASTER%)
	PRINTL 
	BREAK
REND
F:2 += 1
SIF F:2 <= FLAG:特殊営業参加人数 - Z
	GOTO RACING_RESULT_LOOP
REPEAT CHARANUM
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	SIF CFLAG:COUNT:便所公開浣腸 >= 0
		CONTINUE
	PRINTFORM   脱落 %NAME:COUNT%
	SIF Y == COUNT
		PRINTFORM (騎手:%NAME:MASTER%)
	PRINTL 
REND
TARGET = F:1
F:1 = 0
F:2 = 0
DRAWLINE

@HUMAN_RACING_ORDER_2
X = COUNT
E = 1
REPEAT CHARANUM
	SIF COUNT == TARGET
		CONTINUE
	SIF CFLAG:COUNT:特殊営業参加 == 0
		CONTINUE
	SIF CFLAG:COUNT:便所公開浣腸 >= CFLAG:便所公開浣腸
		E += 1
	;同着の馬がいた場合の処理
	SIF CFLAG:COUNT:便所公開浣腸 == CFLAG:便所公開浣腸
		CFLAG:COUNT:便所公開浣腸 -= 1
REND
COUNT = X
