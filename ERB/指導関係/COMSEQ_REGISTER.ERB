@COMSEQ_REGISTER_BEGIN
VARSET LOCAL
DRAWLINE

PRINTL どれを変更しますか？
FOR LOCAL,1,6
	PRINTFORM [{LOCAL}] - 
	IF SAVESTR:(LOCAL + 15) == ""
		IF SAVESTR:(LOCAL + 10) == ""
			PRINT 新規登録
		ELSE
			PRINT 登録済み
		ENDIF
	ELSE
		PRINTS SAVESTR:(LOCAL + 15)
	ENDIF
	PRINTL 
NEXT
PRINTL [99] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 99
	RETURN 1
ELSEIF RESULT >= 1 && RESULT < 6
	CALL COMSEQ_REGISTER(RESULT)
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


@COMSEQ_REGISTER(ARG)
VARSET LOCALS
LOCALS = %SAVESTR:(ARG + 10)%
$LOOP
VARSET LOCAL
DRAWLINE

PRINT 登録名:
IF SAVESTR:(ARG + 15) != ""
	PRINTS SAVESTR:(ARG + 15)
ENDIF
PRINTL 
PRINT 現在の登録内容:
CALL COMSEQ_REGISTER_DATA(LOCALS)
PRINTL 
DRAWLINE

FOR LOCAL,0,200
	SIF LOCAL == 110 || LOCAL == 111
		CONTINUE
	SIF TRAINNAME:LOCAL == ""
		CONTINUE
	TRYCCALLFORM COM_ABLE{LOCAL}
		IF RESULT
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			LOCAL:1++
			SIF LOCAL:1 % 3 == 0
				PRINTL 
		ENDIF
	CATCH
	ENDCATCH
NEXT
PRINTL 
PRINTC 一つ戻る[990]
PRINTC 繰り返し[991]
PRINTC リセット[992]
PRINTL 
PRINTC 登録名をつける[997]
PRINTC キャンセル[998]
PRINTC 決定[999]
$INPUT_LOOP
INPUT

IF RESULT == 990
	CALL COMSEQ_REGISTER_BACK(LOCALS)
	LOCALS = %RESULTS%
ELSEIF RESULT == 991
	CALL COMSEQ_REGISTER_REPEAT(LOCALS)
	LOCALS = %RESULTS%
ELSEIF RESULT == 992
	LOCALS = 
ELSEIF RESULT == 997
	PRINTL 登録名を入力して下さい(未入力で戻る)
	INPUTS
	SAVESTR:(ARG + 15) = %RESULTS%
ELSEIF RESULT == 998
	RETURN 1
ELSEIF RESULT == 999
	SAVESTR:(ARG + 10) = %LOCALS%
	RETURN 1
ELSEIF RESULT >= 0 && RESULT < 200 && RESULT != 110 && RESULT != 111 && TRAINNAME:RESULT != ""
	LOCAL = RESULT
	TRYCCALLFORM COM_ABLE{LOCAL}
		IF RESULT && COMSEQ_REGISTER_NUMBER(LOCALS) < 10
			LOCALS:1 = /%TOSTR(LOCAL)%
			LOCALS += LOCALS:1
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP
		ENDIF
	CATCH
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDCATCH
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

GOTO LOOP


@COMSEQ_REGISTER_BACK(ARGS)
#DIMS TMPS
#DIMS BACK_RESULTS
TMPS = 
BACK_RESULTS = 

VARSET LOCAL
VARSET LOCALS
SPLIT ARGS,"/",LOCALS
LOCAL:1 = RESULT
IF LOCAL:1 < 3
	RESULTS = 
	RETURN 1
ENDIF
FOR LOCAL,1,LOCAL:1 - 1
	TMPS = /%LOCALS:LOCAL%
	BACK_RESULTS += TMPS
NEXT
RESULTS = %BACK_RESULTS%

@COMSEQ_REGISTER_REPEAT(ARGS)
#DIMS TMPS
#DIMS REPEAT_RESULTS
TMPS = 
REPEAT_RESULTS = 

VARSET LOCAL
VARSET LOCALS
SPLIT ARGS,"/",LOCALS
LOCAL:1 = RESULT
SIF LOCAL:1 == 1
	RETURN 0

LOCAL = LOCAL:1 - 1
WHILE LOCAL <= 10
	REPEAT_RESULTS += ARGS
	LOCAL += LOCAL:1 - 1
WEND
RESULTS = %REPEAT_RESULTS%

@COMSEQ_REGISTER_DATA(ARGS)
VARSET LOCALS
VARSET LOCAL
SPLIT ARGS,"/",LOCALS
LOCAL:1 = RESULT
FOR LOCAL,1,LOCAL:1
	PRINTS TRAINNAME:(TOINT(LOCALS:LOCAL))
	PRINT →
NEXT

@COMSEQ_REGISTER_NUMBER(ARGS)
#FUNCTION
VARSET LOCAL
VARSET LOCALS
SPLIT ARGS,"/",LOCALS
LOCAL = RESULT - 1
RETURNF LOCAL



@SHOW_COMSEQ_REGISTER
VARSET LOCAL
DRAWLINE
PRINTL どれを表示しますか？
FOR LOCAL,1,6
	PRINTFORM [{LOCAL}] - 
	IF SAVESTR:(LOCAL + 15) == ""
		IF SAVESTR:(LOCAL + 10) == ""
			PRINT 未登録
		ELSE
			PRINT 登録済み
		ENDIF
	ELSE
		PRINTS SAVESTR:(LOCAL + 15)
	ENDIF
	PRINTL 
NEXT
PRINTL [9] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 9
	RETURN 1
ELSEIF RESULT >= 1 && RESULT < 6
	CALL COMSEQ_REGISTER_DATA(SAVESTR:(RESULT + 10))
	WAIT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RESTART


@COMSEQ_REGISTER_TRAIN
VARSET LOCAL
VARSET LOCALS

IF TFLAG:901
	SPLIT SAVESTR:(TFLAG:901 + 10),"/",LOCALS
	IF RESULT <= TFLAG:900
		TFLAG:900 = 0
		TFLAG:901 = 0
	ELSE
		CALLFORM COM_ABLE{TOINT(LOCALS:(TFLAG:900))}
		IF RESULT
			DOTRAIN TOINT(LOCALS:(TFLAG:900))
		TFLAG:900++
			ELSE
			PRINTW 実行出来ないコマンドの為連続実行を終了します
			TFLAG:900 = 0
			TFLAG:901 = 0
		ENDIF
	ENDIF
ELSE
	DRAWLINE
	PRINTL どれを実行しますか？
	FOR LOCAL,1,6
		PRINTFORM [{LOCAL}] - 
		IF SAVESTR:(LOCAL + 15) == ""
			IF SAVESTR:(LOCAL + 10) == ""
				PRINT 未登録
			ELSE
				PRINT 登録済み
			ENDIF
		ELSE
			PRINTS SAVESTR:(LOCAL + 15)
		ENDIF
		PRINTL 
	NEXT
	PRINTL [9] - 戻る
	$INPUT_LOOP
	INPUT
	IF RESULT == 9
		RETURN 1
	ELSEIF RESULT >= 1 && RESULT < 6
		LOCAL:2 = RESULT
		SPLIT SAVESTR:(LOCAL:2 + 10),"/",LOCALS
		IF RESULT < 2
			CLEARLINE 1
			GOTO INPUT_LOOP
		ELSE
			PRINTW 連続行動を実行します
			TFLAG:900 = 1
			CALLFORM COM_ABLE{TOINT(LOCALS:(TFLAG:900))}
			IF RESULT
				DOTRAIN TOINT(LOCALS:(TFLAG:900))
			TFLAG:900++
			TFLAG:901 = LOCAL:2

			ELSE
				PRINTW 実行出来ないコマンドの為連続実行を終了します
			ENDIF
			
		ENDIF
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF

ENDIF

