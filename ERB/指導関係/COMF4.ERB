;-------------------------------------------------
;フェラする
;愛撫系コマンド：調教者が調教対象の%CSTR:男性器呼び方%を口で刺激
;-------------------------------------------------
@COM4
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;縄使用中や獣姦中でなく、でなく、前回の調教がフェラチオかクンニ強制かシックスナインだとシックスナインに
	IF (PREVCOM == 31 || PREVCOM == 61 || PREVCOM == 69) && TEQUIP:縄 == 0 && TEQUIP:獣姦プレイ == 0
		CALL COM_ABLE69
		SIF RESULT == 1
			JUMP COM69
	ENDIF
ENDIF

PRINTL フェラする
SAVESTR:0 = フェラする
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:12 = 220
SOURCE:14 = 50

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:0 = 50
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:0 = 200
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:0 = 800
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:0 = 1600
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:0 = 2400
ELSE
	SOURCE:0 = 3200
ENDIF

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い || TEQUIP:獣姦プレイ
	TIMES SOURCE:0 , 2.00
	SOURCE:16 += SOURCE:0/20
ENDIF

;獣姦の場合はここで終了
SIF TEQUIP:獣姦プレイ
	RETURN 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＰ⇔調教者の口の汚れが移動
STAIN:2 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:2

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +3
	EXP:ＢＬ経験 += 3
ENDIF

;調教者の初キス
TFLAG:620 |= 1
SIF TALENT:オトコ || TALENT:ふたなり
	TFLAG:620 |= 2

CALL EVENT_SEITSU

;愛情経験
IF TALENT:オトコ
	E = 2
ELSE
	E = 1
ENDIF
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

RETURN 1
;
;-------------------------------------------------
;-------------------------------------------------
;精通
;-------------------------------------------------
@EVENT_SEITSU
A = NO:PLAYER
;【オトコ】か【ふたなり】であり【未熟】
SIF (TALENT:ふたなり == 0 && TALENT:オトコ == 0) || TALENT:未熟 == 0
	RETURN 0
;更に［Ｃ感度］５以上で触手調教や獣姦でなく
SIF ABL:Ｃ感覚 <= 4 || TEQUIP:触手召喚 || TEQUIP:獣姦プレイ
	RETURN 0
;「調教対象」から「調教者」への関係が150以上
SIF RELATION:TARGET:A < 150
	RETURN 0
PRINTFORML %NAME:PLAYER%に%CSTR:男性器呼び方%を吸われる内に、%NAME:TARGET%は精通を迎えた…
TALENT:未熟 = 0

RETURN 1
