;-------------------------------------------------
;ナイズリ
;奉仕系コマンド：調教対象が調教者の%CSTR:男性器呼び方%を乳房で刺激
;-------------------------------------------------
@COM141

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教がフェラかディープスロートか手コキフェラかフェラ自慰かバキュームフェラだとパイズリフェラに
	IF PREVCOM == 31 || PREVCOM == 124 || PREVCOM == 125 || PREVCOM == 126 || PREVCOM == 127
		CALL COM_ABLE123
		SIF RESULT == 1
			JUMP COM123
	ENDIF
ENDIF

PRINTL ナイズリ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINTS ABLNAME:[[ABL:精液中毒]]
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 1
	PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,3,')
	S = 1
ENDIF
;男嫌い
IF TALENT:男性不信 && TALENT:PLAYER:オトコ
	PRINT  - 
	A -= 12
	PRINTS TALENTNAME:[[TALENT:男性不信]]
	PRINTV '(,12,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 1
ENDIF
;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF S
		PRINT  + 
	A += 8
	PRINTS TALENTNAME:[[TALENT:ふたなり]]
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:2 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:2 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:2 & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:2 & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:4 & 32
	Y += 3

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2
;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(30以上で実行)
PRINT  = 
PRINTV A

V = 30
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SAVESTR:0 = ナイズリ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF TALENT:精愛味覚
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 80
ELSE
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 150
ENDIF

SOURCE:13 = 1800
SOURCE:14 = 900

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:4 = 420
	SOURCE:5 = 150
	SOURCE:8 = 400
ELSEIF ABL:奉仕精神 == 1
	SOURCE:4 = 500
	SOURCE:5 = 300
	SOURCE:8 = 300
ELSEIF ABL:奉仕精神 == 2
	SOURCE:4 = 580
	SOURCE:5 = 600
	SOURCE:8 = 150
ELSEIF ABL:奉仕精神 == 3
	SOURCE:4 = 660
	SOURCE:5 = 900
	SOURCE:8 = 50
ELSEIF ABL:奉仕精神 == 4
	SOURCE:4 = 740
	SOURCE:5 = 1500
	SOURCE:8 = 20
ELSE
	SOURCE:4 = 820
	SOURCE:5 = 2200
	SOURCE:8 = 0
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	A = 100
ELSEIF ABL:Ｂ感覚 == 1
	A = 200
ELSEIF ABL:Ｂ感覚 == 2
	A = 400
ELSEIF ABL:Ｂ感覚 == 3
	A = 800
ELSEIF ABL:Ｂ感覚 == 4
	A = 1200
ELSE
	A = 1500
ENDIF

;絶壁
IF TALENT:絶壁
	TIMES A , 1.20
ENDIF
IF TALENT:Ｂ敏感
	TIMES A , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES A , 0.70
ENDIF

SOURCE:17 += A
;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:4 , 0.50
	TIMES SOURCE:5 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:4 , 0.80
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:4 , 1.00
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:4 , 1.20
	TIMES SOURCE:5 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:4 , 1.50
	TIMES SOURCE:5 , 1.50
ELSE
	TIMES SOURCE:4 , 2.00
	TIMES SOURCE:5 , 2.00
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 1500
ELSEIF ABL:技巧 == 1
	B = 2100
ELSEIF ABL:技巧 == 2
	B = 2900
ELSEIF ABL:技巧 == 3
	B = 3500
ELSEIF ABL:技巧 == 4
	B = 4500
ELSE
	B = 6000
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:奉仕技術 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕技術 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕技術 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕技術 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕技術 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;ABL:精液中毒をみる
IF ABL:精液中毒 == 0
	TIMES B , 0.80
ELSEIF ABL:精液中毒 == 1
	TIMES B , 0.90
ELSEIF ABL:精液中毒 == 2
	TIMES B , 1.00
ELSEIF ABL:精液中毒 == 3
	TIMES B , 1.10
ELSEIF ABL:精液中毒 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:7 = 0
		TIMES SOURCE:5 , 2.00
		TIMES SOURCE:13, 6.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:7 = 500
		TIMES SOURCE:5 , 3.00
		TIMES SOURCE:13, 4.50
	ELSEIF ABL:精液中毒 == 2
		SOURCE:7 = 1200
		TIMES SOURCE:5 , 4.00
		TIMES SOURCE:13, 3.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:7 = 3000
		TIMES SOURCE:5 , 6.00
		TIMES SOURCE:13, 3.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:7 = 6000
		TIMES SOURCE:5 , 9.00
		TIMES SOURCE:13, 2.00
	ELSE
		SOURCE:7 = 12000
		TIMES SOURCE:5 , 15.00
		TIMES SOURCE:13, 1.50
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:7 , 2.00
	TIMES SOURCE:5 , 1.50

	EXP:PLAYER:射精経験 += 2
	EXP:精液経験 += 9
	PRINTL 大量射精
	PRINTL 精液経験＋９
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

ENDIF

;-------------------------------------------------
;汚れ
;-------------------------------------------------
;奴隷のＢ⇔調教者のＰの汚れが移動
STAIN:5 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:5

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +7
	EXP:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +7
	EXP:ＢＬ経験 += 7
ENDIF

SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:主人経験 += 1

;愛情経験
E = 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

;------------------------------------------------
;その他の処理
;------------------------------------------------
TFLAG:快楽経験 = 1

;屈服刻印２に相当
TFLAG:屈服刻印2 = 2

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SOURCE:13 /= 2
ENDIF

RETURN 1
;
