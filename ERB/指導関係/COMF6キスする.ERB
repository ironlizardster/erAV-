;-------------------------------------------------
;キス
;愛撫系コマンド：調教者が調教対象の口を口で刺激
;-------------------------------------------------
@COM6
;初キスを奪うかどうかの確認
CALL CONFIRM_FIRSTKISS
SIF RESULT == 0
	RETURN 0


;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が正常位、正常位・胸愛撫、正常位SPだと正常位・キスに
	IF PREVCOM == [[TRAIN:正常位]] || PREVCOM == [[TRAIN:正常位胸愛撫]] || PREVCOM == [[TRAIN:正常位ＳＰ]]
		CALL COM_ABLE128
		SIF RESULT == 1
			JUMP COM128
	ENDIF
ENDIF

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が後背位・胸愛撫、後背位・スパンキング、後背位SPだと立ちバックに
	IF PREVCOM == [[TRAIN:後背位胸愛撫]] || PREVCOM == [[TRAIN:後背位スパンキング]] || PREVCOM == [[TRAIN:後背位ＳＰ]]
		CALL COM_ABLE133
		SIF RESULT == 1
			JUMP COM133
	ENDIF
ENDIF

PRINTL キスする

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;汚れは自動成功判定前に算出するように
Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:0 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:0 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:0 & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:0 & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:0 & 32
	Y += 3

;獣姦の場合は汚れ7で固定
SIF TEQUIP:獣姦プレイ
	Y = 7

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;キスでは汚れがそれほど影響しない
Y /= 2

;従順が2以上か奉仕精神が3以上か愛がある場合は自動成功
SIF ABL:信頼 >= 2 || ABL:奉仕精神 >= 3 || TALENT:恋心
	GOTO AUTO_SUCCESS

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF

;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF

;MARK:快楽刻印
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 1
	PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,1,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF
;獣姦
IF TEQUIP:獣姦プレイ && TALENT:牝犬 == 0
	SIF S
		PRINT  - 
	A -= 15
	PRINTS ITEMNAME:[[ITEM:犬]]
	PRINTV '(,10,')
	S = 1
ENDIF

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(15以上で実行)
PRINT  = 
PRINTV A

V = 15
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

$AUTO_SUCCESS

SAVESTR:0 = キスする
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

;上のほうで計算した汚れデータ
SOURCE:8 = Y*20 + 10

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:4 = 50
	SOURCE:5 = 10
	TIMES SOURCE:8 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:4 = 150
	SOURCE:5 = 50
	TIMES SOURCE:8 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:4 = 200
	SOURCE:5 = 100
	TIMES SOURCE:8 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:4 = 250
	SOURCE:5 = 180
	TIMES SOURCE:8 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:4 = 300
	SOURCE:5 = 300
	TIMES SOURCE:8 , 0.50
ELSE
	SOURCE:4 = 350
	SOURCE:5 = 500
	TIMES SOURCE:8 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:4 , 0.50
	TIMES SOURCE:5 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:4 , 0.80
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:4 , 1.00
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:4 , 1.50
	TIMES SOURCE:5 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:4 , 2.50
	TIMES SOURCE:5 , 2.50
ELSE
	TIMES SOURCE:4 , 4.00
	TIMES SOURCE:5 , 4.00
ENDIF

;獣姦の場合はここで獣姦中毒他を判定して終了
IF TEQUIP:獣姦プレイ
	IF ABL:噴乳中毒 == 0
		TIMES SOURCE:8 , 2.00
	ELSEIF ABL:噴乳中毒 == 1
		TIMES SOURCE:8 , 1.00
	ELSEIF ABL:噴乳中毒 == 2
		TIMES SOURCE:8 , 0.80
		TFLAG:快楽経験 = 1
	ELSEIF ABL:噴乳中毒 == 3
		SOURCE:3 = 100
		TIMES SOURCE:8 , 0.50
		TFLAG:快楽経験 = 1
	ELSEIF ABL:噴乳中毒 == 4
		SOURCE:3 = 300
		SOURCE:10 = 100
		TIMES SOURCE:8 , 0.30
		TFLAG:快楽経験 = 1
	ELSE
		SOURCE:3 = 800
		SOURCE:10 = 200
		TIMES SOURCE:8 , 0.10
		TFLAG:快楽経験 = 1
	ENDIF

	RETURN 1
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	SOURCE:3 = 100
	SOURCE:10 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 1
	SOURCE:3 = 150
	SOURCE:10 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 2
	SOURCE:3 = 200
	SOURCE:10 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 3
	SOURCE:3 = 300
	SOURCE:10 = 50
	A = 50
ELSEIF ABL:PLAYER:技巧 == 4
	SOURCE:3 = 500
	SOURCE:10 = 100
	A = 100
ELSE
	SOURCE:3 = 800
	SOURCE:10 = 200
	A = 300
ENDIF

;TALENT:愛をみる
SIF TALENT:恋心
	TIMES SOURCE:3 , 2.00

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の口⇔調教者の口の汚れが移動
STAIN:0 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:0

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +3
	EXP:ＢＬ経験 += 3
ENDIF

;愛情経験
;基本値
E = 1

;愛かつ主人とのキスなら基本値に＋２
SIF TALENT:恋心 && ASSIPLAY == 0
	E += 2


;初キス
TFLAG:621 |= 1
TFLAG:620 |= 1


SIF TALENT:オトコ || TALENT:ふたなり
	TFLAG:620 |= 2

IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

;-------------------------------------------------
;その他の処理
;-------------------------------------------------
TFLAG:快楽経験 = 1

SIF ASSIPLAY == 0
	TFLAG:主人経験 += 1

RETURN 1
