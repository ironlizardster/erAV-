;-------------------------------------------------
;キス
;愛撫系コマンド：調教者が調教対象の口を口で刺激
;-------------------------------------------------
@COM6
;初キスを奪うかどうかの確認
CALL CONFIRM_FIRSTKISS
SIF RESULT == 0
	RETURN 0


;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が正常位、正常位・胸愛撫、正常位SPだと正常位・キスに
	IF PREVCOM == [[TRAIN:正常位]] || PREVCOM == [[TRAIN:正常位胸愛撫]] || PREVCOM == [[TRAIN:正常位ＳＰ]]
		CALL COM_ABLE128
		SIF RESULT == 1
			JUMP COM128
	ENDIF
ENDIF

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が後背位・胸愛撫、後背位・スパンキング、後背位SPだと立ちバックに
	IF PREVCOM == [[TRAIN:後背位胸愛撫]] || PREVCOM == [[TRAIN:後背位スパンキング]] || PREVCOM == [[TRAIN:後背位ＳＰ]]
		CALL COM_ABLE133
		SIF RESULT == 1
			JUMP COM133
	ENDIF
ENDIF

PRINTL キスする

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;汚れは自動成功判定前に算出するように
Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:口 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:口 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:口 & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:口 & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:口 & 32
	Y += 3

;獣姦の場合は汚れ7で固定
SIF TEQUIP:獣姦プレイ
	Y = 7

SIF IS_汚臭鈍感(TARGET)
	Y /= 3
SIF IS_汚臭敏感(TARGET)
	Y *= 2

;キスでは汚れがそれほど影響しない
Y /= 2

;従順が2以上か奉仕精神が3以上か愛がある場合は自動成功
SIF ABL:信頼 >= 2 || ABL:奉仕精神 >= 3 || TALENT:恋心
	GOTO AUTO_SUCCESS

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF

;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF

;MARK:快楽刻印
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF IS_恥じらい(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_恥じらい(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF IS_汚臭鈍感(TARGET)
	SIF S
		PRINT  + 
	A += 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF IS_汚臭敏感(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF IS_快感の否定(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_快感に素直(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF
;獣姦
IF TEQUIP:獣姦プレイ && TALENT:牝犬 == 0
	SIF S
		PRINT  - 
	A -= 15
	PRINTS ITEMNAME:[[ITEM:犬]]
	PRINTV '(,10,')
	S = 1
ENDIF

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF IS_汚臭鈍感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSEIF IS_汚臭敏感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(15以上で実行)
PRINT  = 
PRINTV A

V = 15
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

$AUTO_SUCCESS

SAVESTR:0 = キスする
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*20 + 10

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 50
	SOURCE:達成 = 10
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 150
	SOURCE:達成 = 50
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 200
	SOURCE:達成 = 100
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 250
	SOURCE:達成 = 180
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 300
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 350
	SOURCE:達成 = 500
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成 , 2.50
ELSE
	TIMES SOURCE:性行動 , 4.00
	TIMES SOURCE:達成 , 4.00
ENDIF

;獣姦の場合はここで獣姦中毒他を判定して終了
IF TEQUIP:獣姦プレイ
	IF ABL:噴乳中毒 == 0
		TIMES SOURCE:不潔 , 2.00
	ELSEIF ABL:噴乳中毒 == 1
		TIMES SOURCE:不潔 , 1.00
	ELSEIF ABL:噴乳中毒 == 2
		TIMES SOURCE:不潔 , 0.80
		TFLAG:快楽経験 = 1
	ELSEIF ABL:噴乳中毒 == 3
		SOURCE:情愛 = 100
		TIMES SOURCE:不潔 , 0.50
		TFLAG:快楽経験 = 1
	ELSEIF ABL:噴乳中毒 == 4
		SOURCE:情愛 = 300
		SOURCE:愛液分泌 = 100
		TIMES SOURCE:不潔 , 0.30
		TFLAG:快楽経験 = 1
	ELSE
		SOURCE:情愛 = 800
		SOURCE:愛液分泌 = 200
		TIMES SOURCE:不潔 , 0.10
		TFLAG:快楽経験 = 1
	ENDIF

	RETURN 1
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	SOURCE:情愛 = 100
	SOURCE:愛液分泌 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 1
	SOURCE:情愛 = 150
	SOURCE:愛液分泌 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 2
	SOURCE:情愛 = 200
	SOURCE:愛液分泌 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 3
	SOURCE:情愛 = 300
	SOURCE:愛液分泌 = 50
	A = 50
ELSEIF ABL:PLAYER:技巧 == 4
	SOURCE:情愛 = 500
	SOURCE:愛液分泌 = 100
	A = 100
ELSE
	SOURCE:情愛 = 800
	SOURCE:愛液分泌 = 200
	A = 300
ENDIF

;TALENT:愛をみる
SIF TALENT:恋心
	TIMES SOURCE:情愛 , 2.00

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の口⇔調教者の口の汚れが移動
STAIN:口 |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:口

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +3
	EXP:ＢＬ経験 += 3
ENDIF

;愛情経験
;基本値
E = 1

;愛かつ主人とのキスなら基本値に＋２
SIF TALENT:恋心 && ASSIPLAY == 0
	E += 2


;初キス
TFLAG:621 |= 1
TFLAG:620 |= 1


SIF HAVE_PENIS(TARGET)
	TFLAG:620 |= 2

IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

;-------------------------------------------------
;その他の処理
;-------------------------------------------------
TFLAG:快楽経験 = 1

SIF ASSIPLAY == 0
	TFLAG:主人経験 += 1

RETURN 1
