;-------------------------------------------------
;背面座位
;セックス系コマンド：調教者の%CSTR:男性器呼び方%を調教対象の%CSTR:女性器呼び方%に挿入
;正常位に比べて露出と情愛が低いが、調教対象の体力消耗が少ない
;-------------------------------------------------
@COM23
;設定が有効で、前のプレイが背面座位かつ調教者の技巧3以上の場合、挿入Ｇスポ責めまたは挿入子宮口責めへ派生
;Ｖ感覚が高く欲情しているほど子宮口へ行きやすい
IF FLAG:追加コマンドなし == 0
	IF PREVCOM == [[TRAIN:背面座位]] && ABL:PLAYER:技巧 > 2 && ((ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0))
		;欲情
		IF PALAM:欲情 < PALAMLV:1
			A = 1
		ELSEIF PALAM:欲情 < PALAMLV:2
			A = 2
		ELSEIF PALAM:欲情 < PALAMLV:3
			A = 3
		ELSEIF PALAM:欲情 < PALAMLV:4
			A = 4
		ELSE
			A = 5
		ENDIF
		B = RAND:11
		A *= B
		B = RAND:11
		A += B*ABL:Ｖ感覚
		;Aの値は0～100
		;背面座位だと60以上で子宮口
		IF A >= 60
			CALL COM_ABLE121
			SIF RESULT == 1
			JUMP COM121
		ELSE
			CALL COM_ABLE120
			SIF RESULT == 1
			JUMP COM120
		ENDIF
	ENDIF
ENDIF

;処女を奪うかどうかの確認
CALL CONFIRM_LOST_VIRGIN
SIF RESULT == 0
	RETURN 0


PRINTL 背面座位
SAVESTR:0 = 背面座位
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:V経験を伴うコマンド = 1
SIF TALENT:恋心 && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:主人による処女喪失 = 1

;調教者の射精チェック
CALL COM_EJAC_PLAYER_SEX

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL = 20
LOCAL:1 = 150

LOCAL:2 = ABL:Ｖ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ｖ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL -= LOCAL:2
SIF LOCAL <= 10
	LOCAL = 10
LOCAL:1 -= LOCAL:3
SIF LOCAL:1 <= 10
	LOCAL:1 = 10

LOSEBASE:体力 += LOCAL
LOSEBASE:気力 += LOCAL:1

SOURCE:露出 = 200

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 50
	SOURCE:情愛 = 50

ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 150
	SOURCE:情愛 = 100
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 300
	SOURCE:情愛 = 200
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 600
	SOURCE:情愛 = 300
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1000
	SOURCE:情愛 = 500
ELSE
	SOURCE:快Ｖ = 1500
	SOURCE:情愛 = 700
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛覚 = 3000

	IF ASSIPLAY && HAVE_VAGINA(PLAYER) 
		EXP:異常経験 += 1
		PRINTFORML %EXPNAME:[[EXP:異常経験]]%＋１
	ENDIF
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛覚 = 240
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛覚 = 30
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛覚 = 5
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛覚 = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛覚 = 0
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 50
	SOURCE:情愛 += 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 200
	SOURCE:情愛 += 200
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 500
	SOURCE:情愛 += 400
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
	SOURCE:情愛 += 600
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1300
	SOURCE:情愛 += 1000
ELSE
	SOURCE:快Ｂ = 1800
	SOURCE:情愛 += 1400
ENDIF

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 40
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 160
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 500
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 900
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1400
ELSE
	SOURCE:快Ｃ = 2100
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛覚 += 600
	TIMES SOURCE:痛覚 , 2.60
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.70
	SOURCE:痛覚 += 180
	TIMES SOURCE:痛覚 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛覚 , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:痛覚 , 0.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:痛覚 , 0.10
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF IS_FUTA(ASSI)
		TIMES SOURCE:快Ｖ , 2.50
ENDIF

;大柄
SIF IS_大柄(TARGET)
	TIMES SOURCE:痛覚 , 0.80
;小柄体型
SIF IS_小柄(TARGET)
	TIMES SOURCE:痛覚 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:痛覚 , 4.00

;貞操観念
IF IS_貞操観念(TARGET)
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感 = 10000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感 = 1000
	ENDIF
;貞操無頓着
ELSEIF IS_貞操無頓着(TARGET)
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感 = 300
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:反感 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:情愛 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ｃ , 1.10
	TIMES SOURCE:情愛 , 1.50
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:情愛 , 2.00
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:快Ｃ , 1.30
	TIMES SOURCE:情愛 , 2.60
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感 , 2.00
ELSEIF ABL:信頼 == 1
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.30
	TIMES SOURCE:反感 , 1.80
ELSEIF ABL:信頼 == 2
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.50
	TIMES SOURCE:反感 , 1.60
ELSEIF ABL:信頼 == 3
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:情愛 , 1.90
	TIMES SOURCE:反感 , 1.40
ELSEIF ABL:信頼 == 4
	TIMES SOURCE:快Ｖ , 2.10
	TIMES SOURCE:情愛 , 2.20
	TIMES SOURCE:反感 , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 2.50
	TIMES SOURCE:情愛 , 2.60
	TIMES SOURCE:反感 , 1.00
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:Ｖ感覚 == 0
	SOURCE:情愛 += 100
	SOURCE:愛液分泌 = 0
	A = 0
ELSEIF ABL:PLAYER:Ｖ感覚 == 1
	SOURCE:情愛 += 150
	SOURCE:愛液分泌 += 50
	A = 0
ELSEIF ABL:PLAYER:Ｖ感覚 == 2
	SOURCE:情愛 += 200
	SOURCE:愛液分泌 += 100
	A = 0
ELSEIF ABL:PLAYER:Ｖ感覚 == 3
	SOURCE:情愛 += 300
	SOURCE:愛液分泌 += 150
	A = 50
ELSEIF ABL:PLAYER:Ｖ感覚 == 4
	SOURCE:情愛 += 500
	SOURCE:愛液分泌 += 250
	A = 100
ELSE
	SOURCE:情愛 += 800
	SOURCE:愛液分泌 += 400
	A = 300
ENDIF

SOURCE:快Ｃ += A

SIF EXPLV:10 >= 3
	SOURCE:快Ｖ += A

;TALENT:愛をみる
SIF TALENT:恋心
	TIMES SOURCE:情愛 , 2.00

;セックス後の処理
CALL COM_AFTER_VAGINA_SEX

RETURN 1
;
;
;
