;-------------------------------------------------
;アナル愛撫
;愛撫系コマンド：調教者が調教対象のアナルを手で刺激
;-------------------------------------------------
@COM2

PRINTL アナル愛撫
SAVESTR:0 = アナル愛撫
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 20
LOSEBASE:気力 += 100

SOURCE:露出 = 850
SOURCE:逸脱 = 200

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 20
	SOURCE:屈従 = 300
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 75
	SOURCE:屈従 = 350
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 300
	SOURCE:屈従 = 400
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 700
	SOURCE:屈従 = 650
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1100
	SOURCE:屈従 = 1000
ELSE
	SOURCE:快Ａ = 1500
	SOURCE:屈従 = 1500
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.20
	TIMES SOURCE:屈従, 0.20
	SOURCE:痛覚 = 500
	SOURCE:逸脱 += 200
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.50
	TIMES SOURCE:屈従, 0.50
	SOURCE:痛覚 = 400
	SOURCE:逸脱 += 100
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従, 1.00
	SOURCE:痛覚 = 300
	SOURCE:逸脱 += 50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従, 1.20
	SOURCE:痛覚 = 200
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.60
	TIMES SOURCE:屈従, 1.60
	SOURCE:痛覚 = 100
ELSE
	TIMES SOURCE:快Ａ , 1.80
	TIMES SOURCE:屈従, 1.80
	SOURCE:痛覚 = 50
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.10
	TIMES SOURCE:屈従, 0.10
	TIMES SOURCE:痛覚 , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.20
	TIMES SOURCE:屈従, 0.20
	TIMES SOURCE:痛覚 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従, 0.60
	TIMES SOURCE:痛覚 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従, 1.00
	TIMES SOURCE:痛覚 , 0.50
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 2.00
	TIMES SOURCE:屈従, 2.00
	TIMES SOURCE:痛覚 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.30
	TIMES SOURCE:屈従, 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従, 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従, 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:屈従, 1.30
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 1.60
	TIMES SOURCE:屈従, 1.60
ENDIF


;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛覚 , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛覚 , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
IF IS処女(TARGET) && TALENT:貞操観念
	TIMES SOURCE:屈従 , 0.80
	TIMES SOURCE:逸脱 , 0.50
ENDIF

;未熟
SIF TALENT:未熟
	TIMES SOURCE:痛覚 , 2.00

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手召喚
	;奴隷のＡに触手の汚れがつく
	STAIN:アナル |= 2
	STAIN:アナル |= 4
ELSE
	;奴隷のＡ⇔調教者の指の汚れが移動
	STAIN:アナル |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:アナル
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
S = 0
;Ａ感覚が高いほどＡ経験が入る
IF ABL:Ａ感覚 <= 1
	S = 1
ELSEIF ABL:Ａ感覚 <= 4
	S = 2
ELSEIF ABL:Ａ感覚 <= 7
	S = 3
ELSE
	S = 4
ENDIF
EXP:Ａ経験 += S
PRINTFORML Ａ経験+{S}
S = 0

;レズ経験・ホモ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +2
	EXP:レズ経験 += 2
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +5
	EXP:ＢＬ経験 += 5
ENDIF

;愛情経験
E = 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

RETURN 1
