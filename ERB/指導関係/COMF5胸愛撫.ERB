;-------------------------------------------------
;胸愛撫
;愛撫系コマンド：調教者が調教対象の乳房を手と口で刺激
;-------------------------------------------------
@COM5

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が正常位、正常位キス、正常位SPだと正常位・胸愛撫に
	IF PREVCOM == [[TRAIN:正常位]] || PREVCOM == [[TRAIN:正常位キス]] || PREVCOM == [[TRAIN:正常位ＳＰ]]
		CALL COM_ABLE129
		SIF RESULT == 1
			JUMP COM129
	ENDIF
ENDIF

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;前回の調教が後背位、後背位・スパンキング、立ちバック、後背位ＳＰだと後背位・胸愛撫に
	IF PREVCOM == [[TRAIN:後背位]] || PREVCOM == [[TRAIN:後背位スパンキング]] || PREVCOM == [[TRAIN:立ちバック]] || PREVCOM == [[TRAIN:後背位ＳＰ]]
		CALL COM_ABLE131
		SIF RESULT == 1
			JUMP COM131
	ENDIF
ENDIF

PRINTL 胸愛撫
SAVESTR:0 = 胸愛撫
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:性行動 = 60
SOURCE:不潔 = 20
SOURCE:露出 = 100

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 500
	SOURCE:情愛 = 160
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1200
	SOURCE:情愛 = 200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2000
	SOURCE:情愛 = 230
ELSE
	SOURCE:快Ｂ = 2800
	SOURCE:情愛 = 250
ENDIF

;獣姦の場合はここで終了
SIF TEQUIP:獣姦プレイ
	RETURN 1

;調教者が［幼児退行］
IF TALENT:PLAYER:幼児退行 && TEQUIP:獣姦プレイ == 0
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

;調教者が［幼稚］
IF TALENT:PLAYER:幼稚 && TEQUIP:獣姦プレイ == 0
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手召喚
	;奴隷のＢに触手の汚れがつく
	STAIN:胸 |= 2
	STAIN:胸 |= 4
ELSE
	;奴隷の胸の汚れがなければ口をつかう（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
	IF STAIN:胸 < 2 || STAIN:胸 == 16 || STAIN:胸 == 17 || ASSIPLAY == 1 || TALENT:MASTER:汚れ無視 == 1
		;調教者のTALENT:舌使いをみる
		IF TALENT:PLAYER:舌使い
			TIMES SOURCE:快Ｂ , 1.40
			SOURCE:恭順 += SOURCE:快Ｂ/20
		ENDIF

		;奴隷のＢ⇔調教者の口の汚れが移動
		STAIN:胸 |= STAIN:PLAYER:口
		STAIN:PLAYER:口 |= STAIN:胸
	ENDIF

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:胸 |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:胸
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +5
	EXP:レズ経験 += 5
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +5
	EXP:ＢＬ経験 += 5
ENDIF
PRINTFORML %EXPNAME:[[EXP:Ｂ開発経験]]%+2
	EXP:Ｂ開発経験 += 2
;愛情経験
E = 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

CALL EVENT_JUNYU

RETURN 1

;-------------------------------------------------
;-------------------------------------------------
;母乳体質への変化
;-------------------------------------------------
@EVENT_JUNYU
A = NO:PLAYER
;【母乳体質】無しかつ【小柄】で無し【貧乳】無しかつ【オトコ】でない
SIF TALENT:母乳体質  || IS_小柄(TARGET) || IS_貧乳(TARGET) || IS_MALE(TARGET)
	RETURN 0
;更に［Ｂ感度］５以上かつ【巨乳】か【爆乳】で触手調教や獣姦でなく
SIF ABL:Ｂ感覚 <= 4 || !IS_チチユレ(TARGET) || TEQUIP:触手召喚 || TEQUIP:獣姦プレイ
	RETURN 0
;調教者が【幼稚】か【幼児退行】か【未熟】
SIF TALENT:PLAYER:幼稚 == 0 && TALENT:PLAYER:幼児退行 == 0 && TALENT:PLAYER:未熟 == 0
	RETURN 0
;「調教対象」から「調教者」への関係が150以上か主人プレイで愛
SIF RELATION:TARGET:A < 150 && (ASSIPLAY || TALENT:恋心 == 0)
	RETURN 0
PRINTFORML %NAME:PLAYER%に乳房を吸われる内に、%NAME:TARGET%は母乳が出るようになった…
TALENT:母乳体質 = 1

RETURN 1
