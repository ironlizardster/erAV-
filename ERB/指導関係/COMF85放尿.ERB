;-------------------------------------------------
;放尿
;過激なコマンド：調教対象が自分の意思で放尿する
;-------------------------------------------------
@COM85

SIF TEQUIP:撮影中
	PRINT 公開
SIF TEQUIP:野外プレイ
	PRINT 野外
PRINTL 放尿
SAVESTR:0 = 放尿
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 200

SOURCE:逸脱 = 5000
SOURCE:達成 = 3000

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:露出 = 1800
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:露出 = 1900
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:露出 = 2000
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:露出 = 2100
ELSEIF PALAM:欲情 >= PALAMLV:4
	SOURCE:露出 = 2200
ENDIF

;露出癖をみる
;露出癖が高くなるにつれ放尿への恥じらいが減っていく
IF ABL:露出癖 == 0
	SOURCE:中毒充足 = 0
	SOURCE:屈従 = 6000
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 = 30
	SOURCE:屈従 = 5000
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 = 100
	SOURCE:屈従 = 4000
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 = 300
	SOURCE:屈従 = 3000
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 = 500
	SOURCE:屈従 = 2000
ELSE
	SOURCE:中毒充足 = 1000
	SOURCE:屈従 = 1000
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:中毒充足 , 0.80
	TIMES SOURCE:露出 , 0.80
	SOURCE:愛液分泌 = 100
	SOURCE:欲情 = 100
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:露出 , 1.00
	SOURCE:愛液分泌 = 200
	SOURCE:欲情 = 300
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:中毒充足 , 1.30
	TIMES SOURCE:露出 , 1.20
	SOURCE:愛液分泌 = 400
	SOURCE:欲情 = 700
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:中毒充足 , 1.40
	TIMES SOURCE:露出 , 1.40
	SOURCE:愛液分泌 = 700
	SOURCE:欲情 = 1200
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:中毒充足 , 1.70
	TIMES SOURCE:露出 , 1.50
	SOURCE:愛液分泌 = 1100
	SOURCE:欲情 = 1800
ELSE
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:露出 , 1.70
	SOURCE:愛液分泌 = 1500
	SOURCE:欲情 = 2500
ENDIF

;場所をみる
;野外
IF TEQUIP:野外プレイ
	TIMES SOURCE:露出 , 2.50
	TIMES SOURCE:逸脱 , 2.50
;大鏡の前
ELSEIF TEQUIP:羞恥大鏡プレイ
	TIMES SOURCE:露出 , 1.50
	TIMES SOURCE:逸脱 , 1.50
;風呂場
ELSEIF TEQUIP:お風呂場プレイ
	TIMES SOURCE:露出 , 0.50
	TIMES SOURCE:逸脱 , 0.50
ENDIF

A = 100
IF EXP:放尿経験 < EXPLV:1
	TIMES A , 3.00
ELSEIF EXP:放尿経験 < EXPLV:2
	TIMES A , 2.50
ELSEIF EXP:放尿経験 < EXPLV:3
	TIMES A , 2.00
ELSEIF EXP:放尿経験 < EXPLV:4
	TIMES A , 1.00
ELSEIF EXP:放尿経験 < EXPLV:5
	TIMES A , 0.80
ELSE
	TIMES A , 0.60
ENDIF

;臆病
SIF TALENT:臆病
	TIMES A , 1.20

;反抗的
SIF TALENT:ナマイキ
	TIMES A , 2.00

;プライド高い
SIF TALENT:プライド高い
	TIMES A , 2.00

;プライド低い
SIF TALENT:プライド低い
	TIMES A , 0.80

;自制心
SIF TALENT:自制心
	TIMES A , 1.50

;感情乏しい
SIF TALENT:感情乏しい
	TIMES A , 0.60

;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES A , 0.80

;抑圧
SIF TALENT:抑圧
	TIMES A , 3.00

;抵抗
SIF TALENT:抵抗
	TIMES A , 3.00

;恥じらい
SIF TALENT:恥じらい
	TIMES A , 3.50

;恥薄い
SIF TALENT:恥薄い
	TIMES A , 0.50

;露出狂
IF TALENT:露出狂
	SOURCE:中毒充足 += 500
	TIMES SOURCE:露出 , 1.50
ENDIF

SOURCE:逸脱 += A * 5

PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋２
EXP:放尿経験 += 2

;奴隷のＶかＰに＜尿＞の汚れが発生
STAIN:2 |= 32
STAIN:3 |= 32

;屈服刻印２に相当
TFLAG:屈服刻印2 = 2

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +1
	EXP:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +1
	EXP:ＢＬ経験 += 1
ENDIF

SIF TEQUIP:触手召喚
	T += 1

;着衣おもらし処理
CALL SOILING_CLOTH_NO1

;利尿剤のフラグをクリア
IF TEQUIP:利尿剤効果
	TEQUIP:利尿剤効果 = 0
ENDIF

RETURN 1
;
;
;
