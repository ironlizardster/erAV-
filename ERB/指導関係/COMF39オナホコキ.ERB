;-------------------------------------------------
;手淫
;奉仕系コマンド：調教対象が調教者の%CSTR:男性器呼び方%を手で刺激
;-------------------------------------------------
@COM39

PRINTL オナホコキ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 1
	PRINTS ABLNAME:[[ABL:精液中毒]]
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF IS_恥じらい(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_恥じらい(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF IS_汚臭鈍感(TARGET)
	SIF S
		PRINT  + 
	A += 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF IS_汚臭敏感(TARGET)
	PRINT  - 
	A -= 3
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF IS_快感の否定(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_快感に素直(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い
IF TALENT:男性不信 && IS_MALE(PLAYER) 
	PRINT  - 
	A -= 5
	PRINTS TALENTNAME:[[TALENT:男性不信]]
	PRINTV '(,5,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 1
ENDIF
;調教者がふたなり
IF IS_FUTA(PLAYER)
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF
;獣姦
IF TEQUIP:獣姦プレイ && TALENT:牝犬 == 0
	SIF S
		PRINT  - 
	A -= 10
	PRINTS ITEMNAME:[[ITEM:犬]]
	PRINTV '(,10,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:ペニス & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:アナル & 32
	Y += 3

;獣姦の場合は汚れ7で固定
SIF TEQUIP:獣姦プレイ
	Y = 7

SIF IS_汚臭鈍感(TARGET)
	Y /= 3
SIF IS_汚臭敏感(TARGET)
	Y *= 2

;手淫では汚れがそれほど影響しない
Y /= 3

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF IS_汚臭鈍感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSEIF IS_汚臭敏感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(14以上で実行)
PRINT  = 
PRINTV A

V = 14
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SAVESTR:0 = オナホコキ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 100

SOURCE:屈従 = 500
SOURCE:逸脱 = 100

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 250
	SOURCE:達成 = 50
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 300
	SOURCE:達成 = 100
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 350
	SOURCE:達成 = 200
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 400
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 450
	SOURCE:達成 = 500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 500
	SOURCE:達成 = 750
	TIMES SOURCE:不潔 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成 , 2.00
ENDIF

;獣姦の場合はここで終了
SIF TEQUIP:獣姦プレイ
	RETURN 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 450
ELSEIF ABL:技巧 == 1
	B = 1000
ELSEIF ABL:技巧 == 2
	B = 1600
ELSEIF ABL:技巧 == 3
	B = 2200
ELSEIF ABL:技巧 == 4
	B = 2700
ELSE
	B = 3200
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:奉仕技術 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕技術 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕技術 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕技術 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕技術 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF HAVE_PENIS(PLAYER)
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 600
		TIMES SOURCE:達成 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成 , 4.00
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成 , 5.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成 , 6.00
		TIMES SOURCE:屈従, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1
	PRINTL 大量射精
	;手で射精させたフラグ
	TFLAG:手で射精 = 4

;通常の射精
ELSEIF E == 1
	EXP:PLAYER:射精経験 += 1
	PRINTL 射精
	BASE:PLAYER:射精ゲージ -= EJAC
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;手で射精させたフラグ
	TFLAG:手で射精 = 3
ENDIF

;-------------------------------------------------
;汚れ
;-------------------------------------------------
;奴隷の指⇔調教者のＰの汚れが移動
IF HAVE_PENIS(PLAYER)
	STAIN:手 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:手
;奴隷の指⇔調教者のＶの汚れが移動
ELSE
	STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:ヴァギナ
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +4
	EXP:レズ経験 += 4
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +4
	EXP:ＢＬ経験 += 4
ENDIF

VARSET LOCAL, 0
IF ABL:サドっ気 <= 1
	SIF TFLAG:助手を犯す処女喪失 == 1
		LOCAL = 8
	LOCAL = 1
ELSEIF ABL:サドっ気 <= 2
	SIF TFLAG:助手を犯す処女喪失 == 1
		LOCAL = 16
	LOCAL = 2
ELSEIF ABL:サドっ気 <= 3
	SIF TFLAG:助手を犯す処女喪失 == 1
		LOCAL = 32
	LOCAL = 3
ELSEIF ABL:サドっ気 <= 4
	SIF TFLAG:助手を犯す処女喪失 == 1
		LOCAL = 64
	LOCAL = 4
ELSE
	SIF TFLAG:助手を犯す処女喪失 == 1
		LOCAL = 128
	LOCAL = 5
ENDIF

EXP:嗜虐快楽経験 += LOCAL
PRINTFORML %EXPNAME:[[EXP:嗜虐快楽経験]]%+{LOCAL}

;愛情経験
E = 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

TFLAG:快楽経験 = 1

;屈服刻印１に相当
TFLAG:屈服刻印2 = 1

;調教者がふたなりなら屈従半減
SIF IS_FUTA(PLAYER)
	SOURCE:屈従 /= 2

RETURN 1
