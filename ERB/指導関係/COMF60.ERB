@COM60
;助手にキス

PRINTL 助手にキスさせる

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 1
	PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,1,')
	S = 1
ENDIF
;女嫌い
IF TALENT:ボーイッシュ && TALENT:PLAYER:オトコ == 0
	PRINT  - 
	A -= 5
	PRINTS TALENTNAME:[[TALENT:ボーイッシュ]]
	PRINTV '(,5,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:0 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:0 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:0 & 8
	Y += 7
SIF STAIN:PLAYER:0 & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;キスでは汚れがそれほど影響しない
Y /= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(15以上で実行)
PRINT  = 
PRINTV A

V = 15
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SELECTCOM = 60
SAVESTR:0 = 助手にキス
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック（ふたなり・オトコの場合）
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 50
ELSEIF ABL:技巧 == 1
	B = 200
ELSEIF ABL:技巧 == 2
	B = 300
ELSEIF ABL:技巧 == 3
	B = 400
ELSEIF ABL:技巧 == 4
	B = 500
ELSE
	B = 600
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:奉仕技術 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕技術 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕技術 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕技術 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕技術 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF TALENT:PLAYER:オトコ  || TALENT:PLAYER:ふたなり
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:気力 += 20

SOURCE:13 = 100
SOURCE:14 = 10

;上のほうで計算した汚れデータ
SOURCE:8 = Y*20 + 10

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:4 = 50
	SOURCE:5 = 0
	TIMES SOURCE:8 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:4 = 150
	SOURCE:5 = 50
	TIMES SOURCE:8 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:4 = 200
	SOURCE:5 = 100
	TIMES SOURCE:8 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:4 = 250
	SOURCE:5 = 180
	TIMES SOURCE:8 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:4 = 300
	SOURCE:5 = 300
	TIMES SOURCE:8 , 0.50
ELSE
	SOURCE:4 = 350
	SOURCE:5 = 500
	TIMES SOURCE:8 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:4 , 0.50
	TIMES SOURCE:5 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:4 , 0.80
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:4 , 1.00
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:4 , 1.50
	TIMES SOURCE:5 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:4 , 2.50
	TIMES SOURCE:5 , 2.50
ELSE
	TIMES SOURCE:4 , 4.00
	TIMES SOURCE:5 , 4.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:7 = 0
		TIMES SOURCE:5 , 2.00
		TIMES SOURCE:13, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:7 = 100
		TIMES SOURCE:5 , 2.50
		TIMES SOURCE:13, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:7 = 300
		TIMES SOURCE:5 , 3.00
		TIMES SOURCE:13, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:7 = 700
		TIMES SOURCE:5 , 4.00
		TIMES SOURCE:13, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:7 = 1500
		TIMES SOURCE:5 , 5.00
		TIMES SOURCE:13, 0.40
	ELSE
		SOURCE:7 = 6000
		TIMES SOURCE:5 , 6.00
		TIMES SOURCE:13, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:7 , 2.00
	TIMES SOURCE:5 , 1.50

	EXP:PLAYER:射精経験 += 2
	EXP:精液経験 += 1
	PRINTL 大量射精
	PRINTL 精液経験＋１
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;キスで射精させたフラグ
	TFLAG:キスで射精 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:射精経験 += 1
	PRINTL 射精
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;キスで射精させたフラグ
	TFLAG:キスで射精 = 1
ENDIF

;奴隷の口⇔調教者の口の汚れが移動
STAIN:0 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:0

;初キス
IF CFLAG:初キス相手 == -1
	TFLAG:初キス口以外 = 1
	CFLAG:初キス相手 = 1
	CSTR:初キス相手 = %CALLNAME:PLAYER%
	TFLAG:初キス = 1
ENDIF
IF CFLAG:PLAYER:初キス相手 == -1
	CFLAG:PLAYER:初キス相手 = 1
	CSTR:PLAYER:初キス相手 = %CALLNAME:TARGET%
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +3
	EXP:ＢＬ経験 += 3
ENDIF

TFLAG:快楽経験 = 1

;屈服刻印１に相当
TFLAG:屈服刻印2 = 1

RETURN 1
