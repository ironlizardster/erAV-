;-------------------------------------------------
;正常位アナル
;セックス系コマンド：調教者の%CSTR:男性器呼び方%を調教対象のアナルに挿入
;通常の%CSTR:女性器呼び方%正常位と比べて情愛は低いが屈従が上がる
;-------------------------------------------------

@COM26
;Ａ処女を奪うかどうかの確認
CALL CONFIRM_LOST_ANALVIRGIN
SIF RESULT == 0
	RETURN 0

;前回の調教が3Pのとき3Pへ
TFLAG:３Ｐ継続中 = 0
IF PREVCOM == [[TRAIN:３Ｐ]]
	CALL COM_ABLE64
	IF RESULT == 1
		TFLAG:３Ｐ継続中 = 1
		JUMP COM64
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF (ASSIPLAY && TFLAG:前回の調教者が助手 == 0) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手)
	;前回の調教がフェラチオかイラマチオのときは3Pへ
	IF PREVCOM == [[TRAIN:フェラチオ]] || PREVCOM == [[TRAIN:イラマチオ]]
		CALL COM_ABLE64
		SIF RESULT == 1
			JUMP COM64
	ENDIF
ENDIF

PRINTL 正常位アナル
SAVESTR:0 = 正常位アナル
CALL TRAIN_MESSAGE_B

;A経験を伴うコマンドのフラグ
TFLAG:Ａ挿入判定 = 1

;射精ゲージチェック
CALL COM_EJAC_PLAYER_ANALSEX

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL = 80
LOCAL:1 = 120

LOCAL:2 = ABL:Ａ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ａ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL -= LOCAL:2
SIF LOCAL <= 10
	LOCAL = 10
LOCAL:1 -= LOCAL:3
SIF LOCAL:1 <= 10
	LOCAL:1 = 10

LOSEBASE:体力 += LOCAL
LOSEBASE:気力 += LOCAL:1

SOURCE:露出 = 400

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 = 10
	SOURCE:屈従 = 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 30
	SOURCE:情愛 = 30
	SOURCE:屈従 = 500
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 400
	SOURCE:情愛 = 150
	SOURCE:屈従 = 1200
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 900
	SOURCE:情愛 = 300
	SOURCE:屈従 = 2400
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1600
	SOURCE:情愛 = 500
	SOURCE:屈従 = 3600
ELSE
	SOURCE:快Ａ = 2100
	SOURCE:情愛 = 900
	SOURCE:屈従 = 5000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛覚 = 20000
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 100
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛覚 = 12000
	LOSEBASE:体力 += 40
	LOSEBASE:気力 += 80
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛覚 = 5000
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 60
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛覚 = 1800
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 40
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛覚 = 1000
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 20
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛覚 = 600
	LOSEBASE:体力 += 0
	LOSEBASE:気力 += 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛覚 += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛覚 += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛覚 += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛覚 += 200
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛覚 += 100
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF IS_FUTA(ASSI)
		TIMES SOURCE:快Ｖ , 2.50
ENDIF

;大柄
SIF IS_大柄(TARGET)
	TIMES SOURCE:痛覚 , 0.80
;小柄体型
SIF IS_小柄(TARGET)
	TIMES SOURCE:痛覚 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:痛覚 , 2.00

;貞操観念
IF IS_貞操観念(TARGET)
	IF EXP:Ａ経験 == 0
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順 , 1.10
		SOURCE:反感 = 500
	ELSE
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順 , 1.10
		SOURCE:反感 = 0
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;ABL:従順をみる
IF ABL:Ｃ感覚 == 0
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感 , 2.00
ELSEIF ABL:Ｃ感覚 == 1
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感 , 1.50
ELSEIF ABL:Ｃ感覚 == 2
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感 , 1.00
ELSEIF ABL:Ｃ感覚 == 3
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感 , 0.80
ELSEIF ABL:Ｃ感覚 == 4
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感 , 0.60
ELSE
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:反感 , 0.30
ENDIF

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF IS_Ａ鈍感(TARGET)
	TIMES SOURCE:痛覚 , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF IS_Ａ敏感(TARGET)
	TIMES SOURCE:痛覚 , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;アナルセックス後の処理
CALL COM_AFTER_ANAL_SEX

RETURN 1
;
;
;
