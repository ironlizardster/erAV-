;-------------------------------------------------
;ボールギャグ
;SM系コマンド
;-------------------------------------------------
@COM45

PRINTL ボールギャグ
SAVESTR:0 = ボールギャグ
CALL TRAIN_MESSAGE_B

;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3 / 2
	LOSEBASE:体力 += 80
	LOSEBASE:気力 += 100
ELSEIF EXP:緊縛経験 < EXPLV:4 / 2
	LOSEBASE:体力 += 60
	LOSEBASE:気力 += 80
ELSE
	LOSEBASE:体力 += 40
	LOSEBASE:気力 += 60
ENDIF

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
SOURCE:痛覚 = 50
SOURCE:充足 = 50
SOURCE:露出 = 80
SOURCE:屈従 = 150
SOURCE:逸脱 = 80
SOURCE:恭順 = 80

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
EXP:緊縛経験 += 2
PRINTL 緊縛経験＋２

;ボールギャグの着脱
TEQUIP:ボールギャグ = 1 - TEQUIP:ボールギャグ

RETURN 1

;-------------------------------------------------
;-------------------------------------------------
;ボールギャグ装備中
;-------------------------------------------------
@EQUIP_COM45

PRINTL ＜ボールギャグ装備中＞

;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 100
ELSEIF EXP:緊縛経験 < EXPLV:4
	LOSEBASE:体力 += 40
	LOSEBASE:気力 += 80
ELSE
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 60
ENDIF

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	A = 40
ELSEIF ABL:マゾっ気 == 1
	A = 120
ELSEIF ABL:マゾっ気 == 2
	A = 250
ELSEIF ABL:マゾっ気 == 3
	A = 450
ELSEIF ABL:マゾっ気 == 4
	A = 600
ELSE
	A = 750
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES A , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES A , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES A , 1.10
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES A , 1.20
ENDIF

SOURCE:露出 += A
SOURCE:屈従 += A
SOURCE:逸脱 += A
SOURCE:恭順 += A

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +1
	EXP:レズ経験 += 1
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +1
	EXP:ＢＬ経験 += 1
ENDIF

EXP:緊縛経験 += 1
PRINTL 緊縛経験＋１

