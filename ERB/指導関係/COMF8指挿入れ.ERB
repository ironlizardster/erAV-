;-------------------------------------------------
;指挿入れ
;愛撫系コマンド：調教者が調教対象の%CSTR:女性器呼び方%を手で刺激
;-------------------------------------------------
@COM8

;前回の調教が指挿入れかつプレイヤの技巧３以上だとＧスポット刺激に
SIF PREVCOM == [[TRAIN:指挿入れ]] && ABL:PLAYER:技巧 >= 3
	JUMP COM84
;前回の調教がGスポットならさらにＧスポット刺激に
SIF PREVCOM == [[TRAIN:Ｇスポット刺激]]
	JUMP COM84

PRINTL 指挿入れ
SAVESTR:0 = 指挿入れ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 30
LOSEBASE:気力 += 80

SOURCE:露出 = 300
SOURCE:逸脱 = 200

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:屈従 = 150
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 50
	SOURCE:屈従 = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 250
	SOURCE:屈従 = 400
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 600
	SOURCE:屈従 = 700
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1200
	SOURCE:屈従 = 1300
ELSE
	SOURCE:快Ｖ = 1800
	SOURCE:屈従 = 2000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:屈従, 0.20
	SOURCE:痛覚 = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:屈従, 0.50
	SOURCE:痛覚 = 180
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:屈従, 0.80
	SOURCE:痛覚 = 80
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:屈従, 1.00
	SOURCE:痛覚 = 30
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:屈従, 1.20
	SOURCE:痛覚 = 0
ELSE
	TIMES SOURCE:快Ａ , 1.80
	TIMES SOURCE:屈従, 1.50
	SOURCE:痛覚 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛覚 += 700
	TIMES SOURCE:痛覚 , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛覚 += 200
	TIMES SOURCE:痛覚 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:痛覚 , 0.80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛覚 , 0.50
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:痛覚 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.50
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.20
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.50
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.80
ENDIF


;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF IS_Ｖ鈍感(TARGET)
	TIMES SOURCE:痛覚 , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF IS_Ｖ敏感(TARGET)
	TIMES SOURCE:痛覚 , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF EXP:Ｖ経験 == 0 && IS_貞操観念(TARGET)
	TIMES SOURCE:屈従 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:痛覚 , 2.00

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手召喚
	;奴隷のＶに触手の汚れがつく
	STAIN:ヴァギナ |= 2
	STAIN:ヴァギナ |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:ヴァギナ
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------

;レズ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +4
	EXP:レズ経験 += 4
ENDIF

IF !IS処女(TARGET)
	PRINTS EXPNAME:[[EXP:Ｖ経験]]
	PRINTL +1
	EXP:Ｖ経験 += 1
ENDIF

;愛情経験
E = 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

;-------------------------------------------------
;その他の処理
;-------------------------------------------------

RETURN 1
