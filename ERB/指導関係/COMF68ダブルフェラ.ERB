;-------------------------------------------------
;ダブルフェラ
;助手系コマンド:奴隷と助手に同時に調教者の%CSTR:男性器呼び方%を舐めさせる
;-------------------------------------------------
@COM68

;初キスを奪うかどうかの確認
CALL CONFIRM_FIRSTKISS
SIF RESULT == 0
	RETURN 0

TFLAG:近親相姦 = 0
CALL INCEST
IF (TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2) && HAVE_VAGINA(TARGET) && HAVE_VAGINA(ASSI)
	PRINT 母娘
ELSEIF TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2
	PRINT 親子
ELSEIF  (TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4) && HAVE_VAGINA(TARGET) && HAVE_VAGINA(ASSI)
	PRINT 姉妹
ELSEIF  (TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4) && IS_MALE(TARGET) && IS_MALE(ASSI)
	PRINT 兄弟
ELSEIF  TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4
	PRINT 兄妹
ELSEIF  TFLAG:近親相姦 == 5 || TFLAG:近親相姦 == 6
	PRINT 従姉弟
ENDIF
PRINTL ダブルフェラ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINTS ABLNAME:[[ABL:精液中毒]]
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF IS_恥じらい(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_恥じらい(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF IS_汚臭鈍感(TARGET)
	SIF S
		PRINT  + 
	A += 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF IS_汚臭敏感(TARGET)
	PRINT  - 
	A -= 3
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF IS_快感の否定(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_快感に素直(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い
IF TALENT:男性不信 && IS_MALE(PLAYER) 
	PRINT  - 
	A -= 12
	PRINTS TALENTNAME:[[TALENT:男性不信]]
	PRINTV '(,12,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 1
ENDIF

;調教者がふたなり
IF IS_FUTA(PLAYER)
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
SIF STAIN:PLAYER:ペニス & 16
	Y += 15

SIF IS_汚臭鈍感(TARGET)
	Y /= 3
SIF IS_汚臭敏感(TARGET)
	Y *= 2



;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF IS_汚臭鈍感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSEIF IS_汚臭敏感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(34以上で実行)
PRINT  = 
PRINTV A

V = 34
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SAVESTR:0 = ダブルフェラ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 800
ELSEIF ABL:技巧 == 1
	B = 1100
ELSEIF ABL:技巧 == 2
	B = 1500
ELSEIF ABL:技巧 == 3
	B = 2000
ELSEIF ABL:技巧 == 4
	B = 2400
ELSE
	B = 2800
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕精神 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕精神 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕精神 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕精神 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;ABL:精液中毒をみる
IF ABL:精液中毒 == 0
	TIMES B , 1.00
ELSEIF ABL:精液中毒 == 1
	TIMES B , 1.20
ELSEIF ABL:精液中毒 == 2
	TIMES B , 1.30
ELSEIF ABL:精液中毒 == 3
	TIMES B , 1.50
ELSEIF ABL:精液中毒 == 4
	TIMES B , 1.70
ELSE
	TIMES B , 2.00
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;助手のABL:技巧をみる
IF ABL:ASSI:技巧 >= 2
	TIMES B , 1.00
ELSEIF ABL:ASSI:技巧 == 3
	TIMES B , 1.20
ELSEIF ABL:ASSI:技巧 == 4
	TIMES B , 1.50
ELSE
	TIMES B , 1.80
ENDIF

;助手のTALENT:舌使いをみる
SIF TALENT:ASSI:舌使い
	TIMES B , 2.00

;マスターのABL:Ｃ感覚をみる
IF ABL:MASTER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:MASTER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:MASTER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:MASTER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:MASTER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF


SIF HAVE_PENIS(MASTER)
	BASE:MASTER:射精ゲージ += B


;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 100

SOURCE:屈従 = 1500
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*40 + 100

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 300
	SOURCE:性行動 = 420
	SOURCE:達成 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 400
	SOURCE:性行動 = 500
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 550
	SOURCE:性行動 = 580
	SOURCE:達成 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 700
	SOURCE:性行動 = 660
	SOURCE:達成 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 900
	SOURCE:性行動 = 740
	SOURCE:達成 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:情愛 = 1000
	SOURCE:性行動 = 820
	SOURCE:達成 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES SOURCE:情愛 , 0.50
ELSEIF ABL:信頼 == 1
	TIMES SOURCE:情愛 , 0.80
ELSEIF ABL:信頼 == 2
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:信頼 == 3
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:信頼 == 4
	TIMES SOURCE:情愛 , 1.50
ELSE
	TIMES SOURCE:情愛 , 2.00
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成 , 2.00
ENDIF

;母娘（親子）ダブルフェラの場合は屈従と逸脱３倍
IF TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2
	TIMES SOURCE:屈従 , 3.00
	TIMES SOURCE:逸脱 , 3.00
;姉妹（兄弟）ダブルフェラの場合は屈従と逸脱２倍
ELSEIF TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:逸脱 , 2.00
ENDIF
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:MASTER:射精ゲージ
EJAC = MAXBASE:MASTER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:充足 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:充足 = 500
		TIMES SOURCE:達成 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:充足 = 1200
		TIMES SOURCE:達成 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:充足 = 3000
		TIMES SOURCE:達成 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:充足 = 6000
		TIMES SOURCE:達成 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:充足 = 12000
		TIMES SOURCE:達成 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:充足 , 2.00
	TIMES SOURCE:達成 , 1.50

	EXP:MASTER:射精経験 += 2
	EXP:精液経験 += 9
	PRINTL 大量射精
	PRINTL 精液経験＋９
	;精液汚れ
	STAIN:MASTER:ペニス |= 4

	BASE:MASTER:射精ゲージ -= EJAC*2
	SIF BASE:MASTER:射精ゲージ >= EJAC
		BASE:MASTER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 2
;通常の射精
ELSEIF E == 1
	EXP:MASTER:射精経験 += 1
	EXP:精液経験 += 3
	PRINTL 射精
	PRINTL 精液経験＋３
	;精液汚れ
	STAIN:MASTER:ペニス |= 4

	BASE:MASTER:射精ゲージ -= EJAC
	SIF BASE:MASTER:射精ゲージ >= EJAC
		BASE:MASTER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 1
ENDIF

PRINTFORML %EXPNAME:[[EXP:フェラ経験]]%＋１
EXP:フェラ経験 += 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の口⇔調教者のＰの汚れが移動
STAIN:口 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:口

;助手の口⇔調教者のＰの汚れが移動
STAIN:ASSI:口 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:ASSI:口


;奉仕精神ＬＶ２以上、技巧ＬＶ２以上なら汚れをなめ取る
IF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:MASTER:ペニス = 2
	SIF E >= 1
		TFLAG:フェラで射精後 = 2
ENDIF

;奴隷と助手が共に女性でどちらかが精液中毒を持っているとき、奴隷の口⇔助手の口の汚れが移動
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(ASSI) && (ABL:精液中毒 || ABL:ASSI:精液中毒)
	STAIN:口 |= STAIN:ASSI:ペニス
	STAIN:ASSI:口 |= STAIN:口
	SIF E >= 1
		TFLAG:フェラで射精後 = 3
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;初キス
;とりあえずマスターの方を優先
TFLAG:621 |= 1
TFLAG:621 |= 2
TFLAG:622 |= 1
TFLAG:622 |= 2

;マスターと奴隷のレズorホモ
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(MASTER)
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +7
	EXP:レズ経験 += 7
ELSEIF IS_MALE(TARGET) && IS_MALE(MASTER)
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +7
	EXP:ＢＬ経験 += 7
ENDIF

;助手と奴隷のレズorホモ
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(ASSI)
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +7
	EXP:レズ経験 += 7
ELSEIF IS_MALE(TARGET) && IS_MALE(ASSI)
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +7
	EXP:ＢＬ経験 += 7
ENDIF

;調教者がふたなり
IF IS_FUTA(MASTER)
	SOURCE:屈従 /= 2
ENDIF

TFLAG:快楽経験 = 1

;屈服刻印２に相当
TFLAG:屈服刻印2 = 2

RETURN 1
