;-------------------------------------------------
;手コキフェラ
;-------------------------------------------------
@COM126
SELECTCOM = 126
PRINTL 手コキフェラ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINTS ABLNAME:[[ABL:精液中毒]]
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 1
	PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 1
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い
IF TALENT:男性不信 && TALENT:PLAYER:オトコ
	PRINT  - 
	A -= 12
	PRINTS TALENTNAME:[[TALENT:男性不信]]
	PRINTV '(,12,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 1
ENDIF
;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF S
		PRINT  + 
	A += 8
	PRINTS TALENTNAME:[[TALENT:ふたなり]]
	PRINTV '(,8,')
	S = 1
ENDIF
;獣姦
IF TEQUIP:獣姦プレイ && TALENT:牝犬 == 0
	SIF S
		PRINT  - 
	A -= 15
	PRINTS ITEMNAME:[[ITEM:犬]]
	PRINTV '(,15,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:2 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:2 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:2 & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:2 & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:4 & 32
	Y += 3

;獣姦の場合は汚れ7で固定
SIF TEQUIP:獣姦プレイ
	Y = 7

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(40以上で実行)
PRINT  = 
PRINTV A

V = 40
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SAVESTR:0 = 手コキフェラ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
PRINTFORML %EXPNAME:[[EXP:フェラ経験]]%＋１
EXP:フェラ経験 += 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF TALENT:精愛味覚
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 90
ELSE
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 150
ENDIF

SOURCE:屈従 = 1500
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*40 + 100

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 500
	SOURCE:達成 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 600
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 700
	SOURCE:達成 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 800
	SOURCE:達成 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 900
	SOURCE:達成 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 1000
	SOURCE:達成 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.80
	TIMES SOURCE:達成 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.40
	TIMES SOURCE:達成 , 2.00
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 1200
ELSEIF ABL:技巧 == 1
	B = 1700
ELSEIF ABL:技巧 == 2
	B = 2300
ELSEIF ABL:技巧 == 3
	B = 3000
ELSEIF ABL:技巧 == 4
	B = 3600
ELSE
	B = 4200
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:奉仕技術 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕技術 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕技術 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕技術 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕技術 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;ABL:精液中毒をみる
IF ABL:精液中毒 == 0
	TIMES B , 1.00
ELSEIF ABL:精液中毒 == 1
	TIMES B , 1.20
ELSEIF ABL:精液中毒 == 2
	TIMES B , 1.30
ELSEIF ABL:精液中毒 == 3
	TIMES B , 1.50
ELSEIF ABL:精液中毒 == 4
	TIMES B , 1.70
ELSE
	TIMES B , 2.00
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF TALENT:PLAYER:ぽっちゃり || TALENT:PLAYER:オトコ  || TALENT:PLAYER:ふたなり
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 = 12000
		TIMES SOURCE:達成 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:達成 , 1.50

	EXP:PLAYER:射精経験 += 2
	EXP:精液経験 += 9
	PRINTL 大量射精
	PRINTL 精液経験＋９
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:射精経験 += 1
	EXP:精液経験 += 3
	PRINTL 射精
	PRINTL 精液経験＋３
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 1
ENDIF

;-------------------------------------------------
;汚れ
;-------------------------------------------------
;奴隷の指⇔調教者のＰの汚れが移動
STAIN:1 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:1

;奴隷の口⇔調教者のＰの汚れが移動
STAIN:0 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:0

;奉仕精神ＬＶ２以上、技巧ＬＶ２以上なら汚れをなめ取る
IF ABL:奉仕精神 >= 2 && ABL:技巧 >= 2
	STAIN:PLAYER:2 = 2
	SIF E >= 1
		TFLAG:フェラで射精後 = 1
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +7
	EXP:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +7
	EXP:ＢＬ経験 += 7
ENDIF

SIF ASSIPLAY == 0 && EXP:Ｖ経験 >= EXPLV:3
		TFLAG:主人経験 += 1

IF TALENT:オトコ
	E = 2
ELSE
	E = 1
ENDIF
;初キス
TFLAG:621 |= 1
TFLAG:621 |= 2
;現在の判定法と違うのが残ってるのを偶然見つけたので↑に書き換え
;IF !CFLAG:初キス相手
;	CFLAG:初キス相手 = NO:PLAYER + 201
;	TFLAG:初キス口以外 = 1

;愛情経験
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
ENDIF

TFLAG:快楽経験 = 1

;屈服刻印２に相当
TFLAG:屈服刻印2 = 2

RETURN 1
