@COM60
;助手にキス

PRINTL 助手にキスさせる

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF IS_恥じらい(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_恥じらい(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF IS_汚臭鈍感(TARGET)
	SIF S
		PRINT  + 
	A += 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF IS_汚臭敏感(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_汚臭耐性(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF IS_快感の否定(TARGET)
	PRINT  - 
	A -= 1
	PRINTS GET_STR_快感に素直(TARGET)
	PRINTV '(,1,')
	S = 1
ENDIF
;女嫌い
IF TALENT:ボーイッシュ && HAVE_VAGINA(PLAYER) 
	PRINT  - 
	A -= 5
	PRINTS TALENTNAME:[[TALENT:ボーイッシュ]]
	PRINTV '(,5,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:口 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:口 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:口 & 8
	Y += 7
SIF STAIN:PLAYER:口 & 16
	Y += 15

SIF IS_汚臭鈍感(TARGET)
	Y /= 3
SIF IS_汚臭敏感(TARGET)
	Y *= 2

;キスでは汚れがそれほど影響しない
Y /= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF IS_汚臭鈍感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSEIF IS_汚臭敏感(TARGET)
		PRINT 汚れあり、
		PRINTS GET_STR_汚臭耐性(TARGET)
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(15以上で実行)
PRINT  = 
PRINTV A

V = 15
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SELECTCOM = 60
SAVESTR:0 = 助手にキス
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック（ふたなり・オトコの場合）
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 50
ELSEIF ABL:技巧 == 1
	B = 200
ELSEIF ABL:技巧 == 2
	B = 300
ELSEIF ABL:技巧 == 3
	B = 400
ELSEIF ABL:技巧 == 4
	B = 500
ELSE
	B = 600
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:奉仕技術 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕技術 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕技術 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕技術 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕技術 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF HAVE_PENIS(PLAYER)
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:気力 += 20

SOURCE:屈従 = 100
SOURCE:逸脱 = 10

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*20 + 10

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 50
	SOURCE:達成 = 0
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 150
	SOURCE:達成 = 50
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 200
	SOURCE:達成 = 100
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 250
	SOURCE:達成 = 180
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 300
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 350
	SOURCE:達成 = 500
	TIMES SOURCE:不潔 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成 , 2.50
ELSE
	TIMES SOURCE:性行動 , 4.00
	TIMES SOURCE:達成 , 4.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 100
		TIMES SOURCE:達成 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 300
		TIMES SOURCE:達成 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 700
		TIMES SOURCE:達成 , 4.00
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成 , 5.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成 , 6.00
		TIMES SOURCE:屈従, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:達成 , 1.50

	EXP:PLAYER:射精経験 += 2
	EXP:精液経験 += 1
	PRINTL 大量射精
	PRINTL 精液経験＋１
	;精液汚れ
	STAIN:PLAYER:ペニス |= 4

	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;キスで射精させたフラグ
	TFLAG:キスで射精 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:射精経験 += 1
	PRINTL 射精
	;精液汚れ
	STAIN:PLAYER:ペニス |= 4

	BASE:PLAYER:射精ゲージ -= EJAC
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;キスで射精させたフラグ
	TFLAG:キスで射精 = 1
ENDIF

;奴隷の口⇔調教者の口の汚れが移動
STAIN:口 |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:口

;初キス
TFLAG:621 |= 1

IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +3
	EXP:ＢＬ経験 += 3
ENDIF

TFLAG:快楽経験 = 1

;屈服刻印１に相当
TFLAG:屈服刻印2 = 1

RETURN 1
