;-------------------------------------------------
;騎乗位アナル
;奉仕系コマンド：調教対象が調教者の%CSTR:男性器呼び方%をアナルで刺激
;-------------------------------------------------
@COM36
;Ａ処女を奪うかどうかの確認
CALL CONFIRM_LOST_ANALVIRGIN
SIF RESULT == 0
	RETURN 0


PRINTL 騎乗位アナル

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 3
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	S = 1
ENDIF
;ABL:Ａ感覚
IF ABL:Ａ感覚
	SIF S
		PRINT  + 
	A += ABL:Ａ感覚 * 2
	PRINTS ABLNAME:[[ABL:Ａ感覚]]
	PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 1
	PRINTS ABLNAME:[[ABL:精液中毒]]
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 3
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	S = 1
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	PRINT  - 
	A -= 5
	PRINT 潤滑不足
	PRINTV '(,5,')
	S = 1
ENDIF
;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 3
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 3,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 2
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,2,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 5
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,5,')
	S = 1
ENDIF
;男嫌い
IF TALENT:男性不信 && IS_MALE(PLAYER) 
	PRINT  - 
	A -= 12
	PRINTS TALENTNAME:[[TALENT:男性不信]]
	PRINTV '(,12,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF

;尻穴狂い
IF TALENT:尻穴狂い
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:尻穴狂い]]
	PRINTV '(,5,')
	S = 5
ENDIF

;調教者がふたなり
IF IS_FUTA(PLAYER)
	SIF S
		PRINT  + 
	A += 4
	PRINT ふたなり
	PRINTV '(,4,')
	S = 1
ENDIF

;しあわせ草
IF TEQUIP:媚薬効果
	SIF S
		PRINT  + 
	A += 6
	PRINTS ITEMNAME:[[ITEM:プリンセスポイズン]]
	PRINTV '(,6,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:ペニス & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:アナル & 32
	Y += 3

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;騎乗位では汚れの影響が少なめ
Y /= 3

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(24以上で実行)
PRINT  = 
PRINTV A

V = 24
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0


;-------------------------------------------------
;実行決定
;-------------------------------------------------

SAVESTR:0 = 騎乗位アナル
CALL TRAIN_MESSAGE_B
;A経験を伴うコマンドのフラグ
TFLAG:Ａ挿入判定 = 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL = 90
LOCAL:1 = 110

LOCAL:2 = ABL:Ａ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ａ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL -= LOCAL:2
SIF LOCAL <= 10
	LOCAL = 10
LOCAL:1 -= LOCAL:3
SIF LOCAL:1 <= 10
	LOCAL:1 = 10

LOSEBASE:体力 += LOCAL
LOSEBASE:気力 += LOCAL:1

SOURCE:露出 = 900

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 200
	SOURCE:達成 = 50
	SOURCE:逸脱 = 300
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 250
	SOURCE:達成 = 200
	SOURCE:逸脱 = 100
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 350
	SOURCE:達成 = 550
	SOURCE:逸脱 = 30
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 450
	SOURCE:達成 = 900
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 600
	SOURCE:達成 = 1500
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 750
	SOURCE:達成 = 2200
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 = 10
	SOURCE:屈従 += 80
	TIMES SOURCE:性行動 , 0.50
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 20
	SOURCE:情愛 = 50
	SOURCE:屈従 += 300
	TIMES SOURCE:性行動 , 0.80
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 200
	SOURCE:情愛 = 100
	SOURCE:屈従 += 700
	TIMES SOURCE:性行動 , 1.00
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 450
	SOURCE:情愛 = 180
	SOURCE:屈従 += 1500
	TIMES SOURCE:性行動 , 1.20
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 850
	SOURCE:情愛 = 300
	SOURCE:屈従 += 2600
	TIMES SOURCE:性行動 , 1.50
ELSE
	SOURCE:快Ａ = 1300
	SOURCE:情愛 = 450
	SOURCE:屈従 += 4000
	TIMES SOURCE:性行動 , 2.00
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛覚 = 18000
	LOSEBASE:体力 += 70
	LOSEBASE:気力 += 80
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛覚 = 10000
	LOSEBASE:体力 += 60
	LOSEBASE:気力 += 70
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛覚 = 4500
	LOSEBASE:体力 += 45
	LOSEBASE:気力 += 55
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛覚 = 1500
	LOSEBASE:体力 += 25
	LOSEBASE:気力 += 35
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛覚 = 700
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 15
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛覚 = 300
	LOSEBASE:体力 += 0
	LOSEBASE:気力 += 0
ENDIF


;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛覚 += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛覚 += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛覚 += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛覚 += 200
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛覚 += 100
ENDIF

;大柄
SIF TALENT:大柄
	TIMES SOURCE:痛覚 , 0.80
;小柄体形
SIF TALENT:小柄
	TIMES SOURCE:痛覚 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:痛覚 , 2.00

;貞操観念
IF TALENT:貞操観念
	IF EXP:Ａ経験 == 0
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順 , 1.10
		SOURCE:反感 = 500
	ELSE
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順 , 1.10
		SOURCE:反感 = 0
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF


;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES SOURCE:快Ａ , 0.50
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感 , 2.00
	SOURCE:屈従 += 700
ELSEIF ABL:信頼 == 1
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感 , 1.50
	SOURCE:屈従 += 500
ELSEIF ABL:信頼 == 2
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感 , 1.00
	SOURCE:屈従 += 300
ELSEIF ABL:信頼 == 3
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感 , 0.80
	SOURCE:屈従 += 100
ELSEIF ABL:信頼 == 4
	TIMES SOURCE:快Ａ , 1.60
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感 , 0.60
	SOURCE:屈従 += 30
ELSE
	TIMES SOURCE:快Ａ , 2.00
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:反感 , 0.30
	SOURCE:屈従 += 0
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:快Ａ , 0.50
	TIMES SOURCE:情愛 , 0.60
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:情愛 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:快Ａ , 1.60
	TIMES SOURCE:情愛 , 1.40
ELSE
	TIMES SOURCE:快Ａ , 2.00
	TIMES SOURCE:情愛 , 1.60
ENDIF

;調教者の射精チェック
CALL COM_EJAC_PLAYER_ANALSEX

;射精している
IF E
	TIMES SOURCE:性行動 , 3.00
	TIMES SOURCE:達成 , 5.00
	TIMES SOURCE:屈従 , 1.50

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 300
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1000
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 2200
	ELSE
		SOURCE:中毒充足 = 3000
	ENDIF
ENDIF

;アナルセックス後の処理
CALL COM_AFTER_ANAL_SEX

TFLAG:快楽経験 = 1

IF EXP:Ａ経験 <= 1
	;屈服刻印３に相当
	TFLAG:屈服刻印2 = 3
ELSE
	;屈服刻印２に相当
	TFLAG:屈服刻印2 = 2
ENDIF

;調教者がふたなり
SIF IS_FUTA(PLAYER)
	SOURCE:屈従 /= 2

RETURN 1
