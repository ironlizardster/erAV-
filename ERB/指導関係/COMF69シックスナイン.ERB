;-------------------------------------------------
;シックスナイン
;特殊系コマンド：調教者と調教対象が同時に互いの性器を口で刺激しある
;特定コマンドの連続選択で発動
;-------------------------------------------------
;シックスナインの発動条件
;「前回と今回の調教者が同じ」で、COM_ABLE69に記述されてる条件を満たした上で、
;フェラチオ〔させる〕⇔フェラする（調教者が男orふたなりで調教対象が男orふたなり）
;フェラチオ〔させる〕⇔クンニ〔する〕（調教者が男で調教対象が女）
;クンニ強制⇔フェラする（調教者が女で調教対象が男orふたなり）
;クンニ強制⇔クンニ〔する〕（調教者が女で調教対象が女）
;-------------------------------------------------
@COM69

TFLAG:近親相姦 = 0
CALL INCEST
IF (TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2) && TALENT:PLAYER:オトコ == 0 && TALENT:オトコ == 0
	PRINT 母娘
ELSEIF TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2
	PRINT 親子
ELSEIF  (TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4) && TALENT:PLAYER:オトコ == 0 && TALENT:オトコ == 0
	PRINT 姉妹
ELSEIF  (TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4) && TALENT:PLAYER:オトコ && TALENT:オトコ
	PRINT 兄弟
ELSEIF  TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4
	PRINT 兄妹
ELSEIF  TFLAG:近親相姦 == 5 || TFLAG:近親相姦 == 6
	PRINT 従姉弟
ENDIF
PRINTL シックスナイン

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 2
	PRINTS ABLNAME:[[ABL:欲望]]
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 2,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINTS ABLNAME:[[ABL:奉仕精神]]
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:[[MARK:快楽刻印]]
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 2
	PRINTS PALAMNAME:[[PALAM:欲情]]
	PRINTV 'LV,L,'(,L * 2,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:恥じらい]]
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 3
	PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:[[TALENT:献身的]]
	PRINTV '(,6,')
	S = 1
ENDIF
;快感に素直
IF TALENT:快感に素直
	SIF S
		PRINT  + 
	A += 3
	PRINTS TALENTNAME:[[TALENT:快感に素直]]
	PRINTV '(,3,')
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:[[TALENT:快感の否定]]
	PRINTV '(,3,')
	S = 1
ENDIF
;愛
IF TALENT:恋心 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:[[TALENT:恋心]]
	PRINTV '(,5,')
	S = 5
ENDIF

;しあわせ草
IF TEQUIP:媚薬効果
	SIF S
		PRINT  + 
	A += 8
	PRINTS ITEMNAME:[[ITEM:プリンセスポイズン]]
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:3 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:3 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:3 & 8
	Y += 7
SIF STAIN:PLAYER:3 & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭鈍感]]
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:[[TALENT:汚臭敏感]]
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

V = 33
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

SELECTCOM = 69
SAVESTR:0 = シックスナイン

CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック（ふたなり・オトコの場合）
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	B = 1200
ELSEIF ABL:技巧 == 1
	B = 1700
ELSEIF ABL:技巧 == 2
	B = 2300
ELSEIF ABL:技巧 == 3
	B = 3000
ELSEIF ABL:技巧 == 4
	B = 3600
ELSE
	B = 4200
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES B , 0.80
ELSEIF ABL:信頼 == 1
	TIMES B , 0.90
ELSEIF ABL:信頼 == 2
	TIMES B , 1.00
ELSEIF ABL:信頼 == 3
	TIMES B , 1.10
ELSEIF ABL:信頼 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	TIMES B , 0.50
ELSEIF ABL:奉仕精神 == 1
	TIMES B , 0.80
ELSEIF ABL:奉仕精神 == 2
	TIMES B , 1.20
ELSEIF ABL:奉仕精神 == 3
	TIMES B , 1.50
ELSEIF ABL:奉仕精神 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;ABL:精液中毒をみる
IF ABL:精液中毒 == 0
	TIMES B , 1.00
ELSEIF ABL:精液中毒 == 1
	TIMES B , 1.20
ELSEIF ABL:精液中毒 == 2
	TIMES B , 1.30
ELSEIF ABL:精液中毒 == 3
	TIMES B , 1.50
ELSEIF ABL:精液中毒 == 4
	TIMES B , 1.70
ELSE
	TIMES B , 2.00
ENDIF

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

SIF TALENT:PLAYER:オトコ  || TALENT:PLAYER:ふたなり
	BASE:PLAYER:射精ゲージ += B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF TALENT:精愛味覚
	LOSEBASE:気力 += 10
	LOSEBASE:気力 += 80
ELSE
	LOSEBASE:気力 += 20
	LOSEBASE:気力 += 160
ENDIF

;△COMF1に比べてだいぶ高いのがううむ
SOURCE:液体追加 = 100
SOURCE:愛液分泌 = 900
SOURCE:露出 = 1400
SOURCE:屈従 = 1300
SOURCE:逸脱 = 800

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*80 + 50

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 40
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 160
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 700
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1500
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2400
ELSE
	SOURCE:快Ｃ = 3300
ENDIF

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 2.00
	SOURCE:恭順 += SOURCE:快Ｃ/20
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 620
	SOURCE:達成 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 700
	SOURCE:達成 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 820
	SOURCE:達成 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 940
	SOURCE:達成 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 1100
	SOURCE:達成 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 1260
	SOURCE:達成 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成 , 2.50
ELSE
	TIMES SOURCE:性行動 , 4.00
	TIMES SOURCE:達成 , 4.00
ENDIF

;調教者のABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成 , 0.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES SOURCE:性行動 , 0.90
	TIMES SOURCE:達成 , 0.70
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成 , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES SOURCE:性行動 , 1.10
	TIMES SOURCE:達成 , 1.20
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成 , 1.40
ELSE
	TIMES SOURCE:性行動 , 1.30
	TIMES SOURCE:達成 , 1.70
ENDIF

IF PREVCOM == [[TRAIN:アナル舐め]]
	;ABL:Ａ感覚をみる
	IF ABL:Ａ感覚 == 0
		SOURCE:快Ａ = 5
	ELSEIF ABL:Ａ感覚 == 1
		SOURCE:快Ａ = 50
	ELSEIF ABL:Ａ感覚 == 2
		SOURCE:快Ａ = 200
	ELSEIF ABL:Ａ感覚 == 3
		SOURCE:快Ａ = 500
	ELSEIF ABL:Ａ感覚 == 4
		SOURCE:快Ａ = 1000
	ELSE
		SOURCE:快Ａ = 1800
	ENDIF
ENDIF

;母娘（親子）シックスナインの場合は屈従と逸脱３倍
IF TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2
	TIMES SOURCE:屈従 , 3.00
	TIMES SOURCE:逸脱 , 3.00
;姉妹（兄弟）シックスナインの場合は屈従と逸脱２倍
ELSEIF TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:逸脱 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:射精ゲージ
EJAC = MAXBASE:PLAYER:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成 , 4.50
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 2500
		TIMES SOURCE:達成 , 6.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 5000
		TIMES SOURCE:達成 , 8.00
		TIMES SOURCE:屈従, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:達成 , 1.50

	EXP:PLAYER:射精経験 += 2
	EXP:精液経験 += 9
	PRINTL 大量射精
	PRINTL 精液経験＋９
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC*2
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:射精経験 += 1
	EXP:精液経験 += 3
	PRINTL 射精
	PRINTL 精液経験＋３
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:射精ゲージ -= EJAC
	SIF BASE:PLAYER:射精ゲージ >= EJAC
		BASE:PLAYER:射精ゲージ = EJAC-1

	;口で射精させたフラグ
	TFLAG:口で射精 = 1
ENDIF

;調教者がオトコかふたなりだとフェラ経験
IF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:[[EXP:フェラ経験]]%＋１
	EXP:フェラ経験 += 1
ENDIF

;奴隷の口⇔調教者のＶの汚れが移動
STAIN:0 |= STAIN:PLAYER:3
STAIN:PLAYER:3 |= STAIN:0
;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:3 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:3

;初キス
TFLAG:620 |= 1
IF TALENT:ふたなり || TALENT:オトコ
	TFLAG:620 |= 2
ELSE
	TFLAG:620 |= 8
ENDIF
TFLAG:621 |= 1
IF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	TFLAG:621 |= 2
ELSE
	TFLAG:621 |= 8
ENDIF


IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +8
	EXP:レズ経験 += 8
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +8
	EXP:ＢＬ経験 += 8
ENDIF

IF TALENT:オトコ
	E = 2
ELSE
	E = 1
ENDIF
;愛情経験
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

SIF ASSIPLAY == 0 && EXP:Ｖ経験 >= EXPLV:3
	TFLAG:主人経験 += 1

TFLAG:快楽経験 = 1

;屈服刻印２に相当
TFLAG:屈服刻印2 = 2

RETURN 1

