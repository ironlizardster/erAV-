;-------------------------------------------------
;後背位アナル
;セックス系コマンド：調教者の%CSTR:男性器呼び方%を調教対象のアナルに挿入
;正常位アナルに比べて気力消費が大きく逸脱が上がるが屈従の上昇が高い
;-------------------------------------------------
@COM27
;アナル処女を奪うかどうかの確認
CALL CONFIRM_LOST_ANALVIRGIN
SIF RESULT == 0
	RETURN 0



;前回の調教が3Pのとき3Pへ
TFLAG:３Ｐ継続中 = 0
IF PREVCOM == 64
	CALL COM_ABLE64
	IF RESULT == 1
		TFLAG:３Ｐ継続中 = 1
		JUMP COM64
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF (ASSIPLAY && TFLAG:前回の調教者が助手 == 0) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手)
	;前回の調教が正常位か後背位かフェラチオかイラマチオのときは3Pへ
	IF PREVCOM == 20 || PREVCOM == 21 || PREVCOM == 31 || PREVCOM == 80
		CALL COM_ABLE64
		SIF RESULT == 1
			JUMP COM64
	ENDIF
ENDIF

PRINTL 後背位アナル
SAVESTR:0 = 後背位アナル
CALL TRAIN_MESSAGE_B

;A経験を伴うコマンドのフラグ
TFLAG:Ａ挿入判定 = 1

;調教者の射精チェック
CALL COM_EJAC_PLAYER_ANALSEX

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL = 120
LOCAL:1 = 200

LOCAL:2 = ABL:Ａ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ａ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL -= LOCAL:2
SIF LOCAL <= 10
	LOCAL = 10
LOCAL:1 -= LOCAL:3
SIF LOCAL:1 <= 10
	LOCAL:1 = 10

LOSEBASE:体力 += LOCAL
LOSEBASE:気力 += LOCAL:1

SOURCE:12 = 1200
SOURCE:14 = 1200

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:2 = 10
	SOURCE:3 = 10
	SOURCE:13 = 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:2 = 30
	SOURCE:3 = 30
	SOURCE:13 = 700
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:2 = 500
	SOURCE:3 = 100
	SOURCE:13 = 1500
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:2 = 1000
	SOURCE:3 = 200
	SOURCE:13 = 3000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:2 = 1700
	SOURCE:3 = 450
	SOURCE:13 = 5000
ELSE
	SOURCE:2 = 2200
	SOURCE:3 = 750
	SOURCE:13 = 8000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:2 , 0.10
	SOURCE:6 = 20000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:2 , 0.30
	SOURCE:6 = 12000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:2 , 0.50
	SOURCE:6 = 5000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:2 , 1.00
	SOURCE:6 = 1800
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:2 , 1.40
	SOURCE:6 = 1000
ELSE
	TIMES SOURCE:2 , 1.60
	SOURCE:6 = 600
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:2 , 0.40
	SOURCE:6 += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	SOURCE:6 += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	SOURCE:6 += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:2 , 1.40
	SOURCE:6 += 200
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:2 , 1.80
	SOURCE:6 += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:13 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:13 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:13 , 1.20
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:2 , 1.40
	TIMES SOURCE:13 , 1.40
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF TALENT:ASSI:ふたなり
		TIMES SOURCE:2 , 2.50
ENDIF

;大柄
SIF TALENT:大柄
	TIMES SOURCE:6 , 0.80
;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:6 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:6 , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:Ｖ経験 == 0 && TALENT:貞操観念
	SOURCE:13 /= 3
ENDIF

;アナルセックス後の処理
CALL COM_AFTER_ANAL_SEX

RETURN 1
;
;
;
