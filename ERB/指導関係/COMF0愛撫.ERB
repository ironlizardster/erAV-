;-------------------------------------------------
;愛撫
;調教者が手と口で調教対象の乳房とクリトリス（%CSTR:男性器呼び方%）を刺激
;-------------------------------------------------
@COM0

PRINTL 愛撫
SAVESTR:0 = 愛撫
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

;快ｃのソース
SOURCE:快Ｃ = 0
;快ｂのソース
SOURCE:快Ｂ = 0
;情愛のソース
SOURCE:情愛 = 0
;性行動のソース
SOURCE:性行動 = 60
;不潔のソース
SOURCE:不潔 = 30
;露出のソース
SOURCE:露出 = 100

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 20
	SOURCE:情愛 = 25
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 100
	SOURCE:情愛 = 50
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 500
	SOURCE:情愛 = 80
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1200
	SOURCE:情愛 = 100
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2000
	SOURCE:情愛 = 115
ELSE
	SOURCE:快Ｃ = 2800
	SOURCE:情愛 = 125
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 15
	SOURCE:情愛 += 25
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 50
	SOURCE:情愛 += 50
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 300
	SOURCE:情愛 += 80
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 700
	SOURCE:情愛 += 100
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1100
	SOURCE:情愛 += 115
ELSE
	SOURCE:快Ｂ = 1600
	SOURCE:情愛 += 125
ENDIF

;△キスを避けると快C半分に減るの……？
;奴隷の口が愛液、精液、アナル、尿で汚れており、
;汚臭敏感で従順3以下のASSIの場合キスはしない（汚れ無視なら問題なし）
IF  (STAIN:口 & 1 || STAIN:口 & 4 || STAIN:口 & 8 || STAIN:口 & 32) && ASSIPLAY
	IF ABL:ASSI:信頼 <= 3 && IS_汚臭敏感(ASSI) && TALENT:ASSI:汚れ無視 == 0
		SOURCE:不潔 = 0
		SOURCE:快Ｃ /= 2
		SOURCE:情愛 /= 4
		SOURCE:愛液分泌 /= 2
	ENDIF
;ボールギャグ使用中もキスはなし
ELSEIF TEQUIP:ボールギャグ
	SOURCE:不潔 = 0
	SOURCE:快Ｃ /= 2
	SOURCE:情愛 /= 4
	SOURCE:愛液分泌 /= 2
;初キス未体験だとキスを避けるので効果減
ELSEIF !CFLAG:初キス相手
	SOURCE:不潔 = 0
	SOURCE:快Ｃ /= 2
	SOURCE:情愛 /= 4
	SOURCE:愛液分泌 /= 2
ELSE
	;汚臭鈍感
	SIF IS_汚臭鈍感(TARGET)
		SOURCE:不潔 /= 4
	;汚臭敏感
	SIF IS_汚臭敏感(TARGET)
		SOURCE:不潔 *= 3
	;自尊心
	SIF IS_プライド高い(TARGET)
		SOURCE:不潔 *= 2
	;愛
	IF TALENT:恋心 && ASSIPLAY == 0
		SOURCE:情愛 *= 2
		SOURCE:不潔 /= 10
	ENDIF
	IF STAIN:PLAYER:口
	;主人の口の汚れがあると不潔150%
		SOURCE:不潔 *= 3
		SOURCE:不潔 /= 2
	ENDIF

	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:口 |= STAIN:PLAYER:口
	STAIN:PLAYER:口 |= STAIN:口
ENDIF

;獣姦の場合はここで終了
SIF TEQUIP:獣姦プレイ
	RETURN 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手召喚
	;奴隷に触手の汚れがつく
	STAIN:手 |= 2
	STAIN:手 |= 4
	STAIN:胸 |= 2
	STAIN:胸 |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:ヴァギナ

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:胸 |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:胸
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ経験・ホモ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +5
	EXP:レズ経験 += 5
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +5
	EXP:ＢＬ経験 += 5
ENDIF

;愛情経験
;基本値
E = 2


PRINTFORML %EXPNAME:[[EXP:Ｂ開発経験]]%+1
	EXP:Ｂ開発経験 += 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

RETURN 1
