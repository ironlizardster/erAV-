;-------------------------------------------------
;愛撫
;調教者が手と口で調教対象の乳房とクリトリス（%CSTR:男性器呼び方%）を刺激
;-------------------------------------------------
@COM0

PRINTL 愛撫
SAVESTR:0 = 愛撫
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

;快ｃのソース
SOURCE:0 = 0
;快ｂのソース
SOURCE:17 = 0
;情愛のソース
SOURCE:3 = 0
;性行動のソース
SOURCE:4 = 60
;不潔のソース
SOURCE:8 = 30
;露出のソース
SOURCE:12 = 100

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:0 = 20
	SOURCE:3 = 25
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:0 = 100
	SOURCE:3 = 50
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:0 = 500
	SOURCE:3 = 80
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:0 = 1200
	SOURCE:3 = 100
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:0 = 2000
	SOURCE:3 = 115
ELSE
	SOURCE:0 = 2800
	SOURCE:3 = 125
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:17 = 15
	SOURCE:3 += 25
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:17 = 50
	SOURCE:3 += 50
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:17 = 300
	SOURCE:3 += 80
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:17 = 700
	SOURCE:3 += 100
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:17 = 1100
	SOURCE:3 += 115
ELSE
	SOURCE:17 = 1600
	SOURCE:3 += 125
ENDIF

;△キスを避けると快C半分に減るの……？
;奴隷の口が愛液、精液、アナル、尿で汚れており、
;汚臭敏感で従順3以下のASSIの場合キスはしない（汚れ無視なら問題なし）
IF  (STAIN:0 & 1 || STAIN:0 & 4 || STAIN:0 & 8 || STAIN:0 & 32) && ASSIPLAY
	IF ABL:ASSI:信頼 <= 3 && TALENT:ASSI:汚臭敏感 && TALENT:ASSI:汚れ無視 == 0
		SOURCE:8 = 0
		SOURCE:0 /= 2
		SOURCE:3 /= 4
		SOURCE:10 /= 2
	ENDIF
;ボールギャグ使用中もキスはなし
ELSEIF TEQUIP:ボールギャグ
	SOURCE:8 = 0
	SOURCE:0 /= 2
	SOURCE:3 /= 4
	SOURCE:10 /= 2
;初キス未体験だとキスを避けるので効果減
ELSEIF !CFLAG:初キス相手
	SOURCE:8 = 0
	SOURCE:0 /= 2
	SOURCE:3 /= 4
	SOURCE:10 /= 2
ELSE
	;汚臭鈍感
	SIF TALENT:汚臭鈍感
		SOURCE:8 /= 4
	;汚臭敏感
	SIF TALENT:汚臭敏感
		SOURCE:8 *= 3
	;自尊心
	SIF TALENT:プライド高い
		SOURCE:8 *= 2
	;愛
	IF TALENT:恋心 && ASSIPLAY == 0
		SOURCE:3 *= 2
		SOURCE:8 /= 10
	ENDIF
	IF STAIN:PLAYER:0
	;主人の口の汚れがあると不潔150%
		SOURCE:8 *= 3
		SOURCE:8 /= 2
	ENDIF

	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:0 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:0
ENDIF

;獣姦の場合はここで終了
SIF TEQUIP:獣姦プレイ
	RETURN 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:触手召喚
	;奴隷に触手の汚れがつく
	STAIN:1 |= 2
	STAIN:1 |= 4
	STAIN:5 |= 2
	STAIN:5 |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:3 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:3

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:5 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:5
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ経験・ホモ経験
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +5
	EXP:レズ経験 += 5
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +5
	EXP:ＢＬ経験 += 5
ENDIF

;愛情経験
;基本値
E = 2


PRINTFORML %EXPNAME:[[EXP:Ｂ開発経験]]%+1
	EXP:Ｂ開発経験 += 1
IF CFLAG:好感度 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:[[EXP:愛情経験]]%+{E}
	EXP:愛情経験 += E
ENDIF
E = 0

RETURN 1
