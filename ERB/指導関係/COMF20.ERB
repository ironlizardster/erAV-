;-------------------------------------------------
;正常位
;セックス系コマンド：調教者の%CSTR:男性器呼び方%を調教対象の%CSTR:女性器呼び方%に挿入
;-------------------------------------------------
@COM20

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0)
	;正常位・キスと正常位・胸愛撫を連続実行し（逆順可）、かつ実行条件を満たすと正常位SPに
	;(前々回のコマンドが正常位・極で正常位・キスor胸愛撫でも可能)
	IF (TFLAG:前々回のコマンド == 128 && PREVCOM == 129) || (TFLAG:前々回のコマンド == 129 && PREVCOM == 128) || TFLAG:前々回のコマンド == 130 && (PREVCOM == 128 || PREVCOM == 129) || PREVCOM == 130
		CALL COM_ABLE130
		SIF RESULT == 1
			JUMP COM130
	ENDIF
ENDIF

;前のプレイが正常位か正常位・キスか正常位・胸愛撫かつ調教者の技巧3以上の場合、挿入Ｇスポ責めまたは挿入子宮口責めへ派生
;Ｖ感覚が高く欲情しているほど子宮口へ行きやすい
IF FLAG:追加コマンドなし == 0
	IF (PREVCOM == 20 || PREVCOM == 128 ||PREVCOM == 129)&& ABL:PLAYER:技巧 > 2 && ((ASSIPLAY && TFLAG:前回の調教者が助手) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手 == 0))
		;欲情
		IF PALAM:欲情 < PALAMLV:1
			A = 1
		ELSEIF PALAM:欲情 < PALAMLV:2
			A = 2
		ELSEIF PALAM:欲情 < PALAMLV:3
			A = 3
		ELSEIF PALAM:欲情 < PALAMLV:4
			A = 4
		ELSE
			A = 5
		ENDIF
		B = RAND:11
		A *= B
		B = RAND:11
		A += B*ABL:Ｖ感覚
		;Aの値は0～100
		;正常位だと40以上で子宮口
		IF A >= 40
			CALL COM_ABLE121
			SIF RESULT == 1
			JUMP COM121
		ELSE
			CALL COM_ABLE120
			SIF RESULT == 1
			JUMP COM120
		ENDIF
	ENDIF
ENDIF

;処女を奪うかどうかの確認
CALL CONFIRM_LOST_VIRGIN
SIF RESULT == 0
	RETURN 0

;前回の調教が3Pのとき3Pへ
TFLAG:３Ｐ継続中 = 0
IF PREVCOM == 64
	CALL COM_ABLE64
	IF RESULT == 1
		TFLAG:３Ｐ継続中 = 1
		JUMP COM64
	ENDIF
;調教者が前回:助手で今回:主人か、前回:主人で今回:助手のとき
ELSEIF (ASSIPLAY && TFLAG:前回の調教者が助手 == 0) || (ASSIPLAY == 0 && TFLAG:前回の調教者が助手)
	;前回の調教が後背位アナルかフェラチオかイラマチオのとき3Pへ
	IF PREVCOM == 27 || PREVCOM == 31 || PREVCOM == 80
		CALL COM_ABLE64
		SIF RESULT == 1
			JUMP COM64
	ENDIF
ENDIF

PRINTL 正常位
SAVESTR:0 = 正常位
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ
TFLAG:V経験を伴うコマンド = 1
SIF TALENT:恋心 && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:主人による処女喪失 = 1

;調教者の射精チェック
CALL COM_EJAC_PLAYER_SEX

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL = 100
LOCAL:1 = 50

LOCAL:2 = ABL:Ｖ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ｖ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL:2 = ABL:Ｖ感覚 * 3
SIF LOCAL:2 <= 0
	LOCAL:2 = 1
LOCAL:3 = ABL:Ｖ感覚 * 2
SIF LOCAL:3 <= 0
	LOCAL:3 = 1

LOCAL -= LOCAL:2
SIF LOCAL <= 10
	LOCAL = 10
LOCAL:1 -= LOCAL:3
SIF LOCAL:1 <= 10
	LOCAL:1 = 10

LOSEBASE:体力 += LOCAL
LOSEBASE:気力 += LOCAL:1

SOURCE:12 = 400

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:1 = 40
	SOURCE:3 = 150
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:1 = 150
	SOURCE:3 = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:1 = 400
	SOURCE:3 = 350
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:1 = 1000
	SOURCE:3 = 500
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:1 = 1700
	SOURCE:3 = 700
ELSE
	SOURCE:1 = 2200
	SOURCE:3 = 1000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 5500
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:1 , 1.30
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.80
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 1000
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 300
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF TALENT:ASSI:ふたなり
		TIMES SOURCE:1 , 2.50
ENDIF

;大柄
SIF TALENT:大柄
	TIMES SOURCE:6 , 0.80
;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:6 , 2.00
;未熟
SIF TALENT:未熟
	TIMES SOURCE:6 , 4.00

;貞操観念
IF TALENT:貞操観念
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 10000
	ELSE
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 300
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:15 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:3 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:3 , 1.50
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
	TIMES SOURCE:3 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:信頼 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
	TIMES SOURCE:15 , 2.00
ELSEIF ABL:信頼 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:15 , 1.50
ELSEIF ABL:信頼 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:信頼 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:信頼 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:15 , 0.60
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:3 , 1.60
	TIMES SOURCE:15 , 0.30
ENDIF

;セックス後の処理
CALL COM_AFTER_VAGINA_SEX

RETURN 1
;
;
;
