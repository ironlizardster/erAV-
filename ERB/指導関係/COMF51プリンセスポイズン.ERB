;-------------------------------------------------
;しあわせ草（媚薬）
;-------------------------------------------------
;他のeraシリーズでは媚薬にあたるアイテム
;CFLAG:媚薬残留=媚薬残留
;CFLAG:媚薬使用=しあわせ中毒の禁断症状判定
;-------------------------------------------------
@COM51

PRINTL 媚薬
SAVESTR:0 = 媚薬
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 300
LOSEBASE:気力 += 300
;主人か助手に[調合知識]があれば体力・気力消費を抑える
IF TALENT:MASTER:調合知識
	LOSEBASE:体力 -= 150
	LOSEBASE:気力 -= 150
ELSEIF ASSI >= 0
	IF TALENT:ASSI:調合知識
		LOSEBASE:体力 -= 150
		LOSEBASE:気力 -= 150
	ENDIF
ENDIF
;薬物経験が多いほど消費体力は減っていく
IF EXP:薬物経験 < EXPLV:1
	LOSEBASE:体力 += 300
ELSEIF EXP:薬物経験 < EXPLV:2
	LOSEBASE:体力 += 50
ELSEIF EXP:薬物経験 < EXPLV:3
	LOSEBASE:体力 += 0
ELSEIF EXP:薬物経験 < EXPLV:5
	LOSEBASE:体力 -= 50
ELSE
	LOSEBASE:体力 -= 100
ENDIF

SOURCE:逸脱 = 2000

;しあわせ草中毒なら体力消費を抑えかつ効果も上げる
IF TALENT:薬物中毒
	LOSEBASE:体力 -= 100
	SOURCE:充足 = 500
	SOURCE:逸脱 = 1000
	;媚薬効果で欲情のアップが2倍になるので実際には+20000
	SOURCE:欲情 = 10000
ELSE
	SOURCE:逸脱 = 2000
	;媚薬効果で欲情のアップが2倍になるので実際には+10000
	SOURCE:欲情 = 5000
ENDIF

;体力消費が0以下になるという事はない
SIF LOSEBASE:体力 < 0
	LOSEBASE:体力 = 0

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	PRINTS EXPNAME:[[EXP:レズ経験]]
	PRINTL +1
	EXP:レズ経験 += 1
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	PRINTS EXPNAME:[[EXP:ＢＬ経験]]
	PRINTL +1
	EXP:ＢＬ経験 += 1
ENDIF

;薬物経験
PRINTFORML %EXPNAME:[[EXP:薬物経験]]%+1
EXP:薬物経験 += 1
;-------------------------------------------------
;その他の処理
;-------------------------------------------------
;しあわせ草使用フラグ
TEQUIP:媚薬効果 = 1

;アイテム消費
SIF TEQUIP:触手召喚 == 0
	ITEM:プリンセスポイズン -= 1

;しあわせ草残留濃度
CFLAG:媚薬残留 += 1

;しあわせ中毒なら禁断症状回避フラグを立てる
SIF TALENT:薬物中毒
	CFLAG:媚薬使用 = 1

RETURN 1

