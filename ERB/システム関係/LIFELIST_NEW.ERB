;新版ライフリスト
;目的：ページ分け。各種のライフリストを纏める。
;変数LL_MODEでモード分け。
;どうせINPUTが要るのでINPUTを取り込んでしまう。
;そのままINPUTのRESULTを返す。値がおかしければ再呼び出しされる。
;１ページ目に戻るぐらいは諦めてください。リストで出てないキャラも選択できるのも以下略。
;キャンセル処理面倒……
;LL_MODE_は#DIM.ERH参照
;まとめたせいで読みにくくなった感もあるのでそのうち関数化して読みやすく……できるといいね

@LIFE_LIST_NEW(LL_MODE, ASSI_ONLY = 0,CHAR_ID=-1)
;行数弄る時に直す場所を一か所で済むように
#DIM CONST LIST_LINE_NUM = 40
#DIM LL_MODE
#DIM ASSI_ONLY
#DIM CHAR_ID
#DIM CNUM = 0
#DIM CNUM_START = 0
#DIM NUM_SHOWN
#DIM LL_LINE
#DIM CNUM_SHOW,RANDCHOOSE_MAX
#DIM CNUM_SHOW_COUNT = 0

VARSET CNUM_SHOW
CNUM_SHOW_COUNT = 0
CNUM_START = 0
IF LL_MODE == LL_MODE_全部 || LL_MODE == LL_MODE_指導可 ||  LL_MODE == LL_MODE_ステータス表示 || LL_MODE == LL_MODE_RELATION変更
	CNUM_SHOW_COUNT++
	CNUM_START++
ENDIF
;キャラ番号フィルター
FOR CNUM,1,CHARANUM
	SIF CNUM == CHARANUM
		BREAK
	SIF CFLAG:CNUM:入寮中 && (LL_MODE != LL_MODE_全部 && LL_MODE != LL_MODE_ステータス表示 && LL_MODE != LL_MODE_ABLUP && LL_MODE != LL_MODE_RELATION変更 && LL_MODE != LL_MODE_娼館 && LL_MODE != LL_MODE_お願い && LL_MODE != LL_MODE_ID限定 && LL_MODE != LL_MODE_入寮中)
		CONTINUE
	SIF LL_MODE == LL_MODE_入寮中 && !CFLAG:CNUM:入寮中
		CONTINUE
	;臨死中のキャラは排除
	SIF BASE:CNUM:体力 < 1
		CONTINUE
	SIF LL_MODE == LL_MODE_お願い && ABL:CNUM:信頼 < 3
		CONTINUE
	;助手可能以外は排除
	SIF ASSI_ONLY && (CNUM == TARGET || CFLAG:CNUM:売却助手可能 < 2 || BASE:CNUM:体力 < 500 || TALENT:CNUM:育児中 || (CFLAG:CNUM:出産日 - 2 <= DAY && TALENT:CNUM:妊娠) || CFLAG:CNUM:営業内容 != 0 || CFLAG:CNUM:入寮中)
		CONTINUE
	SIF LL_MODE == LL_MODE_指導可 && (BASE:CNUM:体力 < 500 || CFLAG:CNUM:出産日 - 2 <= DAY && TALENT:CNUM:妊娠 || TALENT:CNUM:育児中 || (TIME == 1 && CFLAG:CNUM:営業内容))
		CONTINUE
	SIF LL_MODE == LL_MODE_娼館 && (!CFLAG:CNUM:売却助手可能 || TALENT:CNUM:育児中 || (CFLAG:CNUM:出産日 - 2 <= DAY && TALENT:CNUM:妊娠))
		CONTINUE

	SIF LL_MODE == LL_MODE_ID限定 && CHAR_ID > 0 && NO:CNUM != CHAR_ID
		CONTINUE

	CNUM_SHOW:CNUM_SHOW_COUNT = CNUM
	CNUM_SHOW_COUNT++
NEXT

$REFRESH_POINT
LL_LINE = LINECOUNT

DRAWLINE
IF LL_MODE == LL_MODE_全部 || LL_MODE == LL_MODE_ステータス表示 || LL_MODE == LL_MODE_RELATION変更
	PRINTFORML  [0] %NAME:MASTER% （マスター）
	DRAWLINE
ENDIF
IF ASSI_ONLY
	PRINTFORML  [0] 助手なし
	DRAWLINE
ENDIF

SIF LL_MODE == LL_MODE_全部 || LL_MODE == LL_MODE_指導可 || LL_MODE == LL_MODE_娼館 || LL_MODE == LL_MODE_ステータス表示 || LL_MODE == LL_MODE_RELATION変更 || LL_MODE == LL_MODE_ABLUP
	PRINTL [☆：前回調教/助手可　★：前回調教　○：前回助手　●：助手可]

NUM_SHOWN = 0

FOR CNUM,CNUM_START,CNUM_START + LIST_LINE_NUM
	SIF CNUM == CNUM_SHOW_COUNT
		BREAK
	SIF NUM_SHOWN == LIST_LINE_NUM
		BREAK

	PRINTFORM  [{CNUM,3,RIGHT}] %NAME:(CNUM_SHOW:CNUM),30,LEFT%
	IF LL_MODE == LL_MODE_全部 || LL_MODE == LL_MODE_指導可 || LL_MODE == LL_MODE_娼館 || LL_MODE == LL_MODE_ステータス表示 || LL_MODE == LL_MODE_RELATION変更 || LL_MODE == LL_MODE_ABLUP
		SIF BASE:(CNUM_SHOW:CNUM):体力 <= 0
			BASE:(CNUM_SHOW:CNUM):体力 = 0
		SIF BASE:(CNUM_SHOW:CNUM):魅力値 <= 0
			BASE:(CNUM_SHOW:CNUM):魅力値 = 0

		;臨死中のキャラは排除してるので体力が0より下とかありえなかった
		PRINTFORM (体:{BASE:(CNUM_SHOW:CNUM):体力,4,RIGHT}/{MAXBASE:(CNUM_SHOW:CNUM):体力,4,RIGHT})/(魅:{BASE:(CNUM_SHOW:CNUM):魅力値,3,RIGHT}/{MAXBASE:(CNUM_SHOW:CNUM):魅力値,3,RIGHT})/
		IF (CNUM_SHOW:CNUM) == FLAG:前回調教対象 && CFLAG:(CNUM_SHOW:CNUM):売却助手可能 >= 2
			PRINT ☆
		ELSEIF (CNUM_SHOW:CNUM) == FLAG:前回調教対象
			PRINT ★
		ELSEIF (CNUM_SHOW:CNUM) == FLAG:前回助手
			PRINT ○
		ELSEIF CFLAG:(CNUM_SHOW:CNUM):売却助手可能 >= 2
			PRINT ●
		ELSE
			PRINTFORM %"　"%
		ENDIF
		IF TALENT:(CNUM_SHOW:CNUM):永遠の愛
			PRINT [永]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):恋心
			PRINT [恋]
		ELSE
			PRINTFORM %"[　]"%
		ENDIF
		IF TALENT:(CNUM_SHOW:CNUM):絶倫
			PRINT [絶]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):淫乱
			PRINT [淫]
		ELSE
			PRINTFORM %"[　]"%
		ENDIF
		IF CAN_GET_PREGNANT((CNUM_SHOW:CNUM))
			PRINT [
			SETCOLOR 0xF58F98 
			CALL HEART_CODE
			;STRLEN 臨
			;TEMP_NUM = RESULT
			;STRLENS @"%UNICODE(0x2665)%"
			;SIF TEMP_NUM > RESULT
			;	CALL HEART_CODE
			SETFONT ""
			RESETCOLOR		
			PRINT ]
		ELSEIF CFLAG:(CNUM_SHOW:CNUM):出産日 - 2 <= DAY && TALENT:(CNUM_SHOW:CNUM):妊娠
			SETCOLOR 0xF58F98 
			PRINT [臨]
			RESETCOLOR		
		ELSEIF TALENT:(CNUM_SHOW:CNUM):妊娠
			SETCOLOR 0xF58F98 
			PRINT [妊]
			RESETCOLOR		
		ELSEIF TALENT:(CNUM_SHOW:CNUM):抱卵
			SETCOLOR 0xF58F98 
			PRINT [卵]
			RESETCOLOR		
		ELSEIF TALENT:(CNUM_SHOW:CNUM):育児中
			SETCOLOR 0xF58F98 
			PRINT [育]
			RESETCOLOR		
		ELSEIF IS_MALE(CNUM_SHOW:CNUM)
			PRINTFORM %"[Ｘ]"%
		ELSE
			PRINTFORM %"[安]"%
		ENDIF
		IF TALENT:(CNUM_SHOW:CNUM):人気ＡＶ女優
			PRINT [人]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):ＡＶ女優
			PRINT [優]
		ELSE
			PRINTFORM %"[　]"%
		ENDIF
		IF TALENT:(CNUM_SHOW:CNUM):高級娼婦
			PRINT [高]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):風俗嬢
			PRINT [嬢]
		ELSE
			PRINTFORM %"[　]"%
		ENDIF
		SIF LL_MODE == LL_MODE_娼館
			PRINTFORM (人気:%@"{EXP:(CNUM_SHOW:CNUM):人気}",3,RIGHT%)
		PRINT <
		IF CFLAG:(CNUM_SHOW:CNUM):営業内容 != 0
			IF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 1
				PRINT 会
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 2
				PRINT 奉
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 3
				PRINT Ａ
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 4
				PRINT Ｖ
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 5
				PRINT Ｓ
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 6
				PRINT 出
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):営業内容 == 7
				PRINT ソ
			ELSE
				PRINTFORM %"　"%
			ENDIF
			IF CFLAG:(CNUM_SHOW:CNUM):営業コンドーム
				PRINT Ｃ
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):労役妊娠回避
				PRINT 回
			ELSE

				PRINTFORM %"　"%
			ENDIF
		ELSE
			PRINTFORM %"　"*2%
		ENDIF
		PRINT >
		IF LL_MODE == LL_MODE_ABLUP
			TARGET = (CNUM_SHOW:CNUM)
			REPEAT 100
				TRYCALLFORM CAN_ABLUP{COUNT}
				IF RESULT
					PRINT *
					BREAK
				ENDIF
			REND
		ENDIF
	ELSEIF LL_MODE == LL_MODE_お願い
		IF CFLAG:(CNUM_SHOW:CNUM):習い事
			SIF CFLAG:(CNUM_SHOW:CNUM):エステ通い
				CALL エステ通い有効((CNUM_SHOW:CNUM))
			SIF CFLAG:(CNUM_SHOW:CNUM):ジム通い
				CALL ジム通い有効((CNUM_SHOW:CNUM))
			SIF CFLAG:(CNUM_SHOW:CNUM):塾通い
				CALL 学習塾通い有効((CNUM_SHOW:CNUM))
			IF !RESULT
				PRINT <習い事上限>
			ELSE
				STRLEN <習い事上限>
				PRINTFORM %" "*RESULT%
			ENDIF
		ENDIF
	ELSEIF LL_MODE == LL_MODE_衣装
		TARGET = (CNUM_SHOW:CNUM)
		IF CFLAG:(CNUM_SHOW:CNUM):上着タイプ && (CFLAG:(CNUM_SHOW:CNUM):上着上状態 >= 0 || CFLAG:(CNUM_SHOW:CNUM):上着下状態 >= 0)
			PRINT / 
			CALL PRINT_上着タイプ(CNUM_SHOW:CNUM)
		ELSEIF CFLAG:(CNUM_SHOW:CNUM):上着タイプ && (CFLAG:(CNUM_SHOW:CNUM):パンツ状態 >= 0 || CFLAG:(CNUM_SHOW:CNUM):ブラジャー状態 >= 0)
			PRINT / 下着
		ELSE
			PRINT / 全裸
		ENDIF
		IF CFLAG:(CNUM_SHOW:CNUM):特別コスタイプ && CFLAG:(CNUM_SHOW:CNUM):特別コス状態 >= 0
			PRINT / 
			CALL PRINT_CLOTHTYPE_SPECIAL
		ENDIF
	ELSEIF LL_MODE == LL_MODE_入寮中
		PRINT (体力：
		PRINTFORM {BASE:(CNUM_SHOW:CNUM):体力}/{MAXBASE:(CNUM_SHOW:CNUM):体力})
		PRINT /(魅力値：
		IF BASE:(CNUM_SHOW:CNUM):魅力値 < 0
			PRINTFORM 0/{MAXBASE:(CNUM_SHOW:CNUM):魅力値})/
		ELSE
			PRINTFORM {BASE:(CNUM_SHOW:CNUM):魅力値}/{MAXBASE:(CNUM_SHOW:CNUM):魅力値})/
		ENDIF
		IF CFLAG:(CNUM_SHOW:CNUM):出産日 - 2 <= DAY && TALENT:(CNUM_SHOW:CNUM):妊娠
			PRINT [臨]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):妊娠
			PRINT [妊]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):抱卵
			PRINT [卵]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):育児中
			PRINT [育]
		ENDIF
		IF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 >= 1
			PRINT  《
			IF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 == 1
				PRINT エステルーム
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 == 2
				PRINT 談話室
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 == 3
				PRINT 託児所
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 == 4
				PRINT 豪華カラオケルーム
			ELSEIF CFLAG:(CNUM_SHOW:CNUM):寮施設利用 == 5
				PRINT ダンススタジオ
			ENDIF
			PRINT 利用中》
		ENDIF
	ELSEIF LL_MODE == LL_MODE_撮影
		PRINTFORM （芸名：%NICKNAME:(CNUM_SHOW:CNUM)%）
		PRINT (体力：
		PRINTFORM {BASE:(CNUM_SHOW:CNUM):体力}/{MAXBASE:(CNUM_SHOW:CNUM):体力})
		PRINT  / (魅力値：
		IF BASE:(CNUM_SHOW:CNUM):魅力値 < 0
			PRINTFORM 0/{MAXBASE:(CNUM_SHOW:CNUM):魅力値}) / 
		ELSE
			PRINTFORM {BASE:(CNUM_SHOW:CNUM):魅力値}/{MAXBASE:(CNUM_SHOW:CNUM):魅力値}) / 
		ENDIF
		IF CFLAG:(CNUM_SHOW:CNUM):出産日 - 2 <= DAY && TALENT:(CNUM_SHOW:CNUM):妊娠
			PRINT [臨]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):妊娠
			PRINT [妊]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):抱卵
			PRINT [卵]
		ELSEIF TALENT:(CNUM_SHOW:CNUM):育児中
			PRINT [育]
		ENDIF
	ENDIF
	PRINTL
	NUM_SHOWN++
NEXT
DRAWLINE

CALL PRINT_PAGE_NAVI(CNUM_START >= LIST_LINE_NUM,CNUM_START + LIST_LINE_NUM < CNUM_SHOW_COUNT)

INPUT

IF CNUM_START >= LIST_LINE_NUM && RESULT == COMMAND_PREV_PAGE
	CNUM_START -= LIST_LINE_NUM
	;この値だけCLEARLINEすると綺麗にページが切り替わるといいですね
	CLEARLINE LINECOUNT-LL_LINE
	GOTO REFRESH_POINT
ELSEIF CNUM_START + LIST_LINE_NUM < CNUM_SHOW_COUNT && RESULT == COMMAND_NEXT_PAGE
	CNUM_START += LIST_LINE_NUM
	CLEARLINE LINECOUNT-LL_LINE
	GOTO REFRESH_POINT
ELSEIF RESULT == COMMAND_GO_BACK
	RETURN COMMAND_GO_BACK
ELSEIF RESULT > CHARANUM
;ページ移動、戻る以外でキャラクター以外の値が参照されてもしょうがないので弾く
	CLEARLINE LINECOUNT-LL_LINE
	GOTO REFRESH_POINT
ELSE
	RETURN CNUM_SHOW:RESULT
ENDIF
