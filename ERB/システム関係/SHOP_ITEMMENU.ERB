;///////////////////////////////
@SHOW_SHOP_ITEMMENU

DRAWLINE
PRINTL アダルトショップ『NIGHT LOVE STORE』
PRINTL 《指導に使用するアイテムを購入できます》
DRAWLINE
CALL PRINT_DAY_TIME

DRAWLINE

CRESULT:1 = 0
BOUGHT = -1

調合知識持ち = 0
IF TALENT:MASTER:調合知識
	調合知識持ち = 1
ELSE
	REPEAT CHARANUM
		SIF COUNT == 0
			CONTINUE
		IF CFLAG:COUNT:売却助手可能 >= 1 && ISASSI:COUNT == 1 && TALENT:COUNT:調合知識
			調合知識持ち = 1
			BREAK
		ENDIF
	REND
ENDIF

CALL カラム生成, "ITEMMENU", "薬剤", "消耗品", "調教道具", "素質アイテム", "戻る"

;///////////////////////////////
@ITEMMENU_戻る
#DIM DYNAMIC LCOUNT

CALL COLUMNPRINTL, 0,  @"ショップから戻りますか？"
CALL COLUMNPRINTL, 0,  @"[0] はい"

;///////////////////////////////
@ITEMMENU_戻る_INPUT
#DIM DYNAMIC LCOUNT

DEBUGPRINTFORML CRESULT:1={CRESULT:1}

IF !CRESULT:1
	CALL COLUMNDISPOSE, 0
	BOUGHT = -1
	LOOP_BREAK = 1	
ENDIF

;///////////////////////////////
@ITEMMENU_薬剤
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT

CALL COLUMNPRINTL, 0, @"所持金：{MONEY}ポイント"

FOR LOCAL, 0, 10
	SIF ITEMNAME:(LOCAL) == ""
		CONTINUE

	LOCALS '= @"{LOCAL,3}"
	ITEMSALES:(LOCAL) = 1
	TRYCALLFORM ITEM_CAN_BUY_%ITEMNAME:(LOCAL)%
	LOCALS:1 '= RESULTS
	SIF !ITEMSALES:(LOCAL)
		LOCALS '= "---"
	CALL COLUMNPRINTL, 0,  @"[%LOCALS%] %ITEMNAME:(LOCAL), 20, LEFT%:{ITEMPRICE:(LOCAL), 10}P"

	IF EXISTFUNCTION(@"ITEM_EFFECT_%ITEMNAME:(LOCAL)%") == 1
		CALLFORM ITEM_EFFECT_%ITEMNAME:(LOCAL)%
		CALL COLUMNPRINTL(0, "---" + RESULTS)
	ENDIF
	
	STRLENS LOCALS:1
	SIF !ITEMSALES:(LOCAL) && RESULT
		CALL COLUMNPRINTL, 0,  @"---%LOCALS:1%"

NEXT

IF CRESULT:1 < 0 && BOUGHT > -1
	CALL COLUMNPRINTL, 0, "お金が足りないので%ITEMNAME:BOUGHT%を買えません"
	BOUGHT = -1
ENDIF

;///////////////////////////////
@ITEMMENU_薬剤_INPUT
SIF CRESULT:1 < 0 || CRESULT:1 >10
	RETURN
BOUGHT = CRESULT:1
SIF !ITEMSALES:BOUGHT || !ITEMSALES:BOUGHT || ITEMNAME:BOUGHT == ""
	RETURN

IF MONEY < ITEMPRICE:BOUGHT
	CRESULT:1 = -1
	RETURN
ENDIF

RESULT = COMMAND_GO_BACK
TRYCALLFORM ITEM_USE_TARGET%ITEMNAME:BOUGHT%
IF RESULT != COMMAND_GO_BACK
	TRYCALLFORM ITEM_USE_%ITEMNAME:BOUGHT%(RESULT)
ENDIF

;///////////////////////////////
@ITEMMENU_消耗品
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT
#DIM NUM_CAN_BUY

CALL COLUMNPRINTL, 0, @"所持金：{MONEY}ポイント"
CALL COLUMNPRINTL, 0, PRINT_ITEMS("消耗品")

FOR LOCAL, 50, 100
	SIF ITEMNAME:(LOCAL) == ""
		CONTINUE

	NUM_CAN_BUY = MIN(99-ITEM:(LOCAL), MONEY/ITEMPRICE:(LOCAL))
	ITEMSALES:(LOCAL) = 1
	TRYCALLFORM ITEM_CAN_BUY_%ITEMNAME:(LOCAL)%
	LOCALS:1 '= RESULTS
	IF ITEMSALES:(LOCAL) && NUM_CAN_BUY > 0
		LOCALS '= @"{LOCAL,3}"
	ELSE
		LOCALS '= "---"
	ENDIF
	CALL COLUMNPRINTL, 0,  @"[%LOCALS%] %ITEMNAME:(LOCAL), 20, LEFT%:{ITEMPRICE:(LOCAL), 10}P	(所持数:{ITEM:(LOCAL), 4})"

	IF EXISTFUNCTION(@"ITEM_EFFECT_%ITEMNAME:(LOCAL)%") == 1
		CALLFORM ITEM_EFFECT_%ITEMNAME:(LOCAL)%
		CALL COLUMNPRINTL(0, "---" + RESULTS)
	ENDIF
	
	STRLENS LOCALS:1
	SIF !ITEMSALES:(LOCAL) && RESULT
		CALL COLUMNPRINTL, 0,  @"---%LOCALS:1%"

NEXT

IF CRESULT:1 > 0 && BOUGHT > -1
	CALL COLUMNPRINTL, 0, @"%ITEMNAME:BOUGHT%を{CRESULT:1}個購入しました"
	BOUGHT = -1
ENDIF

;///////////////////////////////
@ITEMMENU_消耗品_INPUT
#DIM NUM_CAN_BUY

SIF CRESULT:1 < 50 || CRESULT:1 >100
	RETURN
BOUGHT = CRESULT:1
SIF !ITEMSALES:BOUGHT || ITEMNAME:BOUGHT == "" || ITEM:BOUGHT >= 99
	RETURN

IF MONEY < ITEMPRICE:BOUGHT
	CRESULT:1 = -1
	RETURN
ENDIF

NUM_CAN_BUY = MIN(99-ITEM:BOUGHT, MONEY/ITEMPRICE:BOUGHT)
SIF NUM_CAN_BUY < 1
	RETURN

CALL ITEMMENU_BUY_MUTIPLE(NUM_CAN_BUY)
CRESULT:1 = RESULT
ITEM:BOUGHT += CRESULT:1
MONEY -= ITEMPRICE:BOUGHT * CRESULT:1

;///////////////////////////////
@ITEMMENU_BUY_MUTIPLE(ARG)
PRINTFORML %ITEMNAME:BOUGHT%をいくつ買いますか？ （1-{ARG}、0で戻る)
PRINTFORML [1]    [{ARG}]    [0]
INPUT

SELECTCASE RESULT
	CASE 0 TO ARG
		RETURN RESULT
	CASEELSE
		RESTART
ENDSELECT

;///////////////////////////////
@ITEMMENU_調教道具
#DIM DYNAMIC LCOUNT

CALL COLUMNPRINTL, 0, @"所持金：{MONEY}ポイント"
CALL COLUMNPRINTL, 0, PRINT_ITEMS("調教道具")

FOR LOCAL, 100, 200
	SIF ITEMNAME:(LOCAL) == ""
		CONTINUE

	ITEMSALES:(LOCAL) = 1
	TRYCALLFORM ITEM_CAN_BUY_%ITEMNAME:(LOCAL)%
	LOCALS:1 '= RESULTS
	IF ITEMSALES:(LOCAL) && ITEM:(LOCAL) == 0
		LOCALS '= @"{LOCAL,3}"
	ELSE
		LOCALS '= "---"
	ENDIF
	CALL COLUMNPRINTL, 0,  @"[%LOCALS%] %ITEMNAME:(LOCAL), 20, LEFT%:{ITEMPRICE:(LOCAL), 10}P"
	
	IF EXISTFUNCTION(@"ITEM_EFFECT_%ITEMNAME:(LOCAL)%") == 1
		CALLFORM ITEM_EFFECT_%ITEMNAME:(LOCAL)%
		CALL COLUMNPRINTL(0, "---" + RESULTS)
	ENDIF
	
	STRLENS LOCALS:1
	SIF !ITEMSALES:(LOCAL) && RESULT
		CALL COLUMNPRINTL, 0,  @"---%LOCALS:1%"

NEXT

IF CRESULT:1 < 0 && BOUGHT > -1
	CALL COLUMNPRINTL, 0, "お金が足りないので%ITEMNAME:BOUGHT%を買えません"
	BOUGHT = -1
ENDIF

;///////////////////////////////
@ITEMMENU_調教道具_INPUT
#DIM DYNAMIC LCOUNT
SIF CRESULT:1 < 100 || CRESULT:1 > 200
	RETURN
BOUGHT = CRESULT:1
SIF !ITEMSALES:BOUGHT || ITEM:BOUGHT || ITEMNAME:BOUGHT == ""
	RETURN

IF MONEY < ITEMPRICE:BOUGHT
	CRESULT:1 = -1
	RETURN
ENDIF
ITEM:BOUGHT = 1
MONEY -= ITEMPRICE:BOUGHT

;///////////////////////////////
@ITEMMENU_素質アイテム
#DIM DYNAMIC LCOUNT

CALL COLUMNPRINTL, 0, @"所持金：{MONEY}ポイント"

FOR LOCAL, 200, 300
	SIF ITEMNAME:(LOCAL) == ""
		CONTINUE

	TEMP_NUM = ITEMPRICE:(LOCAL)
	ITEMSALES:(LOCAL) = 1
	TRYCALLFORM ITEM_CAN_BUY_%ITEMNAME:(LOCAL)%
	LOCALS:1 '= RESULTS
	IF ITEMSALES:(LOCAL) && ITEM:(LOCAL) == 0
		LOCALS '= @"{LOCAL,3}"
	ELSE
		LOCALS '= "---"
	ENDIF
	CALL COLUMNPRINTL, 0,  @"[%LOCALS%] %ITEMNAME:(LOCAL), 20, LEFT%:{TEMP_NUM, 10}P"
	
	IF EXISTFUNCTION(@"ITEM_EFFECT_%ITEMNAME:(LOCAL)%") == 1
		CALLFORM ITEM_EFFECT_%ITEMNAME:(LOCAL)%
		CALL COLUMNPRINTL(0, "---" + RESULTS)
	ENDIF
	
	STRLENS LOCALS:1
	SIF !ITEMSALES:(LOCAL) && RESULT
		CALL COLUMNPRINTL, 0,  @"---%LOCALS:1%"

NEXT

IF CRESULT:1 < 0 && BOUGHT > -1
	CALL COLUMNPRINTL, 0, "お金が足りないので%ITEMNAME:BOUGHT%を買えません"
	BOUGHT = -1
ENDIF

;///////////////////////////////
@ITEMMENU_素質アイテム_INPUT
#DIM DYNAMIC LCOUNT
SIF CRESULT:1 < 200 || CRESULT:1 >300
	RETURN
BOUGHT = CRESULT:1
SIF ITEMNAME:BOUGHT == ""
	RETURN

IF MONEY < ITEMPRICE:BOUGHT
	CRESULT:1 = -1
	RETURN
ENDIF

;力尽きたのでここに条件をそのまま書く
SELECTCASE BOUGHT
	CASE [[ITEM:【魅惑】]]
		IF !TALENT:MASTER:魅惑
			TALENT:MASTER:魅惑 = 1
			MONEY -= ITEMPRICE:BOUGHT
			CALL COLUMNPRINTL, 0, "《%NAME:MASTER%は【%TALENTNAME:[[TALENT:魅惑]]%】を身につけた》"
		ENDIF
	CASE [[ITEM:【秘密知識】]]
		IF !TALENT:MASTER:秘密知識
			TALENT:MASTER:秘密知識 = 1
			MONEY -= ITEMPRICE:BOUGHT
			CALL COLUMNPRINTL, 0, "《%NAME:MASTER%は【%TALENTNAME:[[TALENT:秘密知識]]%】を身につけた》"
		ENDIF
	CASE [[ITEM:【調合知識】]]
		IF !TALENT:MASTER:調合知識
			TALENT:MASTER:調合知識 = 1
			MONEY -= ITEMPRICE:BOUGHT
			SIF !調合知識持ち
				調合知識持ち = 1
			CALL COLUMNPRINTL, 0, "《%NAME:MASTER%は【%TALENTNAME:[[TALENT:調合知識]]%】を身につけた》"
		ENDIF
	CASE [[ITEM:【技巧Lv】]]
		IF MONEY < ITEMPRICE:BOUGHT * ITEM_PRICE_MULTIPLIER_【技巧Lv】()
			CRESULT:1 = -1
			RETURN
		ELSEIF ABL:MASTER:技巧 < 10 && ABL:MASTER:技巧 <= FLAG:陥落人数 + 1
			ABL:MASTER:技巧++
			MONEY -=  ITEMPRICE:BOUGHT * ITEM_PRICE_MULTIPLIER_【技巧Lv】()
			CALL COLUMNPRINTL, 0, "《%NAME:MASTER%の技巧は{ABL:MASTER:技巧}になった》"
		ENDIF
ENDSELECT



