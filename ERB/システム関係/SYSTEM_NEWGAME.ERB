;============================================================
;強くてニューゲーム関連
;============================================================
;グッドエンド以上の場合、継続してEXTRAモードで遊ぶか強くてニューゲームを選択
@GAME_CONTINUE

DRAWLINE

PRINTL 強くてニューゲームを行いますか？
CALL GET_はいいいえ()

IF RESULT == COMMAND_はい
	PRINTL 
	PRINTL 主人の能力および【恋心】or【淫乱】を付けた女優/女優候補を引き継いで、
	PRINTL 最初からプレイし直します
	WAIT
	CALL 強くてニューゲーム
ENDIF

RETURN 1

;-----------------------------------------------------------
;強くてニューゲームの処理開始
;-----------------------------------------------------------
@強くてニューゲーム

;引継ぎキャラの「お願いする」フラグクリア
REPEAT CHARANUM
	CFLAG:COUNT:エステ通い = 0
	CFLAG:COUNT:ジム通い = 0
	CFLAG:COUNT:塾通い = 0
	CFLAG:COUNT:アニメチェック = 0
	CFLAG:COUNT:習い事 = 0
	CFLAG:COUNT:朝フェラ係 = 0
	CFLAG:COUNT:合コン参加 = 0
	CFLAG:COUNT:弁当係 = 0
REND

;それに伴い起こす人がいるフラグクリア
;一応検索したけどほかのところでやってるようではないので。
FLAG:朝起こしてもらう = 0

FLAG:弁当 = 0
FLAG:習い事人数 = 0

CALL 引継選択

;++++++++++++++++++++++++++++++++++++++++++++++
;設定引き継ぎ処理
;++++++++++++++++++++++++++++++++++++++++++++++
;貢献度、娼館人気を０に

;周回数を+1する
FLAG:周回数 += 1

PRINTL 所持アイテムをリセットしますか？
CALL GET_はいいいえ()

IF RESULT == COMMAND_はい
	;所持アイテム及びアイテム購入履歴のリセット
	REPEAT 99
		SIF COUNT == 60
			CONTINUE
		SIF COUNT == 61
			CONTINUE
		SIF COUNT == 62
			CONTINUE
		SIF COUNT == 63
			CONTINUE
		SIF COUNT == 90
			CONTINUE
		SIF COUNT == 22
			CONTINUE
		ITEM:COUNT = 0
		ITEMSALES:COUNT = 1
	REND
ENDIF

;ビデオ撮影バリアントなので、開始時入手できるのと同じだけ残す(20120724望崎)
	ITEM:ビデオカメラ = 1
;	ITEM:ビデオテープ = 1

;ロリコンフラグの引継ぎ
IF FLAG:ロリコンなし == 0
	PRINTL ロリコンフラグを引き継ぎますか？
	PRINTL [0] 引き継ぐ
	PRINTL [1] 引き継がない
	PRINTL [9] ロリコンシステムを使わない
	$INPUT_LOOP_LOLI
	INPUT
	IF RESULT == 1
		FLAG:ロリコン = 0
		TALENT:MASTER:ロリコン = 0
	ELSEIF RESULT == 9
		FLAG:ロリコン = 0
		TALENT:MASTER:ロリコン = 0
		FLAG:ロリコンなし = 1
	ELSEIF RESULT != 0
		GOTO INPUT_LOOP_LOLI
	ENDIF
ENDIF

CALL NEWGAME_START

;-------------------------------------------------
;強くてニューゲーム引継ぎキャラ選択
;-------------------------------------------------
@引継選択
#DIM 引継人数
#DIM 削除キャラ,1000

;引継ぎ人数クリア
引継人数 = 0
;引継ぎキャラのフラグクリア
VARSET TEMP_CHARA_LIST
VARSET 削除キャラ

;引継ぎキャラの選択
CALL 引継キャラ選択
引継人数 = RESULT

IF 引継人数 > 0
	DRAWLINE
	REPEAT CHARANUM
		;女優の名前をプリントするついでにリストの値をキャラIDに変換
		IF TEMP_CHARA_LIST:COUNT
			PRINTFORML %NAME:COUNT%
			TEMP_CHARA_LIST:COUNT = NO:COUNT
			削除キャラ:COUNT = -1
		ELSE
			削除キャラ:COUNT = NO:COUNT
		ENDIF
	REND
	DRAWLINE
	PRINTL 以上の女優/女優候補を引き継いでニューゲームを開始しますか？
ELSE
	PRINTL 女優/女優候補の引継ぎなしでニューゲームを開始しますか？
ENDIF

CALL GET_はいいいえ()

SIF RESULT == COMMAND_いいえ
	RESTART


;引き継がない女優関連のフラグをクリア
FOR LOCAL, 200, 220
	;アイーシャ,アスカ,あんず,美夜,透子,春海,紫苑,みちる,律花,美紅,乃衣瑠,ゆうひとひかる,智子,悠喜,愛衣子,アン,クリスティナとエルファリア,ニコル,奈留
	SIF MATCH(削除キャラ, LOCAL) 
		ミッション結果:(LOCAL - 199) = 0
NEXT
;芙美香
FLAG:加瀬芙美香雇用 = !MATCH(削除キャラ,[[キャラ:加瀬芙美香]]) 
;アンドロイド
FLAG:新型アンドロイド製作 = !MATCH(削除キャラ,[[キャラ:アンドロイド]]) 
;リーナ
FLAG:リーナ雇用 = !MATCH(削除キャラ,[[キャラ:リーナ・レスピーチオ]])
;ゆうひとひかる
SIF MATCH(削除キャラ,[[キャラ:赤城ゆうひ]])
	ミッション結果:12 = 0
;クリスティナとエルファリア
SIF MATCH(削除キャラ,[[キャラ:エルファリア・ラーグラーデ]])
	ミッション結果:17 = 0
;鈴花,桜月,未歩
SIF MATCH(削除キャラ, [[キャラ:涼本鈴花]]) || MATCH(削除キャラ, [[キャラ:永江桜月]]) || MATCH(削除キャラ, [[キャラ:宇野未歩]])
	ミッション結果:20 = 0
;リーゼロッテ
SIF MATCH(削除キャラ,[[キャラ:リーゼロッテ・パラディスス]])
	ミッション結果:30 = 0
;エレン
FLAG:もっと姉ボーナス受け取り = !MATCH(削除キャラ,[[キャラ:桐生エレン]])
;アイリス
FLAG:とらぶるサッキュバスボーナス受け取り = !MATCH(削除キャラ,[[キャラ:アイリス]])
;まゆとユーニス
IF MATCH(削除キャラ,[[キャラ:雛見まゆ]]) || MATCH(削除キャラ,[[キャラ:ユーニス・パラディスス]])
	FLAG:処女厨ボーナス受け取り = 0
	FLAG:HAERETICUSボーナス受け取り = 0
ENDIF
;みこと
FLAG:第三の幼馴染ボーナス受け取り = !MATCH(削除キャラ,[[キャラ:羽瀬川みこと]])
;美鈴
FLAG:ＢＩＧ３ボーナス受け取り = !MATCH(削除キャラ,[[キャラ:李美鈴]])
;かなで
SIF MATCH(削除キャラ,[[キャラ:宮間かなで]])
	ミッション結果:51 = 0

LOCAL = 0
DO
	IF 削除キャラ:LOCAL > 0
		DELCHARA LOCAL
	ELSE
		削除キャラ:LOCAL = 0
	ENDIF
LOOP !MATCH(削除キャラ,0)

FLAG:神那悠喜仲間救出 = 0
FLAG:歌音紹介雇用 = 0
FLAG:エレン紹介雇用 = 0
FLAG:教会組雇用 = 0
FLAG:ミッション受領中 = 0
FLAG:マルチナ加入イベント発生済み = 0

;みひろ引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:新島みひろ]])
	FLAG:歌音紹介雇用 |= 1
;結衣花引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:佐倉結衣花]])
	FLAG:歌音紹介雇用 |= 2
;南引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:春風南]]) || MATCH(TEMP_CHARA_LIST,[[キャラ:春風南_重複1]])
	FLAG:歌音紹介雇用 |= 4
;志乃引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:二葉志乃]])
	FLAG:歌音紹介雇用 |= 8
;すみれ引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:神園すみれ]]) || MATCH(TEMP_CHARA_LIST,[[キャラ:神園すみれ_重複1]])
	FLAG:歌音紹介雇用 |= 16
;鎌瀬縫子引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:鎌瀬縫子]])
	FLAG:はるうられ雇用 |= 1
;本庄のどか引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:本庄のどか]])
	FLAG:はるうられ雇用 |= 2
;海老原真与引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:海老原真与]])
	FLAG:はるうられ雇用 |= 4
;橘乙姫引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:橘乙姫]])
	FLAG:はるうられ雇用 |= 8
;翔子引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:桐島翔子]])
	FLAG:教会組雇用 |= 1
;愛理引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:雨草愛理]])
	FLAG:教会組雇用 |= 2
;杏子引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:西方杏子]])
	FLAG:教会組雇用 |= 4
;琴美引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:伊原琴乃]])
	FLAG:神那悠喜仲間救出 |= 1
;彩香引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:後藤彩香]])
	FLAG:神那悠喜仲間救出 |= 2
;凛佳引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:柑野凛佳]])
	FLAG:神那悠喜仲間救出 |= 4
;聖沙引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:篠崎聖沙]])
	FLAG:神那悠喜仲間救出 |= 8
;海静引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:椎名海静]])
	FLAG:神那悠喜仲間救出 |= 16
;由希引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:佐倉由希]])
	FLAG:エレン紹介雇用 |= 1
;乃絵美引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:小鳥遊乃絵美]])
	FLAG:エレン紹介雇用 |= 2
;あゆみ引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:新堂あゆみ]])
	FLAG:エレン紹介雇用 |= 4
;元カノ引き継ぐ
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:野々宮初佳]])
	FLAG:エレン紹介雇用 |= 8
SIF MATCH(TEMP_CHARA_LIST,[[キャラ:マルチナ・パラディスス]])
	FLAG:マルチナ加入イベント発生済み = 1

;++++++++++++++++++++++++++++++++++++++++++++++
;奴隷引き継ぎ処理
;++++++++++++++++++++++++++++++++++++++++++++++
REPEAT CHARANUM
	SIF COUNT == 0
		CONTINUE

	;妊娠・育児中なら出産予定日をずらす
	IF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中
		CFLAG:COUNT:出産日 -= DAY
	;それ以外なら妊娠関連のフラグは消去
	ELSE
		CFLAG:COUNT:主奴膣内精子量 = 0
		CFLAG:COUNT:妊娠精子 = 0
		CFLAG:COUNT:助奴膣内精子量 = 0
		CFLAG:COUNT:奴助膣内精子量 = 0
		CFLAG:COUNT:客奴膣内精子量 = 0
		CFLAG:COUNT:獣奴膣内精子量 = 0
		CFLAG:COUNT:排卵促進剤使用 = 0
		CFLAG:COUNT:出産日 = 0
		CFLAG:COUNT:妊娠精子キャラ番号 = 0
	ENDIF

	;衣服が洗濯中なら戻ってくる
	SIF CFLAG:COUNT:パンツ状態 > 0
		CFLAG:COUNT:パンツ状態 = 0
	SIF CFLAG:COUNT:ブラジャー状態 > 0
		CFLAG:COUNT:ブラジャー状態 = 0
	SIF CFLAG:COUNT:上着上状態 > 0
		CFLAG:COUNT:上着上状態 = 0
	SIF CFLAG:COUNT:上着下状態 > 0
		CFLAG:COUNT:上着下状態 = 0
	SIF CFLAG:COUNT:特別コス状態 > 0
		CFLAG:COUNT:特別コス状態 = 0

	;労役フラグ解除
	CFLAG:COUNT:営業内容 = 0
	CFLAG:COUNT:営業コンドーム = 0
		
REND


@引継キャラ選択
;大幅改変
#DIM 人数
;ここまで要らなかったと思うけどこのぐらい持っておく
#DIM 候補リスト,500
#DIM CNUM
#DIM LINENUM

;リストを構築する
;変数のリセット
VARSET 候補リスト
人数 = 0

;候補抽出
REPEAT CHARANUM
	;主人公は排除
	SIF COUNT == 0
		CONTINUE
	;臨死中のキャラは排除
	SIF BASE:COUNT:体力 < 1
		CONTINUE
	SIF TALENT:COUNT:ミッション
		CONTINUE
	;【永遠の愛】【絶倫】持ちでないキャラは排除
	SIF !TALENT:COUNT:永遠の愛 && !TALENT:COUNT:絶倫
		CONTINUE
	候補リスト:人数 = COUNT
	人数++
REND

;描画

PRINTFORML 強くてニューゲームに引き継ぐコを選んでください
DRAWLINE

DO 
	;行数を覚えておく
	LINENUM = LINECOUNT

	FOR CNUM,0,人数
		PRINTFORML [{候補リスト:CNUM}]  %NAME:(候補リスト:CNUM)% \@ TEMP_CHARA_LIST:(候補リスト:CNUM) ? %"(選択済)"% # %""% \@
	NEXT

	TEMP_STR '= "[選択終了]"
	STRLENS TEMP_STR
	TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
	PRINTFORM %" " * TEMP_NUM%
	CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_選択終了)
	PRINTFORM %" " * TEMP_NUM%

	TEMP_STR '= "[全員を選択/選択解除]"
	STRLENS TEMP_STR
	TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
	PRINTFORM %" " * TEMP_NUM%
	CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_全選択と解除)

	INPUT

	IF RESULT == COMMAND_選択終了
		RETURN MATCH(TEMP_CHARA_LIST, 1)
	ELSEIF RESULT == COMMAND_全選択と解除
		;最初の一人を見てそれに合わせる
		IF MATCH(TEMP_CHARA_LIST, 候補リスト:0)
			VARSET TEMP_CHARA_LIST
		ELSE
			FOR CNUM,0,人数
				TEMP_CHARA_LIST:(候補リスト:CNUM) = 1
			NEXT
		ENDIF		
	ELSEIF RESULT != 0 && MATCH(候補リスト, RESULT) 
		IF TEMP_CHARA_LIST:RESULT
			TEMP_CHARA_LIST:RESULT = 0
		ELSE
			TEMP_CHARA_LIST:RESULT = 1
		ENDIF
	ELSE
		PRINTW 不正な入力です
	ENDIF

	;これで元のリストが消えてちょうどよく表示される予定
	CLEARLINE LINECOUNT-LINENUM
LOOP 1
