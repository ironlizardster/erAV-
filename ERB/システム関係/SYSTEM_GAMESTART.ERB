;このバリアントはeratohoRR v1.31.6およびeratohoA ver1,204およびeraIm@s_ver.0.17βdから
;多数のスクリプトやルーチンを使用させてもらっています。
;さらにeraIm@s（ver.poke）仮はeraIm@s、erapokeを元に作らせて戴いています。
;
;↑そんなバリアントを元にできたerAVを元に適当に手を入れたのがerAV_Hoです
;
;=================================================
;ニューゲームスタート後最初に呼び出されるイベント関数
;=================================================
@EVENTFIRST

LOADGLOBAL

VARSET ミッション結果

DRAWLINE
PRINTL 
PRINTL 　　　　　　　 このゲームには性的に過激な描写が多数含まれており、
PRINTL 　　　　　　　18才未満の方、現実と仮想の区別がつかない方
PRINTL 　　　　　　 自己の行動に責任を持てない方のプレイははお断りします。
PRINTL 　　　　　　　　　　　　　　　　
PRINTL                以上の点を確認の上で、ゲームを開始してください。
PRINTL 
DRAWLINE
WAIT

CALL NEWGAME_START


@NEWGAME_START
CALL RESET_FLAGS
CALL DEFAULT_CONFIGS

;ゲームモード選択
CALL ゲームモード選択

CALL 開始前コンフィグ

[IF_DEBUG]
LOADGLOBAL
SIF 所持クリアポイント < 100000
	所持クリアポイント += 100000
SAVEGLOBAL
[ENDIF]

LOADGLOBAL
IF 所持クリアポイント > 0
	CALL CLEARBONUS
ENDIF

CALL ビデオ売れ筋

CALL オープニングストーリー

BEGIN SHOP

@RESET_FLAGS
;日付、時間、調教対象や助手のリセット
DAY = 0
TIME = 0

;調教対象・助手を空に
TARGET = -1
ASSI = -1
FLAG:前回調教対象 = -1
FLAG:前回助手 = -1

;引き継がれないフラグのクリア
FLAG:休憩 = 0
FLAG:前回調教対象 = 0
FLAG:前回助手 = 0
FLAG:期限日数 = 0
FLAG:目標金額 = 0
FLAG:難易度 = 0

;娼館の拡張状況をクリア
FLAG:警備員人数 = 0
FLAG:娼館拡張 = 0


@DEFAULT_CONFIGS
;デフォルトでは陰毛設定使用
FLAG:陰毛を生やす = 1
;デフォルトでは着衣設定使用
FLAG:着衣使用 = 1
;デフォルトでは妊娠設定使用
FLAG:妊娠なし = 0
;デフォルトでは加齢なし使用
FLAG:加齢フラグ = 0
FLAG:失神なし = 1
FLAG:処女確認 = 1
FLAG:Ａ処女確認 = 1
FLAG:初キス確認 = 1

@ゲームモード選択
PRINTL 
PRINTL ★★モードを選択してください★★

TEMP_STR '= "EASY"
STRLENS TEMP_STR
TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
TEMP_STR += " " * TEMP_NUM
TEMP_STR += @"(120日期限、目標金額{3000000-FLAG:借金チャラ*3000000/4}Pt)"
CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_EASY,-1,1)

IF FLAG:EASY経由
	TEMP_STR '= "NORMAL"
	STRLENS TEMP_STR
	TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
	TEMP_STR += " " * TEMP_NUM
	TEMP_STR += @"(100日期限、目標金額{5000000-FLAG:借金チャラ*5000000/4}Pt)"
	CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_NORMAL,-1,1)
ELSE
	TEMP_STR '= "CHALLENGE"
	STRLENS TEMP_STR
	TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
	TEMP_STR += " " * TEMP_NUM
	TEMP_STR += @"(100日期限、目標金額{5000000-FLAG:借金チャラ*5000000/4}Pt)"
	CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_CHALLENGE,-1,1)
ENDIF

TEMP_STR '= "EXTRA"
STRLENS TEMP_STR
TEMP_NUM = ((SHOWLENGTH / 3 - RESULT)/2)
TEMP_STR += " " * TEMP_NUM
TEMP_STR += "(無期限、目標金額なし)"
CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_EXTRA,-1,1)

TEMP_STR '= "モードの説明を読む"
CALL PRINT_BUTTON(TEMP_STR,1,COMMAND_モード説明,-1,1)

IF FLAG:EASY経由
	CALL INPUTF, COMMAND_EASY, COMMAND_NORMAL, COMMAND_EXTRA, COMMAND_モード説明
ELSE
	CALL INPUTF, COMMAND_EASY, COMMAND_CHALLENGE, COMMAND_EXTRA, COMMAND_モード説明
ENDIF

IF RESULT == COMMAND_EASY
	CALL SETUP_MASTER	;主人公セットアップ
	FLAG:期限日数 = 120
	FLAG:目標金額 = 3000000-FLAG:借金チャラ*3000000/4
	FLAG:難易度 = GAME_MODE_EASY
	FLAG:瀕死時自動終了 = 1
	ITEM:ビデオカメラ = 1
;	ITEM:ビデオテープ = 1
	MONEY = 5000
	MASTER = NO:0
	REALTIME:0 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
	DAY:日付_年 = REALTIME:0
	DAY:日付_月 = 4
	DAY:日付_日 = 1
	FLAG:事務所規模 = 10
	FLAG:EASY経由 = 1
	IF GETCHARA([[キャラ:小鳥遊乃絵美]], 0) < 0
		CALL キャラ追加([[キャラ:小鳥遊乃絵美]])
		PRINTFORMW 《桐生組より風俗嬢「小鳥遊乃絵美」が派遣されました》
	ENDIF
	ABL:MASTER:技巧 += 1
ELSEIF RESULT == COMMAND_NORMAL || RESULT == COMMAND_CHALLENGE
	CALL SETUP_MASTER	;主人公セットアップ
	FLAG:期限日数 = 100
	FLAG:目標金額 = 5000000-FLAG:借金チャラ*5000000/4
	FLAG:難易度 = GAME_MODE_ACHIEVEMENT_CHALLENGE
	FLAG:瀕死時自動終了 = 1
	ITEM:ビデオカメラ = 1
;	ITEM:ビデオテープ = 1
	MONEY = 2000
	MASTER = NO:0
	REALTIME:0 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
	DAY:日付_年 = REALTIME:0
	DAY:日付_月 = 4
	DAY:日付_日 = 1
	FLAG:事務所規模 = 10
ELSEIF RESULT == COMMAND_EXTRA
	FLAG:期限日数 = 0
	FLAG:目標金額 = 0
	FLAG:難易度 = 9
	MONEY = 30000
	FLAG:事務所規模 = 30
ELSEIF RESULT == COMMAND_モード説明
	TEMP_STR = @"EASYモードは期限が120週（行動回数+40回）、目標金額3000000Pt（NORMALの3/5）のほか、初期ポイントが2000pt（NORMALの倍）、主人公技巧が+1、女優候補「小鳥遊乃絵美」が加入する特典があります<br>"
	TEMP_STR += "また、ミッション報告の時に強制成功にできるメダルを実績達成以外からもらえることがあります<br>"
	TEMP_STR += "ただし、"+HTML_HINT("EASYモードを経由したデータ")+"では実績が達成できません<br>"
	TEMP_STR += "実績達成を目指すのであれば、新しく実績チャレンジモードで開始するのがよいでしょう<br>"
	TEMP_STR += "なお、もう一つのボーナスであるキャラクリボーナスはEASYモードでも取得することができます<br>"
	TEMP_STR += "実績チャレンジで遊ぶ前にEASYを極めても無駄にはならないはずです……労力に見合うかはともかく<br>"
	CALL HTML_PRINT_P(TEMP_STR)
	RESTART
ENDIF

SIF FLAG:エレンミッション失敗
	FLAG:目標金額 *= 100


@開始前コンフィグ
;ここで「開始前にコンフィグを変更しますか？」あたりにしたい。要検討
PRINTL 寝取られ機能をＯＮにしますか？
CALL PRINT_BUTTON("はい",1,0,-1,1)
CALL PRINT_BUTTON("いいえ",1,1,-1,1)
CALL PRINT_BUTTON("どういうこと？",1,2,-1,1)

CALL INPUTF, 1, 2, 0

IF RESULT == 0
	PRINTL 寝取られ機能をＯＮにしました
	PRINTL
	FLAG:寝取られなし = 0
ELSEIF RESULT == 1
	PRINTL 寝取られ機能をＯＦＦにしました
	PRINTL
	FLAG:寝取られなし = 1
ELSEIF RESULT == 2
	PRINTL 登場する女性キャラたちは常に他の男たちからモーションをかけられています
	PRINTL それはクラスメイトであったり、ホストであったり、痴漢であったり……
	PRINTL 女性キャラたちが他の男のモノになってしまうのが嫌な方はＯＦＦにするのを推奨します 
	PRINTW
	RESTART
ENDIF


;オープニング表示
@オープニングストーリー

FOR LOCAL, 0, 実績最大ID
	BASE:MASTER:メダル += 実績達成:LOCAL
NEXT

PRINTL ★オープニングストーリーを省略しますか？★
CALL PRINT_BUTTON("省略する",1,0,-1,1)
CALL PRINT_BUTTON("省略しない",1,1,-1,1)
CALL PRINT_BUTTON("三行で",1,2,-1,1)

CALL INPUTF, 1, 2, 0

IF RESULT == 0
	DRAWLINE
	PRINTL （オープニングストーリーを省略します）
ELSEIF RESULT == 1
	CALL OPENING
ELSEIF RESULT == 2
	CALL SIMPLE_OPENING
ENDIF

;主人公セットアップ用サブルーチン
@SETUP_MASTER
$INPUT_LOOP_MASTER
PRINTL 主人公のセットアップを行ないますか？
CALL GET_はいいいえ()

SIF RESULT == COMMAND_はい
	CALL SET_MASTER

RETURN 1
