;=================================================
;@SOURCE_CHECK関連の関数群
;=================================================
;INDEX
;@SOURCE_SEX_CHECK:同性の場合のチェック
;;;@SOURCE_LESBIAN_SEX_CHECK:女性同士の場合のチェック（SYSTEM_SOURCE_SUB2.ERBに記述）
;;;@SOURCE_GAY_SEX_CHECK:男性同士の場合のチェック（SYSTEM_SOURCE_SUB2.ERBに記述）
;@PLAYER_SKILL_CHECK:調教者のスキルによるチェック
;@MASTER_SKILL_CHECK:調教者のスキルによるチェック
;@INCEST_SEX_CHECK:近親相姦のチェック
;;;@INCEST:親族関係の判定（汎用関数\FUNCTIONS.ERBに記述）
;@LOST_VIRGIN_CHECK:処女喪失のチェック
;@TARGET_EJAC_CHECK:調教対象の射精チェック
;@TARGET_MILK_CHECK:調教対象の噴乳チェック
;@UP_TALENT_CVA_CHECK:素質などによる上下の処理（快楽系）
;@UP_TALENT_CHECK:素質などによる上下の処理
;@MARK_GOT_CHECK:刻印取得のチェック
;@YOKUBO_UP_CHECK:欲望の上昇による[抑圧][抵抗]の消滅をチェック
;@JUJUN_UP_CHECK:信頼の上昇による[反抗的]の反転をチェック
;@EXP_GOT_CHECK:被虐快楽経験、奉仕快楽経験のチェック
;@SOKUOCHI_CHECK:[即落ち]のチェック
;@ECST_CHECK:絶頂時の追加処理
;@PISSING_ECST_CHECK:絶頂時のおもらし処理
;@MASTER_FLAG_CHECK:主人による調教の経験値
;=================================================
;-------------------------------------------------
;同性の場合のチェック
;-------------------------------------------------
@SOURCE_SEX_CHECK
;レズ
IF HAVE_VAGINA(TARGET) && HAVE_VAGINA(PLAYER) 
	;女性同士の場合のチェック
	;関数はSOURCE_SUB_SUB.ERBに記述
	CALL SOURCE_LESBIAN_SEX_CHECK
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER) 
	;男性同士の場合のチェック
	;関数はSOURCE_SUB_SUB.ERBに記述
	CALL SOURCE_GAY_SEX_CHECK
ENDIF
;-------------------------------------------------
;調教者のスキルによるチェック
;-------------------------------------------------
@PLAYER_SKILL_CHECK
;調教者のTALENT:解放をみる
IF IS_解放(PLAYER)
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:快Ｂ , 1.20
ENDIF
;調教者のTALENT:小悪魔をみる
IF TALENT:PLAYER:小悪魔
	TIMES SOURCE:露出 , 1.60
ENDIF
;調教者のTALENT:魅惑をみる
IF TALENT:PLAYER:魅惑
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:達成 , 1.20
	TIMES SOURCE:快Ｂ , 1.20
ENDIF
;調教者のTALENT:謎の魅力をみる
IF TALENT:PLAYER:謎の魅力
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:達成 , 1.20
	TIMES SOURCE:快Ｂ , 1.20
ENDIF
;調教者が【母性】で調教対象が【マザコン】
IF TALENT:PLAYER:母性 && TALENT:TARGET:マザコン
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:達成 , 1.20
ENDIF
;調教者が【父性】で調教対象が【ファザコン】
IF TALENT:PLAYER:父性 && TALENT:TARGET:ファザコン
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:達成 , 1.20
ENDIF
;調教者が【妹】で調教対象が【妹想い】
IF TALENT:PLAYER:妹 && TALENT:TARGET:妹想い
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:達成 , 1.20
ENDIF
;調教者が【巫女】で調教対象が【精霊】か【幽霊】
IF TALENT:PLAYER:巫女 && (TALENT:TARGET:ボーイッシュ || TALENT:TARGET:嫉妬)
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.50

	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:達成 , 1.20
ENDIF
;調教者が【ロリコン】で調教対象が【中学生】
IF TALENT:PLAYER:ロリコン && TALENT:TARGET:中学生
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:快Ｂ , 1.20
		
	TIMES SOURCE:逸脱 , 0.80
ENDIF
;調教者が【ロリコン】で調教対象が【中学生】でも【高校生】でもない
IF TALENT:PLAYER:ロリコン && TALENT:TARGET:中学生 == 0 && TALENT:TARGET:高校生 == 0
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:快Ｂ , 0.80
	
	TIMES SOURCE:逸脱 , 1.20
ENDIF

;調教者のABL:技巧
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:快Ａ , 0.50
	TIMES SOURCE:快Ｂ , 0.50
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:快Ｂ , 0.80
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:快Ｂ , 1.00
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:快Ｂ , 1.20
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:快Ａ , 1.50
	TIMES SOURCE:快Ｂ , 1.50
ELSEIF ABL:PLAYER:技巧 >= 5
	TIMES SOURCE:快Ｃ , 2.00
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:快Ａ , 2.00
	TIMES SOURCE:快Ｂ , 2.00
ENDIF
;-------------------------------------------------
;主人による調教のボーナス
;-------------------------------------------------
@MASTER_SKILL_CHECK
;主人による調教経験
IF ASSIPLAY == 0
	IF CFLAG:好感度 >= 500
		TIMES SOURCE:不潔 , 0.70
		TIMES SOURCE:逸脱 , 0.70

		TIMES SOURCE:快Ｃ , 1.20
		TIMES SOURCE:快Ｖ , 1.20
		TIMES SOURCE:快Ａ , 1.20
		TIMES SOURCE:情愛 , 1.30
		TIMES SOURCE:快Ｂ , 1.20
	ELSEIF CFLAG:好感度 >= 300
		TIMES SOURCE:不潔 , 0.80
		TIMES SOURCE:逸脱 , 0.80

		TIMES SOURCE:快Ｃ , 1.10
		TIMES SOURCE:快Ｖ , 1.10
		TIMES SOURCE:快Ａ , 1.10
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:快Ｂ , 1.10
	ELSEIF CFLAG:好感度 >= 100
		TIMES SOURCE:不潔 , 0.90
		TIMES SOURCE:逸脱 , 0.90

		TIMES SOURCE:情愛 , 1.10
	ENDIF
ENDIF

;淫乱
;主人経験との重複前提で計算
IF TALENT:淫乱 && ASSIPLAY == 0
	TIMES SOURCE:快Ｃ , 1.80
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:快Ａ , 1.80
	TIMES SOURCE:快Ｂ , 1.80
ENDIF

;愛
;主人経験との重複前提で計算
IF TALENT:恋心 && ASSIPLAY == 0
	TIMES SOURCE:快Ｃ , 1.30
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:情愛 , 1.80
	TIMES SOURCE:快Ｂ , 1.30
ENDIF
;-------------------------------------------------
;近親相姦のチェック
;-------------------------------------------------
@INCEST_SEX_CHECK

;親族関係の判定
TFLAG:近親相姦 = 0
CALL INCEST

IF TFLAG:近親相姦 && TFLAG:V経験を伴うコマンド
	IF TFLAG:近親相姦 == 1 && IS_MALE(PLAYER) 
		PRINTL 父娘相姦
	ELSEIF  TFLAG:近親相姦 == 1 && HAVE_VAGINA(PLAYER) 
		PRINTL 母娘相姦
	ELSEIF  TFLAG:近親相姦 == 2 && IS_MALE(PLAYER) 
		PRINTL 母子相姦
	ELSEIF  TFLAG:近親相姦 == 2 && HAVE_VAGINA(PLAYER) 
		PRINTL 母娘相姦
	ELSEIF  TFLAG:近親相姦 == 3 && IS_MALE(PLAYER) 
		PRINTL 兄妹相姦
	ELSEIF  TFLAG:近親相姦 == 3 && HAVE_VAGINA(PLAYER) 
		PRINTL 姉妹相姦
	ELSEIF  TFLAG:近親相姦 == 4 && IS_MALE(PLAYER) 
		PRINTL 姉弟相姦
	ELSEIF  TFLAG:近親相姦 == 4 && HAVE_VAGINA(PLAYER) 
		PRINTL 姉妹相姦
	ELSEIF  TFLAG:近親相姦 == 5 && HAVE_VAGINA(PLAYER) 
		PRINTL 従弟相姦
	ELSEIF  TFLAG:近親相姦 == 6 && IS_MALE(PLAYER) 
		PRINTL 従姉相姦
	ENDIF

	IF TFLAG:近親相姦 == 1 || TFLAG:近親相姦 == 2
		TIMES SOURCE:情愛 , 2.00
		TIMES SOURCE:逸脱 , 2.00
		TIMES SOURCE:恭順 , 2.00
	ELSEIF TFLAG:近親相姦 == 3 || TFLAG:近親相姦 == 4
		TIMES SOURCE:情愛 , 1.50
		TIMES SOURCE:逸脱 , 1.50
		TIMES SOURCE:恭順 , 1.50
	ENDIF
ENDIF

;-------------------------------------------------
;処女喪失のチェック
;-------------------------------------------------
@LOST_VIRGIN_CHECK
;処女でないかＶ経験を伴うコマンドフラグが立ってなければそのまま終了
SIF !IS処女(TARGET) || TFLAG:V経験を伴うコマンド == 0
	RETURN 0

SIF RELATION:TARGET:(NO:PLAYER) <= 1000
	RELATION:TARGET:(NO:PLAYER) += 20

;処女喪失フラグ(今回のコマンド)
TFLAG:処女喪失 = 1
;処女喪失フラグ(今回の調教)
TFLAG:今回の調教で処女喪失 = 1
;処女喪失フラグ(ビデオ撮影)
SIF TEQUIP:撮影中
	TFLAG:ビデオ撮影内容 |= 1

;親族関係の判定
TFLAG:近親相姦 = 0
CALL INCEST

IF !CFLAG:処女喪失相手
	IF TEQUIP:野外プレイ == 1
		CFLAG:処女喪失場所 = 初場所_野外
	ELSEIF TEQUIP:野外プレイ == 2
		CFLAG:処女喪失場所 = 初場所_学校
	ELSEIF TEQUIP:野外プレイ == 3
		CFLAG:処女喪失場所 = 初場所_プール
	ELSEIF TEQUIP:野外プレイ == 4
		CFLAG:処女喪失場所 = 初場所_地下室
	ELSE
		CFLAG:処女喪失場所 = 初場所_スタジオ
	ENDIF
	IF (TFLAG:ビデオ撮影内容 & 1)
		CFLAG:処女喪失状況 = 初状況_撮影
	ELSEIF MARK:反発刻印 >= 2
		CFLAG:処女喪失状況 = 初状況_無理犯
	ELSE
		CFLAG:処女喪失状況 = 初状況_指導
	ENDIF

	;初体験がバイブの場合
	IF SELECTCOM == [[TRAIN:バイブ]]
		IF TALENT:淫乱 == 0
			CALL 処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:処女喪失場所,CFLAG:処女喪失状況,"バイブ")
		ELSEIF TALENT:淫乱 == 1
			CALL 処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:処女喪失場所,CFLAG:処女喪失状況,"極太バイブ")
		ENDIF
	ELSEIF SELECTCOM == [[TRAIN:フィストファック]]
		CALL 処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:処女喪失場所,CFLAG:処女喪失状況,"フィストファック")
	ELSEIF TEQUIP:触手召喚 == 1
		CALL 処女喪失実行(TARGET,初相手_触手,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:処女喪失場所,CFLAG:処女喪失状況,"触手生物")
	ELSE
		CALL 処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:処女喪失場所,CFLAG:処女喪失状況)
	ENDIF
ENDIF

;処女喪失相手が主人で【恋心】
IF PLAYER == MASTER && TALENT:恋心
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:反感 , 0.30
;初体験の時点で既に【淫乱】
ELSEIF TALENT:淫乱
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:痛覚 , 0.50
	TIMES SOURCE:反感 , 0.30
;処女喪失相手が助手で相性最高で親族でない
ELSEIF ASSIPLAY && RELATION:R >= 200 && TFLAG:近親相姦 == 0
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
ENDIF


;-------------------------------------------------
;初キスのチェック：調教者
;-------------------------------------------------
@FIRSTKISS_PLAYER_CHECK
;処女でないかＶ経験を伴うコマンドフラグが立ってなければそのまま終了
SIF CFLAG:PLAYER:初キス相手 || TFLAG:620 == 0
	RETURN 0

CALL 初キス喪失実行(PLAYER,NO:TARGET + 1,CFLAG:PLAYER:実年齢,DAY:日付_月,DAY:日付_日,初場所_彼部屋,初状況_口,NAME:TARGET)

;フラグを折っておく
TFLAG:620 = 0

;-------------------------------------------------
;初キスのチェック：助手
;-------------------------------------------------
@FIRSTKISS_ASSI_CHECK
;処女でないかＶ経験を伴うコマンドフラグが立ってなければそのまま終了
SIF ASSIPLAY == 0
	RETURN 0
SIF CFLAG:ASSI:初キス相手 || TFLAG:622 == 0
	RETURN 0

IF TEQUIP:野外プレイ == 1
	CFLAG:ASSI:初キス喪失場所 = 初場所_野外
ELSEIF TEQUIP:野外プレイ == 2
	CFLAG:ASSI:初キス喪失場所 = 初場所_学校
ELSEIF TEQUIP:野外プレイ == 3
	CFLAG:ASSI:初キス喪失場所 = 初場所_プール
ELSEIF TEQUIP:野外プレイ == 4
	CFLAG:ASSI:初キス喪失場所 = 初場所_地下室
ELSE
	CFLAG:ASSI:初キス喪失場所 = 初場所_スタジオ
ENDIF

CALL 初キス喪失実行(ASSI,NO:TARGET + 1,CFLAG:ASSI:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:ASSI:初キス喪失場所,初状況_口,NAME:TARGET)


;-------------------------------------------------
;初キスのチェック
;-------------------------------------------------
@FIRSTKISS_TARGET_CHECK
;キス経験済か唇を使うコマンドフラグが立ってなければそのまま終了
SIF CFLAG:初キス相手 || TFLAG:621 == 0
	RETURN 0

;Ａ処女喪失フラグ(ビデオ撮影)
SIF TEQUIP:撮影中
	TFLAG:ビデオ撮影内容2 |= 1

R = NO:PLAYER

;親族関係の判定
TFLAG:近親相姦 = 0
CALL INCEST

IF TEQUIP:野外プレイ == 1
	CFLAG:初キス喪失場所 = 初場所_野外
ELSEIF TEQUIP:野外プレイ == 2
	CFLAG:初キス喪失場所 = 初場所_学校
ELSEIF TEQUIP:野外プレイ == 3
	CFLAG:初キス喪失場所 = 初場所_プール
ELSEIF TEQUIP:野外プレイ == 4
	CFLAG:初キス喪失場所 = 初場所_地下室
ELSE
	CFLAG:初キス喪失場所 = 初場所_スタジオ
ENDIF

;CFLAG:初キス相手=16にするとこの辺の特殊処理をガン無視することになるんだなあと頭を抱えるなど
IF (TFLAG:621 & 2)
	CALL 初キス喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_男性器)
	TFLAG:初キス口以外 = 1	
ELSEIF (TFLAG:621 & 4)
	CALL 初キス喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_肛門)
	TFLAG:初キス口以外 = 1	
ELSEIF (TFLAG:621 & 8) 
	CALL 初キス喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_女性器)
	TFLAG:初キス口以外 = 1	
ELSEIF (TFLAG:621 & 16) 
	CALL 初キス喪失実行(TARGET,初相手_触手,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_男性器,"触手生物")
	TFLAG:初キス口以外 = 1	
ELSEIF (TFLAG:621 & 32) 
	CALL 初キス喪失実行(TARGET,初相手_触手,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_男性器,"スライム")
	TFLAG:初キス口以外 = 1	
ELSE
	CALL 初キス喪失実行(TARGET,NO:PLAYER + 1,CFLAG:PLAYER:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:初キス喪失場所,初状況_口)
ENDIF


;-------------------------------------------------
;Ａ処女喪失のチェック
;-------------------------------------------------
@LOST_ANALVIRGIN_CHECK
;Ａ処女でない
IF !ISＡ処女(TARGET) || TFLAG:Ａ挿入判定 == 0
	RETURN 0
ENDIF

TFLAG:Ａ処女喪失 = 1

;Ａ処女喪失フラグ(ビデオ撮影)
SIF TEQUIP:撮影中
	TFLAG:ビデオ撮影内容2 |= 1

R = NO:PLAYER

;親族関係の判定
TFLAG:近親相姦 = 0
CALL INCEST

IF !CFLAG:Ａ処女喪失相手
	IF TEQUIP:野外プレイ == 1
		CFLAG:Ａ処女喪失場所 = 初場所_野外
	ELSEIF TEQUIP:野外プレイ == 2
		CFLAG:Ａ処女喪失場所 = 初場所_学校
	ELSEIF TEQUIP:野外プレイ == 3
		CFLAG:Ａ処女喪失場所 = 初場所_プール
	ELSEIF TEQUIP:野外プレイ == 4
		CFLAG:Ａ処女喪失場所 = 初場所_地下室
	ELSE
		CFLAG:Ａ処女喪失場所 = 初場所_スタジオ
	ENDIF
	IF (TFLAG:ビデオ撮影内容 & 1)
		CFLAG:Ａ処女喪失状況 = 初状況_撮影
	ELSEIF MARK:反発刻印 >= 2
		CFLAG:Ａ処女喪失状況 = 初状況_無理犯
	ELSE
		CFLAG:Ａ処女喪失状況 = 初状況_指導
	ENDIF

	;初体験がバイブの場合
	IF SELECTCOM == [[TRAIN:アナルバイブ]]
		IF TALENT:淫乱 == 0
			CALL Ａ処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:Ａ処女喪失場所,CFLAG:Ａ処女喪失状況,"アナルバイブ")
		ELSEIF TALENT:淫乱 == 1
			CALL Ａ処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:Ａ処女喪失場所,CFLAG:Ａ処女喪失状況,"極太アナルバイブ")
		ENDIF
	ELSEIF SELECTCOM == [[TRAIN:アナルフィスト]]
		CALL Ａ処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:Ａ処女喪失場所,CFLAG:Ａ処女喪失状況,"アナルフィスト")
	ELSEIF TEQUIP:触手召喚 == 1
		CALL Ａ処女喪失実行(TARGET,初相手_触手,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:Ａ処女喪失場所,CFLAG:Ａ処女喪失状況,"触手生物")
	ELSE
		CALL Ａ処女喪失実行(TARGET,NO:PLAYER + 1,CFLAG:実年齢,DAY:日付_月,DAY:日付_日,CFLAG:Ａ処女喪失場所,CFLAG:Ａ処女喪失状況)
	ENDIF
ENDIF

;処女喪失相手が主人で【恋心】
IF PLAYER == MASTER && TALENT:恋心
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:反感 , 0.30
;初体験の時点で既に【淫乱】
ELSEIF TALENT:淫乱
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:痛覚 , 0.50
	TIMES SOURCE:反感 , 0.30
;処女喪失相手が助手で相性最高で親族でない
ELSEIF ASSIPLAY && RELATION:R >= 200 && TFLAG:近親相姦 == 0
	;反発刻印回避
	TFLAG:反発刻印の回避 = 1
ENDIF
;-------------------------------------------------
;調教対象の射精チェック
;-------------------------------------------------
@TARGET_EJAC_CHECK
SIF !HAVE_PENIS(TARGET)
	RETURN 0
SIF TALENT:未熟
	RETURN 0

A = UP:[[UP:快Ｃ]] + UP:[[UP:快Ｖ]] + UP:[[UP:快Ａ]] + UP:[[UP:快Ｂ]]

;自制心
SIF TALENT:自制心
	A /= 2

;快感に素直
SIF IS_快感に素直(TARGET)
	TIMES A , 1.20

;淫乱
SIF TALENT:淫乱
	TIMES A , 1.10

;快感の否定
SIF IS_快感の否定(TARGET)
	TIMES A , 0.80

;媚薬
SIF TEQUIP:媚薬効果
	A *= 2

;利尿剤
SIF TEQUIP:利尿剤効果
	A /= 2

;調教対象がコンドーム装着
SIF TEQUIP:調教対象コンドーム
	A /= 2

A = 1000+(A-1000)/2
BASE:射精ゲージ += A

S = BASE:射精ゲージ
EJAC = MAXBASE:射精ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

;大量射精
IF E == 2
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 100

	IF EXP:射精経験 < EXPLV:1
		SOURCE:露出 += 20000
		SOURCE:屈従 += 10000
	ELSEIF EXP:射精経験 < EXPLV:2
		SOURCE:露出 += 10000
		SOURCE:屈従 += 8000
	ELSEIF EXP:射精経験 < EXPLV:3
		SOURCE:露出 += 7000
		SOURCE:屈従 += 6000
	ELSEIF EXP:射精経験 < EXPLV:4
		SOURCE:露出 += 5000
		SOURCE:屈従 += 4000
	ELSEIF EXP:射精経験 < EXPLV:5
		SOURCE:露出 += 3000
		SOURCE:屈従 += 2000
	ELSE
		SOURCE:露出 += 1800
		SOURCE:屈従 += 1200
	ENDIF

	PRINTS NAME:TARGET
	PRINTL 大量射精
	PRINTL 精液経験＋１
	PRINTL 射精経験＋２
	IF EXP:射精経験 == 0 && HAVE_VAGINA(TARGET)
		EXP:異常経験 += 1
		PRINTL 異常経験＋1
	ENDIF
	EXP:精液経験 += 1
	EXP:射精経験 += 2

	;Ｐに精液汚れ
	STAIN:ペニス |= 4

	BASE:射精ゲージ -= EJAC*2
	SIF BASE:射精ゲージ >= EJAC
		BASE:射精ゲージ = EJAC-1

	;調教対象が射精フラグ
	TFLAG:調教対象が射精 = 2
	NOWEX:5 += 1
	EX:5 += 1
;通常の射精
ELSEIF E == 1
	LOSEBASE:気力 += 40

	IF EXP:射精経験 < EXPLV:1
		SOURCE:露出 += 10000
		SOURCE:屈従 += 5000
	ELSEIF EXP:射精経験 < EXPLV:2
		SOURCE:露出 += 5000
		SOURCE:屈従 += 4000
	ELSEIF EXP:射精経験 < EXPLV:3
		SOURCE:露出 += 2500
		SOURCE:屈従 += 2000
	ELSEIF EXP:射精経験 < EXPLV:4
		SOURCE:露出 += 1600
		SOURCE:屈従 += 1400
	ELSEIF EXP:射精経験 < EXPLV:5
		SOURCE:露出 += 800
		SOURCE:屈従 += 500
	ELSE
		SOURCE:露出 += 200
		SOURCE:屈従 += 250
	ENDIF

	PRINTS NAME:TARGET
	PRINTL 射精
	PRINTL 射精経験＋１
	IF EXP:射精経験 == 0 && HAVE_VAGINA(TARGET)
		EXP:異常経験 += 1
		PRINTL 異常経験＋1
	ENDIF
	EXP:射精経験 += 1

	;Ｐに精液汚れ
	STAIN:ペニス |= 4

	BASE:射精ゲージ -= EJAC
	SIF BASE:射精ゲージ >= EJAC
		BASE:射精ゲージ = EJAC-1

	;調教対象が射精フラグ
	TFLAG:調教対象が射精 = 1
	NOWEX:5 += 1
	EX:5 += 1
ENDIF

;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
@TARGET_MILK_CHECK
SIF TALENT:母乳体質 == 0
	RETURN 0

A = UP:[[UP:快Ｃ]] / 5 + UP:[[UP:快Ｖ]] / 5 + UP:[[UP:快Ａ]] / 5 + UP:[[UP:快Ｂ]] * 3

;自制心
SIF TALENT:自制心
	A /= 2

;快感に素直
SIF IS_快感に素直(TARGET)
	TIMES A , 1.20

;淫乱化
SIF TALENT:淫乱
	TIMES A , 1.10

;快感の否定
SIF IS_快感の否定(TARGET)
	TIMES A , 0.80

;B敏感
SIF IS_Ｂ敏感(TARGET)
	TIMES A , 1.50

;媚薬
SIF TEQUIP:媚薬効果
	A *= 2

;利尿剤
SIF TEQUIP:利尿剤効果
	A /= 2

;調教者が幼児退行
SIF TALENT:PLAYER:幼児退行
	A *= 2

;調教者が幼稚
SIF TALENT:PLAYER:幼稚
	A *= 2

;貧乳
SIF IS_貧乳(TARGET)
	TIMES A , 0.50

;絶壁
SIF IS_絶壁(TARGET)
	TIMES A , 0.20

A = 1000+(A-1000)/2
BASE:噴乳ゲージ += A

S = BASE:噴乳ゲージ
EJAC = MAXBASE:噴乳ゲージ

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

;大量噴乳
IF E == 2
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 100

	IF EXP:噴乳経験 < EXPLV:1
		SOURCE:露出 += 20000
		SOURCE:屈従 += 10000
	ELSEIF EXP:噴乳経験 < EXPLV:2
		SOURCE:露出 += 10000
		SOURCE:屈従 += 8000
	ELSEIF EXP:噴乳経験 < EXPLV:3
		SOURCE:露出 += 7000
		SOURCE:屈従 += 6000
	ELSEIF EXP:噴乳経験 < EXPLV:4
		SOURCE:露出 += 5000
		SOURCE:屈従 += 4000
	ELSEIF EXP:噴乳経験 < EXPLV:5
		SOURCE:露出 += 3000
		SOURCE:屈従 += 2000
	ELSE
		SOURCE:露出 += 1800
		SOURCE:屈従 += 1200
	ENDIF

	PRINTFORML %NAME:TARGET%は乳首から大量の母乳を噴出した。
	PRINTL 噴乳経験＋２
	IF EXP:噴乳経験 == 0
		EXP:異常経験 += 1
		PRINTL 異常経験＋1
	ENDIF
	EXP:噴乳経験 += 2
	;Ｂに母乳汚れ
	STAIN:胸 |= 16

	BASE:噴乳ゲージ -= EJAC*2
	SIF BASE:噴乳ゲージ >= EJAC
		BASE:噴乳ゲージ = EJAC-1

	;調教対象が噴乳フラグ
	TFLAG:調教対象が噴乳 += 2
	;搾乳器で母乳が集められているかチェック
	SIF TEQUIP:搾乳機 && TEQUIP:触手召喚 == 0
		TFLAG:搾乳中 += 2

	NOWEX:4 += 1
	EX:4 += 1
;通常の噴乳
ELSEIF E == 1
	LOSEBASE:気力 += 40

	IF EXP:噴乳経験 < EXPLV:1
		SOURCE:露出 += 10000
		SOURCE:屈従 += 5000
	ELSEIF EXP:噴乳経験 < EXPLV:2
		SOURCE:露出 += 5000
		SOURCE:屈従 += 4000
	ELSEIF EXP:噴乳経験 < EXPLV:3
		SOURCE:露出 += 2500
		SOURCE:屈従 += 2000
	ELSEIF EXP:噴乳経験 < EXPLV:4
		SOURCE:露出 += 1600
		SOURCE:屈従 += 1400
	ELSEIF EXP:噴乳経験 < EXPLV:5
		SOURCE:露出 += 800
		SOURCE:屈従 += 500
	ELSE
		SOURCE:露出 += 200
		SOURCE:屈従 += 250
	ENDIF

	PRINTFORML %NAME:TARGET%は乳首から母乳を垂れ流した。
	PRINTL 噴乳経験＋１
	IF EXP:噴乳経験 == 0
		EXP:異常経験 += 1
		PRINTL 異常経験＋1
	ENDIF
	EXP:噴乳経験 += 1
	;Ｂに母乳汚れ
	STAIN:胸 |= 16

	BASE:噴乳ゲージ -= EJAC
	SIF BASE:噴乳ゲージ >= EJAC
		BASE:噴乳ゲージ = EJAC-1

	;調教対象が噴乳フラグ
	TFLAG:調教対象が噴乳 += 1
	;搾乳器で母乳が集められているかチェック
	SIF TEQUIP:搾乳機 && TEQUIP:触手召喚 == 0
		TFLAG:搾乳中 += 1

	NOWEX:4 += 1
	EX:4 += 1
ENDIF

;-------------------------------------------------
;素質などによる上下の処理（快楽系）
;-------------------------------------------------
@UP_TALENT_CVA_CHECK
;媚薬
IF TEQUIP:媚薬効果
	TIMES UP:[[UP:快Ｃ]] , 2.00
	TIMES UP:[[UP:快Ｖ]] , 2.00
	TIMES UP:[[UP:快Ａ]] , 2.00
	TIMES UP:[[UP:快Ｂ]] , 2.00
ENDIF

;利尿剤
IF TEQUIP:利尿剤効果
	TIMES UP:[[UP:快Ｃ]] , 0.70
	TIMES UP:[[UP:快Ｖ]] , 0.70
	TIMES UP:[[UP:快Ａ]] , 0.70
	TIMES UP:[[UP:快Ｂ]] , 0.70
ENDIF

;失神中
IF TFLAG:失神中終了 > 0
	TIMES UP:[[UP:快Ｃ]] , 0.20
	TIMES UP:[[UP:快Ｖ]] , 0.20
	TIMES UP:[[UP:快Ａ]] , 0.20
	TIMES UP:[[UP:快Ｂ]] , 0.20
ENDIF

;自制心
IF TALENT:自制心
	TIMES UP:[[UP:快Ｃ]] , 0.30
	TIMES UP:[[UP:快Ｖ]] , 0.50
	TIMES UP:[[UP:快Ａ]] , 0.70
	TIMES UP:[[UP:快Ｂ]] , 0.30
ENDIF

;オトコ
IF IS_MALE(TARGET)
	TIMES UP:[[UP:快Ａ]] , 1.30
ENDIF

;肥満
IF IS_肥満(TARGET)
	TIMES UP:[[UP:快Ｃ]] , 0.50
	TIMES UP:[[UP:快Ｖ]] , 0.50
	TIMES UP:[[UP:快Ａ]] , 0.50
	TIMES UP:[[UP:快Ｂ]] , 0.50
ENDIF

;性豪
IF TALENT:性豪
	TIMES UP:[[UP:快Ｃ]] , 1.20
	TIMES UP:[[UP:快Ｖ]] , 1.20
	TIMES UP:[[UP:快Ａ]] , 1.20
	TIMES UP:[[UP:快Ｂ]] , 1.20
ENDIF

;-------------------------------------------------
;素質などによる上下の処理
;-------------------------------------------------
@UP_TALENT_CHECK

;媚薬
IF TEQUIP:媚薬効果
	TIMES UP:[[UP:恭順]] , 1.20
	TIMES UP:[[UP:欲情]] , 2.00
	TIMES UP:[[UP:屈服]] , 1.20
	TIMES UP:[[UP:反感]] , 0.75
	TIMES UP:[[UP:不快]] , 0.50
ENDIF

;利尿剤
IF TEQUIP:利尿剤効果
	TIMES UP:[[UP:恭順]] , 0.80
	TIMES UP:[[UP:欲情]] , 1.50
	TIMES UP:[[UP:屈服]] , 0.80
	TIMES UP:[[UP:反感]] , 0.50
	TIMES UP:[[UP:不快]] , 1.20
	TIMES UP:[[UP:抑鬱]] , 1.50
;	TIMES UP:[[UP:快Ｂ]] , 2.00
	TIMES UP:[[UP:享楽]] , 1.20
ENDIF

;失神中
IF TFLAG:失神中終了 > 0
	TIMES UP:[[UP:恭順]] , 0.50
	TIMES UP:[[UP:欲情]] , 0.75
	TIMES UP:[[UP:屈服]] , 0.10
	TIMES UP:[[UP:恥情]] , 0.10
	TIMES UP:[[UP:恐怖]] , 0.00
	TIMES UP:[[UP:反感]] , 0.00
	TIMES UP:[[UP:不快]] , 0.10
	TIMES UP:[[UP:抑鬱]] , 0.00
ENDIF

;臆病
IF TALENT:臆病
	TIMES UP:[[UP:恐怖]] , 2.00
	TIMES UP:[[UP:反感]] , 0.50
	TIMES UP:[[UP:抑鬱]] , 0.25
ENDIF
;反抗的
IF TALENT:ナマイキ
	TIMES UP:[[UP:恭順]] , 0.25
	TIMES UP:[[UP:欲情]] , 0.50
	TIMES UP:[[UP:反感]] , 1.50
ENDIF
;気丈
IF TALENT:気丈
	TIMES UP:[[UP:恭順]] , 0.30
	TIMES UP:[[UP:欲情]] , 0.75
	TIMES UP:[[UP:恐怖]] , 0.80
	TIMES UP:[[UP:反感]] , 2.00
	TIMES UP:[[UP:抑鬱]] , 2.00
ENDIF
;素直
IF TALENT:素直
	TIMES UP:[[UP:恭順]] , 2.00
	TIMES UP:[[UP:反感]] , 0.60
ENDIF
;大人しい
IF IS_大人しい(TARGET)
	TIMES UP:[[UP:反感]] , 0.30
ENDIF
;プライド高い
IF IS_プライド高い(TARGET)
	TIMES UP:[[UP:屈服]] , 0.50
	TIMES UP:[[UP:恐怖]] , 0.60
	TIMES UP:[[UP:反感]] , 1.20
ENDIF
;プライド低い
IF IS_プライド低い(TARGET)
	TIMES UP:[[UP:屈服]] , 2.00
	TIMES UP:[[UP:恐怖]] , 1.50
	TIMES UP:[[UP:反感]] , 0.80
ENDIF

;無関心
IF TALENT:クール
	TIMES UP:[[UP:恭順]] , 0.50
	TIMES UP:[[UP:欲情]] , 0.50
	TIMES UP:[[UP:屈服]] , 0.50
	TIMES UP:[[UP:恐怖]] , 0.80
	TIMES UP:[[UP:反感]] , 0.80
ENDIF
;感情乏しい
IF TALENT:感情乏しい
	TIMES UP:[[UP:恭順]] , 0.60
	TIMES UP:[[UP:欲情]] , 0.60
	TIMES UP:[[UP:屈服]] , 0.60
	TIMES UP:[[UP:恥情]] , 0.60
	TIMES UP:[[UP:恐怖]] , 0.60
	TIMES UP:[[UP:反感]] , 0.60
	TIMES UP:[[UP:不快]] , 0.60
	TIMES UP:[[UP:抑鬱]] , 0.60
ENDIF
;好奇心
SIF TALENT:好奇心
	TIMES UP:[[UP:習得]] , 1.20

;保守的
SIF TALENT:保守的
	TIMES UP:[[UP:習得]] , 0.80

;楽観的
SIF IS_楽観的(TARGET)
	TIMES UP:[[UP:抑鬱]] , 0.30

;悲観的
SIF IS_悲観的(TARGET)
	TIMES UP:[[UP:抑鬱]] , 2.50

;抑圧
IF IS_抑圧(TARGET)
	TIMES UP:[[UP:欲情]] , 0.50
	TIMES UP:[[UP:反感]] , 2.00
	TIMES UP:[[UP:抑鬱]] , 1.50
ENDIF
;解放
IF IS_解放(TARGET)
	TIMES UP:[[UP:欲情]] , 2.00
	TIMES UP:[[UP:屈服]] , 2.00
ENDIF
;抵抗
IF TALENT:抵抗
	TIMES UP:[[UP:欲情]] , 0.50
	TIMES UP:[[UP:反感]] , 2.00
ENDIF

;痛みに弱い
IF IS_痛みに弱い(TARGET)
	TIMES UP:[[UP:恐怖]] , 2.00
	TIMES UP:[[UP:反感]] , 1.50
ENDIF
;痛みに強い
IF IS_痛みに強い(TARGET)
	TIMES UP:[[UP:恐怖]] , 0.50
	TIMES UP:[[UP:反感]] , 0.75
ENDIF

;習得早い
SIF IS_習得早い(TARGET)
	TIMES UP:[[UP:習得]] , 2.00
;習得遅い
SIF IS_習得遅い(TARGET)
	TIMES UP:[[UP:習得]] , 0.50

;献身的
SIF TALENT:献身的
	TIMES UP:[[UP:屈服]] , 2.00

;快感に素直
SIF IS_快感に素直(TARGET)
	TIMES UP:[[UP:欲情]] , 2.00
;快感の否定
SIF IS_快感の否定(TARGET)
	TIMES UP:[[UP:欲情]] , 0.50

;恋心
IF TALENT:恋心 && ASSIPLAY == 0
	TIMES UP:[[UP:恭順]] , 1.20
	TIMES UP:[[UP:屈服]] , 2.00
	TIMES UP:[[UP:反感]] , 0.50
	TIMES UP:[[UP:不快]] , 0.50
ENDIF

;妄信
IF TALENT:妄信
	TIMES UP:[[UP:屈服]] , 4.00
	TIMES UP:[[UP:反感]] , 0.50
ENDIF

;肥満
IF IS_肥満(TARGET)
	TIMES UP:[[UP:抑鬱]] , 0.20
ENDIF

;調教者がTALENT:威圧感
IF TALENT:PLAYER:カリスマ
	TIMES UP:[[UP:恐怖]] , 2.00
	TIMES UP:[[UP:反感]] , 0.50
ENDIF

;性豪
IF TALENT:性豪
	TIMES UP:[[UP:欲情]] , 1.20
ENDIF

;常時発情
IF TALENT:常時発情
	TIMES UP:[[UP:欲情]] , 1.20
	TIMES UP:[[UP:潤滑]] , 1.20
ENDIF

;膣内射精による妊娠への恐怖（コンドーム使用中か【愛】持ちもしくは相性最高なら平気）
IF TFLAG:セックスで射精 && (SELECTCOM == [[TRAIN:正常位]] || SELECTCOM == [[TRAIN:後背位]] || SELECTCOM == [[TRAIN:対面座位]] || SELECTCOM == [[TRAIN:背面座位]] || SELECTCOM == [[TRAIN:騎乗位]])
	IF ASSIPLAY == 0 && TEQUIP:主人コンドーム == 0 && TFLAG:セックスで射精 && TALENT:恋心 == 0
		TIMES UP:[[UP:恐怖]] , 2.00
	ELSEIF ASSI > 0 && ASSIPLAY && TEQUIP:助手コンドーム == 0 
		R = NO:ASSI
		SIF RELATION:TARGET:R < 200
			TIMES UP:[[UP:恐怖]] , 2.00
	ENDIF
ENDIF

;反発刻印
IF MARK:反発刻印 == 3
	TIMES UP:[[UP:恭順]] , 0.10
ELSEIF MARK:反発刻印 == 2
	TIMES UP:[[UP:恭順]] , 0.40
ELSEIF MARK:反発刻印 == 1
	TIMES UP:[[UP:恭順]] , 0.70
ENDIF

;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
@MARK_GOT_CHECK
;-------------------------------------------------
;反発刻印
;------------------------------------------------- 

;PALAM:反感+PALAM:不快の上昇量に応じて反発刻印を取得
A = UP:[[UP:反感]] + UP:[[UP:不快]]
IF A >= 500 && A < 1200 && MARK:4 <= 0 && TFLAG:反発刻印の回避 == 0
	MARK:反発刻印 = 1
	MARK:4 = 1
	TFLAG:反発刻印 = 1
	PRINTL 反発刻印LV1を取得
ELSEIF A >= 1200 && A < 3000 && MARK:4 <= 1 && TFLAG:反発刻印の回避 == 0
	MARK:反発刻印 = 2
	MARK:4 = 2
	TFLAG:反発刻印 = 2
	PRINTL 反発刻印LV2を取得
	;感情乏しいの場合は反発刻印による信頼ダウンはなし
	IF ABL:信頼 == 1 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV0に下がった
		ABL:信頼 = 0
	ELSEIF ABL:信頼 == 2 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV1に下がった
		ABL:信頼 = 1
	ENDIF
ELSEIF A >= 3000 && MARK:4 <= 2 && TFLAG:反発刻印の回避 == 0
	MARK:反発刻印 = 3
	MARK:4 = 3
	TFLAG:反発刻印 = 3
	PRINTL 反発刻印LV3を取得
	;感情乏しいの場合は反発刻印による信頼ダウンはなし
	IF ABL:信頼 > 0 && ABL:信頼 <= 2 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV0に下がった
		ABL:信頼 = 0
	ELSEIF ABL:信頼 == 3 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV2に下がった
		ABL:信頼 = 2
	ENDIF
ENDIF
TFLAG:反発刻印の回避 = 0
;-------------------------------------------------
;苦痛刻印
;-------------------------------------------------
;PALAM:苦痛の上昇量に応じて反発刻印を取得
IF UP:[[UP:苦痛]] >= 500 && UP:[[UP:苦痛]] < 1500 && MARK:苦痛刻印 <= 0
	MARK:苦痛刻印 = 1
	TFLAG:苦痛刻印 = 1
	PRINTL 苦痛刻印LV1を取得
ELSEIF UP:[[UP:苦痛]] >= 1500 && UP:[[UP:苦痛]] < 3000 && MARK:苦痛刻印 <= 1
	MARK:苦痛刻印 = 2
	TFLAG:苦痛刻印 = 2
	PRINTL 苦痛刻印LV2を取得
	;気丈・感情乏しいの場合は苦痛刻印による信頼アップはなし
	IF ABL:信頼 == 0 && TALENT:気丈 == 0 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV1になった
		ABL:信頼 = 1
		CALL JUJUN_UP_CHECK
	ENDIF
ELSEIF UP:[[UP:苦痛]] >= 3000 && MARK:苦痛刻印 <= 2
	MARK:苦痛刻印 = 3
	TFLAG:苦痛刻印 = 3
	PRINTL 苦痛刻印LV3を取得
	;気丈・感情乏しいの場合は苦痛刻印による信頼アップはなし
	IF ABL:信頼 == 0 && TALENT:気丈 == 0 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV1になった
		ABL:信頼 = 1
		CALL JUJUN_UP_CHECK
	ENDIF
	IF TALENT:PLAYER:サド
		;PRINTL （【サド】の助手）
		PRINTFORML %EXPNAME:[[EXP:異常経験]]%＋１
		EXP:異常経験 += 1
	ENDIF
ENDIF


;-------------------------------------------------
;快楽刻印
;-------------------------------------------------
;PALAM:快ＣＶＡＢの上昇量に応じて快楽刻印を取得
A = UP:[[UP:快Ｃ]] + UP:[[UP:快Ｖ]] + UP:[[UP:快Ａ]] + UP:[[UP:快Ｂ]]

IF A >= 500 && A < 1500 && MARK:快楽刻印 <= 0
	MARK:快楽刻印 = 1
	TFLAG:快楽刻印 = 1
	PRINTL 快楽刻印LV1を取得
ELSEIF A >= 1500 && A < 3000 && MARK:快楽刻印 <= 1
	MARK:快楽刻印 = 2
	TFLAG:快楽刻印 = 2
	PRINTL 快楽刻印LV2を取得
ELSEIF A >= 3000 && MARK:快楽刻印 <= 2
	MARK:快楽刻印 = 3
	TFLAG:快楽刻印 = 3
	PRINTL 快楽刻印LV3を取得
	;自制心・感情乏しいの場合は快楽刻印による信頼アップはなし
	IF ABL:信頼 == 0 && TALENT:自制心 == 0 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV1になった
		ABL:信頼 = 1
		CALL JUJUN_UP_CHECK
	ENDIF
ENDIF

;-------------------------------------------------
;屈服刻印
;-------------------------------------------------
;プレイ中の行為に応じて屈服刻印を取得
IF TFLAG:屈服刻印2 == 1 && MARK:屈服刻印 <= 0
	MARK:屈服刻印 = 1
	TFLAG:屈服刻印 = 1
	PRINTL 屈服刻印LV1を取得
ELSEIF TFLAG:屈服刻印2 == 2 && MARK:屈服刻印 <= 1
	MARK:屈服刻印 = 2
	TFLAG:屈服刻印 = 2
	PRINTL 屈服刻印LV2を取得
	;感情乏しいの場合は屈服刻印による信頼アップはなし
	IF ABL:信頼 == 0 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV1になった
		ABL:信頼 = 1
		CALL JUJUN_UP_CHECK
	ENDIF
ELSEIF TFLAG:屈服刻印2 == 3 && MARK:屈服刻印 <= 2
	MARK:屈服刻印 = 3
	TFLAG:屈服刻印 = 3
	PRINTL 屈服刻印LV3を取得
	;感情乏しいの場合は屈服刻印による信頼アップはなし
	IF ABL:信頼 <= 1 && TALENT:感情乏しい == 0
		PRINTL そして信頼がLV2になった
		ABL:信頼 = 2
		CALL JUJUN_UP_CHECK
	ENDIF
ENDIF


;-------------------------------------------------
;欲望の上昇による[抑圧][抵抗]の消滅をチェック
;-------------------------------------------------
@YOKUBO_UP_CHECK
IF ABL:欲望 >= 3 && (IS_抑圧(TARGET) || TALENT:抵抗)
	PRINTFORM %NAME:TARGET%は
	IF IS_抑圧(TARGET)
		PRINTFORM 【%GET_STR_解放的(TARGET)%】
		TALENT:解放的 = 0
	ENDIF
	IF TALENT:抵抗
		PRINTFORM 【%TALENTNAME:[[TALENT:抵抗]]%】
		TALENT:抵抗 = 0
	ENDIF
	PRINTL を失った
	PRINTL 否定の珠が半分になった
	JUEL:否定 /= 2

	TFLAG:抑圧抵抗の消滅 = 1
ENDIF

;-------------------------------------------------
;信頼の上昇による[反抗的]の反転をチェック
;-------------------------------------------------
@JUJUN_UP_CHECK
IF ABL:信頼 >= 4 && TALENT:ナマイキ && TALENT:ツンデレ
	PRINTFORM %NAME:TARGET%は【%TALENTNAME:[[TALENT:ナマイキ]]%】を失い、
	PRINTFORML 【%TALENTNAME:[[TALENT:素直]]%】を得た
	TALENT:ナマイキ = 0
	TALENT:素直 = 1
ENDIF

;-------------------------------------------------
;奉仕快楽経験、被虐快楽経験、Ａ快楽経験のチェック
;-------------------------------------------------
@EXP_GOT_CHECK

A = UP:[[UP:快Ｃ]] + UP:[[UP:快Ｖ]] + UP:[[UP:快Ａ]] + UP:[[UP:快Ｂ]]

;奉仕快楽経験のチェック
E = 0

IF UP:[[UP:習得]] < 100
	A = 0
ELSEIF UP:[[UP:習得]] < 300
	A *= 1
ELSEIF UP:[[UP:習得]] < 700
	A *= 2
ELSEIF UP:[[UP:習得]] < 1500
	A *= 3
ELSE
	A *= 4
ENDIF

IF TFLAG:快楽経験
	IF A >= 12000
		E = 16
		TIMES UP:[[UP:反感]] , 0.65
		TIMES UP:[[UP:不快]] , 0.30
	ELSEIF A >= 8000
		E = 12
		TIMES UP:[[UP:反感]] , 0.70
		TIMES UP:[[UP:不快]] , 0.40
	ELSEIF A >= 5000
		E = 8
		TIMES UP:[[UP:反感]] , 0.75
		TIMES UP:[[UP:不快]] , 0.50
	ELSEIF A >= 3000
		E = 4
		TIMES UP:[[UP:反感]] , 0.80
		TIMES UP:[[UP:不快]] , 0.60
	ELSEIF A >= 2000
		E = 2
		TIMES UP:[[UP:反感]] , 0.85
		TIMES UP:[[UP:不快]] , 0.70
	ELSEIF A >= 1000
		E = 1
		TIMES UP:[[UP:反感]] , 0.90
		TIMES UP:[[UP:不快]] , 0.80
	ENDIF
	IF E
		PRINT 奉仕快楽経験+
		PRINTVL E
		EXP:奉仕快楽経験 += E
		TFLAG:奉仕快楽経験 = E
	ENDIF
ENDIF


;Ａ快楽経験のチェック
A = UP:[[UP:快Ａ]]
E = 0

IF UP:[[UP:快Ａ]] < 300
	A = 0
ELSEIF UP:[[UP:快Ａ]] < 1000
	A *= 1
ELSEIF UP:[[UP:快Ａ]] < 5000
	A *= 2
ELSEIF UP:[[UP:快Ａ]] < 10000
	A *= 3
ELSE
	A *= 4
ENDIF

IF A >= 12000
	E = 16
	TIMES UP:[[UP:反感]] , 0.80
	TIMES UP:[[UP:不快]] , 0.90
	TIMES UP:[[UP:屈服]] , 1.20
ELSEIF A >= 8000
	E = 12
	TIMES UP:[[UP:反感]] , 0.85
	TIMES UP:[[UP:不快]] , 0.90
	TIMES UP:[[UP:屈服]] , 1.15
ELSEIF A >= 5000
	E = 8
	TIMES UP:[[UP:反感]] , 0.85
	TIMES UP:[[UP:不快]] , 0.95
	TIMES UP:[[UP:屈服]] , 1.10
ELSEIF A >= 3000
	E = 4
	TIMES UP:[[UP:反感]] , 0.90
	TIMES UP:[[UP:不快]] , 0.95
	TIMES UP:[[UP:屈服]] , 1.05
ELSEIF A >= 2000
	E = 2
	TIMES UP:[[UP:反感]] , 0.90
	TIMES UP:[[UP:不快]] , 1.00
	TIMES UP:[[UP:屈服]] , 1.00
ELSEIF A >= 1000
	E = 1
	TIMES UP:[[UP:反感]] , 0.95
	TIMES UP:[[UP:不快]] , 1.00
	TIMES UP:[[UP:屈服]] , 1.00
ENDIF

IF E
	PRINT Ａ快楽経験+
	PRINTVL E
	EXP:Ａ快楽経験 += E
	TFLAG:128 = E
ENDIF

;被虐・嗜虐快楽経験のチェック
A = UP:[[UP:快Ｃ]] + UP:[[UP:快Ｖ]] + UP:[[UP:快Ａ]] + UP:[[UP:快Ｂ]]
SIF A == 0
	A = UP:[[UP:欲情]]

E = 0
F = 0
IF A >= 3000 && UP:[[UP:苦痛]] >= 2000
	E = 16
	TIMES UP:[[UP:反感]] , 0.65
ELSEIF A >= 2500 && UP:[[UP:苦痛]] >= 1500
	E = 12
	TIMES UP:[[UP:反感]] , 0.70
ELSEIF A >= 1500 && UP:[[UP:苦痛]] >= 1000
	E = 8
	TIMES UP:[[UP:反感]] , 0.75
ELSEIF A >= 1000 && UP:[[UP:苦痛]] >= 500
	E = 4
	TIMES UP:[[UP:反感]] , 0.80
ELSEIF A >= 600 && UP:[[UP:苦痛]] >= 300
	E = 2
	TIMES UP:[[UP:反感]] , 0.85
ELSEIF A >= 300 && UP:[[UP:苦痛]] >= 100
	E = 1
	TIMES UP:[[UP:反感]] , 0.90
ENDIF
IF E
	PRINT 被虐快楽経験+
	PRINTVL E
	EXP:被虐快楽経験 += E
	TFLAG:苦痛快楽経験 = E
	;助手の嗜虐快楽経験上昇
	IF ASSIPLAY && ASSI > 0
		IF ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ == 0
			TIMES E , 0.00
			F = 0
		ELSEIF  ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ == 1
			TIMES E , 0.50
			F = 0
		ELSEIF  ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ == 2
			TIMES E , 1.00
			F = E / 2
		ELSEIF  ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ == 3
			F = E * 2
		ELSEIF  ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ == 4
			F = E * 10
		ELSEIF  ABL:ASSI:サドっ気 + TEQUIP:助手ボンデージ >= 5
			F = E * 50
		ENDIF
		IF E
			PRINTFORML 嗜虐快楽経験+{E}(%CALLNAME:ASSI%)
			EXP:ASSI:嗜虐快楽経験 += E
		ENDIF
		IF F
			PRINTFORML %PALAMNAME:[[PALAM:欲情]]%の珠＋{F}(%CALLNAME:ASSI%)
			JUEL:ASSI:欲情 += F
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;[即落ち]のチェック
;-------------------------------------------------
@SOKUOCHI_CHECK

SIF TALENT:即落ち == 0
	RETURN 0

IF UP:[[UP:快Ｃ]] > 1 && ABL:Ｃ感覚 < 1
	ABL:Ｃ感覚 = 1
	PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%LV1になった
ELSEIF UP:[[UP:快Ｃ]] > 30 && ABL:Ｃ感覚 < 2
	ABL:Ｃ感覚 = 2
	PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%LV2になった
ELSEIF UP:[[UP:快Ｃ]] > 60 && ABL:Ｃ感覚 < 3
	ABL:Ｃ感覚 = 3
	PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%LV3になった
ELSEIF UP:[[UP:快Ｃ]] > 200 && ABL:Ｃ感覚 < 4
	ABL:Ｃ感覚 = 4
	PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%LV4になった
ELSEIF UP:[[UP:快Ｃ]] > 1000 && ABL:Ｃ感覚 < 5
	ABL:Ｃ感覚 = 5
	PRINTFORML %ABLNAME:[[ABL:Ｃ感覚]]%LV5になった
ENDIF

IF UP:[[UP:快Ｖ]] > 1 && ABL:Ｖ感覚 < 1
	ABL:Ｖ感覚 = 1
	PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%LV1になった
ELSEIF UP:[[UP:快Ｖ]] > 30 && ABL:Ｖ感覚 < 2
	ABL:Ｖ感覚 = 2
	PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%LV2になった
ELSEIF UP:[[UP:快Ｖ]] > 60 && ABL:Ｖ感覚 < 3
	ABL:Ｖ感覚 = 3
	PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%LV3になった
ELSEIF UP:[[UP:快Ｖ]] > 200 && ABL:Ｖ感覚 < 4
	ABL:Ｖ感覚 = 4
	PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%LV4になった
ELSEIF UP:[[UP:快Ｖ]] > 1000 && ABL:Ｖ感覚 < 5
	ABL:Ｖ感覚 = 5
	PRINTFORML %ABLNAME:[[ABL:Ｖ感覚]]%LV5になった
ENDIF

IF UP:[[UP:快Ａ]] > 1 && ABL:Ａ感覚 < 1
	ABL:Ａ感覚 = 1
	PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%LV1になった
ELSEIF UP:[[UP:快Ａ]] > 30 && ABL:Ａ感覚 < 2
	ABL:Ａ感覚 = 2
	PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%LV2になった
ELSEIF UP:[[UP:快Ａ]] > 60 && ABL:Ａ感覚 < 3
	ABL:Ａ感覚 = 3
	PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%LV3になった
ELSEIF UP:[[UP:快Ａ]] > 200 && ABL:Ａ感覚 < 4
	ABL:Ａ感覚 = 4
	PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%LV4になった
ELSEIF UP:[[UP:快Ａ]] > 1000 && ABL:Ａ感覚 < 5
	ABL:Ａ感覚 = 5
	PRINTFORML %ABLNAME:[[ABL:Ａ感覚]]%LV5になった
ENDIF

IF UP:[[UP:快Ｂ]] > 1 && ABL:Ｂ感覚 < 1
	ABL:Ｂ感覚 = 1
	PRINTFORML %ABLNAME:[[ABL:Ｂ感覚]]%LV1になった
ELSEIF UP:[[UP:快Ｂ]] > 30 && ABL:Ｂ感覚 < 2
	ABL:Ｂ感覚 = 2
	PRINTFORML %ABLNAME:[[ABL:Ｂ感覚]]%LV2になった
ELSEIF UP:[[UP:快Ｂ]] > 60 && ABL:Ｂ感覚 < 3
	ABL:Ｂ感覚 = 3
	PRINTFORML %ABLNAME:[[ABL:Ｂ感覚]]%LV3になった
ELSEIF UP:[[UP:快Ｂ]] > 200 && ABL:Ｂ感覚 < 4
	ABL:Ｂ感覚 = 4
	PRINTFORML %ABLNAME:[[ABL:Ｂ感覚]]%LV4になった
ELSEIF UP:[[UP:快Ｂ]] > 1000 && ABL:Ｂ感覚 < 5
	ABL:Ｂ感覚 = 5
	PRINTFORML %ABLNAME:[[ABL:Ｂ感覚]]%LV5になった
ENDIF

IF UP:[[UP:恭順]] > 1 && ABL:信頼 < 1
	ABL:信頼 = 1
	PRINTFORML %ABLNAME:[[ABL:信頼]]%LV1になった
ELSEIF UP:[[UP:恭順]] > 30 && ABL:信頼 < 2
	ABL:信頼 = 2
	PRINTFORML %ABLNAME:[[ABL:信頼]]%LV2になった
ELSEIF UP:[[UP:恭順]] > 60 && ABL:信頼 < 3
	ABL:信頼 = 3
	PRINTFORML %ABLNAME:[[ABL:信頼]]%LV3になった
ELSEIF UP:[[UP:恭順]] > 200 && ABL:信頼 < 4
	ABL:信頼 = 4
	PRINTFORML %ABLNAME:[[ABL:信頼]]%LV4になった
ELSEIF UP:[[UP:恭順]] > 1000 && ABL:信頼 < 5
	ABL:信頼 = 5
	PRINTFORML %ABLNAME:[[ABL:信頼]]%LV5になった
ENDIF

IF UP:[[UP:欲情]] > 1 && ABL:欲望 < 1
	ABL:欲望 = 1
	PRINTFORML %ABLNAME:[[ABL:欲望]]%LV1になった
ELSEIF UP:[[UP:欲情]] > 30 && ABL:欲望 < 2
	ABL:欲望 = 2
	PRINTFORML %ABLNAME:[[ABL:欲望]]%LV2になった
ELSEIF UP:[[UP:欲情]] > 60 && ABL:欲望 < 3
	ABL:欲望 = 3
	PRINTFORML %ABLNAME:[[ABL:欲望]]%LV3になった
ELSEIF UP:[[UP:欲情]] > 200 && ABL:欲望 < 4
	ABL:欲望 = 4
	PRINTFORML %ABLNAME:[[ABL:欲望]]%LV4になった
ELSEIF UP:[[UP:欲情]] > 1000 && ABL:欲望 < 5
	ABL:欲望 = 5
	PRINTFORML %ABLNAME:[[ABL:欲望]]%LV5になった
ENDIF

IF UP:[[UP:習得]] > 1 && ABL:技巧 < 1
	ABL:技巧 = 1
	PRINTFORML %ABLNAME:[[ABL:技巧]]%LV1になった
ELSEIF UP:[[UP:習得]] > 30 && ABL:技巧 < 2
	ABL:技巧 = 2
	PRINTFORML %ABLNAME:[[ABL:技巧]]%LV2になった
ELSEIF UP:[[UP:習得]] > 60 && ABL:技巧 < 3
	ABL:技巧 = 3
	PRINTFORML %ABLNAME:[[ABL:技巧]]%LV3になった
ELSEIF UP:[[UP:習得]] > 200 && ABL:技巧 < 4
	ABL:技巧 = 4
	PRINTFORML %ABLNAME:[[ABL:技巧]]%LV4になった
ELSEIF UP:[[UP:習得]] > 1000 && ABL:技巧 < 5
	ABL:技巧 = 5
	PRINTFORML %ABLNAME:[[ABL:技巧]]%LV5になった
ENDIF

IF UP:[[UP:屈服]] > 1 && ABL:奉仕精神 < 1 && ABL:Ｃ感覚 >= 1
	ABL:奉仕精神 = 1
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%LV1になった
ELSEIF UP:[[UP:屈服]] > 30 && ABL:奉仕精神 < 2 && ABL:Ｃ感覚 >= 2
	ABL:奉仕精神 = 2
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%LV2になった
ELSEIF UP:[[UP:屈服]] > 60 && ABL:奉仕精神 < 3 && ABL:Ｃ感覚 >= 3
	ABL:奉仕精神 = 3
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%LV3になった
ELSEIF UP:[[UP:屈服]] > 200 && ABL:奉仕精神 < 4 && ABL:Ｃ感覚 >= 4
	ABL:奉仕精神 = 4
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%LV4になった
ELSEIF UP:[[UP:屈服]] > 1000 && ABL:奉仕精神 < 5 && ABL:Ｃ感覚 >= 5
	ABL:奉仕精神 = 5
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%LV5になった
ENDIF

IF UP:[[UP:恥情]] > 1 && ABL:露出癖 < 1 && ABL:Ｂ感覚 >= 1
	ABL:露出癖 = 1
	PRINTFORML %ABLNAME:[[ABL:露出癖]]%LV1になった
ELSEIF UP:[[UP:恥情]] > 30 && ABL:露出癖 < 2 && ABL:Ｂ感覚 >= 2
	ABL:露出癖 = 2
	PRINTFORML %ABLNAME:[[ABL:露出癖]]%LV2になった
ELSEIF UP:[[UP:恥情]] > 60 && ABL:露出癖 < 3 && ABL:Ｂ感覚 >= 3
	ABL:露出癖 = 3
	PRINTFORML %ABLNAME:[[ABL:露出癖]]%LV3になった
ELSEIF UP:[[UP:恥情]] > 200 && ABL:露出癖 < 4 && ABL:Ｂ感覚 >= 4
	ABL:露出癖 = 4
	PRINTFORML %ABLNAME:[[ABL:露出癖]]%LV4になった
ELSEIF UP:[[UP:恥情]] > 1000 && ABL:露出癖 < 5 && ABL:Ｂ感覚 >= 5
	ABL:露出癖 = 5
	PRINTFORML %ABLNAME:[[ABL:露出癖]]%LV5になった
ENDIF

IF UP:[[UP:苦痛]] > 1 && ABL:マゾっ気 < 1 && ABL:欲望 >= 1
	ABL:マゾっ気 = 1
	PRINTFORML %ABLNAME:[[ABL:マゾっ気]]%LV1になった
ELSEIF UP:[[UP:苦痛]] > 30 && ABL:マゾっ気 < 2 && ABL:欲望 >= 2
	ABL:マゾっ気 = 2
	PRINTFORML %ABLNAME:[[ABL:マゾっ気]]%LV2になった
ELSEIF UP:[[UP:苦痛]] > 60 && ABL:マゾっ気 < 3 && ABL:欲望 >= 3
	ABL:マゾっ気 = 3
	PRINTFORML %ABLNAME:[[ABL:マゾっ気]]%LV3になった
ELSEIF UP:[[UP:苦痛]] > 200 && ABL:マゾっ気 < 4 && ABL:欲望 >= 4
	ABL:マゾっ気 = 4
	PRINTFORML %ABLNAME:[[ABL:マゾっ気]]%LV4になった
ELSEIF UP:[[UP:苦痛]] > 1000 && ABL:マゾっ気 < 5 && ABL:欲望 >= 5
	ABL:マゾっ気 = 5
	PRINTFORML %ABLNAME:[[ABL:マゾっ気]]%LV5になった
ENDIF

IF EXP:レズ経験 > 1 && ABL:レズっ気 < 1 && ABL:欲望 >= 1
	ABL:レズっ気 = 1
	PRINTFORML %ABLNAME:[[ABL:レズっ気]]%LV1になった
ELSEIF EXP:レズ経験 > 5 && ABL:レズっ気 < 2 && ABL:欲望 >= 2
	ABL:レズっ気 = 2
	PRINTFORML %ABLNAME:[[ABL:レズっ気]]%LV2になった
ELSEIF EXP:レズ経験 > 20 && ABL:レズっ気 < 3 && ABL:欲望 >= 3
	ABL:レズっ気 = 3
	PRINTFORML %ABLNAME:[[ABL:レズっ気]]%LV3になった
ELSEIF EXP:レズ経験 > 40 && ABL:レズっ気 < 4 && ABL:欲望 >= 4
	ABL:レズっ気 = 4
	PRINTFORML %ABLNAME:[[ABL:レズっ気]]%LV4になった
ELSEIF EXP:レズ経験 > 100 && ABL:レズっ気 < 5 && ABL:欲望 >= 5
	ABL:レズっ気 = 5
	PRINTFORML %ABLNAME:[[ABL:レズっ気]]%LV5になった
ENDIF

IF EXP:ＢＬ経験 > 1 && ABL:ＢＬっ気 < 1 && ABL:欲望 >= 1
	ABL:ＢＬっ気 = 1
	PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%LV1になった
ELSEIF EXP:ＢＬ経験 > 5 && ABL:ＢＬっ気 < 2 && ABL:欲望 >= 2
	ABL:ＢＬっ気 = 2
	PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%LV2になった
ELSEIF EXP:ＢＬ経験 > 20 && ABL:ＢＬっ気 < 3 && ABL:欲望 >= 3
	ABL:ＢＬっ気 = 3
	PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%LV3になった
ELSEIF EXP:ＢＬ経験 > 40 && ABL:ＢＬっ気 < 4 && ABL:欲望 >= 4
	ABL:ＢＬっ気 = 4
	PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%LV4になった
ELSEIF EXP:ＢＬ経験 > 100 && ABL:ＢＬっ気 < 5 && ABL:欲望 >= 5
	ABL:ＢＬっ気 = 5
	PRINTFORML %ABLNAME:[[ABL:ＢＬっ気]]%LV5になった
ENDIF

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------
@ECST_CHECK
TFLAG:絶頂の強度 = C + V + A + B

;-------------------------------------------------
;絶頂時のおもらし処理
;-------------------------------------------------
@PISSING_ECST_CHECK
;超大量放尿
IF TFLAG:絶頂の強度 >= 7 && TEQUIP:利尿剤効果 && TALENT:おもらし癖
	PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋５
	EXP:放尿経験 += 5
	TEQUIP:利尿剤効果 = 0
	;奴隷のＶとＰに＜尿＞の汚れが発生
	STAIN:ペニス |= 32
	STAIN:ヴァギナ |= 32
	;着衣おもらし処理
	CALL SOILING_CLOTH_NO1
;大量放尿
ELSEIF (TFLAG:絶頂の強度 >= 7 && TEQUIP:利尿剤効果) || (TFLAG:絶頂の強度 >= 5 && TEQUIP:利尿剤効果 && TALENT:おもらし癖)
	PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋４
	EXP:放尿経験 += 4
	TEQUIP:利尿剤効果 = 0
	;奴隷のＶとＰに＜尿＞の汚れが発生
	STAIN:ペニス |= 32
	STAIN:ヴァギナ |= 32
	;着衣おもらし処理
	CALL SOILING_CLOTH_NO1
;放尿
ELSEIF (TFLAG:絶頂の強度 >= 7 && TALENT:おもらし癖) || (TFLAG:絶頂の強度 >= 5 && TEQUIP:利尿剤効果) || (TFLAG:絶頂の強度 >= 3 && TEQUIP:利尿剤効果 && TALENT:おもらし癖)
	PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋３
	EXP:放尿経験 += 3
	TEQUIP:利尿剤効果 = 0
	;奴隷のＶとＰに＜尿＞の汚れが発生
	STAIN:ペニス |= 32
	STAIN:ヴァギナ |= 32
	;着衣おもらし処理
	CALL SOILING_CLOTH_NO1
;失禁
ELSEIF (TFLAG:絶頂の強度 >= 5 && TALENT:おもらし癖) || (TFLAG:絶頂の強度 >= 3 && TEQUIP:利尿剤効果) || (TFLAG:絶頂の強度 >= 1 && TEQUIP:利尿剤効果 && TALENT:おもらし癖)
	PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋２
	EXP:放尿経験 += 2
	SIF TALENT:おもらし癖 == 0
		TEQUIP:利尿剤効果 = 0
	;奴隷のＶとＰに＜尿＞の汚れが発生
	STAIN:ペニス |= 32
	STAIN:ヴァギナ |= 32
	;着衣おもらし処理
	CALL SOILING_CLOTH_NO1
;微量失禁
ELSEIF (TFLAG:絶頂の強度 >= 3 && TALENT:おもらし癖) || (TFLAG:絶頂の強度 >= 1 && TEQUIP:利尿剤効果)
	PRINTFORML %EXPNAME:[[EXP:放尿経験]]%＋１
	EXP:放尿経験 += 1
	;奴隷のＶとＰに＜尿＞の汚れが発生
	STAIN:ペニス |= 32
	STAIN:ヴァギナ |= 32
ENDIF

;-------------------------------------------------
;主人による調教の経験値
;-------------------------------------------------
@MASTER_FLAG_CHECK
;絶頂
TFLAG:絶頂の強度 += TFLAG:調教対象が射精 + TFLAG:調教対象が噴乳
Q = 0
SIF NOWEX:0 > 0
	Q += 1
SIF NOWEX:1 > 0
	Q += 1
SIF NOWEX:2 > 0
	Q += 1
SIF NOWEX:3 > 0
	Q += 1
SIF TFLAG:調教対象が射精 > 0
	Q += 1
SIF TFLAG:調教対象が噴乳 > 0
	Q += 1
TFLAG:主人経験 += TFLAG:絶頂の強度 * Q

;射精
REPEAT 5
	SIF TFLAG:COUNT > 0 && EXP:精液経験 >= EXPLV:3
		TFLAG:主人経験 += TFLAG:COUNT
REND
SIF TFLAG:素股で射精 > 0 && EXP:精液経験 >= EXPLV:3
	TFLAG:主人経験 += TFLAG:素股で射精

;主人と対象の能力を計算
IF ASSIPLAY == 0 && TEQUIP:触手召喚 == 0
	;ABL:信頼をみる
	R = ABL:信頼

	;素質による増減
	;反抗的
	SIF TALENT:ナマイキ
 		R -= 1
	;素直
	SIF TALENT:素直
		R += 1
	;自制心
	SIF TALENT:自制心
		R -= 1
	;無関心
	SIF TALENT:クール
		R -= 1
	;感情乏しい
	SIF TALENT:感情乏しい
 		R -= 1
	;抵抗
	SIF TALENT:抵抗
		R -= 1
	;献身的
	SIF TALENT:献身的
		R += 1
	;快感に素直
	SIF IS_快感に素直(TARGET)
		R += 1
	;快感の否定
	SIF IS_快感の否定(TARGET)
		R -= 1
	;女嫌い
	SIF TALENT:ボーイッシュ && HAVE_VAGINA(MASTER)
		R -= 1
	;男嫌い
	SIF TALENT:男性不信 && IS_MALE(MASTER)
		R -= 1
	;恋心
	SIF TALENT:恋心
 		R += 2
	;妄信
	SIF TALENT:妄信
		R += 2
	;主人の魅惑
	SIF TALENT:MASTER:魅惑
		R += 1
	;主人の謎の魅力
	SIF TALENT:MASTER:謎の魅力
		R += 1
	;主人の魅力
 	SIF TALENT:MASTER:魅力
		R += 1
	;主人の人気
 	SIF TALENT:MASTER:人気者
		R += 1

	;結果が0以下の場合
	SIF R <= 0
		R = 1

	R += TFLAG:主人経験

	;相性による修正
	IF RELATION:MASTER != 0
		R *= RELATION:MASTER
		R /= 100
	ENDIF

	CFLAG:好感度 += R

	;ラブスコープがあれば好感度表示
	IF ITEM:ラブスコープ
		PRINTFORML 好感度上昇:{R/10}.{R%10}％
		PRINTFORML 好感度合計:{(CFLAG:好感度)/10}％
	ENDIF

ENDIF
R = 0
TFLAG:主人経験 = 0
;
;
;
;