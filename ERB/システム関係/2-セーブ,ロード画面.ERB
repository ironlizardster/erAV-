@USERSAVE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC データ番号
#DIM DYNAMIC データ数
#DIM DYNAMIC ページ数
PRINTL □何番にセーブしますか？
PRINTL 　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間

FOR LCOUNT, ページ数*20, ページ数*20+20
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			PRINTFORML 　[{LCOUNT, 2}] %RESULTS%
		CASE 1
			SETCOLORBYNAME gray
			;表示テンプレ
			;PRINTFORML 　[ 5] 2016/01/10(曜) 00:10:15 | 2016/3/28(曜) 197日目夜 難易度:(N) ∞ 
			PRINTFORML 　[{LCOUNT, 2}] ----　　　　　　　　　　|
			RESETCOLOR
		CASE 2
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 別バリアントのセーブデータです 上書きは可能です
			RESETCOLOR
		CASE 3
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 互換性の無いセーブデータです 上書きは可能です
			RESETCOLOR
		CASE 4
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] ロードできないセーブデータです 上書きは可能です
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAINFORM 　SAVE.{ページ数+1}　
CALL PRINT_PAGE_NAVI(ページ数 > 0, ページ数 < 3)
PRINTL 
DO
	INPUT
	データ番号 = RESULT
	SELECTCASE データ番号
		CASE 0 TO 79
			SELECTCASE CHKDATA(データ番号)
				CASE 0, 2, 3, 4
					PRINTL 既にセーブデータが存在します。上書きしますか？
					CALL GET_はいいいえ()
					SIF RESULT == COMMAND_いいえ
						RESTART
					BREAK
				CASE 1
					BREAK
			ENDSELECT
		CASE COMMAND_PREV_PAGE
			SIF ページ数 > 0
				ページ数--
			RESTART
		CASE COMMAND_NEXT_PAGE
			SIF ページ数 < 3
				ページ数++
			RESTART
		CASE COMMAND_GO_BACK
			RETURN 0
	ENDSELECT
LOOP 1

SAVEDATA データ番号, セーブデータ情報()

RETURN 0

@SYSTEM_AUTOSAVE
#DIM DYNAMIC データ番号
データ番号 = 94+((DAY:日付_経過日数-1)*2%6+TIME)

[IF_DEBUG]
DEBUGPRINTFORML SYSTEM_AUTOSAVE:{データ番号}:%セーブデータ情報()%
[ENDIF]
SAVEDATA データ番号, セーブデータ情報()

@セーブデータ情報
#FUNCTIONS
#DIMS DYNAMIC REALTIMENOW
#DIM DYNAMIC 年
#DIM DYNAMIC 月
#DIM DYNAMIC 日
#DIMS DYNAMIC DETAIL, 2

;リアルタイムの計算
年 = TOINT(SUBSTRING(GETTIMES(), 0, 4))
月 = TOINT(SUBSTRING(GETTIMES(), 5, 2))
日 = TOINT(SUBSTRING(GETTIMES(), 8, 2))

REALTIMENOW = %GETTIMES()%
;曜日挟むために一度分割する
SPLIT REALTIMENOW, " ", DETAIL
;現実の時間を入れる
DETAIL = %DETAIL%(%DOW(ZELLER(年, 月, 日))%) %DETAIL:1%
;ここからゲーム内の時間や情報
DETAIL = %DETAIL% | {DAY:日付_年}/%TOSTR(DAY:日付_月, "D2")%/%TOSTR(DAY:日付_日, "D2")%(%DOW(FLAG:曜日)%)


SELECTCASE FLAG:難易度
	CASE 1
		DETAIL += " EASY"
	CASE 2
		IF FLAG:EASY経由
			DETAIL += " NORMAL"
		ELSE
			DETAIL += " 実績チャレンジ"
		ENDIF
	CASE 3
		DETAIL += " HARD"
	CASE 4
		DETAIL += " POWERFUL"
	CASE 9
		DETAIL += " EXTRA"
ENDSELECT

SELECTCASE TIME
	CASE 0
		DETAIL += @" {DAY:日付_経過日数, 4}日目昼 "
	CASE 1
		DETAIL += @" {DAY:日付_経過日数, 4}日目夜 "
ENDSELECT

SIF FLAG:前回調教対象 >= 0
	TARGET = FLAG:前回調教対象
SIF FLAG:前回助手 >= 0
	ASSI = FLAG:前回助手

SIF TARGET >= 1
	DETAIL += @" 前回:%NAME:TARGET%指導"

RETURNF DETAIL

@USERLOAD
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC ページ数
#DIM DYNAMIC 最新セーブ, 2
#DIMS DYNAMIC SPLITVAR, 2
PRINTL □何番をロードしますか？
PRINTL 　[--]　　　 現実の時間　　　　|　　　ゲーム内の時間

FOR LCOUNT, ページ数*20, ページ数*20+20
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			PRINTFORML 　[{LCOUNT, 2}] %RESULTS%
		CASE 1
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] ----　　　　　　　　　　|
			RESETCOLOR
		CASE 2
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 別バリアントのセーブデータです
			RESETCOLOR
		CASE 3
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] 互換性の無いセーブデータです
			RESETCOLOR
		CASE 4
			SETCOLORBYNAME gray
			PRINTFORML 　[{LCOUNT, 2}] ロードできないセーブデータです
	ENDSELECT
NEXT
DRAWLINE
FOR LCOUNT, 94, 100
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			SPLIT RESULTS, "|", SPLITVAR
			REPLACE SPLITVAR, "[^0-9]", ""
			IF TOINT(RESULTS) > 最新セーブ:1
				最新セーブ:0 = LCOUNT
				最新セーブ:1 = TOINT(RESULTS)
			ENDIF
	ENDSELECT
NEXT
FOR LCOUNT, 94, 100
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			PRINTFORML 　[{LCOUNT, 2}] %RESULTS% \@ LCOUNT == 最新セーブ:0 ? 【最新】#\@
	ENDSELECT
NEXT
DRAWLINE
PRINTPLAINFORM 　LOAD.{ページ数+1}　
CALL PRINT_PAGE_NAVI(ページ数 > 0, ページ数 < 3)
PRINTL 
INPUT
SELECTCASE RESULT
	CASE 0 TO 79, 94 TO 99
		SELECTCASE CHKDATA(RESULT)
			CASE 0
				;STOPBGM
				LOADDATA RESULT
			CASEELSE
				RESTART
		ENDSELECT
	CASE COMMAND_GO_BACK
		RETURN 0
	CASE COMMAND_PREV_PAGE
		SIF ページ数 > 0
			ページ数--
		RESTART
	CASE COMMAND_NEXT_PAGE
		SIF ページ数 < 3
			ページ数++
		RESTART
	CASEELSE
		RESTART
ENDSELECT

RETURN 0

@SYSTEM_LOADEND
CALLF メモリ解放, "SYSTEM_LOADEND"
