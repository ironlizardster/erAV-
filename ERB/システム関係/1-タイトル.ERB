@SYSTEM_TITLE
#DIM DYNAMIC LMONTH
#DIM DYNAMIC LDAYS
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC HTML用, 3
#DIM DYNAMIC 誕生日人数
#DIM DYNAMIC HOGE, 3
#DIM CONST 最大文字数 = 34

SIF EXISTCSV(10000)
	THROW キャラ番号10000は当バリアントでは使用できません。別の番号に置き換えてください

;10秒(デフォルト)
IF ツールチップ表示時間
	TOOLTIP_SETDURATION ツールチップ表示時間
ELSE
	TOOLTIP_SETDURATION 10000
ENDIF

;ツールチップディレイ(デフォルトは無し)
SIF ツールチップディレイ
	TOOLTIP_SETDELAY ツールチップディレイ

CSVCOUNT = 0
FOR LCOUNT, 0, 10000
	SIF EXISTCSV(LCOUNT)
		CSVCOUNT++
NEXT

ALIGNMENT LEFT

VARLINE = LINECOUNT
FLAG:難易度 = -1

DO
	LMONTH = TOINT(SUBSTRING(GETTIMES(), 5, 2))
	LDAYS = TOINT(SUBSTRING(GETTIMES(), 8, 2))
	誕生日人数 = 0
	DRAWLINE
	FOR LCOUNT, 0, 10000
		SIF !EXISTCSV(LCOUNT)
			CONTINUE
		IF (CSVBASE(LCOUNT, [[BASE:誕生月]])) == LMONTH && (CSVBASE(LCOUNT, [[BASE:誕生日]])) == LDAYS
			PRINTFORML 今日は%CSVNAME(LCOUNT)%の誕生日です
			誕生日人数++
		ENDIF
	NEXT
	IF 誕生日人数
		PRINTL ハッピーバースデー！
		DRAWLINE
	ENDIF
	誕生日人数 = 0
	ALIGNMENT CENTER
	PRINTFORML %GAMEBASE_TITLE%
	PRINTFORML %GAMEBASE_YEAR%
	PRINTFORML Ver.{GAMEBASE_VERSION/100}.{GAMEBASE_VERSION%100}
	PRINTFORML TOTAL CHARA {CSVCOUNT}

	WINDOW_TITLE = %GAMEBASE_TITLE% {GAMEBASE_VERSION/100}.{GAMEBASE_VERSION%100}
	DRAWLINE
	CALL PRINT_BUTTON("[最初からはじめる]", 1, COMMAND_最初からはじめる, 最大文字数, 1)
	CALL PRINT_BUTTON("[ロードしてはじめる]", 1, COMMAND_ロードしてはじめる, 最大文字数, 1)
	CALL PRINT_BUTTON("[実績を確認する]", 1, COMMAND_実績を確認する, 最大文字数, 1)

	CALL INPUTF, COMMAND_最初からはじめる, COMMAND_ロードしてはじめる, COMMAND_実績を確認する

	SELECTCASE RESULT
		CASE COMMAND_最初からはじめる
			CALL RESET_DATA
			ADDCHARA 0
			BEGIN FIRST
		CASE COMMAND_ロードしてはじめる
			CALL RESET_DATA
			CALL USERLOAD
		CASE COMMAND_実績を確認する
			ALIGNMENT LEFT
			ADDCHARA 0
			CALL PRINT_ACHIEVEMENT
			DELCHARA 0
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 1
