;=================================================
; シーンタイトル
;--------------------------------------------
; RESULTS = の後にシーンのタイトルを記述する
;=================================================
@SCENE_TITLE_1
RESULTS = インタビュー

;=================================================
; シーン可視条件
;--------------------------------------------
; 特定のキャラクターの存在や、他のシーン結果などを、
; このシーンの表示条件に使用したい時に編集する
;=================================================
@SCENE_VISIBLE_1
; 不可視なら0、可視なら1を戻す
RETURN 1

;=================================================
; シーン報酬
;--------------------------------------------
; シーンの報酬を記述する
; P:1 消費行動ポイント
; P:2 基本消費体力
; P:3 基本消費体力
;=================================================
@SCENE_FEE_1
P:1 += 1
P:2 += 10
P:3 += 50

;=================================================
; シーン解説文
;--------------------------------------------
; シーンの解説文を記述する
;=================================================
@SCENE_INFO_1
DRAWLINE
PRINTL 《インタビュー》
PRINTL 女優の性経験や年齢などを質問します
DRAWLINE

;=================================================
; シーン成功判定
;--------------------------------------------
; シーンの成功判定を行います
;=================================================
@SCENE_CALC_1

LOCAL:1 = 0

LOCAL:1 += ABL:信頼
LOCAL:1 += ABL:話術
LOCAL:1 += ABL:露出癖
LOCAL:1 += ABL:被写技能


SIF TALENT:目立ちたがり
	LOCAL:1 += 10
SIF TALENT:露出狂
	LOCAL:1 += 10
SIF TALENT:饒舌
	LOCAL:1 += 10
SIF TALENT:恥薄い
	LOCAL:1 += 5
SIF TALENT:プライド低い
	LOCAL:1 += 5

SIF TALENT:抵抗
	LOCAL:1 -= 20
SIF TALENT:恥じらい
	LOCAL:1 -= 5

IF LOCAL:1 >= 10
	TFLAG:ビデオ撮影内容 |= 2
	IF LOCAL:1 >= 40
		RETURN 2
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 0
ENDIF

;=================================================
; シーン撮影開始
;--------------------------------------------
; シーン撮影開始時の地の文を表示します
; 処理の都合上、ここで以下の項目を加算
; P:4 獲得メダル
; P:10 基本獲得ポイント
;=================================================
@SCENE_RESULT_START_1

P:4 = 1
P:10 = 500

PRINTFORML %NICKNAME:TARGET%こと%NAME:TARGET%のインタビューを撮影した……
PRINTW


;=================================================
; シーン結果・大成功
;--------------------------------------------
; シーン成功判定で2を返した場合の処理を行います
;=================================================
@SCENE_RESULT_PERFECT_1

PRINTFORM %NICKNAME:TARGET%はいくぶんかリラックスした様子で、
IF TALENT:恥じらい
	PRINTFORM 頬を赤らめながらも
ELSEIF TALENT:恥薄い
	PRINTFORM 明け透けな態度で
ENDIF
PRINTFORML かなりきわどい突っ込んだ質問にもしっかりと受け答えしている……
PRINTL
TIMES P:10, 1.25
P:4 *= 2

;=================================================
; シーン結果・成功
;--------------------------------------------
; シーン成功判定で1を返した場合の処理を行います
;=================================================
@SCENE_RESULT_SUCCESS_1

PRINTFORM %NICKNAME:TARGET%はやや緊張した様子で
IF TALENT:恥じらい
	PRINTFORM 頬を赤らめながら
ELSEIF TALENT:恥薄い
	PRINTFORM 明け透けな態度で
ENDIF
PRINTFORML かなりきわどい突っ込んだ質問にも時折どもりながらも受け答えしている……
PRINTL

TIMES P:10, 1.00
P:4 *= 1

;=================================================
; シーン結果・失敗
;--------------------------------------------
; シーン成功判定で0を返した場合の処理を行います
;=================================================
@SCENE_RESULT_FAILED_1

PRINTFORM %NICKNAME:TARGET%はかなり緊張しているのか、
IF TALENT:恥じらい
	PRINTFORM 頬を真っ赤にし
ENDIF
PRINTFORML 質問に対してズレた回答をしたり、完全に黙りこくってしまっている……
PRINTL
TIMES P:10, 0.80
P:4 = 0

;=================================================
; シーン経験増加処理
;--------------------------------------------
; シーンの経験増加処理を行います
;=================================================
@SCENE_EXP_1

TCVAR:会話経験 += ABL:話術 + RAND:6
	SIF TALENT:饒舌
		TCVAR:会話経験 += RAND:6