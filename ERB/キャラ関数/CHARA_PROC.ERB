@キャラ追加, ARG
ADDCHARA ARG

;名鑑用の処理
LOADGLOBAL
女優名鑑:ARG += 1
SAVEGLOBAL
IF GETCHARA(ARG) != -1
	TARGET = GETCHARA(ARG)
	CALL CLEARBONUS_CALL
ENDIF

@キャラ削除, ARG

;助手の情報を取得
N = -1
SIF ASSI >= 0
	N = NO:ASSI

;お弁当、起こす係のフラグ消し
SIF CFLAG:ARG:お弁当作成判定 == 1
	FLAG:弁当 = 0
SIF CFLAG:ARG:朝フェラ係に任命 == 1 
	FLAG:朝起こしてもらう = 0

;キャラ削除
DELCHARA ARG

;前回の助手・調教対象だった場合はフラグを空に
SIF FLAG:前回調教対象 == ARG
	FLAG:前回調教対象 = -1
SIF FLAG:前回助手 == ARG
	FLAG:前回助手 = -1

;前回の助手・調教対象より前だった場合はフラグを減算
SIF FLAG:前回調教対象 > ARG
	FLAG:前回調教対象 -= 1
SIF FLAG:前回助手 > ARG
	FLAG:前回助手 -= 1

;調教対象を空に
TARGET = -1

;助手を戻す
ASSI = -1
REPEAT CHARANUM
	SIF NO:COUNT == N
		ASSI = COUNT
REND

@RESET_CHARA_STATUS(ARG)
; 経験をクリア
FOR LOCAL, 0, 150
	EXP:ARG:LOCAL = 0
NEXT
; 珠をクリア
FOR LOCAL, 0, 100
	JUEL:ARG:LOCAL = 0
	PALAM:ARG:LOCAL = 0
NEXT
; 能力をクリア
FOR LOCAL, 0, 100
	SIF LOCAL == [[ABL:美容感覚]] || LOCAL == [[ABL:運動能力]] || LOCAL == [[ABL:学力]] || LOCAL == [[ABL:オタク知識]]
		CONTINUE
	ABL:ARG:LOCAL = 0
NEXT
; 刻印をクリア
FOR LOCAL, 0, 4
	MARK:ARG:LOCAL = 0
NEXT

;CFLAGをクリアする前にFLAG関係を戻す
SIF CFLAG:ARG:習い事判定 == 1
	FLAG:習い事人数 -= 1
SIF CFLAG:ARG:朝フェラ係に任命 == 1
	FLAG:朝起こしてもらう = 0

; CFLAGをクリア
FOR LOCAL, 0, 999
	SIF LOCAL == [[CFLAG:着衣状態]] || LOCAL == [[CFLAG:上着のタイプ]] || LOCAL == [[CFLAG:特別コスチュームのタイプ]] || LOCAL == [[CFLAG:靴下の種類]] || LOCAL == [[CFLAG:靴の種類]] || LOCAL == [[CFLAG:靴下の着用状態]] || LOCAL == [[CFLAG:ブラの色]] || LOCAL == [[CFLAG:ブラの種類]] || LOCAL == [[CFLAG:パンツの色]] || LOCAL == [[CFLAG:パンツの種類]] || LOCAL == [[CFLAG:予備の衣装]] || LOCAL == [[CFLAG:髪色]] || LOCAL == [[CFLAG:染髪済]] || LOCAL == [[CFLAG:髪型]] || LOCAL == [[CFLAG:地毛の色]] || ((LOCAL == [[CFLAG:ペニスのサイズ]] || LOCAL == [[CFLAG:ペニスアクセサリの有無]] || LOCAL == [[CFLAG:包皮の状態]]) && (TALENT:ARG:ふたなり || TALENT:ARG:オトコ))
		CONTINUE
	CFLAG:ARG:LOCAL = 0
NEXT

CFLAG:ARG:ファーストキスの相手 = -1