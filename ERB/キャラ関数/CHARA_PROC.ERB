@キャラ追加, ARG
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 相性設定用, 100
#DIMS DYNAMIC 相性値, 200
#DIMS DYNAMIC TMP相性値, 2

ADDCHARA ARG

;名鑑用の処理
LOADGLOBAL
女優名鑑:ARG += 1
SAVEGLOBAL
IF GETCHARA(ARG) != -1
	TARGET = GETCHARA(ARG)
	CALL CLEARBONUS_CALL
ENDIF

;相性の設定
SPLIT CSTR:相性, "/", 相性設定用
LOCAL:1 = RESULT
FOR LOCAL, 0, LOCAL:1
	;なぜか行連結で行頭にスペースが入るので削る
	相性設定用:LOCAL = %SUBSTRINGU(相性設定用:LOCAL, 1)%
	SIF 相性設定用:LOCAL == ""
		BREAK
	SPLIT 相性設定用:LOCAL, "*", TMP相性値
	相性値:(LOCAL*2) '= TMP相性値, TMP相性値:1
NEXT
FOR LOCAL, 0, 固有キャラ最大ID
	STRLENS 固有キャラ名前リスト:LOCAL 
	IF RESULT && MATCH(相性値, 固有キャラ名前リスト:LOCAL)
		FOR LOCAL:2, 0, LOCAL:1
			IF 相性値:((LOCAL:2)*2) == 固有キャラ名前リスト:LOCAL
				RELATION:LOCAL = TOINT(相性値:((LOCAL:2)*2+1))
				BREAK
			ENDIF
		NEXT
	ELSE
		RELATION:LOCAL = 100
	ENDIF
NEXT

STRLENS CSTR:男性器呼び方
SIF !RESULT
	CSTR:男性器呼び方 = おちんぽ
STRLENS CSTR:女性器呼び方
SIF !RESULT
	CSTR:女性器呼び方 = おまんこ
STRLENS CSTR:肛門呼び方
SIF !RESULT
	CSTR:肛門呼び方 = アナル


@キャラ削除, ARG

;助手の情報を取得
N = -1
SIF ASSI >= 0
	N = NO:ASSI

;お弁当、起こす係のフラグ消し
SIF CFLAG:ARG:弁当係 == 1
	FLAG:弁当 = 0
SIF CFLAG:ARG:朝フェラ係 == 1 
	FLAG:朝起こしてもらう = 0

;キャラ削除
DELCHARA ARG

;前回の助手・調教対象だった場合はフラグを空に
SIF FLAG:前回調教対象 == ARG
	FLAG:前回調教対象 = -1
SIF FLAG:前回助手 == ARG
	FLAG:前回助手 = -1

;前回の助手・調教対象より前だった場合はフラグを減算
SIF FLAG:前回調教対象 > ARG
	FLAG:前回調教対象 -= 1
SIF FLAG:前回助手 > ARG
	FLAG:前回助手 -= 1

;調教対象を空に
TARGET = -1

;助手を戻す
ASSI = -1
REPEAT CHARANUM
	SIF NO:COUNT == N
		ASSI = COUNT
REND

@RESET_CHARA_STATUS(ARG)
; 経験をクリア
FOR LOCAL, 0, 150
	EXP:ARG:LOCAL = 0
NEXT
; 珠をクリア
FOR LOCAL, 0, 100
	JUEL:ARG:LOCAL = 0
	PALAM:ARG:LOCAL = 0
NEXT
; 能力をクリア
FOR LOCAL, 0, 100
	SIF LOCAL == [[ABL:美容感覚]] || LOCAL == [[ABL:運動能力]] || LOCAL == [[ABL:学力]] || LOCAL == [[ABL:オタク知識]]
		CONTINUE
	ABL:ARG:LOCAL = 0
NEXT
; 刻印をクリア
FOR LOCAL, 0, 4
	MARK:ARG:LOCAL = 0
NEXT

;CFLAGをクリアする前にFLAG関係を戻す
SIF CFLAG:ARG:習い事 == 1
	FLAG:習い事人数 -= 1
SIF CFLAG:ARG:朝フェラ係 == 1
	FLAG:朝起こしてもらう = 0

; CFLAGをクリア
FOR LOCAL, 0, 999
	SIF LOCAL == [[CFLAG:着衣状態]] || LOCAL == [[CFLAG:上着タイプ]] || LOCAL == [[CFLAG:特別コスタイプ]] || LOCAL == [[CFLAG:靴下タイプ]] || LOCAL == [[CFLAG:靴タイプ]] || LOCAL == [[CFLAG:靴下着用]] || LOCAL == [[CFLAG:ブラの色]] || LOCAL == [[CFLAG:ブラタイプ]] || LOCAL == [[CFLAG:パンツの色]] || LOCAL == [[CFLAG:パンツタイプ]] || LOCAL == [[CFLAG:予備衣装]] || LOCAL == [[CFLAG:髪色]] || LOCAL == [[CFLAG:染髪済]] || LOCAL == [[CFLAG:髪型]] || LOCAL == [[CFLAG:地毛色]] || ((LOCAL == [[CFLAG:ペニスサイズ]] || LOCAL == [[CFLAG:包皮状態]]) && HAVE_PENIS(ARG))
		CONTINUE
	CFLAG:ARG:LOCAL = 0
NEXT

CFLAG:ARG:初キス相手 = 0