@CHARACTER_EDIT(対象)
#DIM 対象
#DIM 現在タブ
#DIMS CONST タブ英名 = "NAME", "BASE", "ABL",  "TALENT", "EXP",  "CFLAG", "INFO"
#DIMS CONST タブ名   = "名前", "基礎", "能力", "素質",   "経験", "フラグ", "情報"
#DIM FIRST_LINE, 2
#DIM CREDRAW

CREDRAW = CURRENTREDRAW()
REDRAW 0

$SHOW_LOOP

FIRST_LINE = LINECOUNT

DRAWLINE
PRINTFORML %NAME:対象%を編集中
DRAWLINE
FIRST_LINE:1 = LINECOUNT
CALLFORM CHARACTER_EDIT_UI_%タブ英名:現在タブ%(対象)
FOR LOCAL, 0, FIRST_LINE:1 + 25 - LINECOUNT
	PRINTL
NEXT
DRAWLINE

FOR LOCAL, 0, VARSIZE("タブ名")
	PRINTBUTTON @"[%タブ名:LOCAL%]", 10000 + LOCAL
	PRINT  
NEXT
PRINTL

DRAWLINE
PRINTBUTTON "[戻る]", 30000

INPUT

IF RESULT == 30000
	REDRAW CREDRAW
	RETURN
ELSEIF INRANGE(RESULT, 10000, 10000 + VARSIZE("タブ名") - 1)
	現在タブ = RESULT - 10000
	IF タブ名:現在タブ == "情報"
		現在タブ = 0
		CALL SHOW_CHARADATA_DETAILS(1)
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CALLFORM CHARACTER_EDIT_INPUT_%タブ英名:現在タブ%(対象, RESULT)

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

