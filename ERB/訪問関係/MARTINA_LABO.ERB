;=================================================
;マルチナ別荘
;=================================================
@MARTINA_LABO
#DIM CONST COMMAND_CUSTOM_NEW_CHAR = 0

;今となっては元々どんな形で女子寮を強化するのかわからんがとりあえずメダルの使い道として

$INPUT_LOOP

DRAWLINE
PRINTL ○○県の郊外・マルチナの別荘
PRINTL 《メダルを消費して女優を調達できます》
DRAWLINE
CALL PRINT_DAY_TIME

PRINTFORML 所有メダル数:{BASE:MASTER:メダル}/{MAXBASE:MASTER:メダル}
DRAWLINE

CALL PRINT_BUTTON("特殊な女優を探す",1,COMMAND_CUSTOM_NEW_CHAR,-1,1)

DRAWLINE
CALL PRINT_BUTTON("戻る",1,COMMAND_GO_BACK,-1,1)

INPUT

IF RESULT == COMMAND_CUSTOM_NEW_CHAR
	CALL CREATE_CHARA();
	SIF RESULT == -1
		PRINTFORML これ以上探せません
ELSEIF RESULT == COMMAND_GO_BACK
	RETURN 0
ENDIF
GOTO INPUT_LOOP

;-------------------------
;汎用女優の作成
;生成したキャラの番号を返すが、人数の限界により生成に失敗した場合-1を返す
;-------------------------
@CREATE_CHARA()
IF FLAG:汎用女優カウント > MAX_RANDOM_CHARA
	RETURN -1
ENDIF

;ターゲットを退避
LOCAL:0 = TARGET

;キャラを追加
CALL キャラパラメータ決定(0)

;名前の決定(※素質決定後に行う)
PRINTL 名前を入力してください
INPUTS
$INPUT_LOOP_NAMEDECIDE1
PRINTFORML %NAME:TARGET%の新たな名前は%RESULTS%でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
		IF RESULT == 0
			PRINTFORMW %NAME:TARGET%の新たな名前を%RESULTS%に決定しました
			NAME:TARGET = %RESULTS%
		ELSEIF RESULT == 1
			PRINTFORML 再入力してください
			INPUTS
			GOTO INPUT_LOOP_NAMEDECIDE1
		ELSE
			GOTO INPUT_LOOP_NAMEDECIDE1
		ENDIF
		
PRINTL 続いて呼び方を入力してください
INPUTS
$INPUT_LOOP_NAMEDECIDE2
PRINTFORML %NAME:TARGET%の新たな呼び方は%RESULTS%でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
		IF RESULT == 0
			PRINTFORMW %CALLNAME:TARGET%の新たな名前を%RESULTS%に決定しました
			CALLNAME:TARGET = %RESULTS%
		ELSEIF RESULT == 1
			PRINTFORML 再入力してください
			INPUTS
			GOTO INPUT_LOOP_NAMEDECIDE2
		ELSE
			GOTO INPUT_LOOP_NAMEDECIDE2
		ENDIF

PRINTL 最後に新たな芸名を入力してください
INPUTS
$INPUT_LOOP_NAMEDECIDE3
PRINTFORML %NAME:TARGET%の新たな芸名は%RESULTS%でよろしいですか？
	PRINTL [0] - はい
	PRINTL [1] - いいえ
	INPUT
		IF RESULT == 0
			PRINTFORMW %CALLNAME:TARGET%の新たな名前を%RESULTS%に決定しました
			NICKNAME:TARGET = %RESULTS%
		ELSEIF RESULT == 1
			PRINTFORML 再入力してください
			INPUTS
			GOTO INPUT_LOOP_NAMEDECIDE3
		ELSE
			GOTO INPUT_LOOP_NAMEDECIDE3
		ENDIF
						
PRINTFORML 名前を《%NAME:TARGET%》、呼び方を《%CALLNAME:TARGET%》、芸名を《%NICKNAME:TARGET%》に変更しました

;素質と能力の決定
;CALL RANDOM_CHARA_SETTING()

;初期設定(※素質決定後に行う)
;CALL INIT_NEWCHARA(TARGET)

;スキルの習得
;CALL RANDOM_CHARA_SKILL_SETTING()

;衣装の設定
;CALL RANDOM_CHARA_CLOTH_SETTING()

CALL CHARACTER_EDIT(TARGET)

;ターゲットの再設定
TARGET = LOCAL:0

RETURN CHARANUM - 1

;-------------------------
;汎用武将の素質と能力を決定する
;ARG:0=ケモ率、ARG:1=能力傾向、ARG:2=性別
;-------------------------
@RANDOM_CHARA_SETTING(ARG:0)

;性別を指定の通りに設定、指定がない場合は設定された男性率に応じて性別をランダムに決定
IF RAND:100 < 5
	IF RAND:30 == 0
		LOCAL:0 = 4
	ELSEIF RAND:30 == 0
		LOCAL:0 = 3
	ELSE
		LOCAL:0 = 0
	ENDIF
ELSE
	IF RAND:20 == 0
		LOCAL:0 = 2
	ELSE
		LOCAL:0 = 1
	ENDIF
ENDIF

TALENT:性経験 = 0
SELECTCASE LOCAL:0
	CASE 0
		;男性
		TALENT:オトコ = 1
		TALENT:オトコの娘 = 0
	CASE 1
		;女性
		TALENT:オトコ = 0
		TALENT:オトコの娘 = 0
	CASE 2
		;女性ふたなり
		TALENT:オトコ = 0
		TALENT:オトコの娘 = 0
		TALENT:ふたなり = 1
	CASE 3
		;男の娘ふたなり
		TALENT:オトコ = 1
		TALENT:オトコの娘 = 1
		TALENT:ふたなり = 1
	CASE 4
		;男の娘
		TALENT:オトコ = 1
		TALENT:オトコの娘 = 1
ENDSELECT

;■BASE値の決定
LOCAL:2 = RAND:6 + RAND:5 - 4
MAXBASE:体力 = 1600 + LOCAL:2 * 100

LOCAL:2 = RAND:8 + RAND:7 - 6
IF LOCAL:2 < 0
	MAXBASE:気力 = 1300 - ABS(LOCAL:2) / 2 * 100
ELSE
	MAXBASE:気力 = 1300 + LOCAL:2 * 100
ENDIF

LOCAL:2 = RAND:8 + RAND:7 - 6
IF LOCAL:2 < 0
	MAXBASE:魅力値 = 100 - ABS(LOCAL:2) / 2 * 10
ELSE
	MAXBASE:魅力値 = 100 + LOCAL:2 * 10
ENDIF

BASE:体力 = MAXBASE:体力
BASE:気力 = MAXBASE:気力
BASE:魅力値 = MAXBASE:魅力値

;■素質の決定
;臆病、気丈
CALL RCHARA_TALENT_OPPOSE, [[TALENT:臆病]], [[TALENT:気丈]], 12, 12
;抵抗、素直
CALL RCHARA_TALENT_OPPOSE, [[TALENT:抵抗]], [[TALENT:素直]], 20, 20
;大人しい
CALL RCHARA_TALENT_SINGLE, [[TALENT:大人しい]], 7
;プライド高い、プライド低い
CALL RCHARA_TALENT_OPPOSE, [[TALENT:プライド高い]], [[TALENT:プライド低い]], 12, 3
;ナマイキ(素直を持つなら確率低下)
IF TALENT:素直
	CALL RCHARA_TALENT_SINGLE, [[TALENT:ナマイキ]], 3
ELSE
	CALL RCHARA_TALENT_SINGLE, [[TALENT:ナマイキ]], 12
ENDIF
;ツンデレ(抵抗を持つキャラは高確率・素直と同居しない)
IF TALENT:抵抗
	CALL RCHARA_TALENT_SINGLE, [[TALENT:ツンデレ]], 23
ELSEIF !TALENT:素直
	CALL RCHARA_TALENT_SINGLE, [[TALENT:ツンデレ]], 5
ENDIF
;快感に素直、快感の否定
CALL RCHARA_TALENT_OPPOSE, [[TALENT:快感に素直]], [[TALENT:快感の否定]], 9, 5
;自制心(快感に素直と同居しない)
IF !TALENT:快感に素直
	CALL RCHARA_TALENT_SINGLE, [[TALENT:自制心]], 7
ENDIF
;好奇心、保守的
CALL RCHARA_TALENT_OPPOSE, [[TALENT:好奇心]], [[TALENT:保守的]], 13, 6
;一線越えない
CALL RCHARA_TALENT_SINGLE, [[TALENT:一線越えない]], 6
;目立ちたがり
CALL RCHARA_TALENT_SINGLE, [[TALENT:目立ちたがり]], 9
;貞操観念、貞操無頓着
CALL RCHARA_TALENT_OPPOSE, [[TALENT:貞操観念]], [[TALENT:貞操無頓着]], 10, 6
;抑圧(一部素質と同居しない)
IF !TALENT:ナマイキ && !TALENT:好奇心 && !TALENT:目立ちたがり && !TALENT:快感に素直
	CALL RCHARA_TALENT_SINGLE, [[TALENT:抑圧]], 6
ENDIF
;カリスマ(素直を持たないキャラ限定)
IF !TALENT:素直
	CALL RCHARA_TALENT_SINGLE, [[TALENT:カリスマ]], 6
ENDIF
;恥じらい、恥薄い(目立ちたがりを持つなら確率変動)
IF TALENT:目立ちたがり
	CALL RCHARA_TALENT_OPPOSE, [[TALENT:恥じらい]], [[TALENT:恥薄い]], 3, 13
ELSE
	CALL RCHARA_TALENT_OPPOSE, [[TALENT:恥じらい]], [[TALENT:恥薄い]], 13, 7
ENDIF
;クール(一部素質と同居しない)
IF !TALENT:好奇心 && !TALENT:目立ちたがり
	CALL RCHARA_TALENT_SINGLE, [[TALENT:クール]], 7
ENDIF
;感情乏しい(一部素質と同居しない)
IF !TALENT:臆病 && !TALENT:ナマイキ && !TALENT:好奇心 && !TALENT:目立ちたがり && !TALENT:恥じらい
	CALL RCHARA_TALENT_SINGLE, [[TALENT:感情乏しい]], 8
ENDIF
;解放(同居しない素質多数)
IF !TALENT:臆病 && !TALENT:ツンデレ && !TALENT:自制心 && !TALENT:感情乏しい && !TALENT:保守的
	IF !TALENT:一線越えない && !TALENT:貞操観念 && !TALENT:抑圧 && !TALENT:カリスマ && !TALENT:恥じらい && !TALENT:快感の否定
		CALL RCHARA_TALENT_SINGLE, [[TALENT:解放]], 12
	ENDIF
ENDIF
;痛みに強い、痛みに弱い(白兵の初期値に応じて確率変動)
CALL RCHARA_TALENT_OPPOSE, [[TALENT:痛みに強い]], [[TALENT:痛みに弱い]], 5, 7
;濡れやすい、濡れにくい
CALL RCHARA_TALENT_OPPOSE, [[TALENT:濡れやすい]], [[TALENT:濡れにくい]], 8, 5
;習得遅い
CALL RCHARA_TALENT_SINGLE, [[TALENT:習得遅い]], 5
;舌使い
CALL RCHARA_TALENT_SINGLE, [[TALENT:舌使い]], 5
;自慰しやすい
CALL RCHARA_TALENT_SINGLE, [[TALENT:自慰しやすい]], 7
;汚臭鈍感、汚臭敏感
CALL RCHARA_TALENT_OPPOSE, [[TALENT:汚臭鈍感]], [[TALENT:汚臭敏感]], 8, 8
;献身的
CALL RCHARA_TALENT_SINGLE, [[TALENT:献身的]], 9
;中毒しやすい
CALL RCHARA_TALENT_SINGLE, [[TALENT:中毒しやすい]], 8
;倒錯的
CALL RCHARA_TALENT_SINGLE, [[TALENT:倒錯的]], 8
;両刀(女性は少し確率高め、男の娘は極端に確率が高い)
IF TALENT:オトコ
	CALL RCHARA_TALENT_SINGLE, [[TALENT:両刀]], 8
ELSEIF TALENT:オトコの娘
	CALL RCHARA_TALENT_SINGLE, [[TALENT:両刀]], 30
ELSE
	CALL RCHARA_TALENT_SINGLE, [[TALENT:両刀]], 14
ENDIF
;Ｓ気質、Ｍ気質
CALL RCHARA_TALENT_OPPOSE, [[TALENT:サド]], [[TALENT:マゾ]], 8, 8
;小悪魔(女性・男の娘は確率高め)
IF TALENT:オトコの娘 == 0
	CALL RCHARA_TALENT_SINGLE, [[TALENT:小悪魔]], 3
ELSE
	CALL RCHARA_TALENT_SINGLE, [[TALENT:小悪魔]], 10
ENDIF
;魅惑
CALL RCHARA_TALENT_SINGLE, [[TALENT:魅惑]], 8
;謎の魅力
CALL RCHARA_TALENT_SINGLE, [[TALENT:謎の魅力]], 2
;回復早い、回復遅い(白兵の初期値に応じて確率変動)
CALL RCHARA_TALENT_OPPOSE, [[TALENT:回復早い]], [[TALENT:回復遅い]], 5, 7
;幼稚
CALL RCHARA_TALENT_SINGLE, [[TALENT:幼児退行]], 6

;指定の確率に応じて動物耳、尻尾
IF RAND:100 < ARG:0
	TALENT:動物耳 = 1
ENDIF

;母乳体質
SIF !TALENT:オトコ
	CALL RCHARA_TALENT_SINGLE, GETNUM(TALENT, "母乳体質"), 5

;■敏鈍感素質の決定
CALL RANDOM_CHARA_SENSE

CALL SET_TALENT_FILTER(TARGET)

;■性的能力の決定
;処女・童貞の場合
IF IS処女(TARGET) && IS童貞(TARGET)
	;1/7の確率で完全無垢に(全ての性経験・性知識が0)
	IF RAND:7 != 0
		LOCAL:0 = RAND:10
		IF LOCAL:0 < 6
			LOCAL:0 = -2
			EXP:絶頂経験 = 0
		ELSEIF LOCAL:0 < 9
			LOCAL:0 = -1
			EXP:絶頂経験 = (RAND:10 - 7) * 5
		ELSE
			LOCAL:0 = 0
			EXP:絶頂経験 = (RAND:6 + 1) * 5
		ENDIF

		ABL:欲望 = MAX(LOCAL:0 + RAND:2 + RAND:2, 0)
		ABL:技巧 = MAX(LOCAL:0 + RAND:2 + RAND:2, 0)
		ABL:奉仕精神 = MAX(LOCAL:0 + RAND:4 - 1, 0)
		ABL:露出癖 = MAX(LOCAL:0 + RAND:20 - 17, 0)
		ABL:自慰中毒 = MAX(LOCAL:0 + RAND:7 - 4, 0)

		IF TALENT:両刀
			IF RAND:2
				IF TALENT:オトコ
					ABL:ＢＬっ気 = MAX(RAND:3 + 1)
					ABL:性交技術 = MAX(ABL:ＢＬっ気 + RAND:6 - 5, 0)
					ABL:Ａ感覚 = MAX(ABL:ＢＬっ気 + RAND:6 - 5, 0)
				ELSE
					ABL:レズっ気 = MAX(RAND:3 + 1)
				ENDIF
			ENDIF
		ELSE
			IF RAND:15 == 0
				IF TALENT:オトコ
					ABL:ＢＬっ気 = MAX(RAND:2 + 1)
					ABL:性交技術 = MAX(ABL:ＢＬっ気 + RAND:6 - 5, 0)
					ABL:Ａ感覚 = MAX(ABL:ＢＬっ気 + RAND:6 - 5, 0)
				ELSE
					ABL:レズっ気 = MAX(RAND:2 + 1)
				ENDIF
			ENDIF
		ENDIF

		LOCAL:1 = 30 - ABL:自慰中毒 * 10
		ABL:Ｃ感覚 = MAX(TALENT:Ｃ敏感 - TALENT:Ｃ鈍感 + RAND:(LOCAL:1 + 2) - LOCAL:1, 0)
		ABL:Ｂ感覚 = MAX(TALENT:Ｂ敏感 - TALENT:Ｂ鈍感 + RAND:(LOCAL:1 + 2) - LOCAL:1, 0)

		IF ABL:Ａ感覚 == 0
			LOCAL:2 = 60 - ABL:自慰中毒 * 15
			ABL:Ａ感覚 = MAX(TALENT:Ａ敏感 - TALENT:Ａ鈍感 + RAND:(LOCAL:2 + 2) - LOCAL:2, 0)
		ENDIF

	ENDIF

;経験ありの場合
ELSE
	LOCAL:0 = RAND:10
	IF LOCAL:0 < 5
		LOCAL:0 = -1
		EXP:絶頂経験 = (RAND:10 - 7) * 5
	ELSEIF LOCAL:0 < 9
		LOCAL:0 = 0
		EXP:絶頂経験 = (RAND:6 + 1) * 5
	ELSE
		LOCAL:0 = 1
		EXP:絶頂経験 = (RAND:9 + 2) * 5
	ENDIF

	ABL:欲望 = MAX(LOCAL:0 + RAND:2 + RAND:2, 0)
	ABL:技巧 = MAX(LOCAL:0 + RAND:2 + RAND:2, 0)
	ABL:奉仕技術 = MAX(LOCAL:0 + RAND:4 - 1, 0)
	ABL:性交技術 = MAX(LOCAL:0 + RAND:4 - 1, 0)
	ABL:露出癖 = MAX(LOCAL:0 + RAND:7 - 4, 0)
	ABL:自慰中毒 = MAX(LOCAL:0 + RAND:5 - 2, 0)

	IF TALENT:両刀
		IF RAND:3 != 0
			IF TALENT:オトコ
				ABL:ＢＬっ気 = MAX(RAND:3 + 1)
				ABL:Ａ感覚 = MAX(ABL:ＢＬっ気 + RAND:4 - 3, 0)
			ELSE
				ABL:レズっ気 = MAX(RAND:3 + 1)
			ENDIF
		ENDIF
	ELSE
		IF RAND:8 == 0
			IF TALENT:オトコ
				ABL:ＢＬっ気 = MAX(RAND:2 + 1)
				ABL:Ａ感覚 = MAX(ABL:ＢＬっ気 + RAND:4 - 3, 0)
			ELSE
				ABL:レズっ気 = MAX(RAND:2 + 1)
			ENDIF
		ENDIF
	ENDIF

	ABL:Ｃ感覚 = MAX(TALENT:Ｃ敏感 - TALENT:Ｃ鈍感 + RAND:5 - 3, 0)
	ABL:Ｂ感覚 = MAX(TALENT:Ｂ敏感 - TALENT:Ｂ鈍感 + RAND:5 - 3, 0)
	ABL:Ｖ感覚 = MAX(TALENT:Ｖ敏感 - TALENT:Ｖ鈍感 + RAND:5 - 3, 0)

	IF ABL:Ａ感覚 == 0
		ABL:Ａ感覚 = MAX(TALENT:Ａ敏感 - TALENT:Ａ鈍感 + RAND:20 - 18, 0)
	ENDIF

ENDIF

;■バストサイズの決定
CALL RANDOM_CHARA_BUSTSIZE

;■性格の決定
;CALL SET_CHARACTER(TARGET)

;■髪色・髪の長さの決定
;CALL SET_HAIR(TARGET)

;-------------------------
;バストサイズをランダムに設定する関数
;-------------------------
@RANDOM_CHARA_BUSTSIZE
LOCAL:2 = RAND:100
LOCAL = 0
IF TALENT:小柄
	;並(40%)
	IF LOCAL:2 < 40
	;貧(48%)
	ELSEIF LOCAL:2 < 88
		TALENT:貧乳 = 1
	;巨(5%)
	ELSEIF LOCAL:2 < 93
		TALENT:巨乳 = 1
	;絶(7%)
	ELSE
		TALENT:絶壁 = 1
	ENDIF
ELSE
	;並(40%)
	IF LOCAL:2 < 40
	;貧(26%)
	ELSEIF LOCAL:2 < 66
		TALENT:貧乳 = 1
	;巨(26%)
	ELSEIF LOCAL:2 < 92
		TALENT:巨乳 = 1
	;爆(6%)
	ELSEIF LOCAL:2 < 98
		TALENT:爆乳 = 1
	;絶(2%)
	ELSE
		TALENT:絶壁 = 1
	ENDIF
ENDIF

;-------------------------
;敏鈍感素質をランダムに設定する関数
;-------------------------
@RANDOM_CHARA_SENSE
LOCAL:0 = RAND:1000

;一箇所敏感型(10%)
IF LOCAL:0 < 100
	CALL SET_SENSE_RANDOM(0)
;一箇所鈍感型(5%)
ELSEIF LOCAL:0 < 150
	CALL SET_SENSE_RANDOM(1)
;敏感＋鈍感型(3%)
ELSEIF LOCAL:0 < 180
	CALL SET_SENSE_RANDOM(0)
	CALL SET_SENSE_RANDOM(1)
;奥手(1%)
ELSEIF LOCAL:0 < 190
	TALENT:Ｃ鈍感 = 1
	TALENT:Ｂ鈍感 = RAND:2 == 0
	TALENT:Ｖ敏感 = RAND:6 == 0
	TALENT:Ａ敏感 = RAND:6 == 0
;表面(1%)
ELSEIF LOCAL:0 < 200
	TALENT:Ｃ敏感 = 1
	TALENT:Ｂ敏感 = RAND:2 == 0
	TALENT:Ｖ鈍感 = RAND:6 == 0
	TALENT:Ａ鈍感 = RAND:6 == 0
;全身敏感(0.4%)
ELSEIF LOCAL:0 < 204
	TALENT:Ｃ敏感 = 1
	TALENT:Ｂ敏感 = 1
	TALENT:Ｖ敏感 = 1
	TALENT:Ａ敏感 = 1
;全身鈍感(0.4%)
ELSEIF LOCAL:0 < 208
	TALENT:Ｃ鈍感 = 1
	TALENT:Ｂ鈍感 = 1
	TALENT:Ｖ鈍感 = 1
	TALENT:Ａ鈍感 = 1
;Ｖ敏＋鈍感型(0.6%)
ELSEIF LOCAL:0 < 214
	TALENT:Ｖ敏感 = 1
	CALL SET_SENSE_RANDOM(1)
	CALL SET_SENSE_RANDOM(1)
	IF RAND:3 == 0
		CALL SET_SENSE_RANDOM(1)
	ENDIF
;Ａ敏＋鈍感型(0.6%)
ELSEIF LOCAL:0 < 220
	TALENT:Ａ敏感 = 1
	CALL SET_SENSE_RANDOM(1)
	CALL SET_SENSE_RANDOM(1)
	IF RAND:3 == 0
		CALL SET_SENSE_RANDOM(1)
	ENDIF
ENDIF

;-------------------------
;同一部位に対して同時に敏感・鈍感が設定された場合に相殺する処理
;-------------------------
@KILL_INCOMPATIBLE_SENSE()
IF TALENT:Ｖ敏感 && TALENT:Ｖ鈍感
	TALENT:Ｖ敏感 = 0
	TALENT:Ｖ鈍感 = 0
ENDIF

IF TALENT:Ａ敏感 && TALENT:Ａ鈍感
	TALENT:Ａ敏感 = 0
	TALENT:Ａ鈍感 = 0
ENDIF

IF TALENT:Ｃ敏感 && TALENT:Ｃ鈍感
	TALENT:Ｃ敏感 = 0
	TALENT:Ｃ鈍感 = 0
ENDIF

IF TALENT:Ｂ敏感 && TALENT:Ｂ鈍感
	TALENT:Ｂ敏感 = 0
	TALENT:Ｂ鈍感 = 0
ENDIF

;-------------------------
;ＣＶＡＢで未設定のランダムな箇所を敏感(ARG:0=0)・鈍感(ARG:0=1)にする関数
;-------------------------
@SET_SENSE_RANDOM(ARG:0)
LOCAL:5 = 4
FOR LOCAL:0, 0, 4
	IF TALENT:(101 + LOCAL:0 * 2) || TALENT:(102 + LOCAL:0 * 2)
		LOCAL:5 --
	ENDIF
NEXT

IF LOCAL:5 <= 0
	RETURN
ENDIF

LOCAL:6 = RAND:(LOCAL:5)

FOR LOCAL:0, 0, 4
	IF !TALENT:(101 + LOCAL:0 * 2) && !TALENT:(102 + LOCAL:0 * 2)
		LOCAL:6 --
		IF LOCAL:6 < 0
			LOCAL:7 = LOCAL:0
			BREAK
		ENDIF
	ENDIF
NEXT

IF ARG:0 == 0
	TALENT:(102 + LOCAL:7 * 2) = 1
ELSE
	TALENT:(101 + LOCAL:7 * 2) = 1
ENDIF

;-------------------------
;ランダムフィルタにかけた値段を引っ張ってくる
;-------------------------
@GET_FILTER_COST
#FUNCTION
#DIM COST
#DIM ID

COST = 0

RETURNF COST

;-------------------------
;TALENT_FILTERを掛ける
;-------------------------
@SET_TALENT_FILTER(ID)
#DIM ID
#DIM TALENT_ID
#DIM MAX_TALENT_ID

VARSIZE TALENT
MAX_TALENT_ID = RESULT:0
FOR TALENT_ID, 0, MAX_TALENT_ID
	IF TALENT:ID:TALENT_ID == 0 && TALENT_FILTER:TALENT_ID
		TALENT:ID:TALENT_ID = 1
		SELECTCASE TALENT_ID
			CASE 11
				TALENT:ID:Ｂ敏感 = 0
			CASE 13
				TALENT:ID:Ａ敏感 = 0
			CASE 15
				TALENT:ID:17 = 0
			CASE 17
				TALENT:ID:15 = 0
			CASE 22
				TALENT:ID:23 = 0
			CASE 23
				TALENT:ID:22 = 0
			CASE 25
				TALENT:ID:26 = 0
			CASE 26
				TALENT:ID:25 = 0
			CASE 27
				TALENT:ID:28 = 0
			CASE 28
				TALENT:ID:27 = 0
			CASE 30
				TALENT:ID:31 = 0
			CASE 31
				TALENT:ID:30 = 0
			CASE 32
				TALENT:ID:33 = 0
			CASE 33
				TALENT:ID:32 = 0
			CASE 35
				TALENT:ID:36 = 0
			CASE 36
				TALENT:ID:35 = 0
			CASE 40
				TALENT:ID:41 = 0
			CASE 41
				TALENT:ID:40 = 0
			CASE 42
				TALENT:ID:43 = 0
			CASE 43
				TALENT:ID:42 = 0
			CASE 50
				TALENT:ID:51 = 0
			CASE 51
				TALENT:ID:50 = 0
			CASE 61
				TALENT:ID:62 = 0
			CASE 62
				TALENT:ID:61 = 0
			CASE 70
				TALENT:ID:71 = 0
			CASE 71
				TALENT:ID:70 = 0
			CASE 81
				TALENT:ID:82 = 0
				TALENT:ID:83 = 0
			CASE 82, 83
				TALENT:ID:80 = 0
			CASE 101
				TALENT:ID:精愛味覚 = 0
			CASE 102
				TALENT:ID:薬物中毒 = 0
			CASE 103
				TALENT:ID:回復早い = 0
			CASE 104
				TALENT:ID:おもらし癖 = 0
			CASE 105
				TALENT:ID:痛みに弱い = 0
			CASE 106
				TALENT:ID:回復遅い = 0
			CASE 107
				TALENT:ID:濡れやすい = 0
			CASE 108
				TALENT:ID:痛みに強い = 0
			CASE 109
				TALENT:ID:涙もろい = 0
			CASE 110
				TALENT:ID:濡れにくい = 0
			CASE 111
				TALENT:ID:素質 = 0
			CASE 112
				TALENT:ID:泣かない = 0
			CASE 115
				TALENT:ID:小柄 = 0
			CASE 116
				TALENT:ID:小柄 = 0
		ENDSELECT
	ENDIF
NEXT

;-------------------------
;ランダムキャラに特定の素質を指定の確率で持たせる関数
;ARG:0=素質の番号、ARG:1=素質の取得確率(百分率)
;-------------------------
@RCHARA_TALENT_SINGLE, ARG:0, ARG:1
LOCAL:0 = RAND:100
IF LOCAL:0 < ARG:1
	TALENT:(ARG:0) = 1
ENDIF

;-------------------------
;ランダムキャラに相反する２つの素質を指定の確率で持たせる関数
;ARG:0=素質１の番号、ARG:1=素質２の番号、ARG:2=素質１の取得確率(百分率)、ARG:3=素質２の取得確率(百分率)
;-------------------------
@RCHARA_TALENT_OPPOSE, ARG:0, ARG:1, ARG:2, ARG:3
LOCAL:0 = RAND:100
IF LOCAL:0 < ARG:2
	TALENT:(ARG:0) = 1
ELSEIF 99 - LOCAL:0 < ARG:3
	TALENT:(ARG:1) = 1
ENDIF
