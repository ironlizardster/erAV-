;===========================================================
;見た目変更呼び出し
;===========================================================
@EVENT_LOOKSCHANGE(ARG)

LOCAL = RAND:100
SIF TALENT:ARG:影響されやすい == 1 || TALENT:ARG:ニート == 1
	LOCAL = 100
SIF LOCAL >= 80
	CALL EVENT_LOOKSCHANGE_DETAIL(ARG)

;===========================================================
;見た目変更表示
;===========================================================
@EVENT_LOOKSCHANGE_DETAIL(ARG)

VARSET TCVAR:ARG

PRINTFORMW 休み明けの月曜日、事務所にやってきた%NAME:ARG%に%CALLNAME:MASTER%は違和感を覚えた……
PRINTFORM %CALLNAME:ARG%を注意深く観察してみると、

CALLFORM PERSONAL_CHANGE(ARG)

PRINTFORML が違和感の原因だった
PRINTFORML %CALLNAME:ARG%に話を聞くと、付き合っている彼氏の%CSTR:ARG:彼氏姓%の好みに合わせたものらしい
PRINTFORM %CALLNAME:ARG%は%CALLNAME:MASTER%に似合っているかどうか尋ねてきたが、あなたはどう返答していいものか分からず
PRINTFORML 言葉に詰まってしまった……

EXP:ARG:イメチェン回数 += 1

CALL ADD_EXP(ARG)

WAIT
DRAWLINE


;===========================================================
;中身仮。
;とりあえず汎用として、分岐できる形を取れたら。
;TRYCCALLFORMだなあ……
;===========================================================
@PERSONAL_CHANGE(ARG)
IF EXP:イメチェン回数 == 0 
	IF CFLAG:ARG:髪色 != 1 && CFLAG:ARG:髪色 != 10
		PRINTFORM %HAIRCOLOR_STRING(CFLAG:ARG:髪色,CFLAG:ARG:染髪済)%だった髪が茶髪に染められているの
		CFLAG:ARG:髪色 = 1
		CFLAG:ARG:染髪済 = 1
	ELSEIF CFLAG:ARG:髪色 == 1
		PRINTFORM %HAIRCOLOR_STRING(CFLAG:ARG:髪色,CFLAG:ARG:染髪済)%だった髪がより明るい栗色に染められているの
		CFLAG:ARG:髪色 = 10
		CFLAG:ARG:染髪済 = 1
	ELSEIF CFLAG:ARG:髪色 == 10
		PRINTFORM %HAIRCOLOR_STRING(CFLAG:ARG:髪色,CFLAG:ARG:染髪済)%の髪を脱色したのか人工的なプラチナブラウンに変わっていたの
		CFLAG:ARG:髪色 = 9
		CFLAG:ARG:染髪済 = 1
	ENDIF
ENDIF
IF EXP:ARG:イメチェン回数 == 1 && CFLAG:ARG:髪型 != 1 && CFLAG:ARG:髪型 != 8 && CFLAG:ARG:髪型 != 5
	PRINTFORM %CHARA_HAIRSTYLE_STRING(ARG)%
	PRINTFORM だった髪型が肩のあたりで切りそろえたセミロングに変わっているの
	CFLAG:ARG:髪型 = 1
ELSEIF EXP:ARG:イメチェン回数 == 1 && (CFLAG:ARG:髪型 == 1 || CFLAG:ARG:髪型 == 5) && CFLAG:ARG:髪型 != 8 
	PRINTFORM %CHARA_HAIRSTYLE_STRING(ARG)%
	PRINTFORM だった髪型にふんわりと緩やかなパーマがかかっているの
	CFLAG:ARG:髪型 = 7
ELSEIF EXP:ARG:イメチェン回数 == 1 && CFLAG:ARG:髪型 == 8
	PRINTFORM 腰に届きそうなほどの
	PRINTFORM %CHARA_HAIRSTYLE_STRING(ARG)%
	PRINTFORM の髪をカットし背中の中ほどまでの、ふんわりと緩やかなパーマがかかった長さに変わっているの
	CFLAG:ARG:髪型 = 7
ENDIF
IF EXP:ARG:イメチェン回数 == 2 && CFLAG:ARG:上着タイプ != 30
	PRINTFORM いつも事務所に来る時は
	PRINTFORM %GET_上着タイプ(ARG)%だった服装が肌の露出の多いパンクロリータファッションになっているの
	CFLAG:ARG:上着タイプ = 30
ELSEIF EXP:ARG:イメチェン回数 == 2 && CFLAG:ARG:上着タイプ == 30
	PRINTFORM いつも事務所に来る時は
	PRINTFORM %GET_上着タイプ(ARG)%だった服装が肌の露出の多いキャミとデニムミニという服装になっているの
	CFLAG:ARG:上着タイプ = 40
ENDIF
IF EXP:ARG:イメチェン回数 == 3 && CFLAG:ARG:髪色 != 9
	PRINTFORM %HAIRCOLOR_STRING(CFLAG:ARG:髪色,CFLAG:ARG:染髪済)%色に染めたはずの髪を脱色したのか、人工的なプラチナブラウンに変わった
	PRINTFORM 毛先をつまんでいじっているの
	CFLAG:ARG:髪色 = 9
	CFLAG:ARG:染髪済 = 1
ELSEIF EXP:ARG:イメチェン回数 == 3 && CFLAG:ARG:髪色 == 9
	PRINTFORM 脱色した髪にうっすらと明るいピンクでメッシュを入れた毛先をつまんでいじっているの
ENDIF
IF EXP:ARG:イメチェン回数 == 4 && (CFLAG:ARG:ピアス状況 & 2) == 0
	PRINTFORM %GET_上着タイプ(ARG)%から覗くへそに淡いピンク色のジュエリーが嵌めこまれたピアスが、鈍い輝きを放っているの
	CFLAG:ARG:ピアス状況 |= 2
ELSEIF EXP:ARG:イメチェン回数 == 4 && (CFLAG:ARG:ピアス状況 & 2)
	PRINTFORM %GET_上着タイプ(ARG)%から覗くへそを貫くピアスをぐるりと一周するようにクロムメッキのベリーチェーンが追加されていたの
ENDIF

IF EXP:ARG:イメチェン回数 == 5 && CFLAG:ARG:タトゥー != 15
	PRINTFORM %GET_上着タイプ(ARG)%から覗く右の二の腕にアゲハ蝶のタトゥーが彫られていたの
	IF CFLAG:ARG:タトゥー == 0
		CSTR:ARG:タトゥー1場所 = 右の二の腕
		CSTR:ARG:タトゥー1モチーフ = アゲハ蝶をモチーフにした
		CFLAG:ARG:タトゥー |= 1
	ELSEIF CFLAG:ARG:タトゥー == 1
		CSTR:ARG:タトゥー2場所 = 右の二の腕
		CSTR:ARG:タトゥー2モチーフ = アゲハ蝶をモチーフにした
		CFLAG:ARG:タトゥー |= 2
	ELSEIF CFLAG:ARG:タトゥー == 3
		CSTR:ARG:タトゥー3場所 = 右の二の腕
		CSTR:ARG:タトゥー3モチーフ = アゲハ蝶をモチーフにした
		CFLAG:ARG:タトゥー |= 4
	ELSEIF CFLAG:ARG:タトゥー == 7
		CSTR:ARG:タトゥー4場所 = 右の二の腕
		CSTR:ARG:タトゥー4モチーフ = アゲハ蝶をモチーフにした
		CFLAG:ARG:タトゥー |= 8
	ENDIF
	TCVAR:ARG:異常経験 = 1
ELSEIF EXP:ARG:イメチェン回数 == 5 && CFLAG:ARG:タトゥー == 15
	PRINTFORM %GET_上着タイプ(ARG)%から覗くタトゥーが増えているような気がするの
ENDIF
IF EXP:ARG:イメチェン回数 == 6 && TALENT:ARG:ギャル系 == 0
	PRINTFORM これまでメイクに費やす時間は短く、化粧自体も自然な感じだったのが
	PRINTFORM マスカラやリップグロスを使うようになっており、いわゆる【ギャル系】のメイクになっているの
	TALENT:ARG:ギャル系 = 1
	TALENT:ARG:大和撫子 = 0
ELSEIF EXP:ARG:イメチェン回数 == 6 && TALENT:ARG:ギャル系
	PRINTFORM これまで以上にメイクに費やす時間が増え、化粧もよりキツくなっているの
ENDIF
IF EXP:ARG:イメチェン回数 == 7 && TALENT:ARG:褐色肌 == 0
	PRINTFORM どうやら日焼けサロンに通うようになったのか、瑞々しい肌が褐色に日焼けしているの
	TALENT:ARG:褐色肌 = 1
ELSEIF EXP:ARG:イメチェン回数 == 7 && TALENT:ARG:褐色肌 == 1
	PRINTFORM 日焼けした肌を惜しげもなく晒しているの
ENDIF
IF EXP:ARG:イメチェン回数 == 8 && CFLAG:ARG:上着タイプ != 205
	PRINTFORM %GET_上着タイプ(ARG)%から更に露出の多いボディコン風のマイクロミニのタイトワンピース、蛍光色のネットストッキングに
	PRINTFORM 腕にはバングルとリングブレスレットといった服装に変わっているの
	CFLAG:ARG:上着タイプ = 205
	CFLAG:ARG:特別コスタイプ = 74
	CFLAG:ARG:靴下タイプ = 8
ELSEIF EXP:ARG:イメチェン回数 == 8 && CFLAG:ARG:上着タイプ == 205
	PRINTFORM ただでさえ大胆な
	PRINTFORM %GET_上着タイプ(ARG)%が更に際どいデザインのモノに変わっているの
ENDIF
IF EXP:ARG:イメチェン回数 == 9 && CFLAG:ARG:タトゥー != 15
	PRINTFORM %GET_上着タイプ(ARG)%から覗く左の太ももの内側にトライバルパターンのタトゥーが彫られていたの
	IF CFLAG:ARG:タトゥー == 0
		CSTR:ARG:タトゥー1場所 = 左の太ももの内側
		CSTR:ARG:タトゥー1モチーフ = トライバルパターンの
		CFLAG:ARG:タトゥー |= 1
	ELSEIF CFLAG:ARG:タトゥー == 1
		CSTR:ARG:タトゥー2場所 = 左の太ももの内側
		CSTR:ARG:タトゥー2モチーフ = トライバルパターンの
		CFLAG:ARG:タトゥー |= 2
	ELSEIF CFLAG:ARG:タトゥー == 3
		CSTR:ARG:タトゥー3場所 = 左の太ももの内側
		CSTR:ARG:タトゥー3モチーフ = トライバルパターンの
		CFLAG:ARG:タトゥー |= 4
	ELSEIF CFLAG:ARG:タトゥー == 7
		CSTR:ARG:タトゥー4場所 = 左の太ももの内側
		CSTR:ARG:タトゥー4モチーフ = トライバルパターンの
		CFLAG:ARG:タトゥー |= 8
	ENDIF
	TCVAR:ARG:異常経験 = 1
ELSEIF EXP:ARG:イメチェン回数 == 9 && CFLAG:ARG:タトゥー == 15
	PRINTFORM %GET_上着タイプ(ARG)%から覗くタトゥーが増えているような気がするの
ENDIF
IF EXP:ARG:イメチェン回数 == 10
	PRINTFORM 彼氏の%CSTR:ARG:彼氏姓%にだいぶ染められたのか、口調も頭の悪そうな感じになり、
	PRINTFORM 刹那的な悦楽を求めるような典型的な【ギャル系】そのものになっていたの
	TALENT:ARG:大人しい = 0
	TALENT:ARG:好奇心 = 1
	TALENT:ARG:保守的 = 0
	TALENT:ARG:ポジティブ = 1
	TALENT:ARG:ネガティブ = 0
	TALENT:ARG:一線越えない = 0
	TALENT:ARG:目立ちたがり = 1
	TALENT:ARG:貞操観念 = 0
	TALENT:ARG:貞操無頓着 = 1
	TALENT:ARG:解放 = 1
	TALENT:ARG:恥じらい = 0
	TALENT:ARG:恥薄い = 1
	TALENT:ARG:小悪魔 = 1
	TALENT:ARG:魅力 = 0
	TALENT:ARG:幼稚 = 0
	TALENT:ARG:心弱い = 0
	TALENT:ARG:大和撫子 = 0
	TALENT:ARG:惚れっぽい = 1
	IF ABL:ARG:欲望 < 3
		ABL:ARG:欲望 = 3
	ENDIF
	BASE:ARG:ギャラ *= 3
	CSTR:ARG:男性器呼び方 = チンポ
	IF RAND:2 == 0
		CSTR:ARG:一人称 = ウチ
	ELSE
		CSTR:ARG:一人称 = アタシ
	ENDIF
ENDIF
